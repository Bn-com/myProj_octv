/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ’‘÷æΩ‹
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


proc LinkShader()
{
	string $moepass[] = `ls -type "moebiusPass"`;	
	for ($i=0; $i<size($moepass); $i++)
	{
		string $lum[] = `listConnections -source 1 ($moepass[$i]+".luminance")`;
		string $surfaceshader[] = `listConnections -destination 1 ($moepass[$i]+".outColor")`;
		if (`objExists $lum[0]`&&`objExists $surfaceshader[0]`&&!`isConnected ($lum[0]+".outMatteOpacity") ($surfaceshader[0]+".outMatteOpacity")`)
			connectAttr -f ($lum[0]+".outMatteOpacity") ($surfaceshader[0]+".outMatteOpacity");
	}
}

proc unLinkShader()
{
	string $moepass[] = `ls -type "moebiusPass"`;	
	for ($i=0; $i<size($moepass); $i++)
	{
		string $lum[] = `listConnections -source 1 ($moepass[$i]+".luminance")`;
		string $surfaceshader[] = `listConnections -destination 1 ($moepass[$i]+".outColor")`;
		if (`objExists $lum[0]`&&`objExists $surfaceshader[0]`&&`isConnected ($lum[0]+".outMatteOpacity") ($surfaceshader[0]+".outMatteOpacity")`)
			disconnectAttr ($lum[0]+".outMatteOpacity") ($surfaceshader[0]+".outMatteOpacity");
	}
}

//Interface
global proc zjLinkShader()
{
	if (`window -q -exists zjLinkShaderWin`)
		return;

	window -title "Link Out Matte Opacity" -resizeToFitChildren true zjLinkShaderWin;
		columnLayout -adjustableColumn true;
			menuBarLayout;
				menu -label "Help";
					menuItem -label "Help on Link Out Matte Opacity..." -command "zjHelpCallback zjLinkShader";
				setParent ..;

		formLayout -numberOfDivisions 100 formLayout1;

			$a = `radioButtonGrp  -numberOfRadioButtons 2 -select 1  -labelArray2 "Link" "Unlink" 
					-columnWidth 1 100 -columnWidth 2 120 
					-columnAlign 1 "left"`;
			$b = `button -label "Apply" -command ("zjLinkShaderfunc"+" "+$a)`;
			$c = `button -label "Close" -command "deleteUI -window zjLinkShaderWin"`;
		
			formLayout -edit
				-attachForm	$a	"top"	5
				-attachForm	$a	"left"	5

				-attachForm	$b	"left"	0
				-attachControl	$b	"top"	5	$a
				-attachPosition	$b     "right"	0       50
				
				-attachControl	  $c	"left"		5	$b
				-attachControl	  $c	"top"	5	$a
				-attachPosition	  $c    "right"		0       100

			formLayout1;

	showWindow zjLinkShaderWin;
}

global proc zjLinkShaderfunc(string $rad)
{
	int $sel = `radioButtonGrp -q -select $rad`;
	if ($sel == 1)
		LinkShader();
	else
		unLinkShader();
}