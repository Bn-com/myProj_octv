/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc kcImportSetup()
{
	if (`window -q -exists kcImportSetupWnd`)
		deleteUI -window kcImportSetupWnd;

	window -title "Import Setup Info" -menuBar true -resizeToFitChildren true kcImportSetupWnd;
		
		menu -label "Tool";
			menuItem -label "Prefix all clusters with \"old_\"" -command "kcAddPrefix old_";

		menu -label "Help";
			menuItem -label "Help on Import Setup Info..." -command "zjHelpCallback kcImportExportSetup";

		string $formLayout = `formLayout`;

			textFieldGrp -columnWidth 1 80 -adjustableColumn 2 -label "Skin" -text "" kcSUSkinClusterTextField;
			button -label "Browse" -command "kcSUImportBrowseCB 1" kcSUImportBrowseButton1;

			separator kcSUSeparator1;

			textFieldGrp -columnWidth 1 80 -adjustableColumn 2 -label "Cluster" -text "" kcSUClusterTextField;
			button -label "Browse" -command "kcSUImportBrowseCB 2" kcSUImportBrowseButton2;

			separator kcSUSeparator2;

			textFieldGrp -columnWidth 1 80 -adjustableColumn 2 -label "Vtx ID Map" -text "" kcSUVtxIDMapTextField;
			button -label "Browse" -command "kcSUImportBrowseCB 3" kcSUImportBrowseButton3;

			separator kcSUSeparator3;

			button -label "Import" -command "kcSUImportCB" kcSUImportButton;

			separator kcSUSeparator4;

			text -label "NOTE 1: Make sure you've renamed the duplicated objects to the original name.\nNOTE 2: Import skin weights requires 2.5 min per 1000 vertices." kcSUImportText;

		setParent ..;

		formLayout -edit

			-attachForm		kcSUSkinClusterTextField	"top"	5
			-attachForm		kcSUSkinClusterTextField	"left"	5
			-attachControl	kcSUSkinClusterTextField	"right"	5	kcSUImportBrowseButton1

			-attachForm		kcSUImportBrowseButton1	"top"	5
			-attachForm		kcSUImportBrowseButton1	"right"	5

			-attachControl	kcSUSeparator1	"top"	5	kcSUImportBrowseButton1
			-attachForm		kcSUSeparator1	"left"	5	
			-attachForm		kcSUSeparator1	"right"	5	

			-attachControl	kcSUClusterTextField	"top"	5	kcSUSeparator1
			-attachForm		kcSUClusterTextField	"left"	5
			-attachControl	kcSUClusterTextField	"right"	5	kcSUImportBrowseButton2

			-attachControl	kcSUImportBrowseButton2	"top"	5	kcSUSeparator1
			-attachForm		kcSUImportBrowseButton2	"right"	5

			-attachControl	kcSUSeparator2	"top"	5	kcSUImportBrowseButton2
			-attachForm		kcSUSeparator2	"left"	5	
			-attachForm		kcSUSeparator2	"right"	5	

			-attachControl	kcSUVtxIDMapTextField	"top"	5	kcSUSeparator2
			-attachForm		kcSUVtxIDMapTextField	"left"	5
			-attachControl	kcSUVtxIDMapTextField	"right"	5	kcSUImportBrowseButton3

			-attachControl	kcSUImportBrowseButton3	"top"	5	kcSUSeparator2
			-attachForm		kcSUImportBrowseButton3	"right"	5

			-attachControl	kcSUSeparator3	"top"	5	kcSUImportBrowseButton3
			-attachForm		kcSUSeparator3	"left"	5	
			-attachForm		kcSUSeparator3	"right"	5	

			-attachControl	kcSUImportButton	"top"	5	kcSUSeparator3
			-attachForm		kcSUImportButton	"left"	5	
			-attachForm		kcSUImportButton	"right"	5	

			-attachControl	kcSUSeparator4	"top"	5	kcSUImportButton
			-attachForm		kcSUSeparator4	"left"	5	
			-attachForm		kcSUSeparator4	"right"	5	

			-attachControl	kcSUImportText	"top"	5	kcSUSeparator4
			-attachForm		kcSUImportText	"left"	5	
			-attachForm		kcSUImportText	"right"	5	
		$formLayout;

	showWindow kcImportSetupWnd;
}

global proc kcAddPrefix(string $prefix)
{
	string $orig[] = `ls -selection`;

	// rename cluster nodes

	select `ls -type cluster`;

	string $selected[] = `ls -selection`;

	for ($obj in $selected)
		rename $obj ($prefix + $obj);

	// rename cluster handle nodes (transform)

	select `ls -type clusterHandle`;
	pickWalk -direction up;

	string $selected[] = `ls -selection`;

	for ($obj in $selected)
		rename $obj ($prefix + $obj);

	// rename cluster handle parent nodes (transform)

	select `ls -type clusterHandle`;
	pickWalk -direction up;
	pickWalk -direction up;

	string $selected[] = `ls -selection`;

	for ($obj in $selected)
		rename $obj ($prefix + $obj);

	// restore original selection

	select $orig;
}

global proc kcSUImportCB()
{
	string $skinFile = `textFieldGrp -q -text kcSUSkinClusterTextField`;
	string $clusterFile = `textFieldGrp -q -text kcSUClusterTextField`;
	string $mapFile = `textFieldGrp -q -text kcSUVtxIDMapTextField`;

	if (!`filetest -r $skinFile` && !`filetest -r $clusterFile`)
		error("Select a skin or a cluster file.");

	if (!`filetest -r $mapFile`)
		error("Select a vertex ID mapping file.");

	waitCursor -state on;

	if ((`filetest -r $skinFile`) && (`filetest -r $mapFile`)) 
		kcImportSkinCluster($skinFile, $mapFile);

	if ((`filetest -r $clusterFile`) && (`filetest -r $mapFile`)) 
		kcImportCluster($clusterFile, $mapFile);

	waitCursor -state off;
}

global proc int kcSUImportBrowseCommand(int $type, string $filename, string $fileType)
{
	if ($fileType == "text")
	{
		if ($type == 1)
			// skin cluster file
			textFieldGrp -e -text $filename kcSUSkinClusterTextField;
		else
		if ($type == 2)
			// cluster file
			textFieldGrp -e -text $filename kcSUClusterTextField;
		else
			// vertex ID mapping file
			textFieldGrp -e -text $filename kcSUVtxIDMapTextField;
	}
	else
		warning("Only choose text file.");

    return 1;
}

global proc kcSUImportBrowseCB(int $type)
{
	fileBrowserDialog -mode 0 -fileCommand ("kcSUImportBrowseCommand " + $type) -fileType "text" -actionName "OK";
}
