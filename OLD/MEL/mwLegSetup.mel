/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: mw
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc mwLegSetup(string $char)
{
	string $side = "l r";
	string $sides[];
	tokenize($side, $sides);
	string $side1;
	for($side1 in $sides)
	{
		//SETUP LOCATOR TOE
		string $toeLoc[] = `spaceLocator`;
		$toeLoc[0] = `rename $toeLoc[0] ($char+"_toeLoc_"+$side1)`;
		select -r ($char+"_toeTip_"+$side1) $toeLoc[0];mwObjSnap;	
		string $toeGroup = `group -n ($char+"_toeRot_"+$side1) $toeLoc[0]`;
		select -r ($char+"_toe_"+$side1) $toeGroup;mwSnapPivot;

		//HEEL
		string $heelLoc[] = `spaceLocator`;
		$heelLoc[0] = `rename $heelLoc[0] ($char+"_heelLoc_"+$side1)`;
		select -r ($char+"_heel_"+$side1) $heelLoc[0];mwObjSnap;

		//ANKLE	
		string $ankleLoc[] = `spaceLocator`;
		$ankleLoc[0] = `rename $ankleLoc[0] ($char+"_ankleLoc_"+$side1)`;
		select -r ($char+"_ankle_"+$side1) $ankleLoc[0];mwObjSnap;	
		string $ballGroup = `group -n ($char+"_ballRot_"+$side1) $heelLoc[0] $ankleLoc[0]`;
		select -r ($char+"_toe_"+$side1) $ballGroup;mwSnapPivot;

		string $ankleGroup = `group -n ($char+"_ankleRot_"+$side1) $ballGroup $toeGroup`;
		select -r ($char+"_ankle_"+$side1) $ankleGroup;mwSnapPivot;

		string $tipGroup = `group -n ($char+"_tipRot_"+$side1) $ankleGroup`;
		select -r ($char+"_toeTip_"+$side1) $tipGroup;mwSnapPivot;

		string $footGroup = `group -n ($char+"_footCon_"+$side1) $tipGroup`;
		select -r ($char+"_heel_"+$side1) $footGroup;mwSnapPivot;

		//CREATE IK HANDLES
		string $leg[] = `ikHandle -sj ($char+"_leg_"+$side1) -ee ($char+"_ankle_"+$side1)`;
		$leg[0] = `rename $leg[0] ($char+"_legHandle_"+$side1)`;
		string $ankle[] = `ikHandle -sj ($char+"_ankle_"+$side1) -ee ($char+"_toe_"+$side1)`;
		$ankle[0] = `rename $ankle[0] ($char+"_ankleHandle_"+$side1)`;
		string $toe[] = `ikHandle -sj ($char+"_toe_"+$side1) -ee ($char+"_toeTip_"+$side1)`;
		$toe[0] = `rename $toe[0] ($char+"_toeHandle_"+$side1)`;
		string $heel[] = `ikHandle -sj ($char+"_ankle_"+$side1) -ee ($char+"_heel_"+$side1)`;
		$heel[0] = `rename $heel[0] ($char+"_heelHandle_"+$side1)`;
		
		//PARENT HANDLES
		parent $toe[0] $ankle[0];
		parent $heel[0] $ankle[0];
		parent $ankle[0] $leg[0];
		string $ankleGroup = `group -n ($char+"_ankleGroup_"+$side1) $ankle[0]`;
		select -r $leg[0] $ankleGroup;mwSnapPivot;
		string $group = `group -n ($char+"_LEG_HANDLES_"+$side1) $leg[0]`;
		select -r ($char+"_leg_"+$side1);select -add $group;mwSnapPivot;

		//CREATE LOCATORS AND CONSTRAINTS
		pointConstraint -weight 1 $toeGroup $ankle[0];
		pointConstraint -weight 1 $heelLoc[0] $heel[0];
		pointConstraint -weight 1 $ankleLoc[0] $leg[0];
		pointConstraint -weight 1 $toeLoc[0] $toe[0];
		orientConstraint -weight 1 $ankleLoc[0] $ankleGroup;

		//SET ATTRIBUTES
		string $attrs = "tx ty tz rx ry rz sx sy sz visibility";
		string $attr_list[];
		int $tok = tokenize($attrs, $attr_list);
		for($attr in $attr_list)
		{
			setAttr -k 0 ($toeLoc[0]+"."+$attr);
			setAttr -k 0 ($heelLoc[0]+"."+$attr);
			setAttr -k 0 ($ankleLoc[0]+"."+$attr);
		}
	}
	select -cl;
}
