/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: 黄仲维
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


//	$filetype	: "maya", "";
//	$foldertype	: "WORKING", "MASTER", ""
//	$isFile		: true, false

global proc zwPublishCopyFile(string $source, string $dest, string $filetype, string $foldertype, int $isFile)
{
	waitCursor -state on;

	//source
	$source = `zwFullPath $source`;
	if (`size $source` > 255)
		error ("文件路径不能超过256字节：" + $source);
	if(!`filetest -f $source`)
		error ($source + " is not exist.");
	string $buf[];
	tokenize $source "/" $buf;
	string $filename = $buf[size($buf)-1];
	tokenize $filename "." $buf;
	if ($filetype == "maya")
	{
		if ($buf[size($buf)-1] != "mb" && $buf[size($buf)-1] != "ma")
			error ($source + " is not a maya scene file.");
	}
	else if ($filetype != "")
	{
		if ($buf[size($buf)-1] == "mb" || $buf[size($buf)-1] == "ma")
			error ($source + " is a maya scene file.");
	}

	//dest
	$dest = `zwFullPath $dest`;
	if ($foldertype == "WORKING" || $foldertype == "MASTER")
	{
		if ($foldertype == "MASTER")
			$foldertype = "DIRECTOR_FINAL";
		if(!`gmatch (toupper($dest)) ("*" + $foldertype + "*")`)
			error ($dest + " is not a " + $foldertype + " folder.");
	}
	if (!$isFile)
	{
		if(!`filetest -d $dest`)
			error ($dest + " is not exist.");
		$dest = $dest + "/" + $filename;
	}

	if (`size $dest` > 255)
		error ("文件路径不能超过256字节：" + $dest);

	if (`toupper $source` != `toupper $dest`)
	{
		string $cmd = "idmtCheckin.exe " + "copy" + " \"" + $source + "\" \"" + $dest + "\"";
		chdir "C:";
		string $rs = `system $cmd`;
		if ($rs != "")
			error ("复制文件失败：" + $rs + "\nSOURCE: " + $source + "\nDEST: " + $dest);
	}
		
	waitCursor -state off;
}