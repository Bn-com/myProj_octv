/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc kcRenameResolutionToken(string $fromMenu, string $toMenu)
{
	string $oldRes = `optionMenu -q -value $fromMenu`;
	string $newRes = `optionMenu -q -value $toMenu`;

	string $objs[] = `ls ("*" + $oldRes + "*")`;

	for ($item in $objs)
		if ((`objExists $item`) && (`nodeType $item` != "mesh") && (`nodeType $item` != "nurbsSurface"))
		{
			string $new = `substitute $oldRes $item $newRes`;

			string $temp = `rename $item $new`;
			print("old: " + $item + "\t\tnew: " + $temp + "\n");

			// verify that rename is OK

			string $tokens[];
			
			if (`tokenize $temp "|" $tokens` > 0)
			{
				$temp = $tokens[size($tokens) - 1];

				if ($temp != $new)
				   warning($item + " is renamed to " + $temp + " instead of " + $new + "\n");
			}
		}
}

global proc kcRenameResolutionTokenMain()
{
	if (!`window -exists kcRenameResolutionTokenWnd`)
	{
		window -menuBar true -title "Rename Resolution Token" -resizeToFitChildren true kcRenameResolutionTokenWnd;

			menu -label "Help";
				menuItem -label "Help" -command "zjHelpCallback kcRenameResolutionToken";

			columnLayout -rowSpacing 10 -adjustableColumn true;

				rowLayout -numberOfColumns 2 -columnWidth2 130 130;
		 
					string $fromMenu = `optionMenu -label "From:"`;
						menuItem -label "_lo_";
						menuItem -label "_md_";
						menuItem -label "_hi_";

					optionMenu -e -select 1 $fromMenu;
							 
					string $toMenu = `optionMenu -label "To:"`;
						menuItem -label "_lo_";
						menuItem -label "_md_";
						menuItem -label "_hi_";

					optionMenu -e -select 2 $toMenu;

				setParent ..;

				button -label "Rename" -command ("kcRenameResolutionToken " + $fromMenu + " " + $toMenu);

			setParent ..;

		showWindow kcRenameResolutionTokenWnd;
	}
}