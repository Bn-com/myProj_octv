// Managnement of specific objects of scene on Maya The Bee show. Studio100 Paris.
//
// Author: Quentin Auger - quentin.auger@free.fr



		// s100objs_selectNodeFromSelectionNameSpace("Main") // "root_ctrl"
		//
		global proc int s100objs_selectNodeFromSelectionNameSpace(string $nodeShortName) // string $nodeShortName="Main" "root_ctrl"
		{
			string $sel[] = `ls -sl`;
			if (size($sel)==0)
			{
				catch(`error ("Nothing selected!")`);
				return -1;
            }

			// now selects:
			string $toSel[] = s100objs_returnNodesFromSelectionNameSpace($nodeShortName);
			if (size($toSel)==0)
				print ("// Nothing found to be selected...\n");
			else
			{
				print ("// Selecting following objects : \n");
				print ("// - "+stringArrayToString($toSel,"\n// - ")+"\n// "+size($toSel)+" objects selected.\n");
				select -r $toSel;
				return 1;
            }
			return 0;
		}

		// s100objs_returnNodesFromSelectionNameSpace("Main") // "root_ctrl"
		//
		global proc string[] s100objs_returnNodesFromSelectionNameSpace(string $nodeShortName)
		{
			string $nodes[] = {};

			string $sel[] = `ls -sl`;

			// first finding namespaces:
			string $nmspcs[] = s100returnNameSpacesFromObjList($sel);

			// now finds:
			for ($nmspc in $nmspcs)
			{
				string $node = ($nmspc+$nodeShortName);
				if (`objExists $node`)
					$nodes[size($nodes)] = $node;
				else
					catch(`error ("No node \""+$node+"\" found in scene!")`);
			}

			return $nodes;
		}

		// s100returnNameSpacesFromObjList(`ls -sl`)
		//
		global proc string[] s100returnNameSpacesFromObjList(string $list[])
		{
			string $nmspcs[] = {};
			for ($i in $list) // string $i=$list[0]
			{
				string $nmspc =  `substitute "[^:]*$"  $i "" `;
				if (!stringArrayContains($nmspc,$nmspcs))
					$nmspcs[size($nmspcs)] = $nmspc;
			}
			stringArrayRemoveDuplicates($nmspcs);
			return $nmspcs;
        }