global proc s100scriptNodeCleaner()
{
	string $scripts[] = `ls -type script`;
	int $ok = 0;
	string $todel[] = {};
	for ($sc in $scripts)  // $sc  = $scripts[0]
	{
		if ( gmatch($sc, "*ConfigurationScriptNode") )
			print ("// '"+$sc+"' is a configuration node, skipped \n");
		else
		{
			string $bef = `getAttr ($sc+".before")`;
			string $aft = `getAttr ($sc+".after")`;
			if (size($bef)==0 && size($aft)==0 )
			{
				print ("// '"+$sc+"' is empty, should be deleted \n");
				$ok += !catch(`delete $sc`);
			}
			else if ( gmatch($bef, "*s100ChangeTxtForPerso*") || gmatch($aft , "*s100ChangeTxtForPerso*") )
			{
				print ("// '"+$sc+"' contains 's100ChangeTxtForPerso', should be deleted ?\n");
				print ("//\n// - Its BEFORE script : \n");
				print $bef;
				print ("\n//\n// - Its AFTER script : \n");
				print $aft;
				print "//\n//\n";
				$todel[size($todel)] = $sc;
			}
			else
				print ("// '"+$sc+"' is safe, kipped. \n");
		}
	}
	if (size($todel))
	{
		select -r $todel;
		print ("// ScriptNode(s) containing 's100ChangeTxtForPerso' in scene :\n");
		print $todel;
		print ("// "+size($todel)+" scriptNode(s) containing 's100ChangeTxtForPerso' selected to check (see list above in scriptEditor)...\n");
	}
	else
		print ("// "+size($todel)+" scriptNode(s) containing 's100ChangeTxtForPerso' found.\n");
}