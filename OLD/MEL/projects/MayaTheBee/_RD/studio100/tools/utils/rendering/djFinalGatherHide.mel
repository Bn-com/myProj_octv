////////////////////////////////////////////////////////////////////////////////////////////
// djFinalGatherHide.mel		
//
// author: David Johnson
// contact: david@djx.com.au
// website: www.djx.com.au
// last rev: 2 March 2009
// version: 1.0
//
// When a surface has its miFinalGatherHide attribute is set to 1, then it will not block final gather rays.
// djFinalGatherHide.mel makes it easier to add and set the attribute for many surfaces at once.
// Works on heirarchies. Ignores anything that is not a poly or nurbs or subdiv surface.
//
//
// USAGE:	Select one or more objects or groups and run the script.
//			An extra attribute called miFinalGatherHide
//			will be added to every surface found in the selected heirarchy
//			and its value set to 1, which stops those surfaces blocking final gather rays.
//
////////////////////////////////////////////////////////////////////////////////////////////

global proc djFinalGatherHide() {

	// validate and store selection
	string $sel[] = `ls -sl -fl`;
	if(!size($sel)) {
		warning("djFinalGatherHide: Nothing selected so nothing was done. Select at least one object or group, then try again.");
		return;
	}
	
	string $shapesChanged[];	// keep a list of shapes that we change

	for ($s in $sel) {
		// get all the shape nodes in the heirarchy
		//
		string $surf[] = `ls -dag -s $s`;
		
		for ($ss in $surf) {
			// ignore anything that is not mesh or nurbsSurface or subdiv
			//
			string $surfType = `nodeType $ss`;
			if($surfType=="mesh" || $surfType=="nurbsSurface" || $surfType=="subdiv") {
				appendStringArray($shapesChanged, {$ss}, 1);
				
				// add the miFinalGatherHide attribute if it doesnt already exist
				if (!`attributeExists "miFinalGatherHide" $ss`) {
					addAttr -ln "miFinalGatherHide"  -at bool  $ss;
					setAttr -e-keyable true ($ss + ".miFinalGatherHide");
				}
				setAttr ($ss + ".miFinalGatherHide") 1;
			}
		}
	}
	
	// report changes
	if (size($shapesChanged)) {
		print("\ndjFinalGatherHide: The following surfaces now have miFinalGatherHide enabled:\n\n");
		for($ss in $shapesChanged) {
			print("    "+$ss+"\n");
		}
	}
	else {
		warning("djFinalGatherHide: Selected heirarchy contained no poly, nurb or subDs, so nothing was done."); 
	}
}