// VERSIONE MODIFICATA PER 0.6


global proc vector cMakerAlignment(int $particleCount, int $currentId, float $visibilityRadius) {
  global matrix $matrixDistance[1001][1001];   // 1001 è il limite max di particelle che posso simulare.
  global vector $particleVelocity[];
  global vector $particlePosition[];
  vector $steeringForce= <<0, 0, 0>>;
  vector $velocitySum= <<0, 0, 0>>;
  vector $desiredVelocity= <<0, 0, 0>>;
  int $i, $tot= 0;

  for ($i= 0; $i< $particleCount; $i++) {
    if ($i != $currentId) {     // Non devo considerare la particella in questione.
      if ($matrixDistance[$currentId][$i] <= $visibilityRadius) {
        $velocitySum += $particleVelocity[$i];
        $tot++;  
      }
    }    
  }

  if ($tot != 0) {
    $desiredVelocity= $velocitySum / $tot;
    $steeringForce= $desiredVelocity - $particleVelocity[$currentId];
  }

  if (mag($steeringForce) > 1) $steeringForce= unit($steeringForce);
  return $steeringForce;
}
