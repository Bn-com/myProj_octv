global proc s100EXPUI()
{
	if(`window -exists s100EXPUI`)
		deleteUI s100EXPUI ;

	window    -title "Previz Manager" s100EXPUI ;
	s100EXPMenuUI;
	s100EXPFormLayout;
	window -e  -widthHeight 250 440 -sizeable 0    s100EXPUI;
	showWindow;
	s100EXPUIRefresh;
}


global proc s100EXPMenuUI()
{
	menuBarLayout s100EXPprodForReadCSVUIMLMain;
	menu -label "Help" -helpMenu true;
	menuItem -c s100EXPUI -label "refresh";
	  //  menuItem -c s100PRCCheckIfAssetExistsIn3dBankFromACSVFileUI -label "Check if Assets Exists";

    setParent ..;
}


global proc s100EXPFormLayout()
{
	$heigh = 440 ;
	$width = 250 ;
	int $text ;
	int $textConvert ;

	$form = `formLayout  -p s100EXPUI s100PRCFormLayout `;

	$text = `optionVar -iv  "s100playbackMaxTextured" 1`;
	$textConvert = `optionVar -iv  "s100playbackMaxTexturedForceConvertion" 1`;

	//shoots
	$textShot = `text -l "shots : " `;
	int $hT = $heigh * .8 ;
	int $wT = $width * .4 ;

	int $wB = $width * .45 ;

	$textF = `textScrollList -selectCommand s100EXPwhenShotAreSelected -allowMultiSelection 1 -h $hT  -w $wT   -p $form  s100EXPFormLayoutTSL`;

	//playblast
	$textPlayblast = `text -l "playblast :"`;
	$butPP = `button -en 0  -w $wB -l "Playblast Sequence" -ann "playblast all the previz scene and create one file"
		-c s100EXPplayblastPreviz s100EXPplayblastPrevizBt  `;
	$butPA = `button -en 0 -w $wB -l "Playblast All Shots" -ann "playblast all the previz scene and create one file by shots located in \"playblast\""
		-c s100EXPplayblastAllShots s100EXPplayblastAllShotsBt ` ;
	$butPS = `button -en 0 -w $wB -l "Playblast Selected" -ann "playblast selected shots and create one file by shots located in \"playblast\""
		-c s100EXPplayblastSelectedShots s100EXPplayblastSelectedShotsBt `;


	$chTextured = `checkBox -v 1 -ann "chek if off to ignore the viewport overide" -label "Textured" -changeCommand  s100EXPChTxCg  s100EXPChTx `;
	$chConvert = `checkBox -v 1 -ann "this will convert all textured to low, process will be much more faster"  -label "convert Textured to low" -changeCommand  s100EXPChTxConvCg  s100EXPChTxConv `;
	$displayLight = `checkBox -v 0 -label "Use all lights"  s100EXPDisplayLight `;

	$sp = `separator -w $wB` ;

	//export
	$textETo = `text -l "export To:" `;
	$optionETo = `optionMenu -en 0 s100PRCFormLayoutOpM`;

	$butESq  = `button -en 0 -w $wB -l "Export Sequence" -ann "export all the previz scene to the written department as tk00" -c  s100FMExportPrevizToLayout s100EXPExpAllShotsBt ` ;
	$butESh = `button -en 0 -w $wB -l "Export selected" -ann "export selected shoot to the written department" -c s100EXPExpSelectedShots s100EXPExpSelectedShotsBt `;

	$butESqDl  = `button  -w $wB -l "Export Seq.->deadline" -ann "export all the previz scene to the written department as tk00 by deadline" -c  s100FM_export_script_empty s100EXPExpAllShotsBtDl` ;
	$butEShDl = `button  -w $wB -l "Exprt selctd->deadline" -ann "export selected shoot to the written department by deadline" -c s100EXPExpSelectedShots_deadline s100EXPExpSelectedShotsBtDl`;

	//info
	$textInfo = `text -l "" -p $form  s100EXPFormLayoutTInfo`;

	formLayout -e
		-attachForm $textShot "top" 10
		-attachForm $textShot "left" 5

		-attachForm $textF "left" 5
		-attachControl  $textF "top" 5 $textShot

	//playblast
		-attachForm  $textPlayblast  "top" 10
		-attachControl  $textPlayblast "left" 10 $textF

		-attachControl  $butPP "top" 0 $textPlayblast
		-attachControl  $butPP "left" 10 $textF

		-attachControl  $butPA "top" 0 $butPP
		-attachControl  $butPA "left" 10 $textF

		-attachControl  $butPS "top" 0 $butPA
		-attachControl  $butPS "left" 10 $textF

		-attachControl  $chTextured "top" 0 $butPS
		-attachControl  $chTextured "left" 10 $textF
		-attachControl  $chConvert "top" 0 $chTextured
		-attachControl  $chConvert "left" 10 $textF

		-attachControl  $displayLight "top" 0 $chConvert
		-attachControl  $displayLight "left" 10 $textF

		-attachControl  $sp "top" 20 $displayLight
		-attachControl  $sp "left" 10 $textF


	//export
		-attachControl  $textETo  "top" 15 $sp
		-attachControl  $textETo "left" 10 $textF

		-attachControl  $optionETo "top" 0 $textETo
		-attachControl  $optionETo "left" 10 $textF

		-attachControl  $butESq "top" 10 $optionETo
		-attachControl  $butESq "left" 10 $textF
		-attachControl  $butESh "top" 10 $butESq
		-attachControl  $butESh "left" 10 $textF

		-attachControl  $butESqDl "top" 10 $butESh
		-attachControl  $butESqDl "left" 10 $textF
		-attachControl  $butEShDl "top" 10 $butESqDl
		-attachControl  $butEShDl "left" 10 $textF

		-attachControl  $textInfo "top" 5 $textF

	s100PRCFormLayout;
}

global proc s100EXPUIRefresh()
{
	textScrollList -e -removeAll  s100EXPFormLayoutTSL ;
	if (`s100FMExportPrevizToLayoutCheck`)
	{
		print "\nscene is conform";
		$file = `file -q -sn `;
		string $scs[] = `s100EXPReturnShootBySeqFromFile($file) `;
		if(size($scs) > 0)
		{
			for($i = 0 ; $i < size($scs) ; $i++)
			{
				textScrollList -e -append $scs[$i]   s100EXPFormLayoutTSL ;
            }
			s100EXPrefreshStep($file);

			button -e -en 1 s100EXPplayblastPrevizBt ;
			button -e -en 1 s100EXPplayblastAllShotsBt  ;

			//optionMenu -en 0 s100PRCFormLayoutOpM;
			button -e -en 1 s100EXPExpAllShotsBt  ;
        }
		else
		{
			warning "no shoots Folder matching" ;
        }
    }
	else
	{
		warning "scene is not conform" ;
    }
}



global proc string[] s100EXPReturnShootBySeqFromFile(string $file)
{
	string $return[] ;
	$fold  = `s100returnSequenceFolder($file ) `;
	$folderInside = `s1KeepOnlyFolder($fold) `;
	$j = 0 ;
	for($i = 0 ; $i < size($folderInside) ; $i++)
	{
		if(!`gmatch $folderInside[$i] "*_PREVIZ" ` && !`gmatch $folderInside[$i] "*info" ` )
		{
			$arr = stringToStringArray($folderInside[$i] ,"/" ) ;
			$return[$j] = $arr[(size($arr) -1 ) ] ;
			$j++;
        }
    }
	return $return ;
}


global proc string[] s100EXPreturnStep(string $file)
{
	$fold  = `s100returnSequenceFolder($file ) `;
	string $shots[] = `s100EXPReturnShootBySeqFromFile($file) `;
	string $step[] ;
	if(size($shots)>0)
	{
		$folderInside = `s1KeepOnlyFolder($fold + $shots[0] +"/") `;
		$j = 0 ;
		for($i = 0 ; $i < size($folderInside) ; $i++)
		{
  			$arrTmp = stringToStringArray($folderInside[$i] ,"/") ;

			if(`gmatch ($arrTmp[size($arrTmp) -1])  "_*"`)
			{

            }
			else
			{
  				$arrTmp2 = stringToStringArray($arrTmp[size($arrTmp) -1] ,"_") ;
				$step[$j] = $arrTmp2[size($arrTmp2) -1];
				$j++;
            }
        }
	}
	else
	{
		warning "no folder list for Refresh Step";
    }
	return $step ;
}



global proc s100EXPrefreshStep(string $file)
{
	string $step[] = s100EXPreturnStep($file);
	string $temp[] = `optionMenu -q -itemListLong s100PRCFormLayoutOpM`;
	for($tmp in $temp )
	{
		deleteUI -menuItem $tmp;
    }
	for($tmp in $step )
	{
		optionMenu -e -en 1  s100PRCFormLayoutOpM ;
		menuItem -label $tmp -parent s100PRCFormLayoutOpM ;
    }
	$fileT = `file -q -sn -shortName `;
	$suff = stringToStringArray($fileT ,"_" ) ;
	s100EXPModifyStepByProd($suff[0] ) ;
}



global proc s100EXPModifyStepByProd(string $suf)
{
	print $suf ;
	if($suf == "MA")
	{
		optionMenu -e -v "Animation" s100PRCFormLayoutOpM ;
		optionMenu -e -en 0  s100PRCFormLayoutOpM ;
    }
}


global proc s100EXPwhenShotAreSelected()
{
	button -e -en 1 s100EXPplayblastSelectedShotsBt ;
	button -e -en 1 s100EXPExpSelectedShotsBt ;
}


global proc s100EXPChTxCg()
{
	optionVar -iv   "s100playbackMaxTextured" `checkBox -q -v s100EXPChTx` ;
	if(  `checkBox -q -v s100EXPChTx` )
	{
		checkBox -e -en 1 s100EXPChTxConv ;
    }else{
		checkBox -e -en 0 s100EXPChTxConv ;
    }
  	if(`checkBox -q -ex s100PBTChTx`)
	{
		checkBox -e -v `checkBox -q -v s100EXPChTx` s100PBTChTx ;
    }
}


global proc s100EXPChTxConvCg()
{
	optionVar -iv   "s100playbackMaxTexturedForceConvertion" `checkBox -q -v s100EXPChTxConv` ;
  	if(`checkBox -q -ex s100PBTChTxConv`)
	{
		checkBox -e -v `checkBox -q -v s100EXPChTxConv` s100PBTChTxConv;
    }
}

