// VERSIONE MODIFICATA PER 0.6


global proc vector cMakerSeparation(int $particleCount, int $currentId, float $visibilityRadius) {
  global matrix $matrixDistance[1001][1001];   // 1001 è il limite max di particelle che posso simulare. 
  global vector $particlePosition[];
  vector $steeringForce= <<0, 0, 0>>;
  vector $currentPos= $particlePosition[$currentId];
  vector $particlePos, $a;
  float $distance;
  int $i;
 
  for ($i= 0; $i< $particleCount; $i++) {
    if ($i != $currentId) {     // Non devo considerare la particella in questione.
      $particlePos= $particlePosition[$i];
      $distance= $matrixDistance[$currentId][$i];    // Calcolo la distanza tra le due particelle
      if ($distance <= $visibilityRadius) {
        $a= <<$currentPos.x-$particlePos.x, 0, $currentPos.z-$particlePos.z>>;
        $a= unit ($a);
        if ($visibilityRadius != 0) $a= $a * (1- ($distance/ $visibilityRadius));   // $a * (0...1) 
        $steeringForce += $a;
      }
    }  
  }
  if (mag($steeringForce) > 1) $steeringForce= unit($steeringForce);
  return $steeringForce;
}
