////////////////////////////////////////////////////////////////////////////////////////////
// djFinalGatherUnhide.mel		
//
// author: David Johnson
// contact: david@djx.com.au
// website: www.djx.com.au
// last rev: 2 March 2009
// version: 1.0
//
// When a surface has its miFinalGatherHide attribute is set to 1, then it will not block final gather rays.
// djFinalGatherUnhide.mel makes it easier to set the attribute back to zero for many surfaces at once.
// Works on heirarchies. Ignores anything that is not a poly or nurbs or subdiv surface.
//
//
// USAGE:	Select one or more objects or groups and run the script.
//			Every surface found in the selected heirarchy that has the miFinalGatherHide attribute
//			get its value set to 0, which means those surfaces will block final gather rays.
//
////////////////////////////////////////////////////////////////////////////////////////////

global proc djFinalGatherUnhide() {

	// validate and store selection
	string $sel[] = `ls -sl -fl`;
	if(!size($sel)) {
		warning("djFinalGatherUnhide: Nothing selected so nothing was done. Select at least one object or group, then try again.");
		return;
	}
	
	string $shapesChanged[];	// keep a list of shapes that we change

	for ($s in $sel) {
		// get all the shape nodes in the heirarchy
		//
		string $surf[] = `ls -dag -s $s`;
		
		for ($ss in $surf) {
			// ignore anything that is not mesh or nurbsSurface or subdiv
			//
			string $surfType = `nodeType $ss`;
			if($surfType=="mesh" || $surfType=="nurbsSurface" || $surfType=="subdiv") {
				
				if (`attributeExists "miFinalGatherHide" $ss`) {
					appendStringArray($shapesChanged, {$ss}, 1);
					setAttr ($ss + ".miFinalGatherHide") 0;
				}
			}
		}
	}
	
	// report changes
	if (size($shapesChanged)) {
		print("\ndjFinalGatherUnhide: The following surfaces now have miFinalGatherHide disabled:\n\n");
		for($ss in $shapesChanged) {
			print("    "+$ss+"\n");
		}
	}
	else {
		warning("djFinalGatherUnhide: Selected heirarchy contained no surfaces with the miFinalGatherHide attribute, so nothing was done."); 
	}
}