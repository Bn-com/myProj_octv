//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 
//
//	Update/Change: WansHoulOng
//	Last Updated: 
//	Description:
//				
//////////////////////////////////////////////////////////


global proc vvSetProject()
{
	string $project = "";
	if (`optionVar -exists vvSetProjectProject`)
	{
		$project = `optionVar -query vvSetProjectProject`;
	}
	string $disk = "D:";
	if (`optionVar -exists vvSetProjectDisk`)
	{
		$disk = `optionVar -query vvSetProjectDisk`;
	}
	
	
	if (`window -exists vvSetProject`)
	{
		deleteUI vvSetProject;
	}
	window -menuBar true -title "Create/Set Project" -resizeToFitChildren true vvSetProject;
		menu -label "Help";
			menuItem -label "Help on Create/Set Project" -command "showHelp -absolute -docs \"http://talk.idmt.com.cn/redirect.php?goto=findpost&ptid=59290&pid=649739\"";
		columnLayout -adjustableColumn true;
			optionMenuGrp -label "Disk:" -columnAlign 1 "right" optionMenuGrpDisk;
				menuItem -label "D:";
				menuItem -label "E:";
				menuItem -label "G:";
				menuItem -label "M:";
			optionMenuGrp -edit -value $disk optionMenuGrpDisk;
			textFieldGrp -label "Project:" -text $project -changeCommand "zwSetProjectTypeChange" textFieldGrpProject;
				popupMenu -button 1 -postMenuCommand "zwSetProjectMenu \"popupMenuProjectL\" \"textFieldGrpProject\"" popupMenuProjectL;
				popupMenu -button 3 -postMenuCommand "zwSetProjectMenu \"popupMenuProjectR\" \"textFieldGrpProject\"" popupMenuProjectR;
			textFieldGrp -label "EP:" textFieldGrpEP;
			textFieldGrp -label "SQ:" textFieldGrpSQ;
			textFieldGrp -label "SC:" textFieldGrpSC;
			button -label "Create/Set Project" -command "vvSetProjectProc";

	showWindow vvSetProject;

	string $filename = `file -q -sn -shn`;
	if ($filename != "")
	{
		string $buff[] = stringToStringArray($filename,"_");
		$project = zwGetProject($filename);
		string $EP,$SQ,$SC;
		if ($project == "")
		{
			$project = "VickyTheViking";
			$EP = $buff[0];
			$SQ = $buff[1];
			$SC = $buff[2];
		}
		else
		{
			$EP = $buff[1];
			$SQ = $buff[2];
			$SC = $buff[3];
		}
		textFieldGrp -e -tx $project textFieldGrpProject;
		textFieldGrp -e -tx $EP textFieldGrpEP;
		textFieldGrp -e -tx $SQ textFieldGrpSQ;
		textFieldGrp -e -tx $SC textFieldGrpSC;
	}
}

global proc vvSetProjectProc()
{
	string $dest;

	string $disk = `optionMenuGrp -query -value optionMenuGrpDisk`;
	string $project = `textFieldGrp -query -text textFieldGrpProject`;
	$project = strip($project);
	string $EP = `textFieldGrp -query -text textFieldGrpEP`;
	$EP = strip($EP);
	string $SQ = `textFieldGrp -query -text textFieldGrpSQ`;
	$SQ = strip($SQ);
	string $SC = `textFieldGrp -query -text textFieldGrpSC`;
	$SC = strip($SC);
	if ($project == "" || $EP == "" || $SQ == "" || $SC == "")
	{
		return;
	}
	
	$dest = $disk + "/" + $project + "/EP_" + $EP + "/SQ_" + $SQ + "/SC_" + $SC;

	optionVar -stringValue vvSetProjectProject $project;
	optionVar -stringValue vvSetProjectDisk $disk;

	int $explore = false;
	if (!`filetest -f ($dest + "/workspace.mel")`)
	{
		$explore = true;
	}

	string $source = "//file-cluster/GDC/Resource/Support/Maya/Import/projects/" + zwAboutVersion();
	zwXcopyEx($source, $dest, false, false);
	setProject $dest;
	deleteUI vvSetProject;
	if ($explore)
	{
		showHelp -absolute -docs $dest;
	}
}