/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ³ÂÖÙ
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


//-----------------------------------------------------------------------
//
//  Creation Date:  
//		July 10th, 2006
//
//  Procedure Name:
//      czExportChar
//
//  Description:
//
//		This scritp will export all dag nodes that starts
//		with the same character name, as a new file which 
//		will be renamed in the rule of sceneName+charName
//	
//
//  Input Arguments: none
//
//  Return Value: None.
//
//----------------------------------------------------------------------

global proc string[] getRefFileName()
{
	int $i = 0;	
	
	// get the RNgroup list
	string $temp[] = `ls -as "*_ms_c000*"`;
	
	// loop to get the referenced file name list
	for($each in $temp)
	{
		
		$temp[$i++] = `substring $each 1 (size($each)-7)`;		
	}
	return $temp;
	
}

global proc string[] gGetCharNames()
{
	// initialization
	int $i = 0;
	string $charList[], $allChar[], $refNames[];
	
	// get referenced file's name
	$refNames = `getRefFileName`;	
	$allChar = `ls "*_MASTER"`;
	
	// loop to get the character MASTER group list
	for ($this in $refNames)
	{			
		// if the MASTER group's name is started
		// with string in the $refNames array, 
		// and put it back into MASTER group list
		for($i=0;$i<size($allChar);$i++)
		{	
			if(startsWith($allChar[$i], $this))
			{
				$allChar[$i] = `substring $allChar[$i] (size($this)+2) (size($allChar[$i]))`;			
			}
		}
	}
	
	// get character name list
	for ($char in $allChar)
	{
		$charList[size($charList)] = `substring $char 1 (size($char)-7)`;
	}
	
	// remove those repeated
	$charList = stringArrayRemoveDuplicates($charList);
	
	return $charList;
}

global proc string gGetSceneDir()
{
	// initialization
	int $len;
	string $fTarget,$sceneName;
	string $temp[];
	
	// get the scene dir
	string $sceneName = `file -q -sceneName`;
	int $len = `tokenize $sceneName "/" $temp`;
	for($i=0;$i<($len-1);$i++)
	{
		$fTarget+=($temp[$i]+"/");
	}	
	
	return $fTarget;
}

global proc czExportChar()
{
	// initialization
	int $flag = 0;
	string $targetDIR, $currentSN;
	string $buffer[];
	
	// get the current scene name
	$currentSN = `file -q -sn -shn`;	
	$currentSN = `substring $currentSN 1 (size($currentSN)-3)`;	
	
	// get the directory which the export file will be put into
	$targetDIR = `gGetSceneDir`;
	
	// get the character name list
	string $charNames[] = `gGetCharNames`;	
	
	// loop to export selection objects
	for ($this in $charNames)
	{	
		// clear the active list
		select -clear;
		
		// file to be exported
		string $fOutput = $targetDIR+$currentSN+"_"+$this;	
		
		// list all the objects that related to the character
		select -r -ne ("*"+$this+"*_set");
		select -add ("*"+$this+"*");
		
		// export selection
	  	string $fOUt = `file -op "v=0" -typ "mayaBinary" -es $fOutput`;			
		
		print("\n -> Exporting "+$this + " ...... Mission Complete!\n");
	}
	select -clear;
}
