global proc zwToggleP092708ornamentalmall()
{
	string $selections[] = `ls -sl`;
	if (size($selections) != 2)
	{
		error "先选低模树MASTER，再加选高模树MASTER";
	}
	string $source = $selections[0];
	string $dest = $selections[1];

	string $locator[] = `spaceLocator`;
	delete `pointConstraint $dest $locator[0]`;
	delete `orientConstraint $dest $locator[0]`;
//	delete `scaleConstraint $dest $locator[0]`;

	delete `pointConstraint $source $dest`;
	delete `orientConstraint $source $dest`;
//	delete `scaleConstraint $source $dest`;

	delete `pointConstraint $locator[0] $source`;
	delete `orientConstraint $locator[0] $source`;
//	delete `scaleConstraint $locator[0] $source`;

	delete $locator[0];

	string $sourceJoints[] = `ls -dagObjects -transforms $source`;
	string $destJoints[] = `ls -dagObjects -transforms $dest`;

	for ($i=0; $i<size($sourceJoints); $i++)
	{
		string $attrs[] = `listAttr -keyable $destJoints[$i]`;
		for ($attr in $attrs)
		{
			string $connections[] = `listConnections -plugs true -connections true ($destJoints[$i] + "." + $attr)`;
			for ($j=0; $j<size($connections); $j+=2)
			{
				if (`isConnected $connections[$j+1] $connections[$j]`)
				{
					disconnectAttr $connections[$j+1] $connections[$j];
				}
			}

			float $temp = `getAttr ($destJoints[$i] + "." + $attr)`;
			setAttr ($destJoints[$i] + "." + $attr) `getAttr ($sourceJoints[$i] + "." + $attr)`;

			$connections = `listConnections -plugs true -connections true ($sourceJoints[$i] + "." + $attr)`;
			for ($j=0; $j<size($connections); $j+=2)
			{
				if (`isConnected $connections[$j+1] $connections[$j]`)
				{
					disconnectAttr $connections[$j+1] $connections[$j];
				}
				connectAttr $connections[$j+1] ($destJoints[$i] + "." + $attr);
			}

			setAttr ($sourceJoints[$i] + "." + $attr) $temp;
		}
	}
}