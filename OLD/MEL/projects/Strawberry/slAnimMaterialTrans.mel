//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2008-11-26
//
//	Update/Change: WansHoulOng
//	Last Updated: 2008-11-26
//	Description:
//			修正给摄像机添加黑框的时候，部分材质显示透明的情况。
//			添加到zwCustomCamera.mel里面，自动执行。	
//////////////////////////////////////////////////////////

global proc slAnimMaterialTrans(string $type)
{
	string $mats[] = `ls -mat`;
	for ($mat in $mats)
	{
		if (`objExists ($mat+".transparencyR")` )
		{
			string $cons[] = `listConnections ($mat+".transparency")`;
			if ((!`getAttr -l ($mat+".transparencyR")`) && size($cons)==0)
			{
				if ($type == "ON")
				{
					if (!`objExists ($mat+".WHO_transparencyR")` && `objExists ($mat+".transparencyR")` && !`connectionInfo -id ($mat+".transparencyR")`)
					{	
						if ((!(`getAttr ($mat+".transparencyR")` == 0  && `getAttr ($mat+".transparencyG")` == 0  && `getAttr ($mat+".transparencyB")` == 0 ))
						    && (!(`getAttr ($mat+".transparencyR")` == 1  && `getAttr ($mat+".transparencyG")` == 1  && `getAttr ($mat+".transparencyB")` == 1 )))
						{
							addAttr -ln "WHO_transparencyR" -at double  $mat;
							setAttr -e -keyable true ($mat+".WHO_transparencyR");
							
							addAttr -ln "WHO_transparencyG" -at double  $mat;
							setAttr -e -keyable true ($mat+".WHO_transparencyG");
							
							addAttr -ln "WHO_transparencyB" -at double  $mat;
							setAttr -e -keyable true ($mat+".WHO_transparencyB");

							float $tr = `getAttr ($mat+".transparencyR")`;
							setAttr ($mat+".WHO_transparencyR") $tr;
							setAttr ($mat+".transparencyR") 0;

							float $tg = `getAttr ($mat+".transparencyG")`;
							setAttr ($mat+".WHO_transparencyG") $tg;
							setAttr ($mat+".transparencyG") 0;

							float $tb = `getAttr ($mat+".transparencyB")`;
							setAttr ($mat+".WHO_transparencyB") $tb;
							setAttr ($mat+".transparencyB") 0;
						}
					}
					else if (`objExists ($mat+".WHO_transparencyR")` )
					{
						float $tr = `getAttr ($mat+".transparencyR")`;
						setAttr ($mat+".WHO_transparencyR") $tr;
						setAttr ($mat+".transparencyR") 0;

						float $tg = `getAttr ($mat+".transparencyG")`;
						setAttr ($mat+".WHO_transparencyG") $tg;
						setAttr ($mat+".transparencyG") 0;

						float $tb = `getAttr ($mat+".transparencyB")`;
						setAttr ($mat+".WHO_transparencyB") $tb;
						setAttr ($mat+".transparencyB") 0;
					}
				}
				else if ($type == "OFF")
				{
					if (`objExists ($mat+".WHO_transparencyR")`)
					{
						float $tr = `getAttr ($mat+".WHO_transparencyR")`;
						setAttr ($mat+".transparencyR") $tr;

						float $tg = `getAttr ($mat+".WHO_transparencyG")`;
						setAttr ($mat+".transparencyG") $tg;

						float $tb = `getAttr ($mat+".WHO_transparencyB")`;
						setAttr ($mat+".transparencyB") $tb;
					}
				}
			}
		}
	}
}