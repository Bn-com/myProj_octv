//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WanShouLong
//	Created: 2008-10-23
//
//	Update/Change: WansHoulOng
//	Last Updated: 2008-10-23
//	Description:
//				
//////////////////////////////////////////////////////////

global proc skAnimCameraSoundTools()
{
	string $buff[];
	string $fileName = `file -q -shn -sceneName`;
	tokenize $fileName "_" $buff;
	string $getEpisodeNum = $buff[1];//集数
	string $getSceneNum = $buff[2];//镜头
	
	
	//建立对应镜头号的摄像机
	string $cam = "cam_"+$getEpisodeNum+"_"+$getSceneNum;
	if (!`objExists $cam`)
	{
		string $newcam[] = `camera`;
		rename -is $newcam[0] $cam;
		rename $newcam[1] ($cam+"Shape");
	}
	camera -edit  -nearClipPlane 0.1 -farClipPlane 100000 -displayResolution true -displaySafeAction true -displaySafeTitle true $cam;
	setAttr ($cam+"Shape.renderable") 1 ;

	//给摄像机增加电视框
	string $cc[] = `ls -tr "*camclip"`;
	if(size($cc)) delete $cc;
	string $cs[] = `ls "camclipShader"`;
	if(size($cs)) delete $cs;
	file -import -type "mayaBinary" -rpr "camclip" -options "v=0"  -pr (`getenv "DEV_PATH"` + "/Maya/Import/camclip_sk.mb");
	string $hjcc[] = `ls -tr "*camclip"`;
	string $jayepointposition[]=`pointConstraint  -offset 0 0 0 -weight 1 $cam $hjcc[0] `;
	string $jayepointrotition[]=`orientConstraint  -offset 0 0 0 -weight 1  $cam $hjcc[0] `;
	select -r $jayepointposition[0] $jayepointrotition[0] ;
	delete;
	parent $hjcc[0] $cam;
	string $hjset="string $fo=`lookThru -q`;\r\nfloat $fovalue=`getAttr ($fo+\".focalLength\")`;"+$hjcc[0]+".translateZ=-0.00286154*($fovalue-35.0);";
	expression -s $hjset -o $hjcc[0] -ae 1 -uc all ;
		
	
	//导入镜头对应的声音
	source "ezLoadSoundTrackMainLayout.mel";ezLoadSoundTrack;


	//根据数据库设置时间栏
	int $times[] = `idmtProject -timeLine -echo on`;
	playbackOptions -minTime $times[0] -maxTime $times[1];
}