//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 
//
//	Update/Change: WansHoulOng
//	Last Updated: 
//	Description:
//				
//////////////////////////////////////////////////////////

global proc slRenderAddPlant()
{
	if (`window -ex slRenderAddPlantWin`)
		deleteUI slRenderAddPlantWin;
	
	window -title "Add Plant" -resizeToFitChildren true slRenderAddPlantWin;
		columnLayout -rs 10;

			button -l "创建Locators" -w 300 -c "slRenderAddPlantCreatLoc";

			frameLayout -l "fset" -cll false ;
				string $form = `formLayout`;
					string $textsl = `textScrollList -ams true -w 200 slRenderAddPlantWin_plants`;
					textScrollList -e -sc "select -r (`textScrollList -q -si slRenderAddPlantWin_plants`)" slRenderAddPlantWin_plants;
					string $column = `columnLayout -rs 5 `;
						button -l "添加植物"	-w 80 -c "slRenderAddPlantAddSelectPlant";
						button -l "删除植物"	-w 80 -c "slRenderAddPlantRemovePlant";
					setParent ..;
				setParent ..;
				formLayout -edit
					-af	$textsl	"top"		5
					-af	$textsl	"left"		5
					-ac	$textsl	"right"		5	$column
					-af	$textsl	"bottom"	5

					-af	$column		"top"		5
					-af	$column	 	"right"		5
					-af	$column		"bottom"	5
				$form;
			setParent ..;
		
		button -l "替换植物" -w 300 -c "slRenderAddPlantReplaceLoc";
		setParent ..;
	showWindow slRenderAddPlantWin;
}

global proc slRenderAddPlantCreatLoc()
{

	string $vcs[] = `filterExpand -sm 28 -sm 31`;
	if (size($vcs)==0)
		error "please select vertexes.";
	string $group = "WHO_locator_group";
	if (!`objExists $group`)
		createNode transform -name $group;
	
	for ($item in $vcs)
	{
		float $pos[] = `pointPosition -world $item`;

		string $locator[] = `spaceLocator -position 0 0 0`;
		xform -worldSpace -translation $pos[0] $pos[1] $pos[2] $locator[0];
		parent $locator[0] $group;
	}

	select -cl; 
}

global proc slRenderAddPlantAddSelectPlant()
{
	string $select[] = `ls -sl`;
	for ($item in $select)
	{
		textScrollList -e -append $item slRenderAddPlantWin_plants;
	}
}

global proc slRenderAddPlantRemovePlant()
{
	string $select[] = `textScrollList -q -si slRenderAddPlantWin_plants`;
	for ($item in $select)
	{
		textScrollList -e -ri $item slRenderAddPlantWin_plants;
	}
}

global proc slRenderAddPlantReplaceLoc()
{
	string $replace[];
	
	string $select[] = `ls -sl`;
	string $plants[] = `textScrollList -q -si slRenderAddPlantWin_plants`;
	if (size($plants) == 0 )
		$replace = $select;
	else 
		$replace = $plants;

	if (size($replace) == 0)
		error "请选择替换的植物!";

	
	string $loctors[] = `listRelatives -c -f WHO_locator_group`;
	for ($loc in $loctors)
	{
		int $index = rand(0,(size($replace)));

		string $obj[] = `instance $replace[$index]`;
		string $cons[] = `parentConstraint  -weight 1 $loc $obj[0]`;
		delete $cons;
		parent $obj[0] $loc;
	}
}
