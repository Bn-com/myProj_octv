//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2010-8-17
//
//	Update/Change: WansHoulOng
//	Last Updated: 2010-8-17
//	Description:    修复参考相关的名字问题，如参考节点名字，参考前缀，参考的组名等
//				
//////////////////////////////////////////////////////////

global proc slRepairReferenceNodeName()
{
	int $reexecute = 0;
	
	string $refFiles[] = `file -q -r`;
	for ($file in $refFiles)
	{
		//忽略相机
		if (gmatch($file,"*/episode_camera/*"))
		{
			continue;
		}
		
		string $buff[] = stringToStringArray($file,"/\\");
		$buff = stringToStringArray($buff[size($buff)-1],"_");
		string $charName = $buff[0]+"_"+$buff[1];
		
		string $num="";
		clear $buff;
		$buff = stringToStringArray($file,"{}");
		if (size($buff)>1)
		{
			$num = "_"+$buff[1];
		}
		
		//参考节点名字，出现在reference editor
		string $refNode = `referenceQuery -rfn $file`;
		if ($refNode != ($charName+$num+"RN"))
		{
			lockNode -l false $refNode;
			string $newNode = `rename $refNode ($charName+$num+"RN")`;
			lockNode -l true $newNode;
		}

		//参考的组名，出现在outline
		string $refNode = `referenceQuery -rfn $file`;
		string $refGroups[] = `listConnections ($refNode+".message")`;
		if (size($refGroups))
		{
			//print ($refNode+"-----"+$refGroups[0]+"\n");
			rename $refGroups[0] ($refNode+"Group");
		}

		//参考的前缀名，出现在reference editor和outliene
		string $nameSpace = `file -q -ns $file`;
		if ($nameSpace != ($charName+$num))
		{
			if (`namespace -ex ($charName+$num)`)
			{
				file -e -ns ($charName+$num+"temp") $file;
				$reexecute = 1;
			}
			else
			{
				file -e -ns ($charName+$num) $file;
			}
		}
	}

	//修复前缀名，如果有占用，就修改为临时，然后重复一次
	if ($reexecute)
	{
		slRepairReferenceNodeName;
	}
}