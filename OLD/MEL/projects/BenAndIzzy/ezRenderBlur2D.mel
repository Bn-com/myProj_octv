global proc ezRenderBlur2D()
{
	ezBlur2DcleanTemp;

	if (!`optionVar -exists "ezBlur2DLength"`)
		optionVar -floatValue "ezBlur2DLength" 1;
	if (!`optionVar -exists "ezBlur2DSharpness"`)
		optionVar -floatValue "ezBlur2DSharpness" 1;
	if (!`optionVar -exists "ezBlur2DColor"`)
		optionVar -intValue "ezBlur2DColor" 0;
	if (!`optionVar -exists "ezBlur2DSmooth"`)
		optionVar -intValue "ezBlur2DSmooth" 2;
	if (!`optionVar -exists "ezBlur2DMemory"`)
		optionVar -intValue "ezBlur2DMemory" 200;
	if (!`optionVar -exists "ezBlur2DFolder"`)
		optionVar -stringValue "ezBlur2DFolder" "";
	if (!`optionVar -exists "ezBlur2Dformat"`)
		optionVar -intValue "ezBlur2Dformat" 0;

	if (`window -exists ezRenderBlur2D`)
		deleteUI ezRenderBlur2D;
	window -menuBar true -width 665 -height 500 -title "Render Blur2D Tool" -resizeToFitChildren true ezRenderBlur2D;
		menu -label "Edit";
			menuItem -label "Add Blur" -command "ezBlur2DAdd";
			menuItem -label "Reset Setings" -command "ezBlur2DReset";
		menu -label "Help";
			menuItem -label "Help on Blur2D Tool..." -command "zjHelpCallback ezBlur2D";
		string $formLayout = `formLayout`;
			string $tabLayout = `tabLayout -childResizable true -scrollable true -tabsVisible false tabLayout_blur2d`;
				columnLayout -adjustableColumn true ;
					separator -style "none";
					columnLayout -adjustableColumn true colum_blur2d;
						rowLayout -numberOfColumns 3 -adjustableColumn 1 -columnWidth 1 370 -columnWidth 2 120 -columnWidth 3 120 ;
							columnLayout -adjustableColumn true ;
								rowLayout -numberOfColumns 3 -adjustableColumn 2 -columnWidth3 140 200 30  -columnAttach 1 "right" 8;
									text -label "RGBA File";
									string $textField1 = `textField -editable false ("rgbablur1")`;
									string $symbolButton1 = `symbolButton -image "navButtonBrowse.xpm"`;
									//symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField1 + "\"") $symbolButton1;
									setParent..;
								rowLayout -numberOfColumns 3 -adjustableColumn 2 -columnWidth3 140 200 30 -columnAttach 1 "right" 8;
									text -label "Vector File";
									string $textField2 = `textField -editable false ("vctorblur1")`;
									string $symbolButton2 = `symbolButton -image "navButtonBrowse.xpm"`;
									//symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField2 + "\"") $symbolButton2;
									setParent..;
								rowLayout -numberOfColumns 3 -adjustableColumn 2 -columnWidth3 140 200 30 -columnAttach 1 "right" 8;
									text -label "Ouput Folder";
									string $textField3 = `textField -editable true ("folderblur1")`;
									string $symbolButton3 = `symbolButton -image "navButtonBrowse.xpm"`;
									symbolButton -edit -command ("string $folder = `idmtFolderDialog`;textField -edit -fileName $folder \"" +$textField3+ "\"") $symbolButton3;
									setParent..;
								setParent ..;

								string $dev_path = `getenv "DEV_PATH"`;
								string $image1 = `image -width 106 -height 60 -image ($dev_path + "/Maya/Icons/blank.bmp")`;
								string $image2 = `image -width 106 -height 60 -image ($dev_path + "/Maya/Icons/blank.bmp")`;
								setParent ..;
							setParent ..;
						separator ;
					//setParent ..;
					floatSliderGrp
						-label "Blur Length"
						-field true
						-precision 3
						-value `optionVar -query "ezBlur2DLength"`
						-minValue 0
						-maxValue 30
						-changeCommand "optionVar -floatValue \"ezBlur2DLength\" #1";
					floatSliderGrp
						-label "Blur Sharpness"
						-field true
						-precision 3
						-value `optionVar -query "ezBlur2DSharpness"`
						-minValue 0
						-maxValue 15
						-changeCommand "optionVar -floatValue \"ezBlur2DSharpness\" #1";
					radioButtonGrp -label "Smooth"
						-numberOfRadioButtons 2
						-label1 "Alpha"
						-label2 "Color"
						-select (`optionVar -query "ezBlur2DColor"` + 1)
						-onCommand1 "optionVar -intValue \"ezBlur2DColor\" 0"
						-onCommand2 "optionVar -intValue \"ezBlur2DColor\" 1";
					intFieldGrp
						-label "Smooth Value"
						-value1 `optionVar -query "ezBlur2DSmooth"`
						-changeCommand "optionVar -intValue \"ezBlur2DSmooth\" #1";
					intFieldGrp
						-label "2d Blur Memory Limit"  
						-extraLabel "MB"
						-columnAlign 3 "left"
						-value1 `optionVar -query "ezBlur2DMemory"`
						-changeCommand "optionVar -intValue \"ezBlur2DMemory\" #1";
					radioButtonGrp -label "Save As"
						-numberOfRadioButtons 3
						-label1 "32 bit TGA"
						-label2 "24 bit TGA"
						-label3 "TIFF"
						-select (`optionVar -query "ezBlur2Dformat"` + 2)
						-onCommand1 "optionVar -intValue \"ezBlur2Dformat\" 0"
						-onCommand2 "optionVar -intValue \"ezBlur2Dformat\" 1"
						-onCommand3 "optionVar -intValue \"ezBlur2Dformat\" 2";
					setParent..;
				setParent..;
			string $button3=`button -label "Save Batch Blur..." -command "ezBlur2DbatchBlur"`;
			string $button5=`button -label "Close"	-command "ezBlur2DcleanTemp;\ndeleteUI ezRenderBlur2D;"`;
			setParent..;
		formLayout -edit
			-attachNone	$button3	"top"
			-attachForm	$button3	"left"		5
			-attachForm	$button3	"bottom"	5
			-attachPosition	$button3	"right"		5	50

			-attachNone	$button5	"top"
			-attachControl	$button5	"left"		5	$button3
			-attachForm	$button5	"bottom"	5
			-attachForm	$button5	"right"		5

			-attachForm	$tabLayout	"top"		0
			-attachForm	$tabLayout	"left"		0
			-attachControl	$tabLayout	"bottom"	5	$button5
			-attachForm	$tabLayout	"right"		0
		$formLayout;
		setParent..;

	symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField1 + "\" \""+$image1+"\"") $symbolButton1;
	symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField2 + "\" \""+$image2+"\"") $symbolButton2;
	window -edit -h 300 -w 650 ezRenderBlur2D;
	showWindow ezRenderBlur2D;
}

global proc  ezBlur2DAdd()
{
		for ($i=1;$i>0 ;$i++ )
		{
			if (!`textField -ex ("rgbablur"+$i)`)
			{
				setParent colum_blur2d;
				rowLayout -numberOfColumns 3 -adjustableColumn 1 -columnWidth 1 370 -columnWidth 2 120 -columnWidth 3 120 ;
					columnLayout -adjustableColumn true ;
						rowLayout -numberOfColumns 3 -adjustableColumn 2 -columnWidth3 140 200 30 -columnAttach 1 "right" 8;
							text -label "RGBA File";
							string $textField1 = `textField -editable false ("rgbablur"+$i)`;
							string $symbolButton1 = `symbolButton -image "navButtonBrowse.xpm"`;
							//symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField1 + "\"") $symbolButton1;
							setParent..;
						rowLayout -numberOfColumns 3 -adjustableColumn 2 -columnWidth3 140 200 30 -columnAttach 1 "right" 8;
							text -label "Vector File";
							string $textField2 = `textField -editable false ("vctorblur"+$i)`;
							string $symbolButton2 = `symbolButton -image "navButtonBrowse.xpm"`;
							//symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField2 + "\"") $symbolButton2;
							setParent..;
						rowLayout -numberOfColumns 3 -adjustableColumn 2 -columnWidth3 140 200 30 -columnAttach 1 "right" 8;
							text -label "Ouput Folder";
							string $textField3 = `textField -editable true ("folderblur"+$i)`;
							string $symbolButton3 = `symbolButton -image "navButtonBrowse.xpm"`;
							symbolButton -edit -command ("string $folder = `idmtFolderDialog`;textField -edit -fileName $folder \"" +$textField3+ "\"") $symbolButton3;
							setParent..;
						setParent ..;
						string $dev_path = `getenv "DEV_PATH"`;
						string $image1 = `image -width 106 -height 60 -image ($dev_path + "/Maya/Icons/blank.bmp")`;
						string $image2 = `image -width 106 -height 60 -image ($dev_path + "/Maya/Icons/blank.bmp")`;
						setParent ..;
				symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField1 + "\" \""+$image1+"\"") $symbolButton1;
				symbolButton -edit -command ("ezBlur2DBrowser \"" + $textField2 + "\" \""+$image2+"\"") $symbolButton2;
				separator;
				break;
			}
		}
		
}

global proc ezBlur2DReset()
{
	ezBlur2DcleanTemp;

	optionVar -floatValue "ezBlur2DLength" 1;
	optionVar -floatValue "ezBlur2DSharpness" 1;
	optionVar -intValue "ezBlur2DColor" 0;
	optionVar -intValue "ezBlur2DSmooth" 2;
	optionVar -intValue "ezBlur2DMemory" 200;
	optionVar -stringValue "ezBlur2DFolder" "";
	optionVar -intValue "ezBlur2Dformat" 0;

	ezRenderBlur2D;
}

global proc ezBlur2DcleanTemp()
{
	string $temp = `internalVar -userTmpDir` + "blur";
	string $cmd = "rd /s/q " + `substituteAllString $temp "/" "\\"`;
	system $cmd;
}

global proc ezBlur2DBrowser(string $textField,string $image)
{
	fileBrowserDialog -mode 0 -fileCommand ("ezBlur2DBrowserCommand \"" + $textField + "\" \""+$image+"\"") -fileType "image" -actionName "OK";
}

global proc ezBlur2DBrowserCommand(string $textField,string $image,string $filename,string $fileType)
{
	textField -edit -fileName $filename $textField;

	string $buff[];
	tokenize $filename "\/" $buff;
	string $file = $buff[size($buff)-1];

	string $temp = `internalVar -userTmpDir` + "blur";
	sysFile -makeDir $temp;
	$temp = `internalVar -userTmpDir` + "blur/icon_" + $file;
	
	if(catch(`idmtImage -film -resize 106 60 $filename $temp`) || !`filetest -f $temp`)
	{
		image -edit -image (`getenv "DEV_PATH"` + "/Maya/Icons/blank.bmp") $image;
		return;
	}

	image -edit -image $temp $image;
}

global proc ezBlur2DPreview(string $file, string $image, string $button)
{
	string $temp = `internalVar -userTmpDir` + "blur";
	sysFile -makeDir $temp;

	string $buf[];
	tokenize $file "/" $buf;
	string $filename = $buf[size($buf)-1];

	image -edit -docTag $file -annotation $file $image;
	button -edit -label $filename -command ("zwBlur2Dfcheck \"\"") -annotation $file $button;

	if (`filetest -f $file`)
	{
		string $imageFile = $file;
		if (`gmatch $filename "*.rar"`)
		{
			$filename = `substring $filename 1 (size($filename)-3)` + "iff";
			$imageFile = `internalVar -userTmpDir` + "blur/" + $filename;
			string $rs = `zwBlur2Drar $file`;
			if (!`filetest -f $imageFile`)
			{
				image -edit -image (`getenv "DEV_PATH"` + "/Maya/Icons/blank.bmp") $image;
				return;
			}
		}
		button -edit -command ("zwBlur2Dfcheck \"" + $imageFile + "\"") $button;
		$temp = `internalVar -userTmpDir` + "blur/icon_" + $filename + ".iff";
		if(catch(`idmtImage -film -resize 192 108 $imageFile $temp`) || !`filetest -f $temp`)
		{
			image -edit -image (`getenv "DEV_PATH"` + "/Maya/Icons/blank.bmp") $image;
			return;
		}

		image -edit -image $temp $image;
	}
	else
	{
		warning ("文件不存在: " + $file);
		image -edit -image (`getenv "DEV_PATH"` + "/Maya/Icons/blank.bmp") $image;
	}
}

global proc int[] ezBlur2DStartEnd(string $textField)
{
	int $startEnd[2] = {-1, -1};

	string $file = `textField -query -fileName $textField`;
	if (!`filetest -f $file`)
		return $startEnd;
	
	string $buf[];
	tokenize $file "/" $buf;
	string $filename = $buf[size($buf)-1];
	string $folder = `substring $file 1 (size($file) - size($filename) - 1)`;
	string $str = `match "[.][0-9]{4}[.]" $filename`;
	if ($str == "")
		return $startEnd;
	
	$startEnd[0] = int(`match "[0-9]{4}" $str`);
	string $filespec = $folder + "/" + `substitute $str $filename ".????."`;
	$buf = `getFileList -filespec $filespec`;
	for ($file in $buf)
	{
		$str = `match "[.][0-9]{4}[.]" $file`;
		if ($str != "")
		{
			int $end = int(`match "[0-9]{4}" $str`);
			if ($end > $startEnd[1])
				$startEnd[1] = $end;
		}
	}

	return $startEnd;
}

global proc string ezBlur2Drar(string $rar)
{
	string $buf[];
	tokenize $rar "/" $buf;
	string $filename = $buf[size($buf)-1];
	if (!`gmatch $filename "*.rar"`)
		return $rar;

	string $temp = `internalVar -userTmpDir` + "blur";
	sysFile -makeDir $temp;
	$filename = `substring $filename 1 (size($filename)-3)` + "iff";
	string $unRar = `internalVar -userTmpDir` + "blur/" + $filename;
	sysFile -delete $unRar;
	string $cmd = substituteAllString(`getenv "DEV_PATH"`, "/", "\\") + "\\Others\\WinRar\\Rar.exe x \"" + `substituteAllString $rar "/" "\\"` + "\" \"" + `substituteAllString $temp "/" "\\"` + "\"";
	chdir "C:";
	string $rs = `system $cmd`;
	if (!`filetest -f $unRar`)
		warning ("解压缩文件出错: " + $rar + "\n" + $rs);

	return $unRar;
}

global proc ezBlur2Dfcheck(string $file)
{
	if ($file == "")
		return;

	system ("shell fcheck -F \"" + `substituteAllString $file "/" "\\"` + "\"");
}

global proc string ezBlur2DqueryFile(string $textField, int $value)
{
	string $file = `textField -query -fileName $textField`;

	string $buf[];
	tokenize $file "/" $buf;
	string $filename = $buf[size($buf)-1];
	string $folder = `substring $file 1 (size($file) - size($filename) - 1)`;
	string $str = `match "[.][0-9]{4}[.]" $filename`;

	$value = int(`match "[0-9]{4}" $str`) + $value -1;
	if ($value < 10)
		$filename = `substitute $str $filename (".000" + $value + ".")`;
	else if ($value < 100)
		$filename = `substitute $str $filename (".00" + $value + ".")`;
	else if ($value < 1000)
		$filename = `substitute $str $filename (".0" + $value + ".")`;
	else
		$filename = `substitute $str $filename ("." + $value + ".")`;
	
	return ($folder + "/" + $filename);
}


global proc ezBlur2DbatchBlur()
{
	//string $rgba[],vctor[],folder[];
	for ($i=1;$i>0 ;$i++ )
	{
		if (`textField -ex ("rgbablur"+$i)`)
		{
		
			//string $folder = `optionVar -query "ezBlur2DFolder"`;
			//if (!`filetest -d $folder`)
				//$folder = `workspace -query -rootDirectory` + "images";
			string $folder = `textField -query -fileName ("folderblur"+$i)`;
			$folder = `substituteAllString $folder "\\" "/"`;


			if (`filetest -d $folder`)
			{
				optionVar -stringValue "ezBlur2DFolder" $folder;
				
				int $min;
				int $max;
				int $startEnd1[2] = `ezBlur2DStartEnd ("rgbablur"+$i)`;
				int $startEnd2[2] = `ezBlur2DStartEnd ("vctorblur"+$i)`;
				
				if (($startEnd1[0] != -1) && ($startEnd2[0] != -1))
				{
					$min = 1;
					$max = min($startEnd1[1] - $startEnd1[0] + 1, $startEnd2[1] - $startEnd2[0] + 1);
				}
				
				for ($j=$min; $j<=$max; $j++)
				{
					string $rgba = `ezBlur2DqueryFile ("rgbablur"+$i) $j`;
					string $vector = `ezBlur2DqueryFile ("vctorblur"+$i) $j`;
					waitCursor -state on;
					int $rs = `ezBlur2Dblur $rgba $vector $folder`;
					waitCursor -state off;
					if (!$rs)
						break;
				}

				if (`optionVar -query "ezBlur2Dformat"` == 1)
				{
					string $cmd = substituteAllString(`getenv "DEV_PATH"`, "/", "\\") + "\\bin\\tgaConvert.exe \"" + `substituteAllString $folder "/" "\\"` + "\" -r";
					system $cmd;
				}
			}
		}
		else 
			break;
	}
	
}

global proc int ezBlur2Dblur(string $rgba, string $vector, string $folder)
{
	string $buf[];

	if (!`filetest -f $rgba`)
	{
		warning ("RGBA 文件不存在: " + $rgba);
		return false;
	}

	if (!`filetest -f $vector`)
	{
		warning ("Vector 文件不存在: " + $vector);
		return false;
	}

	print ($rgba + " + " + $vector + "\n");

	string $temp = `internalVar -userTmpDir` + "blur";
	sysFile -makeDir $temp;

	$temp = `internalVar -userTmpDir` + "blur/ezBlur2D.iff";
	sysFile -delete $temp;
	string $tempVector = `ezBlur2Drar $vector`;
	if (!`filetest -f $tempVector`)
		return false;
	string $cmd = substituteAllString(`getenv "DEV_PATH"`, "/", "\\") + "\\bin\\zwBlur2D.exe \"" + `substituteAllString $rgba "/" "\\"` + "\" \"" + `substituteAllString $tempVector "/" "\\"` + "\" \"" + `substituteAllString $temp "/" "\\"` + "\"";
	chdir "C:";
	string $rs = `system $cmd`;
	if ($tempVector != $vector)
		sysFile -delete $tempVector;
	if(!`filetest -f $temp`)	// $rs != "" || !`filetest -f $temp`
	{
		warning ("合成Vector 时出错: " + $rs);
		return false;
	}
	
	float $length= `optionVar -query "ezBlur2DLength"`;
	float $sharp = `optionVar -query "ezBlur2DSharpness"`;
	int $color = `optionVar -query "ezBlur2DColor"`;
	int $smooth = `optionVar -query "ezBlur2DSmooth"`;
	int $memory = `optionVar -query "ezBlur2DMemory"`;
	$cmd = "blur2d -l " + $length + " -s " + $sharp + " -m " + $smooth + " -r " + $color + " -o " + $memory + " -f \"" + `substituteAllString $temp "/" "\\"` + "\"";
	$temp = `internalVar -userTmpDir` + "blur/ezBlur2D_blur.iff";
	sysFile -delete $temp;
	chdir "C:";
	$rs = `system $cmd`;
	sysFile -delete (`internalVar -userTmpDir` + "blur/ezBlur2D.iff");
	if (`tokenize $rs "\n" $buf` > 1 || !`filetest -f $temp`)
	{
		warning ("blur2d 时出错: " + $buf[0]);
		return false;
	}

	if ($folder == "")
	{
		RenderViewWindow;
		string $renderPanels[] = `getPanel -scriptType "renderWindowPanel"`;
		renderWindowEditor -edit -realSize -loadImage $temp $renderPanels[0];
	}
	else
	{
		tokenize $rgba "/" $buf;
		string $filename = $buf[size($buf)-1];
//		$filename = `substitute "[.][^.]+$" $filename ".tga"`;

		string $folder = `optionVar -query "ezBlur2DFolder"`;
		if (`optionVar -query "ezBlur2Dformat"` == 2)
		{
			$filename = `substitute "[.][^.]+$" $filename ".tif"`;
			string $tga = $folder + "/" + $filename;
			sysFile -delete $tga;
			
			$cmd = "imgcvt -f iff -t tif \"" + `substituteAllString $temp "/" "\\"` + "\" \"" + `substituteAllString $tga "/" "\\"` + "\"";
			$rs = `system $cmd`;
			sysFile -delete $temp;
			if (!`filetest -f $tga`)	// if ($rs != "" || !`filetest -f $tga`)
			{
				warning ("转换成TIFF 时出错: " + $rs);
				return false;
			}
			print ("==> " + $tga + "\n");
		}
		else
		{
			$filename = `substitute "[.][^.]+$" $filename ".tga"`;
			string $tga = $folder + "/" + $filename;
			sysFile -delete $tga;
			
			$cmd = "imgcvt -f iff -t tga \"" + `substituteAllString $temp "/" "\\"` + "\" \"" + `substituteAllString $tga "/" "\\"` + "\"";
			$rs = `system $cmd`;
			sysFile -delete $temp;
			if (!`filetest -f $tga`)	// if ($rs != "" || !`filetest -f $tga`)
			{
				warning ("转换成TGA 时出错: " + $rs);
				return false;
			}
			print ("==> " + $tga + "\n");
		}
	}

	return true;
}