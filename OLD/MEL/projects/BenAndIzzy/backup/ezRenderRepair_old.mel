global proc ezRenderRepair()
{
	if(`window -exists ez_render_repair`)
		deleteUI ez_render_repair;
	window -title "Repair Render" -mb true  -s true -resizeToFitChildren true ez_render_repair;
		
		menu -label "Help";
			menuItem -label "Help on AutoLoadlips..." -command "zjHelpCallback slBrAutoloadlips";

		string $form = `formLayout`;
			textFieldButtonGrp -label "file" -text "" -buttonLabel "Browse" -cw3 90 450 100 -buttonCommand "findRenderFile" renderFile;
			textFieldGrp -label "frame" -text ""  -cw 1  100  renderFrame;
			textFieldGrp -label "layer" -text ""  -cw 1  100  renderLayer;
			textFieldGrp -label "" -text ""  -cw 1 10 -w 220 -editable false machineNum;
			checkBoxGrp -label "通知" -cw2 100 20 -enable  true -onc "textFieldGrp -edit -editable true machineNum" -ofc "textFieldGrp -edit -editable false machineNum" sendMessage;
			button -l "Render" -w 80 -command "ezRenderFrame" renderButton;
		formLayout -edit
			-attachForm		renderFile		"top"		10
			-attachForm		renderFile		"left"		10

			-attachControl		renderFrame		"top"		10		renderFile
			-attachForm		renderFrame		"left"		0		

			-attachControl		renderLayer		"top"		10		renderFrame
			-attachForm		renderLayer		"left"		0

			-attachControl		sendMessage		"top"		10		renderLayer
			-attachForm		sendMessage		"left"		0
			
			-attachControl		machineNum		"top"		10		renderLayer
			-attachControl		machineNum		"left"		0		sendMessage
			
			-attachControl		renderButton		"top"		10		renderLayer
			-attachControl		renderButton		"left"		50		machineNum
		$form;
	showWindow ez_render_repair;
}

global proc loadRenderFile(string $filename,string $fileType)
{
	textFieldButtonGrp -e -text $filename renderFile;
}


global proc string findRenderFile()
{
	string $filePath = `fileBrowserDialog -mode 0  -fileCommand "loadRenderFile"  -actionName "select"`;
	return $filePath;
}

global proc ezRenderFrame()
{
	string $buff1[],$buff2[];

	int $sFrame[],$eFrame[];
	string $frames = `textFieldGrp -q -text renderFrame`;
	tokenize $frames "," $buff1;
	for ($tem in $buff1)
	{
		tokenize $tem "-" $buff2;
		$sFrame[size($sFrame)] = (int)$buff2[0];
		$eFrame[size($eFrame)] = (int)$buff2[size($buff2)-1];
	}
	
	string $renderLayer = "-rl";
	string $layers = `textFieldGrp -q -text renderLayer`;
	tokenize $layers "," $buff1;
	for ($tem in $buff1)
	{
		$renderLayer = $renderLayer + " " + $tem;
	}

	string $file = `textFieldButtonGrp -q -text renderFile`;

	for ($i=0;$i<size($sFrame);$i++)
	{
		if (size($renderLayer)>4)
		{
			$cmd = " -r file -s " + $sFrame[$i] + " -e "+ $eFrame[$i]+" -b 1 -rd d:\\0000 "+ $renderLayer + " " +$file;
			system("render.exe" + $cmd);
		}
		else 
		{	
			$cmd = " -r file -s " + $sFrame[$i] + " -e "+ $eFrame[$i]+" -b 1 -rd d:\\ben\\ddd "+ " " +$file;
			system("render.exe" + $cmd);
		}
	}

	string $machine = `textFieldGrp -q -text machineNum`;
	if ($machine != "")
	{
		$cmd = $machine + " 补帧完成！";
		system("net send "+$cmd);
	}
}