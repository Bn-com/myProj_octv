global proc  ezHairBrowsShadow()
{
	
	radioButtonGrp -e -select 2 LayerEditorTypeRadio;
	tabLayout -edit -selectTab RenderLayerTab DisplayLayerUITabLayout;

	string $objs[]= `ls "*hair*" "*:*hair*" "*brow*" "*:*brow*"`;
	
	string $existsLayer[] = `ls -type renderLayer  "character"`;
	if (`objExists $existsLayer[0]`)
	{
		layerEditorLayerButtonSelect 0 character;
		updateHighlightSelectIcon;
		for ($obj in $objs)
		{
			if (`objExists ($obj+".receiveShadows")`)
			{
				editRenderLayerAdjustment ($obj+".receiveShadows");
				setAttr ($obj+".receiveShadows") 0;
			}
			if (`objExists ($obj+".primaryVisibility")`)
			{
				editRenderLayerAdjustment ($obj+".primaryVisibility");
				setAttr ($obj+".primaryVisibility") 0;
			}

		}
	}

	string $existsLayer[] = `ls -type renderLayer  "character_hair"`;
	if (`objExists $existsLayer[0]`)
	{
		layerEditorLayerButtonSelect 0 character_hair;
		updateHighlightSelectIcon;
		for ($obj in $objs)
		{
			if (`objExists ($obj+".receiveShadows")`)
			{
				editRenderLayerAdjustment -remove ($obj+".receiveShadows");
				setAttr ($obj+".receiveShadows") 0;
				setAttr ($obj+".receiveShadows") 1;
			}
			if (`objExists ($obj+".primaryVisibility")`)
			{
				editRenderLayerAdjustment -remove ($obj+".primaryVisibility");
				setAttr ($obj+".primaryVisibility") 0;
				setAttr ($obj+".primaryVisibility") 1;
			}

		}
	}

	layerEditorLayerButtonSelect 0 defaultRenderLayer;
	updateHighlightSelectIcon;

}

global proc string ezHairShadowCheck()
{
	string $com = "";
	
	int $adj = 0;
	string $existsLayer[] = `ls -type renderLayer "character_hair" "character"`;
	for ($layer in $existsLayer)
	{
		layerEditorLayerButtonSelect 0 $layer;
		updateHighlightSelectIcon;

		string $objs[] = `editRenderLayerMembers -fullNames -q $layer`;
		select -hi $objs;
		string $all[] = `ls -sl -type mesh -type nurbsSurface -type subdiv`;
		for ($one in $all)
		{
			if (`gmatch $one "*:*hair*"` && `objExists ($one+".receiveShadows")` && `objExists ($one+".primaryVisibility")`)
			{
				int $rec = `getAttr ($one+".receiveShadows")`;
				int $pri = `getAttr ($one+".primaryVisibility")`;
				if ($layer == "character_hair")
				{
					if ($rec == 0 || $pri ==0)
					{
						$adj=1;
						break;
					}
				}
				if ($layer == "character")
				{
					if ($rec == 1 || $pri ==1)
					{
						$adj=1;
						break;
					}
				}
			}
		}
		if ($adj == 1)	break;
	}
	select -cl;

	if ($adj == 1)
	{
		$com = `confirmDialog -title "Warning" -message "character或者character_hair层的头发渲染属性有错误。点击确定修改，点击取消继续"
				-button "确定" -button "取消" -defaultButton "确定"
				-cancelButton "No" -dismissString "No"`;
	}

	if ($com == "确定")
	{
		ezHairBrowsShadow();
		return  "modified";
	}
	else return "nomodified";
}