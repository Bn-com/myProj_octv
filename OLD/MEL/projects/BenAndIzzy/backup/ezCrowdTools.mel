global proc ezCrowdTools()
{
	
	string $dFloder = substituteAllString(`getenv "PROJECT_PATH"`, "/", "\\") +"\\BenAndIzzy\\Production\\Asset\\character";

	if (`window -q -exists ezCrowdTools`) deleteUI ezCrowdTools;
   
	window -title "Ben&Izzy Crowd Tools" -resizeToFitChildren true ezCrowdTools;
		string $formLayout1 = `formLayout`;
			string $colayout1 = `columnLayout -adjustableColumn true`;
				menuBarLayout;
					menu -label "Edit";
						menuItem -label "Reset Settings" ezCrowdToolsReset;
					menu -label "Help";
						menuItem -label "Help on Ben&Izzy Crowd Tools..." -command "zjHelpCallback ezCrowdTools";
				setParent ..;
			setParent $formLayout1;
			string $tabLayout = `tabLayout`;
			formLayout -edit 
				-attachForm		$colayout1	"top"		0
				-attachForm		$colayout1	"left"		5
				-attachForm		$colayout1	"right"		5

				-attachControl		$tabLayout	"top"		5	$colayout1
				-attachForm		$tabLayout	"left"		5
				-attachForm		$tabLayout	"right"		5
				-attachForm		$tabLayout	"bottom"	5
			$formLayout1;

				string $formLayoutCrowd = `formLayout`;
					columnLayout -columnAttach "both" 5 -rowSpacing 8 -adjustableColumn 1;
					text -align left -label "1。手动创建nurbs曲线，其范围内不会有群组（可忽略）";
					button -label "闭合曲线" -command "ezCrowdCreatObstacle";
					text -align left -label "2。设置群组中每个角色间最小相对距离。";
					floatSliderGrp -label "Collision Distance" -field 1 -value 10.0 -fieldMaxValue 100 crowdDist;
					text -align left -label "3。设置群组的总的角色个数。";
					intSliderGrp -label "Num Locators" -field 1 -value 10 -fieldMaxValue 1000000 crowdNum;
					text -align left -label "4。选择需要创建群组的地面（nurbs或者poly）。";
					button -label "Create" -command ("ezCrowdCreatLoc"); 
				setParent ..;


			setParent ..;
				
				
				string $formLayoutChar= `formLayout`;

				string $charFrLayout 	= `frameLayout -label "Charactors for Crowd" -labelAlign "top" -cll false -borderStyle "etchedIn"`;
					string $charFoLayoutf	=`formLayout`;
						textFieldButtonGrp -label "Char Path" -text $dFloder  -changeCommand  "ezCharLibFloderSelect" -buttonLabel "Browse" -cw3 90 450 100 -buttonCommand "ezCharPath" charListPath;
						textScrollList -h 120 -doubleClickCommand "ezAddCharToCrowd(1)" -selectCommand "ezCrowdCharPicSelect(\"crowdChar\")"  -allowMultiSelection true crowdChar ;
						textScrollList -e -removeAll crowdChar;
						string $dxFloder[] = `getFileList -folder ($dFloder + "\\")`;
						for ($tem in $dxFloder )
						{
							textScrollList -edit -append $tem crowdChar;
						}
						textScrollList -h 120 -doubleClickCommand "ezAddCharToCrowd(0)"  -selectCommand "ezCrowdCharPicSelect(\"selCrowdChar\")" -allowMultiSelection true selCrowdChar;
						button -label "添加>>" -command ("ezAddCharToCrowd(1)") addChar;
						button -label "<<减少" -command ("ezAddCharToCrowd(0)") cutChar;
						setParent ..;
						formLayout -edit
							-attachForm		charListPath		"top"		5
							-attachForm		charListPath		"left"		15
							
							-attachControl		crowdChar		"top"		10	charListPath
							-attachForm		crowdChar		"left"		15
							-attachPosition		crowdChar		"right"		0	40
							-attachForm		crowdChar		"bottom"	5	
							
							-attachControl		addChar			"top"		30	charListPath
							-attachControl		addChar			"left"		20	crowdChar
							-attachPosition		addChar			"right"		0	55
							
							-attachControl		cutChar			"top"		30	addChar
							-attachControl		cutChar			"left"		20	crowdChar
							-attachPosition		cutChar			"right"		0	55

							-attachControl		selCrowdChar		"top"		10	charListPath
							-attachControl		selCrowdChar		"left"		20	addChar
							-attachPosition		selCrowdChar		"right"		0	95
							-attachForm		selCrowdChar		"bottom"	5	
						$charFoLayoutf;
					setParent ..;
			
				text -align left -label "  选择群组中需要用到角色的*.MB文件路径（按照目前文件结构选择到character目录会自动列出其子目录，默认针对Ben&Izzy项目）。" crowdTex1;
//				text -align left -label "2。设置群组中使用到的角色数量。" crowdTex2;
				textScrollList  -h 40 -width 400 -visible false -doubleClickCommand "ezOpenCharPic" ezCrowdCharPics;
				button -label "浏览图片" -visible false  -command "ezOpenCharPic" ezOpenCharPicBut;
//				string $crowdNum = `intSliderGrp -field true -l "Number of Charactors" -columnWidth 1 110 -columnWidth 2 60 -min 2 -max 30 -fieldMinValue 2 -fieldMaxValue 1000 -v 5`;
				string $button1 = `button -label "Import" -width 300 -command ("ezImportCrowdChar")`;
				formLayout -edit
					-attachForm		crowdTex1		"top"	        10
					
//					-attachControl		crowdTex2		"top"	        10	crowdTex1
					
					-attachControl		$charFrLayout		"top"	        10	crowdTex1
					-attachForm		$charFrLayout		"left"		5
					-attachForm		$charFrLayout		"right"		5

					-attachControl		ezCrowdCharPics		"top"		10	$charFrLayout
					-attachForm		ezCrowdCharPics		"left"		25
					
					-attachControl		ezOpenCharPicBut	"top"		10	$charFrLayout
					-attachControl		ezOpenCharPicBut	"left"		20	ezCrowdCharPics
					
//					-attachControl		$crowdNum		"top"		10	ezCrowdCharPics
//					-attachForm		$crowdNum		"left"		25

					-attachControl		$button1		"top"		10	ezOpenCharPicBut
					-attachForm		$button1		"left"		5

				$formLayoutChar;
			setParent ..;

				string $formLayoutAnim = `formLayout`;
			setParent ..;
				
			
			tabLayout -edit
				-tabLabel		$formLayoutCrowd	"Creat Crowd"
				-tabLabel		$formLayoutChar		"Creat Char"
				-tabLabel		$formLayoutAnim		"Creat Anim"
			$tabLayout;
	showWindow ezCrowdTools;
	source "ezCrowdLoctor.mel";
	source "ezCrowdChar.mel";
}

global proc string ezCharPath()
{
	string $filePath = `fileBrowserDialog -mode 4  -fileCommand "ezLoadCharFile"  -actionName "select_folder" -operationMode "Reference"`;
	return $filePath;
}

global proc ezLoadCharFile(string $filename,string $fileType)
{
	textFieldButtonGrp -e -text $filename charListPath;
	ezCharLibFloderSelect();
}

global proc ezCharLibFloderSelect()
{
	string $floder = `textFieldButtonGrp -q -text charListPath`;

	textScrollList -edit -removeAll -visible false crowdChar;
	textScrollList -edit -removeAll -visible false ezCrowdCharPics;
	button -edit -visible false  ezOpenCharPicBut;

	string $buf[] = `getFileList -folder ($floder + "\\")`;
	for ($anim in $buf)
	{
		textScrollList -edit -append $anim crowdChar;
	}
	textScrollList -edit -visible true -numberOfRows 8 -selectIndexedItem 1 crowdChar;
}

global proc ezCrowdCharPicSelect(string $win)
{
	string $floder = `textFieldButtonGrp -q -text charListPath`;
	string $char[] = `textScrollList -query -selectItem $win`;

	textScrollList -edit -removeAll -visible false ezCrowdCharPics;
	button -edit -visible false  ezOpenCharPicBut;

	int $i=0;
	string $jpg[] = `getFileList -filespec ($floder + "\\"  + $char[0] + "\\master\\*.jpg") `;
	string $tga[] = `getFileList -filespec ($floder + "\\"  + $char[0] + "\\master\\*.tga") `;
	string $pics[]= stringArrayCatenate ($jpg,$tga);
	for ($pic in $pics)
	{
		textScrollList -edit -append $pic ezCrowdCharPics;
		$i++;
	}
	if ($i)
	{
		textScrollList -edit -visible true -numberOfRows $i -selectIndexedItem 1 ezCrowdCharPics;
		button -edit -visible true  ezOpenCharPicBut;
	}
}

global proc ezOpenCharPic()
{
	string $charquels[]=`textScrollList -q -si crowdChar `;
	string $path = `textFieldButtonGrp -q -text charListPath` + "\\"+$charquels[0]+"\\master\\";

	string $charPic[] = `textScrollList -q -si ezCrowdCharPics `;
	string $charPicFile = $path + $charPic[0];
		
	system ("load" + $charPicFile);
}

global proc ezAddCharToCrowd(int $adj)
{
	
	string $from = "crowdChar";
	string $to = "selCrowdChar";
	if ($adj == 0)
	{
		$from = "selCrowdChar";
		$to = "crowdChar";

	}

	string $charquels[]=`textScrollList -q -si $from `;

	for ($char in $charquels)
	{
		textScrollList -edit -removeItem $char $from;
		textScrollList -edit -append $char $to;
	}
}
