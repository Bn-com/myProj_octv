global proc ezRenderCam()
{
	string $shapes[] = `ls -type camera`;
	string $cams[]={};

	for ($cam in $shapes)
	{
		$trans = `listRelatives -f -p $cam`;
		$cams[size($cams)] = $trans[0];
	}
	
	
	string $vis = `lookThru -q`;
	string $curCam[] = `ls -l $vis`;
	string $shape[] = `listRelatives -f -shapes $curCam[0]`;

	if (`window -q -exists ezRenderCam`) deleteUI ezRenderCam;
   
	window -title "Camera Tool" -resizeToFitChildren true ezRenderCam;
		
		string $formLayout = `formLayout`;
			
			optionMenu -changeCommand "ezRenderCamLoopOp" currentCam;
			for ($cam in $cams)
			{
				menuItem -label $cam;
			}
			optionMenu -e  -v $curCam[0] currentCam;
			attrFieldSliderGrp -l "near" -cw 1 60 -attribute ($shape[0]+".nearClipPlane") nearClip;
			attrFieldSliderGrp -l "far"  -cw 1 60 -attribute ($shape[0]+".farClipPlane") farClip;
			attrFieldSliderGrp -l "scan" -cw 1 60 -attribute ($shape[0]+".overscan") overScan;
			button -label "<<front" -w 80 -h 40 -command "ezRenderCamLoop(0)" frontCam;
			button -label "back>>"  -w 80 -h 40 -command "ezRenderCamLoop(1)" backCam;
			setParent ..;
		formLayout -edit
			-attachForm	currentCam	"top"		20
			-attachForm	currentCam	"left"		30

			-attachControl	nearClip	"top"		15	currentCam
			-attachControl	farClip		"top"		15	nearClip
			-attachControl	overScan	"top"		15	farClip
			//-attachForm	overScan	"left"		0
			
			-attachControl	frontCam	"top"		15	overScan
			-attachForm	frontCam	"left"		30

			-attachControl	backCam		"top"		15	overScan
			-attachControl	backCam		"left"		30	frontCam
		$formLayout;

	showWindow ezRenderCam;
}

global proc ezRenderCamLoop(int $mod)
{
	string $default[] = {"|persp","|top","|front","|side"};

	string $shapes[] = `ls -type camera`;
	string $cams[]={};

	for ($cam in $shapes)
	{
		$trans = `listRelatives -f -p $cam`;
		if (stringArrayCount($trans[0],$default) == 0)
		{
			$cams[size($cams)] = $trans[0];
		}
	}
	
	string $vis = `lookThru -q`;
	string $curCam[] = `ls -l $vis`;
	int $num = 0;
	for ($i=0;$i<size($cams) ;$i++ )
	{
		if ($cams[$i] == $curCam[0]) $num = $i;
	}
	
	if ($mod == 0)
	{
		if ($num == 0)
			$num = (size($cams)-1);
		else	$num = $num - 1;
	}
	else if ($mod == 1)
	{
		if ($num == (size($cams)-1))
			$num = 0;
		else	$num = $num + 1;	
	}

	lookThru $cams[$num];
	optionMenu -e -v $cams[$num] currentCam;

	string $shape[] = `listRelatives -f -shapes $cams[$num]`;
	attrFieldSliderGrp -e -attribute ($shape[0]+".nearClipPlane") nearClip;
	attrFieldSliderGrp -e -attribute ($shape[0]+".farClipPlane") farClip;
	attrFieldSliderGrp -e -attribute ($shape[0]+".overscan") overScan;
}

global proc ezRenderCamLoopOp()
{
	string $vis = `optionMenu -q -v currentCam`;
	lookThru $vis;

	string $shape[] = `listRelatives -f -shapes $vis`;
	attrFieldSliderGrp -e -attribute ($shape[0]+".nearClipPlane") nearClip;
	attrFieldSliderGrp -e -attribute ($shape[0]+".farClipPlane") farClip;
	attrFieldSliderGrp -e -attribute ($shape[0]+".overscan") overScan;
}