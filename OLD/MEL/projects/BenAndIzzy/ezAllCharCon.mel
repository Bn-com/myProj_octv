global proc ezAllCharCon()
{
	string $charAttr[] = {"skinning_vis","skinProxy_vis","skeleton_vis","hair_vis"};
	string $char[] = ezAllGetChara();
	string $buffer[];
	string $charName;
	

	if(`window -exists w_allCharCon`)		deleteUI w_allCharCon;

	window -title "Char Control" -s true  w_allCharCon;
		columnLayout -adjustableColumn true;
			text   -backgroundColor 0.8 0.8 0.9 -align "left" -font  "smallBoldLabelFont"   
				-label "            角色名             隐藏  |    显示/高精   |    低精模型   |       头发      |  Smooth";
			frameLayout -label "整体控制" -labelAlign "top"	-marginHeight 2	-borderStyle "etchedIn";
				rowColumnLayout -numberOfColumns 6  -height 80
					-columnWidth 1 110 
					-columnWidth 2 60
					-columnWidth 3 80
					-columnWidth 4 80
					-columnWidth 5 80
					-columnWidth 6 90
				;
					text -label "";
					button  -label   "隐藏角色"		-command ("ezAllCharaCon(0)") ; 
					button  -label   "显示高精角色"		-command ("ezAllCharaCon(1)");
					button  -label   "显示低精角色"		-command ("ezAllCharaCon(2)");
					button  -label   "显示隐藏头发"		-command ("ezAllCharaCon(3)");
					button  -label   "Smooth所有角色"		-command ("ezAllCharaCon(4)");
				setParent ..;
			setParent ..;
			frameLayout -label "" -labelAlign "top"   -collapsable false -borderStyle "etchedIn";
				rowColumnLayout	-numberOfColumns 3 -columnWidth 1 370 -columnWidth 2 70  -columnWidth 3 50;
				for ($tem in $char) 
				{
						int $num = `tokenize $tem ":_" $buffer`;
						$charName = $buffer[$num-2];
						
						
						int $skin = `getAttr ($tem+"."+$charAttr[0])`;
						int $proxy = `getAttr ($tem+"."+$charAttr[1])`;
						int $skeleton = `getAttr ($tem+"."+$charAttr[2])`;
						if ($skin == 1)	$num = 2;
						else if ($proxy ==1) $num =3;
						else if ($skeleton == 0 ) $num = 1;
						else 
						{
							$num =2;
							setAttr ($tem+"."+$charAttr[0]) 1;
						}
						radioButtonGrp -numberOfRadioButtons 3 	-columnWidth4 140  70 70 70  -labelArray3  ""  ""  ""
						-select $num
						-onCommand1 ("ezHideShow(\""+$tem+"\",0)")
 						-onCommand2 ("ezHideShow(\""+$tem+"\",1)")
 						-onCommand3 ("ezHideShow(\""+$tem+"\",2)")
 						-label ($charName +"                  ")
						($tem+"_char_con")
						;
						
						int $hair=0;
						int $adj = `objExists ($tem+"."+$charAttr[3])`;
						if ($adj == 1)	$hair = `getAttr ($tem+"."+$charAttr[3])`;
						checkBox -label ""
						-enable $adj -value $hair 
						-onCommand  ("ezHideShow(\""+$tem+"\",3)") 
						-offCommand  ("ezHideShow(\""+$tem+"\",4)")  
						 ($tem + "_hair_con");
						
						
						int $smooth = `getAttr ($tem + ".smooth")`;
						checkBox -label "" 
						-value $smooth
						-onCommand  ("ezHideShow(\""+$tem+"\",5)")
						-offCommand  ("ezHideShow(\""+$tem+"\",6)")
						($tem + "_smooth_con");
				}
				setParent ..;
			setParent ..;
	window -edit -w 520 -h (115+size($char)*16) w_allCharCon;
	showWindow w_allCharCon;

}

global proc string[] ezAllGetChara()
{
	string $charAttr[] = {"skinning_vis","skinProxy_vis","skeleton_vis"};
	string $char[];
	int $adj;

	string $masters[] = `ls "*:*MASTER" "*MASTER" "*:*:*MASTER"`;
	for ($master in $masters)
	{
		$adj = 1;
		for ($attr in $charAttr)
		{	
			if (!`objExists ($master+"."+$attr)`)	
			{
				$adj = 0;
				break;
			}
		}
		if ($adj == 1)
		{
			$char[size($char)] = $master;
		}
	}
	return $char;
}

global proc ezHideShow(string $char,int $adj)
{
	string $charAttr[] = {"skinning_vis","skinProxy_vis","skeleton_vis","hair_vis"};
	switch ($adj)
	{
		case 0 :
			for ($tem in $charAttr)
			{
				if (`objExists ($char + "."+ $tem)`)	setAttr ($char + "."+ $tem) 0;
			}
			if (`objExists ($char + "."+ $charAttr[3])`)	checkBox -edit -value 0 ($char+"_hair_con");
			break;
		case 1 :
			setAttr ($char + "."+ $charAttr[0]) 1;	
			setAttr ($char + "."+ $charAttr[1]) 0;	
			setAttr ($char + "."+ $charAttr[2]) 1;	
			break;
		case 2 :
			setAttr ($char + "."+ $charAttr[0]) 0;	
			setAttr ($char + "."+ $charAttr[1]) 1;	
			setAttr ($char + "."+ $charAttr[2]) 1;	
			break;
		case 3 :
			setAttr ($char + "."+ $charAttr[3]) 1;	
			break;
		case 4 :
			setAttr ($char + "."+ $charAttr[3]) 0;	
			break;
		case 5 :
			setAttr ($char + ".smooth") 1;	
			break;
		case 6 :
			setAttr ($char + ".smooth") 0;	
			break;
	}
}

global proc ezAllCharaCon(int $mod)
{
	string $chars[] = ezAllGetChara();
	switch ($mod)
	{
		case 0 :
			for ($char in $chars )
			{
				ezHideShow($char,0);
				radioButtonGrp -edit -select 1 ($char+"_char_con");
			}
			break;
		case 1 :
			for ($char in $chars )
			{
				ezHideShow($char,1);
				radioButtonGrp -edit -select 2 ($char+"_char_con");
			}
			break;
		case 2 :
			for ($char in $chars )
			{
				ezHideShow($char,2);
				radioButtonGrp -edit -select 3 ($char+"_char_con");
			}
			break;
		case 3 :
			string $charHair[];
			int $i=0;
			for ($char in $chars )
			{
				int $enable = `checkBox -q -enable ($char+"_hair_con")`;
				if ($enable == 1)
				{
					int $value = `checkBox -q -value ($char+"_hair_con")`;
					if ($value == 1) $i++;
					$charHair[size($charHair)] = $char;
				}
			}
			for ($char in $charHair)
			{
				if ($i != size($charHair))	
				{
					ezHideShow($char,3);
					checkBox -edit -value 1 ($char+"_hair_con");
				}
				else	
				{
					ezHideShow($char,4);
					checkBox -edit -value 0 ($char+"_hair_con");
				}
			}
			break;
		case 4 :
			int $i=0;
			for ($char in $chars )
			{
				int $value = `checkBox -q -value ($char+"_smooth_con")`;
				if ($value == 0) $i++;
			}
			for ($char in $chars)
			{
				if ($i == size($chars))	
				{
					ezHideShow($char,5);
					checkBox -edit -value 1 ($char+"_smooth_con");
				}
				else	
				{
					ezHideShow($char,6);
					checkBox -edit -value 0 ($char+"_smooth_con");
				}
			}
			break;
	}
}