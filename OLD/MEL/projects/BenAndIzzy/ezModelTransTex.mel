/// Copyright (C) 2000-2007 IDMT. All rights reserved.
///
/// Author: 万寿龙
///
/// Creation Date: 2007/02/26
///
/// Description: ben&izzy项目模型环节，修改贴图，根据贴图找到对应file节点，自动替换
///

	
global proc ezModelTransTex()
{
	
	if(`window -exists w_ezModelTransTex`)
		deleteUI w_ezModelTransTex;
	window -title "Translate Texture" -mb true  -s true -resizeToFitChildren true w_ezModelTransTex;
		
		menu -label "Help";
			menuItem -label "Help on Translate Texture..." -command "zjHelpCallback ezModelTransTex";

		string $form = `formLayout`;
			textFieldButtonGrp -label "texture file path" -text "" -buttonLabel "Browse" -cw3 90 250 100 -buttonCommand "ezFindFilePath" _textureListPath;
			string $transButton = `button -l "translate" -w 80 -command "ezModelTrans"`;
			string $cancleButton = `button -l "cancle" -w 80 -command ("deleteUI -window  w_ezModelTransTex")`;
			formLayout -edit
				-attachForm $transButton					        "bottom"	10
				-attachForm  $transButton 						"left"		10
				-attachPosition $transButton						"right"		0	50
				
				-attachForm $cancleButton                                               "bottom"	10
				-attachControl  $cancleButton 						"left"		10	$transButton
				-attachPosition $cancleButton						"right"		0	100
				
				-attachControl  _textureListPath 						"bottom"	10	$transButton
				-attachForm  _textureListPath 						"left"		10	
			$form;
	showWindow w_ezModelTransTex;
}

global proc string ezFindFilePath()
{
	string $filePath = `fileBrowserDialog -mode 4  -fileCommand "ezLoadTexture"  -actionName "select_folder" -operationMode "Reference"`;
	return $filePath;
}

global proc ezLoadTexture(string $filename,string $fileType)
{
	textFieldButtonGrp -e -text $filename _textureListPath;
}

global proc ezModelTrans()
{
	string $texPath = `textFieldButtonGrp -q -text _textureListPath`;
	string $texFile = $texPath + "/*.tga";
	string $catchTexFiles[] = `getFileList -filespec $texFile`;
	string $buffer[];
	string $transFiles[];
	if (`size $catchTexFiles` != 0)
	{
		string $exFiles[]= `ls -type file`;
		for ($file in $exFiles)
		{
			string $exTexture = `getAttr ($file + ".fileTextureName")`;
			int $num = `tokenize $exTexture "\/" $buffer`;
			string $exName = `substring $buffer[$num-1] 1 (size($buffer[$num-1])-3)`;
			for ($tex in $catchTexFiles)
			{	
				if (`gmatch $tex ($exName+"*")`)
				{
					string $name = $texPath + "/" + $tex;
					setAttr -type "string" ($file+".fileTextureName") $name;
					$transFiles[size($transFiles)] = $file;
				}
			}
		}
	}

	ezOutputWindowForModelTrans();
	for ($tem in $transFiles)
	{
		ezOutputForModelTrans "\n";
		ezOutputForModelTrans $tem;
	}
}


global proc ezOutputWindowForModelTrans()
{
	if(`window -ex ezOutputWindow`) 
	{
		deleteUI ezOutputWindow;
	}
	window 
		-widthHeight 500 400 
		-sizeable 1 
		-title "Ben&Izzy------已替换以下file节点贴图"  
		-ret
		ezOutputWindow;

	string $form = `formLayout -numberOfDivisions 100`;

	string $a1 =` scrollField -wordWrap false -editable false ezoutput`;

	formLayout 
		-edit 
		-attachForm $a1 "top"    5
		-attachForm $a1 "bottom" 5
		-attachForm $a1 "left"   5
		-attachForm $a1 "right"  5 
		$form;

	showWindow;
}

global proc ezOutputForModelTrans(string $out)
{
	scrollField -e -ip 0 -it $out ezoutput;
	setFocus ezoutput;
}
