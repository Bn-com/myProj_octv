global proc slOutputInformationWindow()
{
	if(`window -ex slOutputInformationWindow`) 
		deleteUI slOutputInformationWindow;
	
	window 	-widthHeight 500 400 	-sizeable 1 	-title "请查看信息"  -ret	slOutputInformationWindow;

		string $tab = `tabLayout slOutputInformationWindow_tab` ;
			
			string $form = `formLayout -numberOfDivisions 100`;
				string $a1 =` scrollField -wordWrap false -editable false `;

			formLayout 
				-edit 
				-attachForm $a1 "top"    1
				-attachForm $a1 "bottom" 1
				-attachForm $a1 "left"   1
				-attachForm $a1 "right"  1
				$form;

		tabLayout -edit
			-tl	$form		"默认的"
		$tab;

	showWindow slOutputInformationWindow;
}

global proc slOutputInformation(int $clear,string $tab,string $out)
{
	string $forms[],$scr[];
	if (!`window -ex slOutputInformationWindow` || !`window -q -vis slOutputInformationWindow`)
	{
		slOutputInformationWindow;

		$forms = `tabLayout -q -ca slOutputInformationWindow_tab`;
		$scr = `formLayout -q -ca $forms[0]`;
		
		tabLayout -edit -tl $forms[0] $tab slOutputInformationWindow_tab;
	}
	else
	{
		int $tabind;
		string $tl[] = `tabLayout -q -tl slOutputInformationWindow_tab`;
		for ($i=0;$i<size($tl);$i++ )
		{
			if ($tl[$i] == $tab)
				$tabind = $i+1;
		}

		$forms = `tabLayout -q -ca slOutputInformationWindow_tab`;
		if ($tabind)
		{
			tabLayout -e -sti $tabind slOutputInformationWindow_tab;
			$scr = `formLayout -q -ca $forms[$tabind-1]`;
			if ($clear)
				scrollField -e -tx "" $scr[0];
		}
		else
		{
			setParent slOutputInformationWindow_tab;
				string $form = `formLayout -numberOfDivisions 100`;
					string $a1 =` scrollField -wordWrap false -editable false `;

				formLayout 
					-edit 
					-attachForm $a1 "top"    1
					-attachForm $a1 "bottom" 1
					-attachForm $a1 "left"   1
					-attachForm $a1 "right"  1
					$form;

			tabLayout -edit	-tl	$form $tab slOutputInformationWindow_tab;

			tabLayout -edit	-st	$form slOutputInformationWindow_tab;
			$scr[0] = $a1;
		}
	}
	
	
	scrollField -e -ip 0 -it $out $scr[0];
	setFocus $scr[0];
}