/// Copyright (C) 2000-2007 IDMT. All rights reserved.
///
/// Author: 万寿龙
///
/// Creation Date: 2007/02/26
///
/// Description: ben&izzy项目渲染环节重新链接灯光
///



global proc ezLightingLink(string $filename,string $filetype)
{
	file -import -ra false  -options "v=0"   $filename;
	
	string $lights[]= `ls -type light`;
	string $transforms[] = `ls -type transform`;

	string $buffer[];
	tokenize $filename "\/" $buffer;
	string $path;
	for ($i=0;$i<`size $buffer`-2;$i++)
	{
		$path += $buffer[$i] + "/";
	}
	string $scene = `substring $buffer[$i+1] 1 (size($buffer[$i+1])-7)`;
	string $lightingSetFile = $path + "notes/" + "*_set_lightlink*.txt";
	string $catchLightingSetFile[] = `getFileList -filespec $lightingSetFile`;
	string $lightBuf[];
	string $objBuf[];
	
	if (`size $catchLightingSetFile` == 0)
	{
		$lightingSetFile = $path + "notes/" + "*_lightlink*.txt";
		print ("\n"+$lightingSetFile);
		$catchLightingSetFile = `getFileList -filespec $lightingSetFile`;
		if (`size $catchLightingSetFile` == 0)
			error "对应notes目录不存在对应的lightlinks文件，或者命名不正确请检查！";
	}
	
	$lightingSetFile =  $path + "notes/" + $catchLightingSetFile[0];

	if (`file -q -ex $lightingSetFile`)
	{
		string $obj[];
		string $light;
		string $linktype;
		$fileId = `fopen $lightingSetFile "r"`;
		string $nextLine = `fgetline $fileId`;
		int $adj = 0;
		string $lost[];
		while (!`feof $fileId`)
		{
			tokenize $nextLine "<>\n" $lightBuf;
			tokenize $nextLine ":|\n" $objBuf;
			
			if (`size $lightBuf` == 2)
			{
				$light = substituteAllString($lightBuf[0]," ","");
				$linktype = substituteAllString($lightBuf[1]," ","");
			}
			else if (`size $objBuf` >= 2 )
			{
				/*tokenize $nextLine "|" $objBuf;
				string $tempObj = "";
				for ($tem in $objBuf)
				{
					tokenize $tem ":" $buffer;
					$tempObj =  $tempObj + "|" + $buffer[size($buffer)-1];
				}*/
				tokenize $nextLine ":|" $objBuf;
				string $tempObj = "";
				for ($nb in $objBuf)
				{
					if ($nb != $objBuf[0])
						$tempObj += $nb + ":";
				}
				$obj[size($obj)] =  $tempObj;
			}
			else
			{
				$adj = 1;
			}
			$nextLine = `fgetline $fileId`;


			if ($adj == 1)
			{
				$adj = 0;
				string $iName;
				string $fName;
				string $foName;
				string $nameBuf[];
				if (`objExists $light`)
				{
					string $default[] = `listConnections ($light+".instObjGroups")`;
					if (!`objExists $default[0]`)
					{
						connectAttr -nextAvailable ($light+".instObjGroups") defaultLightSet.dagSetMembers;
					}
					if ($linktype == "break")
					{
						for ($tem in $obj )
						{
							tokenize $tem "\n" $nameBuf;
							$fName = $nameBuf[0];
							//print ($fName+"\n");
							for ($temp in $transforms)
							{
								
								if (`objExists ($temp+".initialName")`)
								{
									$iName = `getAttr ($temp+".initialName")`;
									tokenize $iName "\n" $nameBuf;
									$iName = substituteAllString($nameBuf[0],"|",":");
									if (tolower($iName) == tolower($fName))
									{
										lightlink -break -light $light -object $temp;
										//print ($light+"-----break------"+$temp+"\n");
									}
								}
							}
							
						}
					}
					else if ($linktype == "link")
					{
						string $lightSet[] = `listConnections -type objectSet -p true $light`;
						if (`size $lightSet` == 1)
							disconnectAttr ($light+".instObjGroups[0]") $lightSet[0];
						
						for ($tem in $obj )
						{
							tokenize $tem "\n" $nameBuf;
							$fName = $nameBuf[0];
							for ($temp in $transforms)
							{
								if (`objExists ($temp+".initialName")`)
								{
									$iName = `getAttr ($temp+".initialName")`;
									tokenize $iName "\n" $nameBuf;
									$iName = substituteAllString($nameBuf[0],"|",":");
									if (tolower($iName) == tolower($fName))
									{
										lightlink -make -light $light -object $temp;
										//print ($light+"-----link------"+$temp+"\n");
									}
								}
							}
							
							
						}
					}
					clear $obj;
				}
				else 
				{
					$lost[size($lost)] = $light;
				}
			}
		}
		fclose $fileId;
		if (`size($lost)`)
		{
			print "以下灯光链接不存在，请检查lightlinks文件确认－－－－－－－\n";
			print $lost;
		}
	}
}


fileBrowserDialog -m 0 -fc "ezLightingLink"  -an "import";