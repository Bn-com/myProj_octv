global proc zwModifyRibBrowse(string $textFieldButtonGrp)
{
	string $folder = `optionVar -query zwModifyRibFolder`;
	if (!`filetest -d $folder`)
	{
		$folder = "//file-cluster/GDC/Projects/ROMA/PRJ_roma/MC_roma";
	}
	$folder = `idmtFolderDialog -initialDir $folder -title "ribs 文件夹"`;
	if ($folder != "")
	{
		textFieldButtonGrp -edit -text $folder $textFieldButtonGrp;
		optionVar -stringValue zwModifyRibFolder $folder;
	}
}

global proc zwModifyRibProc(string $textFieldButtonGrp)
{
	string $folder = `textFieldButtonGrp -query -text $textFieldButtonGrp`;
	if (`filetest -d $folder`)
	{
		python ("try:\n\treload(idmt.maya.roma)\nexcept:\n\timport idmt.maya.roma\nidmt.maya.roma.ModifyRib('" + `substituteAllString $folder "\\" "\\\\"` + "')");
	}
}

global proc zwModifyRib()
{
	if(`window -exists "zwModifyRib"`)
	{
		deleteUI "zwModifyRib";
	}

	window -menuBar true -title "修改rib 文件" -width 640 -height 92 "zwModifyRib";
		string $formLayout = `formLayout`;
			string $text = `text -label "修改rib 文件里面的路径为绝对路径，原文件会备份到Z:\\Netrender\\Maya_Even\\T150\\ribs"`;
			string $textFieldButtonGrp = `textFieldButtonGrp -label "文件夹：" -buttonLabel "Browse" -columnWidth 1 80 -columnWidth 3 50 -adjustableColumn 2`;
			textFieldButtonGrp -edit -buttonCommand ("zwModifyRibBrowse \"" + $textFieldButtonGrp + "\"") $textFieldButtonGrp;
			string $button = `button -label "修改" -command ("zwModifyRibProc \"" + $textFieldButtonGrp + "\"")`;
	formLayout -edit
		-attachForm	$text			"top"		8
		-attachForm	$text			"left"		80

		-attachControl	$button			"top"		8	$text
		-attachForm	$button			"right"		8

		-attachControl	$textFieldButtonGrp	"top"		8	$text
		-attachControl	$textFieldButtonGrp	"right"		8	$button
		-attachForm	$textFieldButtonGrp	"left"		0
	$formLayout;

	showWindow "zwModifyRib";
}