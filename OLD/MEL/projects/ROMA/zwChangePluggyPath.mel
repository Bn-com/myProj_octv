global proc int zwChangePluggyPath()
{
	int $dirty = false;

	if (!`pluginInfo -query -loaded "MayaMan"`)
	{
		return $dirty;
	}
	string $SLIncludePath = `getenv "PIPELINE_SCRIPTS"` + "/mel/plugins/windows/maya2008-32bit/mm_magic";
	if (!`filetest -d $SLIncludePath`)
	{
		return $dirty;
	}
	string $SLCodeNodes[] = `ls -type "SLCodeNode"`;
	for ($SLCodeNode in $SLCodeNodes)
	{
		string $str = `getAttr ($SLCodeNode + ".SLIncludePath")`;
		if ($str != $SLIncludePath)
		{
			setAttr -type "string" ($SLCodeNode + ".SLIncludePath") $SLIncludePath;
			$dirty = true;
		}
	}
//	string $cmd = "try:\n\treload(cpp)\nexcept:\n\timport RBW.SC.lighting.changePluggyPath as cpp\ncpp.changePluggyPath(\"" + $SLIncludePath + "\")";
//	python $cmd;

	// RiFilters
	int $size = `MayaManInfo -getarraysize "RiFilterActive"`;
	for ($i=$size-1; $i>=0; $i--)
	{
		MayaManSetData -delarrayelement "RiFilter" $i;
		MayaManSetData -delarrayelement "RiFilterActive" $i;
		MayaManSetData -delarrayelement "RiFilterArgs" $i;

		if (!`about -batch`)
		{
			if (`scriptTable -exists rifFilters`)
			{
				scriptTable -edit -deleteRow ($i + 1) rifFilters;
			}
		}

		$dirty = true;
	}

	// RenderMan Search Paths
	string $types[] = {"shader", "archive", "texture", "display", "procedural"};
	for ($type in $types)
	{
		string $paths[] = `MayaManInfo -searchpaths $type`;
		for ($i=size($paths)-1; $i>=0; $i--)
		{
			MayaManSetData -modifysearchpath $type $i "";
			$dirty = true;
		}
	}
	if (!`about -batch`)
	{
		if (`tabLayout -exists "mayaManOptionsWindow|formLO|thePane|rmanpathsPane|rmanpathsCL|theTabs"`)
		{
			mayaManRenderManPathsDialogRefresh;
		}
	}

	string $UserRibOptions = `getAttr "MayaManNugget.UserRibOptions"`;
	if ($UserRibOptions != "")
	{
		evalEcho "setAttr -type \"string\" \"MayaManNugget.UserRibOptions\" \"\"";
		$dirty = true;
	}

	int $no = `MayaManInfo -getarraysize "EOType"`;
	for ($i=$no-1; $i>=0; $i--)
	{
		MayaManSetData -delarrayelement "EOChannel"		$i;
		MayaManSetData -delarrayelement "EOEnabled"		$i;
		MayaManSetData -delarrayelement "EOType"		$i;
		MayaManSetData -delarrayelement "EODoDither"		$i;
		MayaManSetData -delarrayelement "EODitherValue"		$i;
		MayaManSetData -delarrayelement "EOQuantization"	$i;
		MayaManSetData -delarrayelement "EOQuantMin"		$i;
		MayaManSetData -delarrayelement "EOQuantMax"		$i;
		MayaManSetData -delarrayelement "EOQuantZero"		$i;
		MayaManSetData -delarrayelement "EOQuantOne"		$i;
		MayaManSetData -delarrayelement "EOFilter"		$i;
		MayaManSetData -delarrayelement "EOFilterX"		$i;
		MayaManSetData -delarrayelement "EOFilterY"		$i;
		MayaManSetData -delarrayelement "EOPath"		$i;
		MayaManSetData -delarrayelement "EODriver"		$i;
		MayaManSetData -delarrayelement "EODriverArgs"		$i;
		MayaManSetData -delarrayelement "EOExt"			$i;
	}
	if ($no > 0)
	{
		$dirty = true;
		if (!`about -batch`)
		{
			if (`scriptTable -exists chansList`)
			{
				mayaManExtraChannelsRefreshList;
			}
		}
	}

	return $dirty;
}