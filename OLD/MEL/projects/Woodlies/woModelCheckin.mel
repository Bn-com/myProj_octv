//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2010-8-10
//
//	Update/Change: WansHoulOng
//	Last Updated: 
//	Description:
//				
//////////////////////////////////////////////////////////


global proc woModelCheckin(int $resize,string $rename)
{
	string $imageSize[] = {"128","256","512","1k","2k","4k"};

	
	if ($rename == "")
	{
		$rename = (string)$resize;
	}
	string $fileNodes[] = `ls -type file`;
	for ($node in $fileNodes)
	{
		if (!`referenceQuery -inr $node`)
		{
			string $image = `getAttr ($node+".fileTextureName")`;
			if (`filetest -r $image`)
			{
				string $buff[] = stringToStringArray($image,"/\\");
				string $name = $buff[size($buff)-1];
				string $path = `substring $image 1 (size($image)-size($name))`;

				string $rsPath = $path+$rename+"/";//`internalVar -utd`;
				clear $buff;
				$buff = stringToStringArray($name,".");
				string $buff1[] = stringToStringArray($buff[0],"_");
				string $size = $buff1[size($buff1)-1];
				string $rsName="";
				if (stringArrayCount($size,$imageSize))
				{
					$rsName = `substituteAllString $name ("_"+$size) ("_"+$rename)`;
				}
				else
				{
					$rsName = `substituteAllString $name ("_"+$size) ("_"+$size+"_"+$rename)`;
				}

				//print ($path+"-----"+$name+"\n");
				//print ($rsPath+"------"+$rsName+"\n");
				//print "++++++++++++++\n";

				int $rs = 1;
				$rs = catch(`idmtImage -r $resize $resize ($path+$name) ($rsPath+$rsName)`);
				if (!$rs)
				{
					setAttr -type "string"  ($node+".fileTextureName") ($rsPath+$rsName);
				}

			}
		}
	}

	string $referenceFiles[] = `file -q -r`;
	for ($rf in $referenceFiles)
	{
		string $referenceNode = `referenceQuery -rfn $rf`;
		string $notex = "";
		if (gmatch($rf,"*_ms_tex.*"))
		{
			$notex = `substituteAllString $rf "_ms_tex." "_ms_notex."`;
			if (`filetest -r $notex`)
			{
				file -lr $referenceNode $notex;
			}
		}
		else if (gmatch($rf,"*_tx.*"))
		{
			$notex = `substituteAllString $rf "/texture/" "/master/"`;
			$notex = `substituteAllString $notex "_tx." "_ms_notex."`;
			if (`filetest -r $notex`)
			{
				file -lr $referenceNode $notex;
			}
		}
	}
}