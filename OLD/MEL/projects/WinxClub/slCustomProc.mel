/// Copyright (C) 2000-2008 IDMT. All rights reserved.
///
/// Author: 万寿龙
///
/// Creation Date: 2008/05/28
///
/// Description: 
///				自定义通用程序



//////////////////////////////////////////////////////////
//	Procdures Name: slPPToolsLayoutName
//	
//	Arguments : 
//				string $layout		需要寻找的Layout类型
//				string $name		需要寻找的Layout的名字
//	Return : 
//				string $mdName		返回与指定名字不同的字符（在其后自动+"e"或者多个"e"）
//////////////////////////////////////////////////////////
global proc string slPPToolsLayoutName(string $layout,string $name)
{
	string $mdName ;

	string $cmd =  $layout + " -ex " + $name ;
	if (`eval $cmd`)
	{
		$mdName = $name + "e";
		$mdName = slPPToolsLayoutName($layout,$mdName);
		return $mdName;
	}
	else 
	{
		$mdName = $name;
		return $mdName;
	}
}

//////////////////////////////////////////////////////////
//	Procdures Name: slPPToolsDeleteFolder
//	
//	删除指定的目录（包含其子目录和文件）
//	Arguments : 
//				string $folder	指定需要删除的目录
//	Return : NULL
//////////////////////////////////////////////////////////
global proc slPPToolsDeleteFolder(string $folder)
{
	string $children[] = `getFileList -folder $folder`;
	for ($child in $children)
	{
		string $files[] = `getFileList -folder ($folder+$child+"/")`;
		if (size($files[0]))
			slPPToolsDeleteFolder($folder+$child+"/");
		else
		{
			sysFile -delete ($folder+$child);
		}
		sysFile -removeEmptyDir ($folder+$child);
	}
	sysFile -removeEmptyDir $folder;
}

global proc slPPToolsCopyFolder(string $folder,string $cfolder)
{
	string $files[] = `getFileList -folder $folder`;
	if (size($files))
	{
		sysFile -makeDir $cfolder;
		for ($file in $files)
		{
			slPPToolsCopyFolder(($folder+$file+"/"),($cfolder+$file+"/"));
		}
	}
	else
	{
		string $fo = `substring $folder 1 (size($folder)-1)`;
		string $cfo = `substring $cfolder 1 (size($cfolder)-1)`;
		
		int $copy = `sysFile -copy $cfo $fo`;
		if (!$copy)
		{
			sysFile -makeDir $cfolder;
		}
	}
}