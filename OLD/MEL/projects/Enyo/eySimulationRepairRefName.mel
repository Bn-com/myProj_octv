//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2009-9-7
//
//	Update/Change: WansHoulOng
//	Last Updated: 2009-11-16
//	Description:
//			修复参考前缀名问题.
//				
//////////////////////////////////////////////////////////

global proc eySimulationRepairRefName()
{
	string $files[] = `file -q -r`;
	for ($file in $files)
	{
		file -ur $file $file;
	}

	for ($file in $files)
	{
		string $oldNS = `file -q -ns $file`;

		string $buff[];
		int $num = `tokenize $file "\/" $buff`;
		$num = `tokenize $buff[$num-1] "_" $buff`;
		string $newNS = $buff[0]+"_"+$buff[1];
		
		
		clear $buff;
		$num = `tokenize $file "{}" $buff`;
		//print $num;
		if ($num==1)
		{
			if ($oldNS != $newNS)
			{
				file -lr $file $file;
				if (`namespace -ex $newNS`)
				{
					//临时
					$newNS = $newNS+"_6";
				}
				file -e -ns $newNS $file;
			}
		}
		else 
		{
			$newNS = $newNS + "_" + $buff[1];
			if ($oldNS != $newNS)
			{
				file -lr $file $file;
				if (`namespace -ex $newNS`)
				{
					//临时
					string $newpost = (int)$buff[1] + 6;
					$newNS = `substituteAllString $newNS ("_"+$buff[1]) ("_"+$newpost)`;
				}
				file -e -ns $newNS $file;
			}
		}

		print ($oldNS+"----------"+$newNS+"\n");
	}
}

global proc eySimulationRepairRefNameRN()
{
	string $files[] = `file -q -r`;
	for ($file in $files)
	{
		file -ur $file $file;
		
		string $referenceNode = `file -query -referenceNode $file`;
		string $currentNamespace = `file -query -namespace $file`;
		if (`match ("^" + $currentNamespace + "RN[0-9]*$") $referenceNode` == "")
		{
			int $lockNode[] = `lockNode -query $referenceNode`;
			if ($lockNode[0])
			{
				lockNode -lock false $referenceNode;
			}
			$referenceNode = `rename $referenceNode ($currentNamespace + "RN")`;
			if ($lockNode[0])
			{
				lockNode -lock true $referenceNode;
			}
		}

	}
	
}