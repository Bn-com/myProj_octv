//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2010-12-1
//
//	Update/Change: WansHoulOng
//	Last Updated: 2010-12-1
//	Description:
//				获取选择的物体中出现最多的前缀名
//////////////////////////////////////////////////////////

global proc string clRenderGetCharPre()
{
	string $allPre[];
	string $sel[] = `ls -sl`;
	for ($obj in $sel)
	{
		string $children[] = `listRelatives -ad $obj`;
		for ($child in $children)
		{
			string $buff[];
			int $num = `tokenize $child ":" $buff`;
			if ($num>=2)
			{
				$allPre[size($allPre)] = $buff[$num-2];
			}
		}
	}

	string $shotPre[] = stringArrayRemoveDuplicates($allPre);
	int $total;
	string $char;
	for ($pre in $shotPre)
	{
		int $count = stringArrayCount($pre,$allPre);
		if ($count > $total)
		{
			$total = $count;
			$char = $pre;
		}
	}

	return $char;
}