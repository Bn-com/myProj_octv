//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 
//
//	Update/Change: WansHoulOng
//	Last Updated: 
//	Description:
//				
//////////////////////////////////////////////////////////

global proc slReferenceToolsForLoad()
{
	string $ref[] = `file -q -r`;
	for($r in $ref)
	{
		int $unload = `file -q -dr $r`;
		if($unload)
		{
			print ($r+"\n");
			string $rn=`referenceQuery -rfn $r`;
			file -lr $rn $r;
		}
	}
}

//select value----selected,unselected,all
//unload value---unload,remove
global proc slReferenceToolsForUnLoad(string $select,string $unload)
{
	string $rns[]={};

	if ($select=="selected")
	{
		string $sel[]= `ls -sl`;
		for ($item in $sel)
		{
			if (`referenceQuery -inr $item`)
			{
				$rns[size($rns)]=`referenceQuery -rfn $item`;
			}
		}
		$rns=stringArrayRemoveDuplicates($rns);
	}
	else if ($select=="unselected")
	{
		string $allref[] = `file -q -r`;
		string $allrns[]={};
		for ($item in $allref)
		{
			$allrns[size($allrns)]=`referenceQuery -rfn $item`;
		}
		
		string $selrns[]={};
		string $sel[]= `ls -sl`;
		for ($item in $sel)
		{
			if (`referenceQuery -inr $item`)
			{
				$selrns[size($selrns)]=`referenceQuery -rfn $item`;
			}
		}
		$selrns=stringArrayRemoveDuplicates($selrns);

		$rns = stringArrayRemove($selrns,$allrns);
	}
	else if ($select == "all")
	{
		string $allref[] = `file -q -r`;
		for ($item in $allref)
		{
			$rns[size($rns)]=`referenceQuery -rfn $item`;
		}
	}

	for ($rn in $rns)
	{
		if ($unload=="unload")
			file -ur $rn;
		else if ($unload=="remove")
		{
			string $rf = `referenceQuery -f $rn`;
			file -rr $rf;
		}
	}
}

global proc slReferenceToolsForRemoveUnload()
{
	string $ref[] = `file -q -r `;
	for($r in $ref)
	{
		int $unload = `file -q -dr $r`;
		if($unload)
		{
			//print ($r+"\n");
			file -rr $r;
		}
	}
}

global proc slReferenceToolsWindow()
{
	if (`window -ex slReferenceToolsWindow_Main`)
	{
		deleteUI slReferenceToolsWindow_Main;
	}
	
	window -t "Reference Tools" slReferenceToolsWindow_Main;
		columnLayout ;
			radioButtonGrp -numberOfRadioButtons 3  -label "" -labelArray3 "Selected" "UnSelected" "All" -sl 1  slReferenceToolsWindow_Main_sel;
			radioButtonGrp -numberOfRadioButtons 2  -label "" -labelArray2 "Unload" "Remove" -sl 1 slReferenceToolsWindow_Main_do;
			button -l "Go" -w 100 -h 30 -c "slReferenceToolsGo";
	showWindow slReferenceToolsWindow_Main;
}

global proc slReferenceToolsGo()
{
	int $sel = `radioButtonGrp -q -sl slReferenceToolsWindow_Main_sel`;
	int $do = `radioButtonGrp -q -sl slReferenceToolsWindow_Main_do`;

	string $select="";
	string $unload="";
	
	switch ($sel)
	{
		case 1 :
			$select = "selected";
			break;
		case 2 :
			$select = "unselected";
			break;
		case 3 :
			$select = "all";
			break;
	}
	switch ($do)
	{
		case 1 :
			$unload = "unload";
			break;
		case 2 :
			$unload = "remove";
			break;
	}

	slReferenceToolsForUnLoad $select $unload;
}