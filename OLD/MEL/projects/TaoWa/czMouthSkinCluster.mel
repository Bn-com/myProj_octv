//-----------------------------------------------------------------------
//
//  Creation Date:  August 28th, 2006
//
//  Procedure Name:
//      czMouthSkinCluster
//
//  Description:
//
//		Attach all mouthShape to Joint_Head.
//
//	
//  Usage:
//		Select char_Rig node and run this script.
//
//  Input Arguments: none
//
//  Return Value: None.
//
//----------------------------------------------------------------------

global proc czMouthSkinCluster()
{
	// variable initialization
	string $charRig, $worldAnim, $mouthShape, $jointHead, $skinCluster;
	string $buffer[];

	// get char_Rig name
	$buffer = `ls  -sl -l "*_Rig"`;
	if (size($buffer)>1)
	{
		error("One character is allowed!");
	}
	else
	{
		$charRig = $buffer[0];
	}

	// get the World_ANIM
	$buffer = `ls -l "World_ANIM"`;
	for ($world in $buffer)
	{
		if (`startsWith $world $charRig`)
		{
			$worldAnim = $world;
			break;
		}
	}
	// print $worldAnim;
	// get the mouth shape node name
	$buffer = `ls -l "mouth_display*" "Mouth_display*"`;
	for ($mouth in $buffer)
	{
		if (`startsWith $mouth $charRig`)
		{
			$mouthShape = $mouth;
			break;
		}
	}
	// print $mouthShape;
	// get the Joint_Head name
	$buffer = `ls -l "Joint_Head"`;
	for ($join in $buffer)
	{
		if (`startsWith $join $charRig`)
		{
			$jointHead = $join;
			break;
		}
	}
// print $jointHead;
	// check if everything is ready
	if ($worldAnim!=""&&$mouthShape!=""&&$jointHead!="")
	{
	
		// back to frame -31 and initialization
		int $curTime = `currentTime -q`;
		currentTime -31;

		setAttr ($worldAnim+".tx") 0;
		setAttr ($worldAnim+".ty") 0;
		setAttr ($worldAnim+".tz") 0;
		setAttr ($worldAnim+".rx") 0;
		setAttr ($worldAnim+".ry") 0;
		setAttr ($worldAnim+".rz") 0;
		
		// get all the mouth shapes
		$buffer = `listRelatives -c -f $mouthShape`;

		if (size($buffer))
		{
			for ($each in $buffer)
			{
				// flag
				int $flag = 1;

				// get the shape node of current mouth shape
				// string $temp[] = `listRelatives -s $each`;

				// check if the shape node got a input connection
				// of skinCluster
				string $temp[] = `listConnections -connections 1 $each `;

				for ($item in $temp)
				{
					if (`nodeType $item`=="skinCluster")
					{
						$skinCluster = $item;
						$flag = 0;
						break;
					}
				}

				// if there is no skinCluster connected
				// fix it
				if ($flag)
				{
					select -r $jointHead;
					select -add $each;
					catch(`newSkinCluster "-mi 5 -dr 4"`);
				}

			}
		
		// current time recovery
		currentTime $curTime;			
		}

	}
}