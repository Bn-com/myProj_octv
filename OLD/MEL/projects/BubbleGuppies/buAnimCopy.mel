global proc buAnimCopy()
{
	int $obj;
	string $chars[] = `ls -sl`;
	if (size($chars)!=2)
		error "请选择两个角色的Character!";
	for ($char in $chars)
	{
		if (!endsWith($char,"Character"))
			$obj++;
	}
	
	if ($obj)
	{
		if (`copyKey -time ":" -float ":" -hierarchy none -controlPoints 0 -shape 1 $chars[0]`)
		{
			pasteKey -option replaceCompletely -copies 1 -connect 1 -timeOffset 0 -floatOffset 0 -valueOffset 0 $chars[1];
		}
		else
			slCopyAttr($chars[0],$chars[1]);
	}
	else
	{
		string $preS = `substring $chars[0] 1 (size($chars[0])-9)`;
		string $preD = `substring $chars[1] 1 (size($chars[1])-9)`;
		
		string $set[] = `listConnections -type objectSet $chars[0]`;
		select -noExpand -deselect $set[0];
		select -add $set[0];
		if (`objExists ($preS+"Tail_ctrl")` && `objExists ($preS+"rig_ctrl")`)
		{
			select -add ($preS+"Tail_ctrl");
			select -add ($preS+"rig_ctrl");
		}
		string $fromCtr[] = `ls -sl`;
		select -cl;

		//print $fromCtr;

		for ($ctr in $fromCtr)
		{
			string $buff[];
			tokenize $ctr ":" $buff;

			if (`objExists ($preD+$buff[size($buff)-1])`)
			{
				if (`copyKey -time ":" -float ":" -hierarchy none -controlPoints 0 -shape 1 $ctr`)
				{
					pasteKey -option replaceCompletely -copies 1 -connect 1 -timeOffset 0 -floatOffset 0 -valueOffset 0 ($preD+$buff[size($buff)-1]);
				}
				else
					slCopyAttr($ctr,($preD+$buff[size($buff)-1]));
			}
		}
	}
}

global proc slCopyAttr(string $from,string $to)
{
	string $keyAttr[] = `listAttr -k $from`;
	string $cbAttr[] = `listAttr -cb $from`;

	string $attr[] = stringArrayCatenate($keyAttr,$cbAttr);

	for ($at in $attr)
	{
		if (`objExists ($to+"."+$at)` && `objExists ($from+"."+$at)`)
		{
			setAttr ($to+"."+$at) `getAttr ($from+"."+$at)`;
		}
	}
}