global proc buAnimLipsTool()
{
	string $buff[];

	string $sel[] = `ls -sl`;
	if (size($sel) != 1)
		error "请选择导入口型的角色!";
	tokenize $sel[0] ":" $buff;
	string $pre = `substring $sel[0] 1 (size($sel[0])-size($buff[size($buff)-1]))`;
	string $mouth = $pre + "Mouth";
	
	string $filename = `file -q -sn -shn`;
	if (!size($filename))
		return ;
	tokenize $filename "_" $buff;
	string $episode = $buff[1];
	string $shot = `substring $buff[2] 1 3`;

	int $start = `playbackOptions -q -min`;

	string $path = "//File-cluster/gdc/Projects/BubbleGuppies/BubbleGuppies_Scratch/Animation/LipsFile/"+$episode+"/";
	
	string $tFolder[] = `getFileList -fld $path`;
	if (size($tFolder) == 1) $path += ($tFolder[0]+"/");
	$tFolder = `getFileList -fld $path`;
	if (size($tFolder) == 1) $path += ($tFolder[0]+"/");

	string $lipsFile;
	string $files[] = `getFileList -fld $path -fs ($episode+"_"+$shot+"*.txt")`;
	if (size($files) && `attributeExists "MouthTemp" $mouth`)
	{
		cutKey -cl -t ":" -f ":" -at "MouthTemp" $mouth;

		$lipsFile = $path + $files[size($files)-1];
		
		$fileId = `fopen $lipsFile "r"`;
		$nextLine = `fgetline $fileId`;
		$nextLine = `fgetline $fileId`;
		while (size($nextLine)>0)
		{
			int $num = `tokenize $nextLine " \n" $buff`;
			if ($num == 2)
			{
				int $time = (int)$buff[0];
				int $val = (int)`substring $buff[1] 2 (size($buff[1]))`;

				setKeyframe -t ($start+$time-1) -v $val -ott "step" -at "MouthTemp" $mouth;
			}
			$nextLine = `fgetline $fileId`;
		}
		fclose $fileId;

		print "导入口型成功!";
	}
	else
		warning "导入口型错误---口型文件不存在或者口型控制器错误";
	
	//print $lipsFile;
}