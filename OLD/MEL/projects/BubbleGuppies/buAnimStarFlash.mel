//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2009-9-5
//
//	Update/Change: WansHoulOng
//	Last Updated: 
//	Description:
//				选择物体的闪烁.
//				
//////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////////////////////////////
//删除选择的物体和闪烁相关的属性K帧（缩放值）
//////////////////////////////////////////////////////////////////////////////////////////////
global proc buAnimStarFlashDelKey()
{
	source "generateChannelMenu.mel";
	string $stars[] = `ls -sl`;
	string $attrs[] = {"scaleX","scaleY","scaleZ"};
	for ($star in $stars)
	{
		for ($attr in $attrs)
		{
			CBdeleteConnection ($star+"."+$attr);
		}
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////
//闪烁的动画曲线
//	$star             闪烁的物体
//      $cutTime      闪烁的时间起始帧
////////////////////////////////////////////////////////////////////////////////////////////////////
global proc buAnimPerStarFlash(string $star,float $cutTime)
{
	//float $cutTime = `currentTime -q`;
	
	float $bigTime = 2;
	float $bigVal = 1.3;
	float $midTime = 2;
	float $midVal = 1.1;
	
	//string $stars[] = `ls -sl`;
	string $attrs[] = {"scaleX","scaleY","scaleZ"};
	//for ($star in $stars)
	//{
		for ($attr in $attrs)
		{
			//CBdeleteConnection ($star+"."+$attr);
			float $val = `getAttr ($star+"."+$attr)`;
			setKeyframe -t $cutTime							-v ($val*1)			-itt "linear"	 -ott "step"		($star+"."+$attr);
			setKeyframe -t ($cutTime+1)						-v ($val*$bigVal)	-itt "linear"	 -ott "step"		($star+"."+$attr);
			setKeyframe -t ($cutTime+$bigTime)				-v ($val*$bigVal)	-itt "linear"	 -ott "step"		($star+"."+$attr);
			setKeyframe -t ($cutTime+$bigTime+1)				-v ($val*$midVal)	-itt "linear"	 -ott "step"		($star+"."+$attr);
			setKeyframe -t ($cutTime+$bigTime+$midTime)		-v ($val*$midVal)	-itt "linear"	 -ott "step"		($star+"."+$attr);
			setKeyframe -t ($cutTime+$bigTime+$midTime+1)		-v ($val*1)			-itt "linear"	 -ott "step"		($star+"."+$attr);
		}
	//}

}

//////////////////////////////////////////////////////////////////////////////////////
//是否闪烁及闪烁的间隔判断
// 调用子程序     buAnimPerStarFlash
//      $time                    指定闪烁的起始时间
//		$time >=10000           当前帧开始至结束
//		$time <10000		  文件动画帧数范围
/////////////////////////////////////////////////////////////////////////////////////
global proc buAnimAllStarFlash(float $time)
{
	string $stars [] = `ls -sl`;

	float $minTime = `playbackOptions -q -min`;
	float $maxTime = `playbackOptions -q -max`;
	if ($time != 10000)
	{
		$minTime = $time;
		//$maxTime = $time+1;
	}

	float $minDis = 5;
	float $maxDis = 10;

	for ($star in $stars)
	{
		for ($i=$minTime;$i<$maxTime; )
		{
			int $flash = `rand 0 2`;
			if ($i == $time)
				$flash = 1;

			//print $i;
			//print "------";
			//print $flash;
			//print "\n";
			if ($flash)
			{
				buAnimPerStarFlash($star,$i);
				$i = $i +6;
			}
			$i = $i + (int)`rand $minDis $maxDis`;
		}
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////
//主程序，可选择当前帧至结束或者文件范围
//调用子程序  buAnimAllStarFlash  buAnimStarFlashDelKey
///////////////////////////////////////////////////////////////////////////////////////////////////////////
global proc buAnimStarFlash()
{
	float $cutTime = `currentTime -q`;
	
	
	if (`window -ex buAnimStarFlashWin`)
		deleteUI buAnimStarFlashWin;
	
	window -title "Star Flash                  (WHO)" -in "SFW" -mnb 0 -mxb 0 buAnimStarFlashWin;
		columnLayout -adj 1 -rs 3;
			button -l "删除闪动帧" -c "buAnimStarFlashDelKey";
			button -l "手动(当前帧至结束帧闪动)" -c "float $cutTime = `currentTime -q`;buAnimAllStarFlash $cutTime;";
			button -l "自动(起始帧范围闪动)" -c "buAnimAllStarFlash 10000";
	window -e -wh 200 105 buAnimStarFlashWin;
	showWindow;
}

