//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 
//
//	Update/Change: 梁宇
//	Last Updated: 
//	Description: 修改为没有场次的项目创建工程目录
//				
//////////////////////////////////////////////////////////


global proc skSetProject()
{
	string $project = "";
	if (`optionVar -exists skSetProjectProject`)
	{
		$project = `optionVar -query skSetProjectProject`;
	}
	string $disk = "D:";
	if (`optionVar -exists skSetProjectDisk`)
	{
		$disk = `optionVar -query skSetProjectDisk`;
	}
	
	
	if (`window -exists skSetProject`)
	{
		deleteUI skSetProject;
	}
	window -menuBar true -title "Create/Set Project" -resizeToFitChildren true skSetProject;
		menu -label "Help";
			menuItem -label "Help on Create/Set Project" -command "showHelp -absolute -docs \"http://talk.idmt.com.cn/redirect.php?goto=findpost&ptid=59290&pid=649739\"";
		columnLayout -adjustableColumn true;
			optionMenuGrp -label "Disk:" -columnAlign 1 "right" optionMenuGrpDisk;
				menuItem -label "D:";
				menuItem -label "E:";
				menuItem -label "G:";
				menuItem -label "M:";
			optionMenuGrp -edit -value $disk optionMenuGrpDisk;
			textFieldGrp -label "Project:" -text $project -changeCommand "zwSetProjectTypeChange" textFieldGrpProject;
				popupMenu -button 1 -postMenuCommand "zwSetProjectMenu \"popupMenuProjectL\" \"textFieldGrpProject\"" popupMenuProjectL;
				popupMenu -button 3 -postMenuCommand "zwSetProjectMenu \"popupMenuProjectR\" \"textFieldGrpProject\"" popupMenuProjectR;
			textFieldGrp -label "EP:" textFieldGrpEP;
			textFieldGrp -label "SC:" textFieldGrpSC;
			button -label "Create/Set Project" -command "skSetProjectProc";

	showWindow skSetProject;

	string $filename = `file -q -sn -shn`;
	if ($filename != "")
	{
		string $buff[] = stringToStringArray($filename,"_");
		$project = zwGetProject($filename);
		string $EP,$SQ,$SC;
		if ($project == "")
		{
			$project = "VickyTheViking";
			$EP = $buff[0];
			$SC = $buff[1];
		}
		else
		{
			$EP = $buff[1];
			$SC = $buff[2];
		}
		textFieldGrp -e -tx $project textFieldGrpProject;
		textFieldGrp -e -tx $EP textFieldGrpEP;
		textFieldGrp -e -tx $SC textFieldGrpSC;
	}
}

global proc skSetProjectProc()
{
	string $dest;

	string $disk = `optionMenuGrp -query -value optionMenuGrpDisk`;
	string $project = `textFieldGrp -query -text textFieldGrpProject`;
	$project = strip($project);
	string $EP = `textFieldGrp -query -text textFieldGrpEP`;
	$EP = strip($EP);
	string $SC = `textFieldGrp -query -text textFieldGrpSC`;
	$SC = strip($SC);
	if ($project == "" || $EP == "" || $SC == "")
	{
		return;
	}
	
	$dest = $disk + "/" + $project + "/EP_" + $EP  + "/SC_" + $SC;

	optionVar -stringValue skSetProjectProject $project;
	optionVar -stringValue skSetProjectDisk $disk;

	int $explore = false;
	if (!`filetest -f ($dest + "/workspace.mel")`)
	{
		$explore = true;
	}
/*
	string $source = "//file-cluster/GDC/Resource/Support/Maya/Import/projects/" + zwAboutVersion();
	zwXcopyEx($source, $dest, false, false);
	setProject $dest;
*/
	if ($explore)
	{
		sysFile -md $dest;
		eval "source \"setProject.mel\"";
		sp_createAndSetDefaultProject $dest true;
	}
	else
	{
		setProject $dest;
	}
	deleteUI skSetProject;
	if ($explore)
	{
		showHelp -absolute -docs $dest;
	}
}