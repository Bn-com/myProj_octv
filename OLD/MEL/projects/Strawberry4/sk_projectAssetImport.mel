/*=========================================================
[新][通用]asset 导入工具
沈康 2013.6.7
***********************************************************/

global proc sk_projectAssetImport(int $projectID)
{
    if(`window -ex sk_projectAssetImport_m100_UI`)
    	deleteUI sk_projectAssetImport_m100_UI;
        window -t "===项目资源库===(左两栏单击生效，模型参考双击导入)" -menuBar 1 -rtf 1 sk_projectAssetImport_m100_UI;
        menu -label "说明";
        	menuItem -label "帮助" ;
        	   
        columnLayout;
            columnLayout -adjustableColumn 1;
                 rowLayout -numberOfColumns 3   -columnWidth3 230 125 125   -adjustableColumn 2
                            -columnAlign  1 "right"   -columnAttach 1 "both"     0    -columnAttach 2 "both"  0   -columnAttach 3 "both"  0;
                        optionMenuGrp -adjustableColumn 1 -columnWidth 200 150 -cc "sk_projectAssetList_Lv1_Update;textScrollList -e -removeAll sk_projectAssetList_Lv2;textScrollList -e -removeAll sk_projectAssetList_Lv3;"  sk_AnimLibraryProjectList;
                            menuItem  -label   "ZoomWhiteDolphin";
                            menuItem  -label   "Zorro";
                        optionMenuGrp  -e -select $projectID sk_AnimLibraryProjectList;
                        optionMenuGrp -adjustableColumn 1 -columnWidth 200 150 -cc "catch(`sk_projectAssetList_Lv3_Update`)"  sk_AnimLibraryHMLTypeList;
                            menuItem  -label   "_h_";
                            menuItem  -label   "_m_";
                            menuItem  -label   "_l_";
            setParent..;
            
            
            columnLayout;
                rowLayout -numberOfColumns 3   -columnWidth3 80 150 250   -adjustableColumn 2
                        -columnAlign  1 "right"   -columnAttach 1 "both"     0    -columnAttach 2 "both"  0   -columnAttach 3 "both"  0;
                    textScrollList -width 80 -height 300  -numberOfRows 20 -allowMultiSelection 0 -selectCommand "sk_projectAssetList_Lv2_Update"  sk_projectAssetList_Lv1;
                    
                    textScrollList -width 150 -height 300  -numberOfRows 20 -allowMultiSelection 0 -selectCommand "sk_projectAssetList_Lv3_Update"  sk_projectAssetList_Lv2;
                    
                    
                    textScrollList -width 150 -height 300  -numberOfRows 20 -allowMultiSelection 0 -doubleClickCommand "sk_projectAssetList_Lv3_SelectCMD"  sk_projectAssetList_Lv3;
                    
                    sk_projectAssetList_Lv1_Update();            
                    textScrollList -e -removeAll sk_projectAssetList_Lv2;
                    textScrollList -e -removeAll sk_projectAssetList_Lv3;
                    
                 setParent..;
                    
            setParent..;
        	
        	
    showWindow sk_projectAssetImport_m100_UI;
}

global proc sk_projectAssetList_Lv1_Update()
{
    string $project = `optionMenuGrp -q -value  sk_AnimLibraryProjectList`;
    
    string $path =  "\\\\file-cluster\\GDC\\Projects\\" + $project + "\\Project\\scenes\\" ;
    string $infoDir[] = `getFileList - folder $path`;
    
    textScrollList -e -removeAll sk_projectAssetList_Lv1;
    
    for( $dir in $infoDir)
    {
        //仅仅收容文件夹
        string $check_dir[]  = {};
        tokenize $dir "."  $check_dir;
        if(size($check_dir) == 1)
        {
            //剔除动画
            if($dir != "Animation")
            {
            textScrollList -e -append $dir sk_projectAssetList_Lv1;
            }
        }
    }
}

global proc sk_projectAssetList_Lv2_Update()
{
    string $project = `optionMenuGrp -q -value  sk_AnimLibraryProjectList`;
    string $itemSel_Lv1[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv1`;
    
    string $path =  "\\\\file-cluster\\GDC\\Projects\\" + $project + "\\Project\\scenes\\" +$itemSel_Lv1[0] + "\\" ;
    string $infoDir[] = `getFileList - folder $path`;
    
    textScrollList -e -removeAll sk_projectAssetList_Lv2;
    
    for( $dir in $infoDir)
    {
        if($dir != "bak")
        {
        textScrollList -e -append $dir sk_projectAssetList_Lv2;
        }
    }
}

global proc sk_projectAssetList_Lv3_Update()
{
    string $project = `optionMenuGrp -q -value  sk_AnimLibraryProjectList`;
    string $itemSel_Lv1[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv1`;
    string $itemSel_Lv2[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv2`;
    
    string $path =  "\\\\file-cluster\\GDC\\Projects\\" + $project + "\\Project\\scenes\\" +$itemSel_Lv1[0] + "\\" + $itemSel_Lv2[0] +"\\"+ "master" + "\\" ;
    string $infoDir[] = `getFileList - folder $path`;
    
    //加入高中低模切换
    string $hmlType =  `optionMenuGrp -q -value  sk_AnimLibraryHMLTypeList`;;
    
    textScrollList -e -removeAll sk_projectAssetList_Lv3;
    
    for( $dir in $infoDir)
    {
        string $file_ma = `match ".ma" $dir`;
        string $file_mb = `match ".mb" $dir`;
        //maya文件匹配
        if($file_ma == ".ma" || $file_mb == ".mb" )
        {
            //master文件匹配
            string $file_anim = `match "_ms_anim" $dir`;
            if( $file_anim == "_ms_anim" )
            {
                //项目匹配
                string $pro = sk_projectInfoTransform($project);
                string $file_pro = `match $pro $dir`;
                if($file_pro ==$pro )
                {
                    //高中低模切换
                    string $file_hmlType = `match $hmlType $dir`;
                    if($file_hmlType == $hmlType )
                    {
                        textScrollList -e -append $dir sk_projectAssetList_Lv3;
                    }
                }
            }
        }
    }
}

global proc sk_projectAssetList_Lv1_SelectCMD()
{
    string $itemSel_Lv1[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv1`;
    sk_projectAssetList_Lv2_Update();
}

global proc sk_projectAssetList_Lv3_SelectCMD()
{
    //开始创建参考
    //importReference "E:/TD_work/Calimefo/Q/cl_LIB_BuildingCA_Ext_HD_h_tx.ma" "mayaBinary";
    string $project = `optionMenuGrp -q -value  sk_AnimLibraryProjectList`;
    string $itemSel_Lv1[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv1`;
    string $itemSel_Lv2[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv2`;
    string $itemSel_Lv3[] = ` textScrollList -q -selectItem sk_projectAssetList_Lv3`;
    
    string $path =  "\\\\file-cluster\\GDC\\Projects\\" + $project + "\\Project\\scenes\\" +$itemSel_Lv1[0] + "\\" + $itemSel_Lv2[0] +"\\"+ "master" + "\\" + $itemSel_Lv3[0] ;
    //source slImportRef.mel;
    importReference $path "mayaBinary";
    
    
    sk_projectAssetList_Lv3_Update();
}

global proc string sk_projectInfoTransform(string $project)
{
    string $pro = "";
    if ($project ==  "ZoomWhiteDolphin")
    {
        $pro = "zm";    
    }
    return $pro;  
}