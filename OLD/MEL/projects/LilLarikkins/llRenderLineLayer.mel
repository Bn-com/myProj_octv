//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2010-1-8
//
//	Update/Change: WansHoulOng
//	Last Updated: 2010-1-8
//	Description:
//				
//////////////////////////////////////////////////////////

global proc llRenderLineLayer()
{
	string $Array[] = `listConnections "renderLayerManager.renderLayerId"`;
	for($item in $Array)
	{
		if((`gmatch $item "*:*defaultRenderLayer*"`)||(`gmatch $item "*defaultRenderLayer*"`))
		setAttr ($item + ".renderable") 0;
	}	
	string $objShape[] = `ls -sl`;
	string $renderLayer = `createRenderLayer -name "Line" -makeCurrent -noRecurse $objShape`;
	
	
	editRenderLayerGlobals -currentRenderLayer $renderLayer;
	editRenderLayerAdjustment "defaultRenderGlobals.currentRenderer";

	//contours shader
	string $contours = `shadingNode -asShader surfaceShader -n propContours`;
	string $contoursSG = `sets -renderable true -noSurfaceShader true -empty -name propContoursSG`;
	connectAttr -f ($contours+".outColor") ($contoursSG+".surfaceShader");
	setAttr ($contours+".outColor") -type double3 1 1 1 ;
	setAttr ($contoursSG+".miContourEnable") 1;
	setAttr ($contoursSG+".miContourColor") -type double3 0 0 0 ;
	setAttr ($contoursSG+".miContourWidth") 0.05;
	setAttr ($contoursSG+".miContourRelativeWidth") 1;

	sets -e -forceElement $contoursSG $objShape;

	//renderSetting setup
	setAttr "miDefaultFramebuffer.contourEnable" 1;
	editRenderLayerAdjustment "miDefaultFramebuffer.contourEnable";
	setAttr "miDefaultFramebuffer.contourSamples" 3;
	setAttr "miDefaultFramebuffer.contourFilter" 2;
	setAttr "miDefaultFramebuffer.contourClearImage" 1;
	setAttr "miDefaultFramebuffer.contourClearColor" -type double3 1 1 1 ;

	string $contrast = `mrCreateCustomNode -asUtility "" contour_contrast_function_levels`;
	string $store = `mrCreateCustomNode -asUtility "" contour_store_function`;
	defaultNavigation -ce -source $contrast -destination miDefaultOptions.contourContrast;
	defaultNavigation -ce -source $store -destination miDefaultOptions.contourStore;
	setAttr ($contrast+".diff_index") 0;
	setAttr ($contrast+".zdelta") 8;
	setAttr ($contrast+".ndelta") 22;
	
	select -cl;
}