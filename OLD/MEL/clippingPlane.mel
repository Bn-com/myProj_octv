global proc clippingPlane()
{
	string $pane = `getPanel -wf`;

	//figure out the camera in the current view in order to use its values in the window
	string $whichCam = `modelPanel -q -camera $pane`;
	string $whichCamShape[] = `ls -dag -shapes -ap $whichCam`;

	//figure out what cameras to use when building the menu
	string $cameras[] = `ls -ca`;
	string $diffCams[] = stringArrayRemove($whichCamShape, $cameras);

      if (`window -ex clippingPlaneWin`)
          deleteUI clippingPlaneWin;
      window -ip -in "Camera Clipping Plane" -w 400 -h 120 -title "Camera Clipping Plane" clippingPlaneWin;
   
      //build gui
//	columnLayout;
//		frameLayout -borderVisible true -labelVisible true -labelAlign "center" -label "Zoom Options" -marginWidth 5 -marginHeight 5;
			string $formLayout = `formLayout`;
                optionMenuGrp -label "Camera" -columnAlign 1 "right" -columnWidth 1 100 -changeCommand "changeCamera" whichCam;
					menuItem -label $whichCamShape[0];
				int $i;
				for ( $i = 0; $i < size($diffCams); $i++ )
					{
					menuItem -label $diffCams[$i];
					}
				$whichCamAttr = $whichCamShape[0] + ".nearClipPlane";
     			floatSliderGrp -label "Near Plane" -columnWidth 1 100 -adjustableColumn 3 -v `getAttr $whichCamAttr` -min 0.001 -max 10 -precision 4 -step 1 -dc "setClipPlane" -cc "setClipPlane" -field true nearClipPlane;
				$whichCamAttr = $whichCamShape[0] + ".farClipPlane";
				floatSliderGrp -label "Far Plane" -columnWidth 1 100 -adjustableColumn 3 -v `getAttr $whichCamAttr` -min 10 -max 100000 -sliderStep 1000 -dc "setClipPlane" -cc "setClipPlane" -field true farClipPlane;
			setParent ..;

			formLayout -edit
				-attachForm		whichCam	"top"	5
				-attachForm		whichCam	"left"	5
				-attachForm		whichCam	"right"	5

				-attachControl	nearClipPlane		"top"	5	whichCam
				-attachForm		nearClipPlane		"left"	5
				-attachForm		nearClipPlane		"right"	5

				-attachControl	farClipPlane		"top"	5	nearClipPlane
				-attachForm		farClipPlane		"left"	5
				-attachForm		farClipPlane		"right"	5
			$formLayout;

//		setParent ..;
//	setParent ..;

	showWindow;
}

global proc changeCamera()
{
	// get the active viewport

//	string $pane = `getPanel -withFocus`;

//	if (`getPanel -typeOf $pane` == "modelPanel")
//	{
		// change the viewport to the selected camera

		string $cam = `optionMenuGrp -q -v whichCam`;
//		modelPanel -e -camera $cam $pane;

		// set the sliders to be of the camera values

		float $near = `getAttr ($cam + ".nearClipPlane")`;		
		floatSliderGrp -edit -v $near nearClipPlane;
		float $far = `getAttr ($cam + ".farClipPlane")`;		
		floatSliderGrp -edit -v $far farClipPlane;
//	}
//	else
//	{
//		warning("Please select a model panel.");
//	}
}

global proc setClipPlane()
{
	string $cam = `optionMenuGrp -q -v whichCam`;
	string $buffer;

	float $near = `floatSliderGrp -q -v nearClipPlane`;
	float $far = `floatSliderGrp -q -v farClipPlane`;
	string $buffer = $cam + ".nearClipPlane";
	setAttr $buffer $near;
	string $buffer = $cam + ".farClipPlane";
	setAttr $buffer $far;
}




 
