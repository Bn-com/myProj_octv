/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ª∆÷ŸŒ¨
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc string zwScaleImage(string $inFile, string $outFile, float $shrinkFactor)
{
	// get formats
	string $buf[];
	tokenize $inFile "." $buf;
	string $from = `tolower $buf[size($buf)-1]`;
	tokenize $outFile "." $buf;
	string $to = `tolower $buf[size($buf)-1]`;

	// convert $inFile to tif
	string $tempInFile, $cmd;
	if ($from != "tif")
	{
		$tempInFile = `internalVar -userTmpDir` + "scaleImageIn.tif";
		sysFile -delete $tempInFile;
		$cmd = "imgcvt -f " + $from + " -t tif \"" + `substituteAllString $inFile "/" "\\"` + "\" \"" + `substituteAllString $tempInFile "/" "\\"` + "\"";
		system $cmd;
	}
	else
		$tempInFile = $inFile;
	
	// del $outFile
	string $tempOutFile;
	if (`filetest -f $outFile` || $to != "tif")
	{
		$tempOutFile = `internalVar -userTmpDir` + "scaleImageOut.tif";
		sysFile -delete $tempOutFile;
	}
	else
		$tempOutFile = $outFile;

	// scale
	int $resolution[] = `idmtImage -size $inFile`;
	int $width = $resolution[0] / $shrinkFactor;
	int $height = $resolution[1] / $shrinkFactor;

	string $convertExe = `getenv "DEV_PATH"` + "/Others/ImageMagick-5.4.6-Q16/convert.exe";
	$cmd = $convertExe + " \"" + `substituteAllString $tempInFile "/" "\\"` + "\" -resize " + $width + "x" + $height + " \"" + `substituteAllString $tempOutFile "/" "\\"` + "\"";

	system $cmd;
	if ($tempInFile != $inFile)
		sysFile -delete $tempInFile;
	
	// output
	if ($tempOutFile != $outFile)
	{
		if ($to != "tif")
			$cmd = "imgcvt -v -t " + $to + " \"" + `substituteAllString $tempOutFile "/" "\\"` + "\" \"" + `substituteAllString $outFile "/" "\\"` + "\"";
		else
			$cmd = "copy \"" + `substituteAllString $tempOutFile "/" "\\"` + "\" \"" + `substituteAllString $outFile "/" "\\"` + "\"";
		system $cmd;
	}
	if (`filetest -f $tempOutFile`)
	{
		if ($tempOutFile != $outFile)
			sysFile -delete $tempOutFile;
		return $outFile;
	}

	return "";
}