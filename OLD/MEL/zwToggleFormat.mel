global proc int zwToggleFormat(string $from, string $to)
{
	int $dirty = false;

	string $files[] = `ls -type "file"`;
	for ($file in $files)
	{
		string $ext = $to;
		if ($ext == "")
		{
			if (`attributeExists "originalTexture" $file`)
			{
				string $originalTexture = `getAttr ($file + ".originalTexture")`;
				$ext = `match "[^.]+$" $originalTexture`;
			}
			else
			{
				$ext = "iff";
			}
		}

		string $source = `getAttr ($file + ".fileTextureName")`;
		string $dest = `substitute ("[.]" + $from + "$") $source ("." + $ext)`;
		if ($source == $dest)
		{
			continue;
		}
		string $path = `workspace -expandName $dest`;
		if (`filetest -f $path`)
		{
			evalEcho ("setAttr " + $file + ".fileTextureName -type \"string\" \"" + $dest + "\"");
			$dirty = true;
		}
	}

	return $dirty;
}