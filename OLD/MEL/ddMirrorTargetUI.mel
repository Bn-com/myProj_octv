//////////////////////////////////////////////////////////
//
//	Script Name: ddMirrorTargetUI
//	Verision: 0.61 beta
//	Author: Gordon FreeMAN, SZ-CN
//	Date Created: November 12 2005
//	Changed Projcet Date: July 14 2006
//	Last Updated: July 14 2006
//	Update/Change this file at: Julii FreeMAN
//	Note:镜像Target plugin的
//
//////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////
// Procdures Name: ddMTTest
// 
// 测试用过程
// 
//  
//////////////////////////////////////////////////////////
global proc ddMTTest()
{
 polyCube -w 1 -h 1 -d 1 -sx 2 -sy 2 -sz 2 -ax 0 1 0 -tx 1 -ch 1 ;
 duplicate -rr;
 select -r pCube2 ;
 move -rpr -x 2 ;
 select -r pCube2.vtx[0] ;
 move -r -0.198997 -0.0127295 0.208544 ;
 select -r pCube1 pCube2 ;
}

//////////////////////////////////////////////////////////
// Procedure Name: ddMTdefaultCMD
// 设置界面默认参数
// 
// 
//////////////////////////////////////////////////////////
global proc ddMTdefaultCMD()
{
 radioCollection -edit -select ddMT_x ddMTcollection1 ;
 floatField -edit -value 0.001 ddMTPrecisionField ;
 checkBox -edit -value false ddMTMSCheckbox0 ;
 radioCollection -edit -select ddMTms_cx ddMTcollection2 ;
 optionVar -stringValue "ddMTAxis" "ddMTms_cx" ;
 optionVar -floatValue "ddMTPrecision" 0.001 ;
 optionVar -intValue "ddMTSelf" 0 ;
}

global proc ddMTmsCheckBoxCMD(int $switch)
{
 if($switch)
 {
  optionVar -intValue "ddMTSelf" 1 ;
  radioButton -edit -visible true ddMTms_cx ;
  radioButton -edit -visible true ddMTms_mx ;
  radioButton -edit -visible true ddMTms_cy ;
  radioButton -edit -visible true ddMTms_my ;
  radioButton -edit -visible true ddMTms_cz ;
  radioButton -edit -visible true ddMTms_mz ;
 }
 else
 {
  optionVar -intValue "ddMTSelf" 0 ;
  radioButton -edit -visible false ddMTms_cx ;
  radioButton -edit -visible false ddMTms_mx ;
  radioButton -edit -visible false ddMTms_cy ;
  radioButton -edit -visible false ddMTms_my ;
  radioButton -edit -visible false ddMTms_cz ;
  radioButton -edit -visible false ddMTms_mz ;
 }
}

//////////////////////////////////////////////////////////
// Procedure Name: ddMTGetBaseMeshCMD
// 获取baseMesh
// Arguments: null
// 
//////////////////////////////////////////////////////////
global proc ddMTGetBaseMeshCMD()
{
 string $selected[] ;

 $selected = `ls -sl` ;
 textField -edit -text $selected[0] ddMTBaseMeshField ;
 optionVar -stringValue "ddMTBaseMesh" $selected[0] ;
}
//////////////////////////////////////////////////////////
// Procedure Name: ddMirrorTargetCMD
// read interface selected par
// 镜像组合命令
// Arguments: null
//////////////////////////////////////////////////////////
global proc ddMirrorTargetCMD()
{
 int $self,$direction ;
 float $precision ;
 string $selected[],$mirror[],$axis,$baseMesh,$msDirt ;
 
 $baseMesh = `textField -query -text ddMTBaseMeshField` ;
 $axis = `radioCollection -query -select ddMTcollection1` ;
 $msDirt = `radioCollection -query -select ddMTcollection2` ;
 if($baseMesh == "")
 {
  error "please get baseMesh!" ;
 }
 switch ($axis)
 {
 case "ddMT_x" :
	$axis = "x" ;
	break ;
 case "ddMT_y" :
	$axis = "y" ;
	break ;
 case "ddMT_z" :
	$axis = "z" ;
	break ;
 }
 
 switch($msDirt)
 {
  case "ddMTms_cx" :
	$direction = 0 ;
	break ;
  case "ddMTms_mx" :
	$direction = 1 ;
	break ;
  case "ddMTms_cy" :
	$direction = 2 ;
	break ;
  case "ddMTms_my" :
	$direction = 3 ;
	break ;
  case "ddMTms_cz" :
	$direction = 4 ;
	break ;
  case "ddMTms_mz" :
	$direction = 5 ;
	break ;
 }
 $precision = `floatField -query -value ddMTPrecisionField` ;
 $self = `checkBox -query -value ddMTMSCheckbox0` ;

 $selected = `ls -sl` ;

	string $version = `about -version`;	// add by huangzhongwei

 if( $self )
 {
  select -r $baseMesh $selected[0] ;

//	ddMirrorTarget -axis $axis -precision $precision -self true -direction $direction ;	// modify by huangzhongwei

	if (`gmatch $version "6.0*"`)
	{
		jfMirrorTarget -axis $axis -precision $precision -self true -direction $direction;
	}
	else
	{
		ddMirrorTarget -axis $axis -precision $precision -self true -direction $direction;
	}

  select -r $selected[0] ;
 }
 else
 {
  $mirror = `duplicate -rr $baseMesh` ;
  select -r $baseMesh $selected[0] $mirror[0] ;

  //	ddMirrorTarget -axis $axis -precision $precision ;	// modify by huangzhongwei

	if (`gmatch $version "6.0*"`)
	{
		jfMirrorTarget -axis $axis -precision $precision;
	}
	else
	{
		ddMirrorTarget -axis $axis -precision $precision;
	}

  select -r $mirror[0] ;
 }
}

//////////////////////////////////////////////////////////
// ddMirrorTarget UI
// 镜像blendShape Target 命令交互界面
//
// Arguments:null
//
//////////////////////////////////////////////////////////
global proc ddMirrorTargetUI()
{
 if (`window -ex ddMirrorTargetWin`)
 {
  deleteUI ddMirrorTargetWin ;
  if (`window -ex ddMirrorTargetWin`)
  {
   windowPref -remove ddMirrorTargetWin ;
  }
 }
 
 window -widthHeight 400 228
        -title "durdur's mirror blend target v0.91 beta"
        ddMirrorTargetWin ;
 
/* commented by huangzhongwei
 menuBarLayout;
 menu -label "Edit" editmenu;
      menuItem -label "Save Settings" ;
      menuItem -label "Reset Settings" ;
      menuItem -label "Exit" ;
 menu -label "Help" helpmenu ;
      menuItem -label "Help on ddMirrorTarget..." ;
 setParent ..;
*/

 columnLayout -width 400 -adjustableColumn true ;

 rowLayout -numberOfColumns 3
           -columnWidth3 100 200 80
	   -columnAlign3 "right" "center" "center" ;
	text -label "Base Mesh" ;
	textField -annotation "设置基本物体"
		  -changeCommand ("optionVar -stringValue " + "\"" + "ddMTBaseMesh" + "\"" + "`textField -query -text ddMTBaseMeshField`")
		  -width 190
		  ddMTBaseMeshField ;
	button -label "Get"
	       -width 70
	       -command "ddMTGetBaseMeshCMD"
	       ddMTGetButton ;
 setParent.. ;
 separator -width 460 ;
 radioCollection ddMTcollection1;
            string $rb1, $rb2, $rb3 ;
        rowLayout -numberOfColumns 4 
                  -columnWidth4 100 80 80 80
		  -columnAlign4 "right" "center" "center" "center" ;
		text -label "Orientation" ;
		$rb1 = `radioButton -label "X" ddMT_x` ;
		$rb2 = `radioButton -label "Y" ddMT_y` ;
		$rb3 = `radioButton -label "Z" ddMT_z` ;
        setParent.. ;

 radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTAxis" + "\"" + "`radioCollection -query -select ddMTcollection1`") ddMT_x ;
 radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTAxis" + "\"" + "`radioCollection -query -select ddMTcollection1`") ddMT_y ;
 radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTAxis" + "\"" + "`radioCollection -query -select ddMTcollection1`") ddMT_z ;
 
 rowLayout -numberOfColumns 5
           -columnWidth5 100 62 40 150 30
	   -columnAlign5 "right" "center" "center" "center" "center" ;
	text -label "Precision" ;
	floatField -value 0.001
		   -precision 3
		   -annotation "precision 容错精度."
		   -changeCommand ("optionVar -floatValue " + "\"" + "ddMTPrecision" + "\"" + "`floatField -query -value ddMTPrecisionField`")
		   ddMTPrecisionField ;
	text -label "<0.001>" ;
	floatSlider
		       -width 150
		       -minValue 0.001
		       -maxValue 0.01

		       -value 0.001
		       -step 0.00000001
		       -changeCommand ("floatField -edit -value `floatSlider -query -value ddMTPrecisionSlider` ddMTPrecisionField ;optionVar -floatValue " + "\"" + "ddMTPrecision" + "\"" + "`floatField -query -value ddMTPrecisionField`")
		       ddMTPrecisionSlider ;
	text -label "<0.01>" ;
 setParent.. ;

 radioCollection ddMTcollection2 ;
  string $ddMTmsRB1,
	 $ddMTmsRB2,
	 $ddMTmsRB3,
	 $ddMTmsRB4,
	 $ddMTmsRB5,
	 $ddMTmsRB6 ;
 rowLayout -numberOfColumns 8
           -columnWidth 1 100
	   -columnWidth 2 30
	   -columnWidth 3 42
	   -columnWidth 4 42
	   -columnWidth 5 42
	   -columnWidth 6 42
	   -columnWidth 7 42
	   -columnWidth 8 42
	   -columnAlign 1 "right"
	   -columnAlign 2 "center"
	   -columnAlign 3 "center"
	   -columnAlign 4 "center"
	   -columnAlign 5 "center"
	   -columnAlign 6 "center"
	   -columnAlign 7 "center"
	   -columnAlign 8 "center"
	   ;
	text -label "Mirror Self" ;
	checkBox -width 30
		 -label ""
		 -onCommand  "ddMTmsCheckBoxCMD(1)"
		 -offCommand "ddMTmsCheckBoxCMD(0)"
		 ddMTMSCheckbox0 ;
	
	$ddMTmsRB1 = `radioButton -label "+ X" ddMTms_cx` ;
	$ddMTmsRB2 = `radioButton -label "- X" ddMTms_mx` ;
	$ddMTmsRB3 = `radioButton -label "+ Y" ddMTms_cy` ;
	$ddMTmsRB4 = `radioButton -label "- Y" ddMTms_my` ;
	$ddMTmsRB5 = `radioButton -label "+ Z" ddMTms_cz` ;
	$ddMTmsRB6 = `radioButton -label "- Z" ddMTms_mz` ;

	radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTmsOpt" + "\"" + "`radioCollection -query -select ddMTcollection2`") ddMTms_cx ;
	radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTmsOpt" + "\"" + "`radioCollection -query -select ddMTcollection2`") ddMTms_mx ;
	radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTmsOpt" + "\"" + "`radioCollection -query -select ddMTcollection2`") ddMTms_cy ;
	radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTmsOpt" + "\"" + "`radioCollection -query -select ddMTcollection2`") ddMTms_my ;
	radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTmsOpt" + "\"" + "`radioCollection -query -select ddMTcollection2`") ddMTms_cz ;
	radioButton -edit -changeCommand ("optionVar -stringValue " + "\"" + "ddMTmsOpt" + "\"" + "`radioCollection -query -select ddMTcollection2`") ddMTms_mz ;

 setParent.. ;

 separator -width 460 ;

 rowLayout -numberOfColumns 6
           -columnWidth6 98 98 98 98 98 98
	   -columnAlign6 "center" "center" "center" "center" "center" "center" ;
  button -label "Mirror"
         -width 98
         -height 26
         -command "ddMirrorTargetCMD ; deleteUI ddMirrorTargetWin" ;
  button -label "Apply"
         -command "ddMirrorTargetCMD"
         -width 98
         -height 26;
  button -label "Default"
	 -width 98
	 -height 26
	 -command "ddMTdefaultCMD"
	 ddMTdefaultButton ;
  button -label "Close"
	 -command "deleteUI ddMirrorTargetWin;"
	 -width 98
	 -height 26 ;

/* commented by huangzhongwei
  button -label "Load Plug-in"
	 //-command "unloadPlugin ddMirrorTarget.mll"
	 -command "loadPlugin ddMirrorTarget.mll"
	 -width 98
	 -height 26 ;

  button -label "Unload Plug-in"
	 //-command "unloadPlugin ddMirrorTarget.mll"
	 -command "unloadPlugin -force ddMirrorTarget.mll"
	 -width 98
	 -height 26 ;
*/

 setParent .. ;
 separator -width 460 ;
 text "BlendShape" ;
 separator -width 460 ;
 rowLayout -numberOfColumns 4
           -columnWidth4 98 98 98 98
	   -columnAlign4 "center" "center" "center" "center" ;
 /*
 button -label "test"
	 -command "ddMTTest ;"
	 -width 90
	 -height 26 ;
 */
 button -label "BlendShape..."
	 -command "performBlendShape 1 ;"
	 -width 98
	 -height 26 ;

 button -label "Add..."
	 -command "performBlendShapeAdd 1 ;"
	 -width 98
	 -height 26 ;
 button -label "Remove..."
	 -command "performBlendShapeRemove 1 ;"
	 -width 98
	 -height 26 ;
 button -label "BlendEditor..."
	 -command "BlendShapeEditor ;"
	 -width 98
	 -height 26 ;

 setParent .. ;
 setParent .. ;

 //set defaul options
 if(`optionVar -exists "ddMTBaseMesh"`)
 {
  string $baseMeshOpt = `optionVar -query "ddMTBaseMesh"` ;
  if(`objExists $baseMeshOpt`)
  {
   textField -edit -text $baseMeshOpt ddMTBaseMeshField ;
  }
 }
 else
 {
 }
 if(`optionVar -exists "ddMTAxis"`)
 {
  radioCollection -edit -select `optionVar -query "ddMTAxis"` ddMTcollection1 ;
 }
 else
 {
  optionVar -stringValue "ddMTAxis" "ddMT_x" ;
  radioCollection -edit -select ddMT_x ddMTcollection1 ;
 }
 
 if(`optionVar -exists "ddMTPrecision"`)
 {
  floatField -edit -value `optionVar -query "ddMTPrecision"` ddMTPrecisionField ;
 }
 else
 {
  optionVar -floatValue "ddMTPrecision" 0.001 ;
  floatField -edit -value 0.001 ddMTPrecisionField ;
 }

 if(`optionVar -exists "ddMTSelf"`)
 {
  checkBox -edit -value `optionVar -query "ddMTSelf"` ddMTMSCheckbox0 ;
  ddMTmsCheckBoxCMD(`optionVar -query "ddMTSelf"`) ;
 }
 else
 {
  optionVar -intValue "ddMTSelf" 0 ;
  checkBox -edit -value false ddMTMSCheckbox0 ;
  ddMTmsCheckBoxCMD(0) ;
 }
 if(`optionVar -exists "ddMTmsOpt"`)
 {
  radioCollection -edit -select `optionVar -query "ddMTmsOpt"` ddMTcollection2 ;
 }
 else
 {
  optionVar -stringValue "ddMTmsOpt" "ddMTms_cx" ;
  radioCollection -edit -select ddMTms_cx ddMTcollection2 ;
 }

 //waitCursor -state on ;
 //catch(`loadPlugin "ddMirrorTarget.mll"`) ;
 //waitCursor -state off ;
 //loadPlugin "ddMirrorTarget.mll" ;
 //loadPlugin "ddMirrorBlendShape.mll" ;
 //loadPlugin "E:/zhujian_work/Visual Studio Projects/ddMirrorTarget/Debug/ddMirrorTarget.mll" ;
 //("unloadPluginWithCheck( " + "\"" + "F:/work/Visual Studio Projects/ddMirrorTarget/Debug/ddMirrorTarget.mll" + "\" )" )

 showWindow ddMirrorTargetWin ;//$window;

}

//ddMirrorTarget();

//End of file.