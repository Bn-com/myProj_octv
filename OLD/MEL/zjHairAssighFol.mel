/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ’‘÷æΩ‹
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zjHairsetEnable(string $radioButton, string $PercentSlider, string $seed, string $FillSlider)
{
	int $status = `radioButtonGrp -q -select $radioButton`;
	
	floatSliderGrp -e -enable (!($status-1)) $PercentSlider;
	floatFieldGrp -e -enable (!($status-1)) $seed;
	intSliderGrp -e -enable ($status-1) $FillSlider;
}

global proc zjHairAssighFol()
{
	if (`window -q -exists hairAssighFolWin`)
		return;

	window -title "Assign Hair Follicles" -resizeToFitChildren true hairAssighFolWin;
		columnLayout -adjustableColumn true;
			menuBarLayout;
				menu -label "Help";
					menuItem -label "Help on Assign Hair Follicles..." -command "zjHelpCallback zjhairAssighFol";
				setParent ..;
		
		frameLayout -label ""  -collapsable false -borderStyle "etchedIn";
			string $formLayout = `formLayout`;

			string $radioButton = `radioButtonGrp  -numberOfRadioButtons 2 -select 2 -label "" -labelArray2 "random" "even"`;

			string $PercentSlider = `floatSliderGrp -enable 0 -label "Passive Ratio" -field true -minValue 0.0 -maxValue 1.0 -fieldMinValue 0.0 -fieldMaxValue 1.0 -value 0.4`;
			string $seed = `floatFieldGrp  -enable 0 -label "Seed" -value1 0`;

			string $FillSlider = `intSliderGrp  -enable 1 -field true -l "Passive Fill" -min 0 -max 5 -v 3`;
			
			string $button1 = `button -label "Apply" -command ("AssignOrder " + $radioButton + " " + $PercentSlider + " " + $seed + " " + $FillSlider)`;
			string $button2 = `button -label "Close" -command ("deleteUI -window  hairAssighFolWin")`;
			
			radioButtonGrp	-e -changeCommand ("zjHairsetEnable " + $radioButton + " " + $PercentSlider + " " + $seed + " " + $FillSlider) $radioButton;
			
			setParent ..;

			formLayout -edit
				-attachForm		$radioButton		"top"		5
				-attachForm		$radioButton		"left"		15
				-attachForm		$radioButton		"right"		5
				
				-attachControl		$PercentSlider		"top"		5	$radioButton
				-attachForm		$PercentSlider		"left"		5
				-attachForm		$PercentSlider		"right"		5
			
				-attachControl		$seed			"top"		5	$PercentSlider
				-attachForm		$seed			"left"		5
				-attachForm		$seed			"right"		5
 			
				-attachControl		$FillSlider		"top"		10	$seed
				-attachForm		$FillSlider		"left"		5
				-attachForm		$FillSlider		"right"		5

				-attachControl		$button1		"top"		5	$FillSlider
				-attachForm		$button1		"left"		0
				-attachPosition		$button1		"right"		0	50

				-attachControl		$button2		"top"		5	$FillSlider
				-attachControl		$button2		"left"		5	$button1
				-attachPosition		$button2		"right"		0	100

			$formLayout;
		setParent ..;


	showWindow hairAssighFolWin;
}


// create paintEfx hair
global proc AssignOrder(string $rad, string $ctr1, string $ctr2, string $ctr3)
{
	int $status = `radioButtonGrp -q -select $rad`;
	
	string $gp[] = `ls -sl`;
	string $systemFollicles[] = `ls -sl -ni -dag -type follicle`;
	
	if ($status == 1)
	{
		float $percent = `floatSliderGrp -q -value $ctr1`;
		float $seed = `floatFieldGrp -q -value1 $ctr2`;
		
		seed($seed);
		for ($item in $systemFollicles)
		{
			setAttr ($item + ".simulationMethod") 2;
			if (rand(1)<=$percent)
			{
				setAttr ($item + ".simulationMethod") 1;
			}
		}
	}
	else
	{
		int $fill = `intSliderGrp -q -value $ctr3`;
		for ($item in $systemFollicles)
		{
			setAttr ($item + ".simulationMethod") 2;
		}

		
		if (($fill > 0)&&($fill <5))
		{
			int $iter = 10/$fill;
			for ($i = 0; $i<size($systemFollicles); $i+= $iter)
			{
				setAttr ($systemFollicles[$i] + ".simulationMethod") 1;
			}
		}
		else if ($fill == 5)
		{
			for ($i = 0; $i<size($systemFollicles); $i++)
			{
				setAttr ($systemFollicles[$i] + ".simulationMethod") 1;
			}
		}
	}
	
	select -clear;
	select $gp;
}
