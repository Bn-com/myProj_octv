//////////////////////////////////////////////////////////
//
//	Script Name: ddSpaceKey
//	Verision: 0.01 beta
//	Author: Julii FreeMAN, SZ-CN
//	Date Created: January 23 2007
//	Changed Projcet Date:
//	Last Updated: January 23 2007
//	Update/Change this file at: Julii FreeMAN
//	Note: 间隔key
//		
//		
//		
//////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////
// Procdures Name: ddSpaceKey
//	间隔key
//
// Arguments :
//	float $space 间隔
//	float $startFrame 起始帧
//	float $endFrame 结束帧
//	string $object[] 操作对象
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddSpaceKey(float $space,float $startFrame,float $endFrame,string $object[])
{
 int $loop = 0 ;
 float $currentFrame = 0.0 ;

// $currentFrame = $startFrame ;
 do
 {
  $currentFrame = $startFrame + $space * $loop++ ;
  setKeyframe -time $currentFrame -breakdown 0 -hierarchy none -controlPoints 0 $object ;
//  print ($currentFrame + "\n") ;
 }
 while ($currentFrame < $endFrame);

}

//////////////////////////////////////////////////////////
// Procdures Name: ddSpaceKeyCMD
//	
//
// Arguments : NULL
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddSpaceKeyCMD()
{
 float $space,$startFrame,$endFrame ;
 string $selected[] ;
 $selected = `ls -sl` ;
 $space = `floatField -query -value ddSKSpaceField` ;
 $startFrame = `floatField -query -value ddSKStartFrameField` ;
 $endFrame = `floatField -query -value ddSKEndFrameField` ;

 ddSpaceKey $space $startFrame $endFrame $selected ;
}

//////////////////////////////////////////////////////////
// Procdures Name: ddSpaceKeyUI
//	用户界面
//
// Arguments : NULL
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddSpaceKeyUI()
{
 if ((`window -ex ddSpaceKeyWin`) == true)
 {
  deleteUI ddSpaceKeyWin ;
  if(`windowPref -exists ddSpaceKeyWin`)
  {
   windowPref -remove ddSpaceKeyWin ;
  }
 }
 window -widthHeight 372 165
	-title "durdur's Space Key beta 0.16"
	ddSpaceKeyWin ;

 menuBarLayout ;
	menu -label "Edit" editmenu ;
		menuItem -label "Reset Settings"
			-command "floatField -edit -value \"3\" ddSKSpaceField ;" ;
	menu -label "Help" helpmenu ;
		menuItem -label "Help on Amation Panel ..."
			-command ("system \(\"load Z:/Resource/Groups/Production/setup/Tutorial Movie/spaceKey/spaceKey.avi\"\)") ; ;
	setParent .. ;

 columnLayout
//	-columnAttach "both" 12
	-columnAlign "center"
	-adjustableColumn true
	-rowSpacing 2 ;
//	separator ;
	rowLayout
		-numberOfColumns 2
		-columnWidth2 100 100
		-columnAlign 1 "right" 
		-columnAlign 2 "left" ;
		text "Space" ;
		floatField
			-editable true
			-width 90
			-changeCommand "optionVar -floatValue \"ddSKSpace\" `floatField -query -value ddSKSpaceField`"
			ddSKSpaceField ;
	setParent .. ;

	rowLayout
		-numberOfColumns 2
		-columnWidth2 100 100
		-columnAlign 1 "right" 
		-columnAlign 2 "left" ;
		text "Start Frame" ;
		floatField
			-editable true
			-width 90
			ddSKStartFrameField ;
	setParent .. ;

	rowLayout -numberOfColumns 2
		-columnWidth2 100 100
		-columnAlign 1 "right" 
		-columnAlign 2 "left" ;
		text "End Frame" ;
		floatField
			-editable true
			-width 90
			ddSKEndFrameField ;
	setParent .. ;

	separator ;

	rowLayout
		-numberOfColumns 3
		-columnWidth3 122 122 122
		-columnAlign 1 "center"
		-columnAlign 2 "center"
		-columnAlign 3 "center" ;

		button -label "Space keys"
				-width 120
				-height 28
				-align "center"
				-annotation ""
				-command "ddSpaceKeyCMD ;deleteUI ddSpaceKeyWin ;" ;

		button -label "Apply"
				-width 120
				-height 28
				-align "center"
				-annotation ""
				-command "ddSpaceKeyCMD ;" ;

		button -label "Close"
					-width 120
				-height 28
				-align "center"
				-annotation ""
				-command "deleteUI ddSpaceKeyWin ;" ;

 showWindow ddSpaceKeyWin ;

 //默认参数
 if(`optionVar -exists "ddSKSpace"`)
 {
  floatField
	-edit
	-value `optionVar -query ddSKSpace`
	ddSKSpaceField ;
 }
 else
 {
  optionVar -floatValue "ddSKSpace" 3 ;
  floatField
	-edit
	-value "3.0"
	ddSKSpaceField ;
 }

 floatField
	-edit
	-value `playbackOptions -query -min`
	ddSKStartFrameField ;

 floatField
	-edit
	-value `playbackOptions -query -max`
	ddSKEndFrameField ;

}

//End of file.