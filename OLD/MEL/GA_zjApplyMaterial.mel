
///
/// Author: 赵志杰
///
/// Creation Date: 2006/11/15
///
///修改：韩虹（添加了MODEL组识别
/// Description: 
///

///////////
/// new methold was made by:edward.Sun
///
///add Date:2013:05:30
///////////

//////
///@Update_Change: Justin.CHan
///@LastUpdated: 2016-08-12
///@Description: distinguishing between the Shape and ShapeDeformed
/////

global proc GA_zjApplyMaterial()
{
	global int $mat_check = 0;
	global int $transuv_check = 1;
	global int $transmat_check = 1;
	global int $transuv_check1 = 1;
	global int $transmat_check1 = 1;
	global int $transmat_localWithouthis = 0;
	global int $transuv_model=1;

	global int $zjApplyMaterial_selected = 0;
    global int $zjApplyMaterial_history=1;
	int $i;
	string $child[];

	if (`window -q -exists mushconvMatWin`)
		deleteUI mushconvMatWin;

	window -title "Transfer Material" -resizeToFitChildren true mushconvMatWin;

		columnLayout -adjustableColumn true;
			menuBarLayout;
				menu -label "Help";
					menuItem -label "Help on Transfer Material..." -command "GA_zjHelpCallback GA_zjApplyMaterial";
				setParent ..;
    			frameLayout -label "For Scene Use" -labelAlign "top" -cll true
    				-borderStyle "etchedIn";

				$i = 0;

				string $formLayout = `formLayout`;
 					$child[$i++] = `textFieldGrp -columnWidth 1 100 -adjustableColumn 2 -label "Mapping File:" -editable false -text ""`;
					$child[$i++] = `button -label "Browse" -w 80 -command ("GA_mll_brow " + $child[0])`;
					$child[$i++] = `checkBox -label "Transfer UVs" -value $transuv_check -changeCommand "$transuv_check = #1"`;
					$child[$i++] = `checkBox -label "Transfer Material" -value $transmat_check -changeCommand "$transmat_check = #1"`;
					$child[$i++] = `checkBox -label "MODEL" -value $transuv_model  -changeCommand "$transuv_model = #1"`;
					$child[$i++] = `checkBox -label "With Full Path" -value $mat_check -changeCommand "$mat_check = #1"`;
					$child[$i++] = `checkBox -label "For Selected Objects Only" -value $zjApplyMaterial_selected -changeCommand "$zjApplyMaterial_selected = #1"`;
					$child[$i++] = `checkBox -label "transfer by local without history" -value $zjApplyMaterial_history -changeCommand "$zjApplyMaterial_history = #1"`;
					$child[$i++] = `button -label "Apply" -command ("GA_zjApplyMaterialTransferUVMatApply " + $child[0])`;
					
					//print $child[0];
				setParent ..;

				formLayout -edit
					-attachForm		$child[0]	"top"		10
					-attachForm		$child[0]	"left"		0
					-attachControl		$child[0]	"right"		5	$child[1]
					
					-attachForm		$child[1]	"top"		10
					-attachForm		$child[1]	"right"		5

					-attachControl		$child[2]	"top"		15	$child[1]
					-attachForm		$child[2]	"left"		25
					
                    -attachControl		$child[3]	"top"		15	$child[1]
					-attachControl		$child[3]	"left"		30	$child[2]

                    -attachControl		$child[4]	"top"		15	$child[1]
					-attachControl		$child[4]	"left"		30	$child[3]

					-attachControl		$child[5]	"top"		15	$child[2]
					-attachForm		$child[5]	"left"		25

					-attachControl	$child[6]	"top"		15	$child[2]
					-attachControl	$child[6]	"left"		30	$child[5]

					-attachControl	$child[7]	"top"		15	$child[2]
					-attachControl	$child[7]	"left"		30	$child[6]
					
					-attachControl		$child[8]	"top"		15	$child[6]
					-attachForm		$child[8]	"left"		5
					-attachForm		$child[8]	"right"		5

				$formLayout;

				setParent ..;

    			frameLayout -label "For Selected Objects Use" -labelAlign "top" -cll true
    				-borderStyle "etchedIn";
				$i = 0;

				string $formLayout = `formLayout`;
					$child[$i++] = `checkBox -label "Transfer UVs" -v $transuv_check1 -cc "$transuv_check1 = #1"`;
					$child[$i++] = `checkBox -label "Transfer Material" -v $transmat_check1 -cc "$transmat_check1 = #1"`;
					$child[$i++] = `button -label "Apply(Select both the source object and the destination object, in that order.)" -command ("GA_TransferUVMatSel " + $child[0] + " " + $child[1])`;

				setParent ..;

				formLayout -edit

					-attachForm		$child[0]	"top"		15	
					-attachForm		$child[0]	"left"		25
					
					-attachForm		$child[1]	"top"		15	
					-attachControl		$child[1]	"left"		30	$child[0]

					-attachControl		$child[2]	"top"		10	$child[0]
					-attachForm		$child[2]	"left"		5
					-attachForm		$child[2]	"right"		5
				$formLayout;

				setParent ..;

			frameLayout -label "For Lighting File Use" -labelAlign "top" -collapsable true -borderStyle "etchedIn";
				string $formLayout = `formLayout`;
 					string $textFieldGrp = `textFieldGrp -label "Material Rar File:" -editable false -columnWidth 1 100 -adjustableColumn 2`;
					string $symbolButton = `symbolButton -image "navButtonBrowse.xpm" -command ("GA_zjApplyMaterialBrowse " + $textFieldGrp)`;
					string $button = `button -label "Apply" -command ("GA_zjApplyLightingMaterial " + $textFieldGrp)`;
					setParent ..;
				formLayout -edit
					-attachForm		$textFieldGrp	"left"		0
					-attachOppositeControl	$textFieldGrp	"bottom"	0	$button
					-attachControl		$textFieldGrp	"right"		0	$symbolButton

					-attachOppositeControl	$symbolButton	"bottom"	0	$button
					-attachControl		$symbolButton	"right"		30	$button

					-attachForm		$button		"top"		10
					-attachForm		$button		"right"		5
				$formLayout;
				setParent ..;

	showWindow mushconvMatWin;
}

global proc GA_edo_setAllVertexZero()
{
    string $meshes[]=`ls -type "mesh"`;
    if (size($meshes)>0)
    {
        for ($mesh in $meshes)
        {
            //string $mesh=$meshes[0];
            //print ($mesh+"\n");
            int $vnums[]=`polyEvaluate -v  $mesh`;
            int $vnum=$vnums[0];
            int $i=0;
            for ($i=0;$i<$vnum;$i++)
            {
                string $vtx=  $mesh + ".pnts["+string($i)+"]";
                //print ($vtx+"\n");
                setAttr ($vtx+".pntx") 0;
                setAttr ($vtx+".pnty") 0;
                setAttr ($vtx+".pntz") 0;
            };
        };
    };
};

// by huangzhongwei
global proc GA_zjApplyMaterialBrowse(string $textFieldGrp)
{
	string $file = `fileDialog -directoryMask "*.rar"`;
	if (`filetest -f $file`)
	{
		if (`gmatch $file "*.rar"`)
			textFieldGrp -edit -fileName $file $textFieldGrp;
		else
			warning "Not a rar file.";
	}
}

// by huangzhongwei
global proc GA_zjApplyLightingMaterial(string $textFieldGrp)
{
	string $selected[] = `ls -long -sl`;

	string $file = `textFieldGrp -query -fileName $textFieldGrp`;
	if (`filetest -f $file`)
	{
		string $buf[];
		tokenize $file "/" $buf;
		string $filename = `substring $buf[size($buf)-1] 1 (size($buf[size($buf)-1])-3)` + "mb";
	//	string $filename = `dirname $file`;
	//	$filename = `substring $file (size($filename)+2) (size($file)-3)` + "mb";
		string $folder = `internalVar -userAppDir` + "projects/default/scenes";

		string $cmd = substituteAllString(`getenv "DEV_PATH"`, "/", "\\") + "\\Others\\WinRar\\Rar.exe x -o+ \"" + `substituteAllString $file "/" "\\"` + "\" \"" + `substituteAllString $folder "/" "\\"` + "\"";
		// waitCursor -state on;
		system $cmd;
		catch(`TransferUVMat ($folder + "/" + $filename) false true false false $selected`);
		// waitCursor -state off;

		sysFile -delete ($folder + "/" + $filename);
	}
};

global proc GA_zjApplyMaterialTransferUVMatApply(string $textFieldGrp)
{
    //string $textFieldGrp="mushconvMatWin|columnLayout66|frameLayout87|formLayout169|textFieldGrp11";
    //print $textFieldGrp;
	string $renderLayers[] = `listConnections "renderLayerManager.renderLayerId"`;
	if (size($renderLayers)>1)
	{
		string $confresult = `confirmDialog -title "Confirm" -message "Render Layer Exists! Continue Anyway?"
			-button "Yes" -button "No" -defaultButton "No"
			-cancelButton "No" -dismissString "No"`;
		if ($confresult == "No")
			return;
	};
	
	global int $transuv_check;
	global int $transmat_check;
	global int $mat_check;
	global int $zjApplyMaterial_selected;
	global int $transmat_localWithouthis;
	global int $transuv_model;
	

	string $selected[] = `ls -long -sl`;
    //string $filename="E:/mayaProject/test_project/scenes/bbb.ma";
	string $filename = `textFieldGrp -q -fileName $textFieldGrp`;
	// waitCursor -state on;
	//system ("D:\\Alias\\MAYA6~1.0\\bin\\mayabatch.exe -command \"zjAMbatchOutput \\\"\\\"\" -file " + "\"" + $filename + "\"");
	string $mayalocation =`getenv "MAYA_LOCATION"`;
	string $cmd = ("\"\"" + $mayalocation + "/bin/mayabatch.exe \" -command \"zjAMbatchOutput \\\"\\\"\" -file " + "\"" + $filename + "\"\"");
	system $cmd;
	GA_TransferUVMat $filename $transuv_check $transmat_check $mat_check $zjApplyMaterial_selected  $transuv_model $selected;
	//print $filename;
	//print $transuv_check;
	//print $transmat_check;
	//print $mat_check;
	//print $zjApplyMaterial_selected;
	//print $selected;
	// waitCursor -state off;
	if ($transmat_localWithouthis==1)
	{
	    GA_edo_setAllVertexZero();
	    flushUndo;
	};
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//for scene use
global proc GA_mll_brow(string $control)
{
	fileBrowserDialog -mode 0  -fileCommand ("GA_mll_browProc " + $control) -fileType "mayaBinary" -actionName "Browse" -operationMode "Reference";
}

global proc int GA_mll_browProc(string $control, string $filename, string $fileType)
{
	textFieldGrp -e -text $filename $control;
	return true;
}

//get namespace of imported things
proc string GA_getnamespace()
{
	string $namesp;
	string $imobj[] = `ls -shapes "ImUVs*:*"`;
	string $buf[];
	tokenize $imobj[0] ":" $buf;
	$namesp = $buf[0];
	return $namesp;
}

//test whether there is a UVMinSmooth node on given object
proc int GA_getsmooth(string $obj)
{
	string $his[] = `listHistory -pruneDagObjects true $obj`;
	for ($i=0;$i<size($his);$i++ )
	{
		if (`gmatch $his[$i] "UVMinSmooth*"`) 
			return 1;
	}
	return 0;
}

//if object has UVMinSmooth attr, smooth it firstly.
proc GA_smoothBeforetransUVs()
{
	print "UV MIN SMOOTH\n";
	string $namesp = GA_getnamespace()+":";

	string $imobj[] = `ls -tr ($namesp+"*")`;

	for ($i=0;$i<size($imobj);$i++ )
	{
		string $lS[] = `listAttr -userDefined -string UVMinSmooth $imobj[$i]`;
		if (size($lS) == 1)
		{
			int $value = `getAttr ($imobj[$i]+".UVMinSmooth")`;
			string $buf[];
			tokenize $imobj[$i] "|" $buf;
			$imtr = $buf[size($buf) - 1];
			tokenize $imtr ":" $buf;
			$mobj = $buf[size($buf) - 1];

			string $matchobj[] = `ls -type "transform" $mobj`;

			for ($obj in $matchobj)
			{
				$UVsm = GA_getsmooth($obj);
				if ($UVsm == 0)
				{
					string $shapes[] = `listRelatives -fullPath -shapes -type "mesh" $obj`;
					if (size($shapes)>0)
					{
						$smname = `polySmooth -divisions $value -continuity 1 -keepBorder 0 -keepSelectionBorder 1 -keepTessellation 1 -constructionHistory 1 $shapes[0]`;
						rename $smname "UVMinSmooth";
						print("\n	Set UVMinSmooth on: "+$obj);
					}
				}
			}
		}

	}
}

proc string GA_mySubstitute(string $sources, string $old, string $new)
{
	string $buf[];
	
	if (`tokenize $sources "|" $buf`)
		return ` substitute $old $buf[size($buf)-1]  ""`;
	return "";
}

//remove namespace
proc GA_docleannamespace()
{
	string $namesp[] = `ls "ImUVs*:*"`;
	for ($j=0;$j<size($namesp);$j++)
	{
		string $buff[];
		tokenize $namesp[$j] "|" $buff;
		string $obj = $buff[size($buff) - 1];

		string $buf[];
		tokenize $obj ":" $buf;
		string $sp = $buf[0]+":";

		string $new = GA_mySubstitute($obj,$sp,"");
		if (`objExists($namesp[$j])` && $new!=$namesp[$j] && size($new)>0)
			catch(`rename $namesp[$j] $new`);
	}
}

//remove namespace
proc GA_multibodydocleannamespace(string $group)
{
	string $namesp[] = `listRelatives -allDescendents -fullPath $group`;
	for ($j=0;$j<size($namesp);$j++)
	{
		string $buff[];
		tokenize $namesp[$j] "|" $buff;
		string $obj = $buff[size($buff) - 1];

		string $buf[];
		tokenize $obj ":" $buf;
		string $sp = $buf[0]+":";

		string $new = GA_mySubstitute($obj,$sp,"");
		if (`objExists($namesp[$j])` && $new!=$namesp[$j] && size($new)>0)
			rename $namesp[$j] $new;
	}
}


proc string GA_getBasegroup(string $tr)
{
	string $grpname = $tr;
	string $rel[] = `listRelatives -p -fullPath -type "transform" $tr`;
	while(size($rel)>0) 
	{
		$grpname=$rel[0];
		$rel = `listRelatives -p -fullPath -type "transform" $rel[0]`;
	}
	return $grpname;
}

global proc GA_zwTransferMaterialPlace3d()
{
	print "TRANSFER PLACE3D\n";
	string $namesp = GA_getnamespace() + ":";
	string $buf[] = `ls -long -type "place3dTexture" ($namesp + "*")`;
	for ($place3dTexture in $buf)
	{
		string $buf1[] = `listRelatives -parent -fullPath $place3dTexture`;
		if (size($buf1))
		{
			if (`tokenize $buf1[0] "|" $buf1` > 1)
			{
				tokenize $buf1[size($buf1)-1] ":" $buf1;
				$buf1 = `ls -long -type "transform" $buf1[size($buf1)-1]`;
				if (size($buf1))
					parent $place3dTexture $buf1[0];
			}
			else
				parent -world $place3dTexture;
		}
	}
}

//test whether there is a PolySmoothFace node on given object
proc string GA_getfinalsmooth(string $obj)
{
	string $his[] = `listHistory -pruneDagObjects true $obj`;
	for ($i=0;$i<size($his);$i++ )
	{
		if (`gmatch $his[$i] "finalSmooth*"`) 
			return $his[$i];
	}
	return "";
}

//de-apply all smooth objects
proc GA_DeapplySmooth(string $list[])
{
	for ($mesh in $list)
	{
		$poly = GA_getfinalsmooth($mesh); 
		if ($poly!= "")
		{
			int $value = `getAttr ($poly+".divisions")`;
			if ($value != 0)
				setAttr ($poly+".divisions") 0 $mesh;
		}
	}
}

//function to connect uvChooser
proc GA_ConnetUvChooser(int $state)
{
	print "CONNECT UVCHOOSER\n";

	$filename = (`internalVar -userAppDir` + "projects/default/scenes/tmpuvch.txt");
	string $prefix = GA_getnamespace()+":";

	$fileID = `fopen $filename r`;

	if ($fileID == 0)
	{
		// cannot open the file
		error("Fail to open " + $filename);
	}

	string $buffer = `fgetline $fileID`;
	$buffer = substituteAllString($buffer, "\n", "");

	while (size($buffer) > 0) 
	{
		string $buf[];

		if (`tokenize $buffer " " $buf` == 2)
		{

			string $SG = $prefix + $buf[1];

			if (!`objExists $SG`)
			{
				warning($SG + " does not exist.");
				$buffer = `fgetline $fileID`;
				$buffer = substituteAllString($buffer, "\n", "");
				continue;
			}

			string $shape = $buf[0];
			if ($state == 0)
			{
				string $pbuf[];
				tokenize $shape "|" $pbuf;
				$imshape = $pbuf[size($pbuf) - 1];

				string $matchmesh[] = `ls -noIntermediate $imshape`;

				//to display unused object
//				if (size($matchmesh)==0)
//					print ("\n	Mapping object does not connect uvChooser: " + $imshape);

				for ($curmesh in $matchmesh)
					connectAttr -f $curmesh $SG;

			}
			else 
			{
				string $matchmesh[] = `ls -noIntermediate $shape`;

				//to display unused object
//				if (size($matchmesh)==0)
//					print ("\n	Mapping object does not connect uvChooser: " + $shape);

				for ($curmesh in $matchmesh)
					connectAttr -f $curmesh $SG;
			}

		}
		
		$buffer = `fgetline $fileID`;
		$buffer = substituteAllString($buffer, "\n", "");
	}
	fclose $fileID;
}


global proc GA_TransferUVMat(string $filename, int $uvstate, int $matstate, int $pathstate, int $selectedOnly, int $transuv_model,string $selected[])
{
	if ($uvstate || $matstate)
	{
		if (`filetest -f $filename`)
		{
			cycleCheck -e off;

			//import material scene
			string $imtrans[] = `ls -type "mesh" -noIntermediate "ImUVs*:*"`;
			if (size($imtrans) == 0)
			{
				file -import -namespace "ImUVs" -renameAll true -groupReference $filename;
			}

			print "APPLY MATERIAL:\n";
			print "------------------------------------------------------------------------------------------\n";
			
			//transfer uv
			if ($uvstate)
			{
				GA_smoothBeforetransUVs();
				string $list[] = `ls -dagObjects -type mesh -noIntermediate`;
				GA_DeapplySmooth($list);
				//int $selectedOnly=0;
				//int $pathstate=0;
				//string $selected=["pSphere1"];
				GA_zwTransferMaterialUV $pathstate $selectedOnly $transuv_model $selected;
				//connect uvChooser
				//ConnetUvChooser($pathstate);
			}
			//apply shadingGroup
			if ($matstate)
			{
				GA_zwTransferMaterialPlace3d();
				GA_zwTransferMaterialSG $pathstate $selectedOnly  $transuv_model $selected;
			}

			//here for 3body file
			print "DEAL WITH 3BODY PART:\n";
			string $grp[] = `ls -tr "ImUVs*:texture_reference"`;
			if (`objExists $grp[0]`)
			{
				//reconnect reference and shapes
				parent -world $grp[0];
				GA_multibodydocleannamespace($grp[0]);
			}

			string $imtrans[] = `ls -shapes "ImUVs*:*"`;
			string $impobj = getTransform($imtrans[0]);
			if (`objExists $impobj`)
			{
				string $grpname = GA_getBasegroup($impobj);
				delete $grpname;
			}

			GA_docleannamespace();

			//deal with ShadingSwitch
			string $trswich[] = `ls -type tripleShadingSwitch`;
			string $douSw[] = `ls -type doubleShadingSwitch`;
			string $sinSw[] = `ls -type singleShadingSwitch`;
			if (size($trswich)>0||size($douSw)>0||size($sinSw)>0)
				zjAMConswitch();

			//re-arrange final smooth nodes
			zjArrangeNodes();

			GA_ConnectRefandShape();

			print "------------------------------------------------------------------------------------------\n";
			print "Apply Material\n";
			
			cycleCheck -e on;
		}
	}
}


proc int GA_Isinstanceshape(string $mesh)
{
	string $ins[] = `listRelatives -fullPath -allParents -type "transform" $mesh`;
	if (size($ins)>1)
		return 1;
	else
		return 0;
}

//To get all the Transforms from a Shape node:
proc string[] GA_getAllTransforms( string $shape )
{
	string $transforms[];

	if ( "transform" != `nodeType $shape` )
	// If given node is already a transform, just pass on through
	{
		string $parents[] = `listRelatives -fullPath -allParents -type "transform" $shape`;
		$transforms = $parents;
	}

	return $transforms;
}


//To get the instances of a Transform
proc string[] GA_getTranswithSameShape( string $xform )
{
	string $transforms[];
	string $shapes[] = `listRelatives -fullPath -shapes -noIntermediate -type "mesh" $xform`;

	for ($s in $shapes)
	{
		string $buf[];
		tokenize $s "|" $buf;
		string $sh = $buf[size($buf) - 1];

		string $meshes[] = `ls -type "mesh" $sh`;
		for ($k in $meshes)
		{
			string $obj[] = GA_getAllTransforms($k);
			if (size($obj) != 0)
			{
				appendStringArray($transforms, $obj, size($obj));
			}
		}
	}

	$transforms = stringArrayRemoveDuplicates($transforms);
	return $transforms;
}
//function to connect reference&shape
global proc GA_ConnectRefandShape()
{
	print "TRANSFER MATERIAL\n";

	$filename = (`internalVar -userAppDir` + "projects/default/scenes/tmpref.txt");

	$fileID = `fopen $filename r`;

	if ($fileID == 0)
	{
		// cannot open the file
		error("Fail to open " + $filename);
	}

	string $buffer = `fgetline $fileID`;
	$buffer = substituteAllString($buffer, "\n", "");

	while (size($buffer) > 0) 
	{
		string $buf[];

		if (`tokenize $buffer " " $buf` == 2)
		{
			// apply the material to the shape

			string $SG = $buf[0];

			string $shape = $buf[1];
			if (`objExists $SG`&&`objExists $shape`)
			{
				connectAttr -f ($SG+".message") ($shape+".referenceObject");
			}
			else if (`objExists $SG`)
				delete $SG;
		}
		
		$buffer = `fgetline $fileID`;
		$buffer = substituteAllString($buffer, "\n", "");
	}
	fclose $fileID;
}

global proc GA_zwTransferMaterialSG(int $fullPath, int $selectedOnly,int $transuv_model, string $sel[])
{
	string $vet, $buf[], $buf1[];

	print "TRANSFER MATERIAL\n";

	string $selected[] = `ls -long -dagObjects $sel`;

	if ($transuv_model!=0)
	{
		string $nomodList[];
		for ($i=0; $i<size($selected); $i++)
		{
			if (`match  "MODEL|" $selected[$i]`=="")
			{
				string $nomod[];
				$nomod=stringToStringArray($selected[$i],"");
				appendStringArray($nomodList, $nomod, 2);
			}

		}
		string $objs[]=stringArrayRemove($nomodList, $selected);
		$selected=$objs;
	}

	string $prefix = GA_getnamespace() + ":";

	string $filename = (`internalVar -userAppDir` + "projects/default/scenes/tmpmat.txt");
	int $fileId = `fopen $filename r`;
	if (!$fileId)
		error ("Fail to open " + $filename);

	while (!`feof $fileId`) 
	{
		string $line = `fgetline $fileId`;
		$line = `substitute "\n" $line ""`;
		if (`tokenize $line " " $buf` == 2)
		{
			string $SG = $prefix + $buf[0];
			if (!`objExists $SG`)
			{
				warning ($SG + " does not exist.");
				continue;
			}

			string $shape = $buf[1];
			int $size = `tokenize $shape "." $buf1`;
			$shape = $buf1[0];
			if ($size == 2)
				$vet = "." + $buf1[1];
			else
				$vet = "";
			
			if ($fullPath)
			{
				if (`objExists $shape`)
				{
					if (!$selectedOnly || `stringArrayCount $shape $selected`)
						if (`objExists ($shape + $vet)`)
							sets -edit -forceElement $SG ($shape + $vet);
				}
			}
			else
			{
				tokenize $shape "|" $buf;
				$shape = $buf[size($buf)-1];

				string $dests[] = `ls -long -transforms -shapes -noIntermediate $shape`;
				for ($i=0; $i<size($dests); $i++)
				{
					
					if (`nodeType $dests[$i]` == "mesh" || `nodeType $dests[$i]` == "nurbsSurface"|| `nodeType $dests[$i]` == "subdiv")
					{
						if (GA_Isinstanceshape($dests[$i]))
						{
							$buf1 = `listRelatives -fullPath -allParents $dests[$i]`;
							for ($j=0; $j<size($buf1); $j++)
							{
								if (!$selectedOnly || `stringArrayCount $buf1[$j] $selected`)
									if (`objExists ($buf1[$j] + $vet)`)
										sets -edit -forceElement $SG ($buf1[$j] + $vet);
							}
						}
						else
						{
							if (!$selectedOnly || `stringArrayCount $dests[$i] $selected`)
								if (`objExists ($dests[$i] + $vet)`)
									sets -edit -forceElement $SG ($dests[$i] + $vet);
						}
					}
					else if (`nodeType $dests[$i]` == "transform")
					{
						$buf1 = GA_getTranswithSameShape($dests[$i]);
						for ($m in $buf1)
						{
							string $sp[] = `listRelatives -fullPath -shapes -type "mesh" $m`;
							if (size($sp))
							{
								if (!$selectedOnly || `stringArrayCount $m $selected`)
								{
									if (`objExists ($m + $vet)`)
									{
										//sets -edit -forceElement $SG ($m + $vet);
										// modified by huangzhongwei@idmt.com.cn, 2013-08-07
										string $cmd = "sets -edit -forceElement \"" + $SG + "\" \"" + $m + $vet + "\"";
										if (catch(`eval $cmd`))
										{
											print ($cmd + "\n");
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}
	fclose $fileId;
}

//function to transfer UV
global proc GA_myPolyTransfer(string $sources, string $dests)
{
	// add by huangzhongwei@idmt.com.cn, 2013-08-07
	GA_zwTransferMaterialUnlock $dests;

	global int $transmat_localWithouthis;
	//string $sources= $source[$i];
	//string $dests= $dest[$j];
	int $uvcount[] = `polyEvaluate -uvcoord $sources`;
	//to display undone mesh
	int $imvcount[] = `polyEvaluate -vertex $sources`;
	int $matchvcount[] = `polyEvaluate -vertex $dests`;
	if ($transmat_localWithouthis==1)
	{
		print ("local transfer methold is ... "+string($transmat_localWithouthis));
		print ("\n	Number of vertices is unmatched: "+$dests);
		print ("dest is   " + $dests+"\n");
	    	string $pas[]=`listRelatives -p -pa $dests`;
	    	print  ("dest parent is  " + $pas[0]+"\n");
	    	string $temptransfer[]=`duplicate -n ("EDO_TEMPTRANSFER_"+$pas[0]) $dests`;
	    	string $tempshape[]=`listRelatives -s $temptransfer[0]`;
	    	print ("temp transfer is "+ $temptransfer[0]+"\n");
	    	$trfname = `transferAttributes -transferPositions 0 -transferNormals 0 -transferUVs 2 -transferColors 2 -sampleSpace 1 -sourceUvSpace "map1" -targetUvSpace "map1" -searchMethod 3-flipUVs 0 -colorBorders 1  $sources $temptransfer[0]`;
	    	select -r $temptransfer[0];
	    	DeleteHistory;
	    	string $shas[]=`listRelatives -s -pa $pas[0]`;
	    	string $rshas[]=`listRelatives -s -ni -pa $pas[0]`;
		print $pas;
		print $rshas;
	    	string $inputs[]=`listConnections -s 1 -d 0 ($rshas[0]+".inMesh")`;
		print $inputs;
	    	if (size($inputs)==0)
	    	{
	    	    connectAttr -f ($tempshape[0]+".worldMesh[0]") ($rshas[0]+".inMesh");
	    	    string $norg=`rename $tempshape[0] ($rshas[0]+"Orig")`;
	    	    setAttr ($norg+".io") 1;
	    	    parent -r -s $norg $pas[0];
	    	    //delete $temptransfer;
	    	    return;
	    	}
	    	for ($org in $shas)
	    	{
                //$org=$shas[2]
			if ($org==$rshas[0])
			{
				print "its shape node...pass...\n";
				continue;  
			}
			//print ($org+"\n");
			string $inputs[]=`listConnections -s 1 -d 0 ($org+".inMesh")`;
			if (size($inputs)>0)
			{
				print "it has inputMesh...pass...\n";
			}
			string $worldoutputs[]=`listConnections -s 0 -d 1 -p 1 ($org+".worldMesh[0]")`;
			string $localoutputs[]=`listConnections -s 0 -d 1 -p 1 ($org+".outMesh")`;
			string $outputs[]=stringArrayCatenate($worldoutputs,$localoutputs);
			if (size($outputs)>0)
			{
				for ($output in $outputs)
				{   
					//$output = $outputs[0]
					print ("the org mesh node is    "+$org+"\n");
					print ("the output node is    "+$output+"\n");
					connectAttr -f ($tempshape[0]+".worldMesh[0]") $output;
					//select $dests;
					//disconnectAttr ($temptransfer[0]+".worldMesh[0]") ($org+".inMesh");
					//delete $temptransfer;
				}
				delete $org;
				print ("temp transfer shape is    "+$tempshape[0]+"\n");
				string $orgname=`rename $tempshape[0] $org`;
				parent -r -s $orgname $pas[0];
				delete $temptransfer;
				setAttr ($orgname+".io") 1;
			}
	    	}
		return;
	}
	if ($imvcount[0]!=$matchvcount[0])
	{
	    return;  
	};
	if ($uvcount[0]>0)
	{
		string $deluv[] = `polyUVSet -q -allUVSets $dests`;
		for ($uv in $deluv)
		{
			if ($uv != "map1")
				catch(`polyUVSet -uvSet $uv -delete $dests`);	// use catch use, KC (27 Sep 2003)
				//a maya bug may cause error here.
		}
		$curUV =`polyUVSet -q -currentUVSet $sources`; 
		string $allUV[] =`polyUVSet -q -allUVSets $sources`; 
		string $trfname[];	
		//delete old polyTransfer node
		string $oldpolytransfer[] = `listHistory -pruneDagObjects true $dests`;
		for ($t in $oldpolytransfer)
		{
			if (`nodeType $t` == "polyTransfer")
				delete $t;
		}
		$trfname = `polyTransfer -uvSets 1 -alternateObject $sources $dests`;
		//another maya bug may cause error here.
		$emptyuv = `polyUVSet  -create $dests`;
		catch(`polyUVSet -uvSet $emptyuv -delete $dests`);
		//// modified by huangzhongwei@idmt.com.cn, 2013-08-07
		//string $cmd = "$emptyuv = `polyUVSet -create \"" + $dests + "\"`; polyUVSet -uvSet $emptyuv -delete \"" + $dests + "\";";
		//if (catch(`eval $cmd`))
		//{
		//	print ($cmd + "\n");
		//}
		connectAttr -force ($trfname[0]+".output") ($dests+".inMesh");
		string $todeluv[] = `ls -type "deleteUVSet"`;
		for ($du in $todeluv)
		{
			string $left[] = `listConnections -destination true -source false $du`;
			string $right[] = `listConnections -destination false -source true -type "createUVSet" $du`;
			if (size($left) == 0&&size($right) == 1)
			{
				delete $du;
				//delete $right;   createUVSets will be removed with the deleteUVSets together  
			}
		}
		for ($j=0;$j<size($allUV) ;$j++ )
		{
			polyUVSet -e -uvSet $allUV[$j]  $sources; 
			// successfully copy UV set over, now connect the uvSetName attribute to the target
			int $numUVset = `getAttr -size ($sources + ".uvSet")`;

			for ($ii = 0; $ii < $numUVset; $ii++)
			{
				string $uvSetName = `getAttr ($sources + ".uvSet[" + $ii + "].uvSetName")`;

				if ($uvSetName == $allUV[$j])
				{
					// check whether the UV set is connected to a UVChooser node
					string $targets[] = `listConnections -destination true ($sources + ".uvSet[" + $ii + "].uvSetName")`;
					for ($jj = 0; $jj < size($targets); $jj++)
					{
						if (`nodeType $targets[$jj]` == "uvChooser")
						{
							int $numUVset2 = `getAttr -size ($dests + ".uvSet")`;

							for ($iii = 0; $iii < $numUVset2; $iii++)
							{
								string $uvSetName2 = `getAttr ($dests + ".uvSet[" + $iii + "].uvSetName")`;

								if ($uvSetName2 == $allUV[$j])
									break;
							}

							int $uvSetIdx = $iii;
							string  $uvsets[] = `listConnections -sh true -s true -d false $targets[$jj]`;
							int $nextAvailable = size($uvsets);
							connectAttr -force ($dests + ".uvSet[" + $uvSetIdx + "].uvSetName") ($targets[$jj] + ".uvSets[" + $nextAvailable + "]");
						}
					}	
				}
			}
		}

		polyUVSet -e -uvSet $curUV  $sources; 
	}
}

global proc GA_zwTransferMaterialUV(int $fullPath, int $selectedOnly, int $transuv_model,string $sel[])
{    
    //int $fullPath=0;
    //int $selectedOnly=0;
    //string $sel[]={};
	print "TRANSFER UV\n";

	string $selected[] = `ls -long -dagObjects -shapes $sel`;
	if ($transuv_model!=0)
	{
		string $nomodList[];
		for ($i=0; $i<size($selected); $i++)
		{
			if (`match  "MODEL|" $selected[$i]`=="")
			{
				string $nomod[];
				$nomod=stringToStringArray($selected[$i],"");
				appendStringArray($nomodList, $nomod, 2);
			}

		}
		string $objs[]=stringArrayRemove($nomodList, $selected);
		$selected=$objs;
	}
	string $namesp = GA_getnamespace() + ":";
	string $sources[] = `ls -long -noIntermediate -type "mesh" ($namesp + "*")`;
	for ($i=0; $i<size($sources); $i++ )
	{
		string $obj = "";
		string $buf[];
		clear($buf);
		string $buf1[];
		clear($buf1);

		tokenize $sources[$i] "|" $buf;
		if ($fullPath)
		{
			for ($j = 1; $j<size($buf); $j++)
			{
				tokenize $buf[$j] ":" $buf1;
				$obj += "|" + $buf1[size($buf1) - 1];
			}
		}
		else
		{
			tokenize $buf[size($buf) - 1] ":" $buf1;
			$obj = $buf1[size($buf1) - 1];
		}
		
		string $dests[] = `ls -long -noIntermediate -type "mesh" $obj`;
		for ($j=0; $j<size($dests); $j++)
		{
			if (!$selectedOnly || `stringArrayCount $dests[$j] $selected`)
			{
                //print $sources[$i];
                //print $dests[$j];
				GA_myPolyTransfer $sources[$i] $dests[$j];
				flushUndo;
			}
		}
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//for selected objects use
proc string GA_getSelTransform(string $shape)
{
	string $transform = "";

	if ( "transform" != `nodeType $shape` )
	// If given node is already a transform, just pass on through
	{
		string $parents[] = `listRelatives -parent $shape`;
		$transform = $parents[0];
	}

	return $transform;
}

//function to output the relationship between obj and shadingEngine
proc GA_OutputSelSG_shapes(string $obj, string $objtr, string $shading[])
{

	$tmp = (`internalVar -userAppDir` + "projects/default/scenes/tmpselmat.txt");
	$fileID = `fopen $tmp w`;
	
	if ($fileID == 0)
	{
		// cannot open the file

		error("Fail to open " + $tmp);
	}
	
	for ($j = 0; $j < size($shading); $j++)		// skip last item because it is ""
	{
		string $objs[] = `sets -q $shading[$j]`;
		for ($i = 0; $i < size($objs); $i++)
		{
			string $buf[];
			tokenize $objs[$i] "." $buf;
			string $shape = $buf[0];
			string $long[] = `ls -long $shape`;
			if ($long[0] == $obj||$long[0] == $objtr) 
				fprint $fileID ($shading[$j] + " " + $objs[$i] + "\n");
		}
	}

	fclose $fileID;
}

//function to transfer material
proc GA_transferSelMat(string $sources, string $dests)
{
	$filename = (`internalVar -userAppDir` + "projects/default/scenes/tmpselmat.txt");
	$fileID = `fopen $filename r`;

	if ($fileID == 0)
	{
		// cannot open the file
		error("Fail to open " + $filename);
	}

	string $buffer = `fgetline $fileID`;
	$buffer = substituteAllString($buffer, "\n", "");

	while (size($buffer) > 0) 
	{
		string $buf[];

		if (`tokenize $buffer " " $buf` == 2)
		{
			// apply the material to the shape
			string $SG = $buf[0];
			string $vet = "";
			string $buff[];
			int $num = `tokenize $buf[1] "." $buff`;
			string $shape = $buff[0];
			if ($num == 2)
				$vet = "."+$buff[1];
			
			sets -e -forceElement $SG ($dests+$vet);
		}
		$buffer = `fgetline $fileID`;
		$buffer = substituteAllString($buffer, "\n", "");
	}
	fclose $fileID;
}

//re-create smooth node
proc string GA_smoothSearch(string $list[])
{
	for ($i in $list)
	{
		if (`gmatch $i "finalSmooth*"`&&(`nodeType $i` == "polySmoothFace"))
			return $i;
	}
	return "";
}
 proc GA_recreateSmooth(string $object)
{
	if (attributeExists("sm_divisions",$object))
	{
		string $his[] = `listHistory -groupLevels true -pruneDagObjects 1 -leaf true $object`;
		string $smoothNode = GA_smoothSearch($his);
		if ($smoothNode != "")
		{
			delete $smoothNode;
			string $shapes[] = `listRelatives -fullPath -noIntermediate -type "mesh" $object`;
			if (size($shapes)>0)
			{
				$plyname = `polySmooth -divisions 0 -continuity 1 -keepBorder 0 -keepSelectionBorder 1 -keepTessellation 1 -constructionHistory 1 $shapes[0]`;
				rename $plyname "finalSmooth";
			}
		}	
	}
}

proc GA_Conswitch(string $sources, string $dests)
{
	string $switchNode1[]  = `listConnections -plugs 1 -source 0 -destination 1 -type tripleShadingSwitch $sources`;
	string $switchNode2[]  = `listConnections -plugs 1 -source 0 -destination 1 -type doubleShadingSwitch $sources`;
	string $switchNode[] = stringArrayCatenate($switchNode1,$switchNode2); 
  	string $switchNode3[]  = `listConnections -plugs 1 -source 0 -destination 1 -type singleShadingSwitch $sources`;
	$switchNode = stringArrayCatenate($switchNode,$switchNode3); 
	for ($i=0; $i<size($switchNode); $i++)
		connectAttr -f ($dests+".instObjGroups[0]") $switchNode[$i];
}

global proc GA_TransferUVMatSel(string $uvcheck,string $matcheck)
{
	string $renderLayers[] = `listConnections "renderLayerManager.renderLayerId"`;
	if (size($renderLayers)>1)
	{
		string $confresult = `confirmDialog -title "Confirm" -message "Render Layer Exists! Continue Anyway?"
			-button "Yes" -button "No" -defaultButton "No"
			-cancelButton "No" -dismissString "No"`;
		if ($confresult == "No")
			return;
	}
	int $uv = `checkBox -q -v $uvcheck`;
	int $mat = `checkBox -q -v $matcheck`;
	
	GA_TransferUVMatSelProc $uv $mat;
}

global proc GA_TransferUVMatSelProc(int $uv, int $mat)
{
	string $sel[] = `ls -long -selection -dagObjects -type shape -noIntermediate`;
	string $seltr[] = `ls -long -selection -dagObjects -type transform -noIntermediate`;
	if (size($sel) != 2)
		error ("polyTransfer requires two polygon objects");

	cycleCheck -evaluation on;

	//transfer UV
	if ($uv)
	{
		//add uvmin smooth node if necessary
		string $lS[] = `listAttr -userDefined -string UVMinSmooth $seltr[0]`;
		if (size($lS) == 1)
		{
			int $value = `getAttr ($seltr[0]+".UVMinSmooth")`;
			$UVsm = GA_getsmooth($seltr[1]);
			if ($UVsm == 0)
			{
				$smname = `polySmooth -divisions $value -continuity 1 -keepBorder 0 -keepSelectionBorder 1 -keepTessellation 1 -constructionHistory 1 $sel[1]`;
				rename $smname "UVMinSmooth";
				print("\n	Set UVMinSmooth on: "+$seltr[1]);
			}
		}
		GA_DeapplySmooth({$sel[0]});
		GA_myPolyTransfer($sel[0], $sel[1]);
	}
	//transfer material
	if ($mat)
	{
		//re-locate place3dTexture node
		string $place3d[] = `listRelatives -children -fullPath -type "place3dTexture" $seltr[0]`;
		for ($pd in $place3d)
			parent $pd $seltr[1];

		//transfer material
		string $shading[] = `listConnections -source 0 -destination 1 -type shadingEngine $sel[0]`;
		GA_OutputSelSG_shapes($sel[0], $seltr[0], $shading);
		GA_transferSelMat($sel[0],$sel[1]);
		GA_recreateSmooth($seltr[1]);

		//re-connect switchshading node
		GA_Conswitch($sel[0], $sel[1]);
	}
	GA_docleannamespace();

	cycleCheck -evaluation on;
}

global proc GA_zwTransferMaterialUnlock(string $node)
{
	string $meshs[] = `listRelatives -shapes $node`;
	if (size($meshs) == 0)
	{
		$meshs[size($meshs)] = $node;
	}
	for ($mesh in $meshs)
	{
	    string $attrs[] = `listAttr -locked $mesh`;
	    for ($attr in $attrs)
	    {
		if (`objExists ($mesh + "." + $attr)`)
		{
			setAttr -lock false ($mesh + "." + $attr);
		}
	    }
	    if (`objExists ($mesh + ".iog[0].og")`)
	    {
		if (`getAttr -lock ($mesh + ".iog[0].og")`)
		{
			setAttr -lock false ($mesh + ".iog[0].og");
		}
	    }
	}
}

global proc GA_TransferUVMatBackgroundMode(string $filename, int $uvstate,int $transuv_model)
{
	if (`filetest -f $filename`)
	{
		cycleCheck -e off;

		//import material scene
		string $imtrans[] = `ls -type "mesh" -noIntermediate "ImUVs*:*"`;
		if (size($imtrans) == 0)
		{
			file -import -namespace "ImUVs" -renameAll true -groupReference $filename;
		}

		print "APPLY MATERIAL:\n";
		print "------------------------------------------------------------------------------------------\n";

		//transfer uv
		if ($uvstate)
		{
			GA_smoothBeforetransUVs();
			string $list[] = `ls -dagObjects -type mesh -noIntermediate`;
			GA_DeapplySmooth($list);
			GA_zwTransferMaterialUV 0 0 $uvstate {};
		}

		//here for 3body file
		print "DEAL WITH 3BODY PART:\n";
		string $grp[] = `ls -tr "ImUVs*:texture_reference"`;
		if (`objExists $grp[0]`)
		{
			//reconnect reference and shapes
			parent -world $grp[0];
			GA_multibodydocleannamespace($grp[0]);
		}

		string $imtrans[] = `ls -shapes "ImUVs*:*"`;
		string $impobj = getTransform($imtrans[0]);
		if (`objExists $impobj`)
		{
			string $grpname = GA_getBasegroup($impobj);
			delete $grpname;
		}

		GA_docleannamespace();

		//deal with ShadingSwitch
		string $trswich[] = `ls -type tripleShadingSwitch`;
		string $douSw[] = `ls -type doubleShadingSwitch`;
		string $sinSw[] = `ls -type singleShadingSwitch`;
		if (size($trswich)>0||size($douSw)>0||size($sinSw)>0)
			zjAMConswitch();

		//re-arrange final smooth nodes
		zjArrangeNodes();

		print "------------------------------------------------------------------------------------------\n";
		print "Apply Material\n";

		cycleCheck -e on;
	}

}


