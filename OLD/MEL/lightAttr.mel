 /*
 ______________________________________________________________________________________
 |
 |  $Proc name: lightAttr  	(was lightIntense)
 |
 |   $Author: Patrick Palmer, ppalmer@aw.sgi.com 
 |	  & Revised by William McCullough, will@lumis.com
 |        & Revised by Hartmut Engel III., Getonbored@aol.com
 |   $Home: http://www.lumis.com/maya/mel_scripts/
 |   $Date: 5-27-98 / 8-3-98
 |
 |   Original Author:
 |       Patrick Palmer
 |
 |   Description:
 |		 This script will create a window with light intesity and color
 |		 selection attributes for all lights within a scene.  It also has
 |		 the ability to use a map and access to the color picker.
 |		 Patrick Created the intensity and I figured it would be nice
 |		 to have the ability to change the color and map for each
 |		 light also.  
 |               H. Engel: I also felt that having the ability to switch on/off a 
 |                         light's visibility, as well as quickly select it for further    
 |                         use in the attribute editor (and thus access all other  
 |                         attributes quickly & easy) could be helpfull.   
 |                         In addition, each attribute section is now collapsable and 
 |                         the window has scroll bars.
 |                      NOTE: Though it works the other way round, changing a light's
 |                      ----- visibility from anywhere else than this window will not
 |                            automatically update the state of this checkbox unless
 |                            the window gets reopened again. 
 |                            (Hope to fix that some day...)
 ______________________________________________________________________________________
 */
 
global proc lightAttr()
 {
 	string $lights[] = `ls -lt`;
 	string $curLight, $attr, $visib, $vison, $visoff, $chbox, $aebut, $aeopen;
	int $vstate; 	

	// Does this window exist already?  If so quit.
	if (`window -exists lightWin`) return;
 
	// Create the Window named LightWin without a miximizeButton (mxb):

	window -title "Light Attributes List" -mxb false -iconName "LightAttrList" lightWin;
     	scrollLayout -horizontalScrollBarThickness 14 -verticalScrollBarThickness 14;
        columnLayout;


	for ($curLight in $lights) {

	// Put every ComponentGroup in a collapsable, framed Section:

             frameLayout -label $curLight -labelAlign "center" -borderStyle "etchedIn" -collapsable 1;
                columnLayout;

		// Add color and map sliders for all lights: 	
		string $color;
		$color = $curLight + ".color";
		attrColorSliderGrp -label "Color" -at $color;

		// Add intensity sliders for all lights:
		$attr = $curLight + ".intensity"; 
		attrFieldSliderGrp -min -25 -max 100 -label "Intensity" -at $attr;

		// Add visibility checkbox and selectButton in same row for all lights:
		$visib = $curLight + ".visibility";
		$vstate = `getAttr ($curLight + ".visibility")`;
		$vison = "setAttr "+ $visib + " 1";
		$visoff = "setAttr "+ $visib + " 0";
		$aeopen = "select -r "+ $curLight;

		   rowLayout -numberOfColumns 2 -columnWidth2 325 300 -columnAlign2 "left" "right";

		      checkBoxGrp -ncb 1 -label " " -label1 "Visibility" -on1 $vison -of1 $visoff -v1 $vstate;
		      button -label " Select " -height 23 -c $aeopen;

               	   setParent ..;

                setParent ..;
             setParent ..;

 }
 showWindow lightWin;
}
