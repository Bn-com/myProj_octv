/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: 黄仲维
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwParticle2Instance()
{
	string $buf[] = `ls -sl`;
	if (size($buf) != 1)
		error "请选择一个instancer 节点";
	string $instancer = $buf[0];
	if (nodeType($instancer) != "instancer")
		error "请选择一个instancer 节点";

	string $particle = `instancer -query -pointDataSource $instancer`;
	if ($particle == "")
		return;

	$buf = `instancer -query -object $instancer`;
	if (!size($buf))
		return;
	string $obj = $buf[0];

	int $count = `particle -query -count $particle`;
	string $group[];
	for ($i=0; $i<$count; $i++)
	{
		float $pos[] = `particle -order $i -attribute "position" -query $particle`;
		$buf = `instance $obj`;
		xform -worldSpace -translation $pos[0] $pos[1] $pos[2] -rotation (rand(360)) (rand(360)) (rand(360)) $buf[0];
		$group[$i] = $buf[0];
	}
	group -world $group;
}