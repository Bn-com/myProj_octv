/*
用法：	publishCamera "e:/camera.mb" "Z:/Scratch/publish/MASTER"
结果：	Z:/Scratch/publish/MASTER/camera_001.mb
	Z:/Scratch/publish/MASTER/camera_master.mb
*/

global proc publishCamera(string $source, string $folder)
{
	string $buf[], $buf1[];
	int $max;

	tokenize $source "/\\" $buf;
	string $fileName = `substitute ".mb" $buf[size($buf)-1] ""`;
	string $file = $folder + "/" + $fileName + "_???.mb";
	$buf = `getFileList -filespec $file`;
	for ($file in $buf)
	{
		tokenize $file "_." $buf1;
		int $index = (int)$buf1[size($buf1)-2];
		if($index>$max)
			$max = $index;
	}
	
	$max += 1;
	string $version = $max;
	if($max<10)
		$version = "00" + $version;
	else if($max<100)
		$version = "0" + $version;
	
	string $dest = $folder + "/" + $fileName + "_" + $version + ".mb";
	zwPublishCopyFile $source $dest "maya" "MASTER" true;

	$dest = $folder + "/" + $fileName + "_master.mb";
	zwPublishCopyFile $source $dest "maya" "MASTER" true;
}