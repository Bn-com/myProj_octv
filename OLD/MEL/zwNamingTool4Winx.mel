global proc zwNamingTool4Winx()
{
	if (`window -exists zwNamingTool4Winx`)
	{
		deleteUI zwNamingTool4Winx;
	}

	window -title "Object Naming Tool for WinxClub" -resizeToFitChildren true zwNamingTool4Winx;
		formLayout formLayout1;
			text -label "修改选择物体的名字\n\nWinxClub 项目的物体名字格式为 MSH_partName_\n\n请根据实际修改下面文本框的 partName 部分，或者点击右键菜单选择" text1;
			textField -text "MSH_partName_" -width 250 textField1;
				popupMenu;
 					menuItem -label "MSH_body_" -command "textField -edit -text \"MSH_body_\" textField1";
					menuItem -divider true;
					menuItem -label "MSH_eye_L_" -command "textField -edit -text \"MSH_eye_L_\" textField1";
					menuItem -label "MSH_eye_R_" -command "textField -edit -text \"MSH_eye_R_\" textField1";
					menuItem -divider true;
					menuItem -label "MSH_eyeBrows_L_" -command "textField -edit -text \"MSH_eyeBrows_L_\" textField1";
					menuItem -label "MSH_eyeBrows_R_" -command "textField -edit -text \"MSH_eyeBrows_R_\" textField1";
					menuItem -divider true;
					menuItem -label "MSH_hair_" -command "textField -edit -text \"MSH_hair_\" textField1";
					menuItem -label "MSH_head_" -command "textField -edit -text \"MSH_head_\" textField1";
					menuItem -divider true;
					menuItem -label "MSH_teeth_UP_" -command "textField -edit -text \"MSH_teeth_UP_\" textField1";
					menuItem -label "MSH_teeth_DWN_" -command "textField -edit -text \"MSH_teeth_DWN_\" textField1";
					menuItem -divider true;
					menuItem -label "MSH_tongue_" -command "textField -edit -text \"MSH_tongue_\" textField1";
			button -label "OK" -command "zwNamingTool4WinxRename" button1;

	formLayout -edit
		-attachForm	text1		"top"		10
		-attachForm	text1		"left"		10

		-attachControl	textField1	"top"		20	text1
		-attachForm	textField1	"left"		10

		-attachControl	button1		"top"		20	text1
		-attachControl	button1		"left"		0	textField1
	formLayout1;

	showWindow zwNamingTool4Winx;
}

global proc zwNamingTool4WinxRename()
{
	string $selection[] = `ls -selection`;
	if (size($selection) != 1)
	{
		error "请选择一个物体";
	}

	string $name = `textField -query -text textField1`;
	string $oldName = `match "[^|]+$" $selection[0]`;
	if ($name == $oldName)
	{
		return;
	}
	if (`match "^MSH(_[^_]+)+_$" $name` != $name || $name == "MSH_partName_")
	{
		string $confirmDialog = `confirmDialog -message "不是规范的名字，WinxClub 项目的物体名字格式为 MSH_partName_" -button "Continue" -button "Cancel" -cancelButton "Cancel" -defaultButton "Cancel" -dismissString "Cancel"`;
		if ($confirmDialog == "Cancel")
		{
			return;
		}
	}

	evalEcho ("rename \"" + $selection[0] + "\" \"" + $name + "\"");
}