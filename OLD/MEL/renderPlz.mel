/*  This file downloaded from Highend3d.com
''  
''  Highend3d.com File Information:
''  
''    Script Name: Render Please v1.0
''    Author: Nitisara
''    Last Updated: February 9, 2002
''    Update/Change this file at:
''    http://www.highend3d.com/maya/mel/?section=rendering#1510
''  
''  Please do not alter any information above this line
''  it is generated dynamically by Highend3d.com and will
''  be changed automatically on any updates.
*/

global proc renderPlz()
{
    float $startTime, $lastTime = 0, $restTime, $passedTime = 0;
    int $passedHr, $passedMin, $passedSec, $restHr, $restMin, $restSec;
    int $f, $poMin, $poMax;
    int $sf = `getAttr defaultRenderGlobals.startFrame`;
    int $ef = `getAttr defaultRenderGlobals.endFrame`;
    int $df = `getAttr defaultRenderGlobals.byFrame`;
    string $camera, $status;

    $poMin = `playbackOptions -q -min`;
    $poMax = `playbackOptions -q -max`;
    playbackOptions -min $sf -max $ef;
    playButtonStart;

    RenderIntoNewWindow;
    $camera = `renderWindowEditor -q -crc renderView`;
    RenderViewWindow;
    progressWindow -isInterruptable yes -min $sf -max $ef -title "Render Please   (c) 2002 Nitisara";

    for($f = $sf; $f <= $ef; $f += $df)
    {
        $restTime = $lastTime*($ef-$f+1);
        $passedHr = $passedTime/3600;
        $passedMin = ($passedTime-$passedHr*3600)/60;
        $passedSec = $passedTime-$passedHr*3600-$passedMin*60;
        $restHr = $restTime/3600;
        $restMin = ($restTime-$restHr*3600)/60;
        $restSec = $restTime-$restHr*3600-$restMin*60;
        $status = "Time passed: "+$passedHr+":"+$passedMin+":"+$passedSec+"      Time left: "+$restHr+":"+$restMin+":"+$restSec;
        progressWindow -edit -progress $f -status $status;
        $startTime = `timerX`;
        render $camera;
        $lastTime = `timerX -startTime $startTime`;
        $passedTime += $lastTime;
        if(`progressWindow -query -isCancelled` == yes)
            break;
        playButtonStepForward;
    }

    progressWindow -endProgress;
    playbackOptions -min $poMin -max $poMax;
}
