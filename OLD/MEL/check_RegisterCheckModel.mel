//-----------------------------------------------------------------------
//  Alias Script File
//
//  Creation Date:  January 16, 2003
//
//  Procedure Name:
//      check_RegisterUsercheck
//
//  Description:
//
//		Initialization function that is called when the Check Scene
//		operation is invoked.  The purpose of this function is to
//		allow users to register their own custom check operations.
//
//  Input Arguments: none
//
//	Return Value: None.
//
//-----------------------------------------------------------------------

//
// Modified by Cz - May 18th, 2006
//

global proc check_RegisterCheckModel()
{
	// modify this at check_AddUsercheck()
	global string $gUserScenechecks[]; 

	// clear this before U add user check
	clear $gUserScenechecks; 

	//	use the check_AddUsercheck() proc to register all your
	//	custom check steps here.
	//
	

	//check_AddUsercheck( "check_filename", "File Name", 1, "checkModelFilename()" );
	check_AddUsercheck( "check_ilgletter", "Objects' Illegal Letters", 1, "checkIllegalLetter()" );
	check_AddUsercheck( "check_materail", "Material", 1, "checkMaterials()" );
	check_AddUsercheck( "check_layer", "Layers", 1, "checkLayer()" );
	check_AddUsercheck( "check_history", "Objects' History", 0, "checkHistory()" );
	check_AddUsercheck( "check_ilgCamera", "Illegal Camera", 1, "checkCamera()" );
	check_AddUsercheck( "check_ilgObjSet", "Illegal ObjectSet", 1, "checkObjectSet()" );
	check_AddUsercheck( "check_Smooth", "MaxSmooth Attribute", 0, "checkSmooth()" );
	check_AddUsercheck( "check_shapeName", "Same Shapes' Names", 0, "zjRemoveNamespace;checkShapeName()" );
	check_AddUsercheck( "check_ilgVertex", "Illegal Vertex", 1, "checkVertex()" );
	check_AddUsercheck( "check_uselessMesh", "Useless Mesh", 1, "checkModelUselessMeshes()" );
	check_AddUsercheck( "check_charName", "Objects' Name", 1, "checkModelcharName()" );
	check_AddUsercheck( "check_modelGEO", "GEO/geo", 1, "checkModelGeo()" );


}


//++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
//
//+++++++++++++++++++++++++++++++++++ Check Functions Start ++++++++++++++++++++++++++++++++++++++++++++
//
//++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


global proc checkModelFilename()
{
	print "\nChecking File Name\n";
	print "--------------------";

		int $numDel = 0;
		string $fname = `file -q -sn`;
		if ($fname != "")
		{
			string $buffer[] = {};
			tokenize $fname "/." $buffer;
			string $sname = $buffer[size($buffer)-2];
			if (`match "^[a-z0-9_]+$" $sname` == "") 
			{
				print "file name includes illegal letters";
				$numDel++;
			}
			else
			{
				tokenize $sname "_" $buffer;
				if (size($buffer)==5)
				{
					if ($buffer[3]!="mo")
					{
						print ("\n" + "  -> " + "Scene name should be in this format: "+"\"project_asset_aescription_lod_pp_version\""+",like \"br_bernicem005_001_l_mo_005\""+"\n");
						$numDel++;
					}
					else if (`strcmp "l" $buffer[2]`&&`strcmp "m" $buffer[2]`&&`strcmp "h" $buffer[2]`)
					{
						print ("\n" + "  -> " + "Scene name should be in this format: "+"\"project_asset_aescription_lod_pp_version\""+",like \"br_bernicem005_001_l_mo_005\""+"\n");
						$numDel++;
					}
				}
				else if (size($buffer)==6)
				{
					if ($buffer[4]!="mo")
					{
						print ("\n" + "  -> " + "Scene name should be in this format: "+"\"project_asset_aescription_lod_pp_version\""+",like \"br_bernicem005_001_l_mo_005\""+"\n");
						$numDel++;
					}
				}
				else
				{
					print ("\n" + "  -> Wrong scene name format! It should be " +"\"project_asset_aescription_lod_pp_version\"!" + "\n");
					$numDel++;
				}

			}
		}

		check_Summary( "Checked file name", ("Found " + $numDel + " illegal file name") );

		print "\n";
}


global proc checkIllegalLetter()
{
	print "\nChecking Illegal Letters\n";
	print "--------------------------";

		int $numDel = 0;

		string $objs[] = `ls -tr`;
		for ($item in $objs)
		{
			if (`match "^[a-zA-Z0-9_|:]+$" $item` == "") 
			{
				print ("\n"+ "  -> " + $item + " includes illegal letters" + "\n");
				$numDel++;
			}
		}

		string $objs[] = `ls -shapes`;
		for ($item in $objs)
		{
			if (`match "^[a-zA-Z0-9_|:]+$" $item` == "") 
			{
				print ("\n"+ "  -> " + $item + " includes illegal letters" + "\n");
				$numDel++;
			}
		}
		check_Summary( "Checked Illegal Letters", ("Found " + $numDel + " illegal objects") );

	print "\n";
}


// check non-default materials
global proc checkMaterials()
{
	// This proc will check current scene for non-default materials
	// If found, this proc will list them out.
	
	print "\nChecking Materials\n";
	print "------------------";

		int $numDel = 0;
		
		// Get the list of materials in the current scene
                string $allMaterials[] = `ls -materials`;
 
		// Maya got ONLY 2 materials by default
		// So, if the number of materials is more
		// than 2, we conclude that current scene includes illegal material
		if ((size($allMaterials))>2)
		{
			$numDel+=(size($allMaterials)-2);
			print ("\n" + "Current scene includes "+ $numDel +" non-default materials!" + "\n");
			

		}

		check_Summary( "Checked materials", ("Found " + $numDel + " non-default materials!") );

	print "\n";
}


// check illegal layers
global proc checkLayer()
{
	// This proc will check current scene for illegal layers
	// If found, this proc will list them out.
	
	print "\nChecking Layers\n";
	print "-----------------";

		int $numDel = 0;

		// get the displayLayer list
		string $allLayers[] = `ls -type displayLayer`;
		
		// if the layer-Array is larger than 1
		// we conclude that current scene includes illegal layers.
		if ((size($allLayers))>1)
		{
			$numDel+=(size($allLayers)-1);
			print ("\n" + "Current scene includes " + $numDel +" non-default layers!" + "\n");

		}

		check_Summary( "Checked layers", ("Found " + $numDel + " non-default layers!") );

	print "\n";
}


// check history
global proc checkHistory()
{
	// This proc will check objects for history that should have been deleted.
	// If found, this proc will list them out.

	// bug: items with the same name caused error.
	// bug fixed by cz - May 25th, 2005
	
	print "\nChecking History\n";
	print "------------------";

		int $numDel = 0;
		
		// get the geometry list
		string $allGeoShape[] = `ls -geometry`;
		
		// loop to list history of each of them if 
		// the history-Array is larger than 1
                // if yes, this geometry includes history
                for($each in $allGeoShape)
		{
			string $history[] = `listHistory $each`;

			if(size($history)>1)
	                {
			     string $temp[] = `listRelatives -allParents $each`;
			     print ("\n" + "  -> "+ $temp[0] + " includes history!" + "\n");
	                     $numDel++;
	                }

                }

                check_Summary( "Checked history", ("Found " + $numDel + " objects including history!") );

                print "\n";
}


// check cameras
global proc checkCamera()
{
	// This proc will check current scene for illegal cameras.
	// If found, this proc will list them out.
	
	print "\nChecking Cameras\n";
	print "------------------";

		int $numDel = 0;

		string $illegalCam[]={""};
		string $legalCamName = "re_cam";

		string $allCamShape[] = `ls -cameras`;
		string $defaultCams[] = {"persp", "top", "front", "side"};
 			
		string $temp[] = `listRelatives -allParents $allCamShape`;                     
			
		// get the cameras' list
		appendStringArray( $illegalCam, $temp, size($temp));
		
		// remove all the default cameras, which are supposed as legal ones
                string $allCameras[] = stringArrayRemove( $defaultCams, $illegalCam);

		// Here we move on to check if the cameras in the list 
		// start with "re_cam". If not, record them as illegal ones

		for ($each in $allCameras )
		{
                  
		// Just make sure current variable is non-null
                  if($each!="")
		  {
		    
		    // Camera, with a prefix "re_cam", 
		    // is supposed as legal camera
		    int $isLegalCam = startsWith($each,$legalCamName);

		    if(!$isLegalCam)
		    {
			     print("\n" + "  -> " + "Illegal camera: " + $each + "\n");
	                     $numDel++;
	            }

		  }


                }

                check_Summary( "Checked camera", ("Found " + $numDel + " illegal cameras!") );

                print "\n";
}


// check objectSets
global proc checkObjectSet()
{
	// This proc will check the current scene for non-objectSets
	// If found, this proc will list them out.
	
	print "\nChecking ObjectSets\n";
	print "---------------------";

		int $numDel = 0;

		// initialize the default objectSets, which are read ONLY node
		string $defaultObjSets[] = {"defaultLightSet", "defaultObjectSet", 
		                            "initialParticleSE", "initialShadingGroup"};
		
		// get the list of objectSets in the current scene
	
 		string $allObjOnTop[] = `ls -type objectSet`;

		// remove all the default objectSets, which are supposed as legal ones
                string $ObjSet[] = stringArrayRemove( $defaultObjSets, $allObjOnTop);


		// if the $illegalObjSets is non-null(true), print message as below:
		if (size($ObjSet))
		{
		    for($each in $ObjSet)
		    {
			string $temp[] = `listRelatives -allParents $each`;  
			if((nodeType($each)=="objectSet")&&(size($temp))&&(setFilterScript($each)))
			{
			     print ("\n" + "  -> Current scene includes non-default objectSet: " + $each + "\n");
			     $numDel++;
                        }
		    }

		}

	check_Summary( "Checked objectSets", ("Found " + $numDel + " non-default objectSets!") );

	print "\n";
}

// check smooth attributes
global proc checkSmooth() 
{
	// This proc will check objects' Maxsmooth attribute, which
	// are set by tool "IDMT/Modeling Tools/Set Smooth...".
	// If not set, this proc will list them out.

	// bug: items with the same name caused error.
	// bug fixed by cz - May 25th, 2005

	print "\nChecking MaxSmooth\n";
	print "----------------------";

		int $numDel = 0;	
		string $buffer[];
		string $allObj[];

		string $allHO[] = `ls -type "mesh" "*_h_*"`;	
		string $allLO[] = `ls -type "mesh" "*_l_*"`;	
		string $allMO[] = `ls -type "mesh" "*_m_*"`;	
		string $allFO[] = `ls -type "mesh" "*_f_*"`;
		
		// append all those meshes that MaxSmooth Attribute should
		// have been set
		appendStringArray($allObj, $allHO, size($allHO));
		appendStringArray($allObj, $allLO, size($allLO));
		appendStringArray($allObj, $allMO, size($allMO)); 
		appendStringArray($allObj, $allFO, size($allFO)); 

		// get the transform name of that list
		$allObj = `listRelatives -allParents $allObj`;		

		// get those MaxSmooth attribute had been set
		string $okayObj[] = `ls "*.MaxSmooth"`;
		for ($i=0;$i<size($okayObj);$i++)
		{
			$num = `tokenize $okayObj[$i] "." $buffer`;
			$okayObj[$i]=$buffer[0];
		}

		// update the main array
		$allObj = stringArrayRemove($okayObj, $allObj);

		// those MaxSmooth attribute have not been set yet
		if (size($allObj)>10)
		{
			print("\n" + "  -> " + size($allObj) + " objects' \"MaxSmooth\" attribute are NOT set!\n");
			$numDel+=size($allObj);

		}
		else
		{
			for ($each in $allObj)
			{
				if ($each!="")
				{
					print("\n" + "  -> " + $each + " \"MaxSmooth\" attribute is NOT set!\n");
				}
			}
		}

	  check_Summary( "Checked MaxSmooth", ("Found " + $numDel + " objects, MaxSmooth attribute NOT set!") );

          print "\n";

}

// check shape name
global proc checkShapeName()
{
	// This proc will check the objects' shape name.
	// If some objects with the same shape name,
	// this proc will list them out

	// NOTE: we will recommand you clear all 
	//       history before you run this script.
	
	print "\nChecking Shapes' Names\n";
	print "------------------------";

		// initialize all variables for later use
		int $numDel = 0;

		// main array that stores all the objects' shape names in the current scene 
		string $allShapes[] = `ls -dag -s`;
		
		// loop to find objects with the same name
		for ($each in $allShapes)
		{
			// list all object with the same name
			string $buffer[];
			string $temp[];
			int $num;
			
			// get the shape name and list objects with this name
			tokenize $each "|" $buffer;
			$num = size($buffer);
			$temp = `ls $buffer[$num-1]`;

			if (size($temp)>9)
			{
				print ("\n  -> " + size($temp) + " objects with the same shape name \"" + $buffer[$num-1] + "\"!\n");
				$allShapes = stringArrayRemove($temp, $allShapes);
				$numDel+=size($temp);

			}
			else if(size($temp)>1&&size($temp)<10)
			{
				print ("\n" + "Objects with the same shape name \"" + $buffer[$num-1] + "\":\n");
				
				for($item in $temp)
				{
				     print ("\n" + "  -> " + $item + "\n");
				     $numDel++;
				}				
				// refresh the main array to speed up and 
				// avoid repeating this loop
				$allShapes = stringArrayRemove($temp, $allShapes);
			}
		}


	check_Summary( "Checked shapes' names", ("Found " + $numDel + " Objects with the same shape name!") );

	print "\n";
}


// check vertex
global proc checkVertex()
{

	  // This proc will check objects for 
	  // colinear vertexes and coincident vertexes
	  // If found, this proc will list them out.
	  
	  print "\nChecking Vertexes\n";
	  print "-------------------";

		  int $numDel = 0;

		  // initialize the tolerance value for check coincident vertex 
		  // and colinear vertex
		  float $tolerance = 0.001;

		  // get the object shape list
		  string $allPolyShape[] = `ls -type mesh`;

		  // loop to check all the items
		  for($each in $allPolyShape)
		  {
			
			// back to transform state and print feeback info
			string $thisParent[] = `listRelatives -allParents $each`;

			// this is NOT a proc
			// check non-manifold vertex
			 {
				 // find all non-manifold vertices in the specified objects
				 string $nmv[] = `polyInfo -nmv $each`;

				 if (size($nmv))
				 {
					     print ("\n" + "  -> " + $thisParent[0] + " has non-manifold vertex!" + "\n");
					     $numDel++;
				 }
			 }
		      
			 // check coincident vertex 
			 // load the plugin for later use
			 if (!`pluginInfo -q -loaded -name "checkCoincident.mll"`)
			 {
				     loadPlugin -quiet "checkCoincident.mll";
			 }

			 // select current poly and execute check coincident vertex command 
			 select -r $each;
			 eval("checkCoincident " + $tolerance);

			 // get the coincident vertex print if there is any
			 string $coincidentVertex[] = `ls -sl`;
			 select -clear;
			 if (size($coincidentVertex))
			 {
				    print ("\n" + "  -> " + $thisParent[0] + " has coincident vertex!" + "\n");
				    $numDel++;
			 }

			 // check colinear vertex
			 if (!`pluginInfo -q -loaded -name "checkStrayPoint.mll"`)
			 {
				    loadPlugin -quiet "checkStrayPoint.mll";
			 }
			 // select current poly and execute check colinear vertex command
			 select -r $each;
			 eval("checkStrayPoint " + $tolerance);

			 // get the colinear vertex print if there is any
			 string $colinearVertex[] = `ls -sl`;
			 select -clear;
			 if (size($colinearVertex))
			 {
				    print ("\n" + "  -> " + $thisParent[0] + " has colinear vertex!" + "\n");
				    $numDel++;
			 }

		  }  // end for-loop
  
	  check_Summary( "Checked vertex", ("Found " + $numDel + " vertex mistakes!") );

	  print "\n";

}


global proc checkModelUselessMeshes()
{

	// This proc will check objects for 
	// colinear vertexes and coincident vertexes
	// If found, this proc will list them out.

	print "\nChecking Useless Meshes\n";
	print "-------------------------";

		int $numDel = 0;
		string $uselessMesh[];

		// get all the meshes in the current scene
		string $allMeshes[] = `ls -long -dagObjects -type mesh -noIntermediate`;

		// loop to get all the uselessMeshes
		for ($i = 0;$i<size($allMeshes);$i++ )
		{
			// get the vertexes info
			string $tempCMD = "int $v[] = `polyEvaluate -vertex " + $allMeshes[$i] + "`";
			int $infoVertex = catch(`eval($tempCMD)`);

			// get the faces info
			$tempCMD = "int $v[] = `polyEvaluate -face " + $allMeshes[$i] + "`";
			int $infoFace = catch(`eval($tempCMD)`);

			// concludes the result for the current mesh
			if ($infoVertex||$infoFace)
			{
				$uselessMesh[size($uselessMesh)] = $allMeshes[$i];
			}
		}
		
		// concludes the final result for the current scene
		if (size($uselessMesh))
		{
			for ($each in $uselessMesh)
			{
				print ("\n  -> Useless mesh " + $each + "!\n");
				$numDel++;
			}
		}

  
	  check_Summary( "Checked useless meshes", ("Found " + $numDel + " useless meshes!") );

	  print "\n";

}


global proc checkModelcharName()
{
	print "\nChecking Character's GEO/geo group\n";
	print "-------------------------------------";

	int $numDel = 0;
	
	string $charName;	
	// get the character's name
	string $charGroup[] = stringArrayCatenate(`ls -as "*_geo"`, `ls -as "*_GEO"`);

	string $defaultCams[] = {"persp", "top", "front", "side"};
	if ($charGroup[0]!="")
	{
		string $buffer[];
		tokenize $charGroup[0] "_" $buffer;
		$charName =  $buffer[0];
	}

	// get geo list
	string $allGeo[] = `ls -tr -ni`;

	$allGeo = stringArrayRemove($defaultCams,$allGeo);

	// check if geo starts with the charName
	for ($each in $allGeo)
	{
		
		string $buffer[];
		tokenize $each "|" $buffer;
		{
			string $temp[];
			tokenize $each "_" $temp;
			if ($temp[0]!=$charName)
			{
				print ("\n" + "  -> " + $each + " 角色组件命名错误!\n");
				$numDel++;
			}
		}
	}

}

global proc checkModelGeo()
{
	// NOTE:
	// This proc will check if the characterName_GEO
	// or characterName_geo group exists or not

	print "\nChecking Character's GEO/geo group\n";
	print "-------------------------------------";

	int $numDel = 0;

	string $geoGroup[] = stringArrayCatenate(`ls -as "*_geo"`, `ls -as "*_GEO"`);

	if (!size($geoGroup))
	{
		print ("\n" + "  -> Found mistake in character's GEO group!\n");
		$numDel++;
	}

	check_Summary( "\nChecked character's GEO/geo group", ("Found " + $numDel + " naming error in character's GEO groups!") );
	print "\n";
}
//++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
//
//++++++++++++++++++++++++++++++++++++++++++++ Check Functions End +++++++++++++++++++++++++++++++++++++
//
//++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++