global proc string SnapshotIcon(string $path,string $fname,int $size,int $Ornam,int $wGrid,int $disp)
{

       string $panl[] = `getPanel  -vis`;
       $flag = 0;
       for ($i=0;$i<size($panl);$i++) {
          if (`getPanel -typeOf $panl[$i]`=="modelPanel")
          {
              $flag = 1;
	      break;
	  }
       }
       if ($flag==0) {
          warning "no active view for Snapshot!";
	  return "";   
       }


       string $fpath = $path;

       if (`filetest -d $path`==0)   {
	   string $sceneName = `file -q -sn`;
	   string $buffer[];
	   tokenize $sceneName "/\\" $buffer;
	   //path
	   $fpath = "";
	   for ($i=0;$i<(size($buffer)-1);$i++){
	       $fpath += $buffer[$i]+"/";
           }

       };

   
       if ($fpath == "")  $fpath = `workspace -q -sn`;

       string $fileQuery = $fpath+$fname;
    
       if ($fname !="") {

	    string $panel = `getPanel -withFocus`; 
	    int $dispflag = -1;
	    $vo  = `optionVar -q objectDetailsVisibility`;
	    $vp  = `optionVar -q polyCountVisibility`;
	    $van = `optionVar -q animationDetailsVisibility`;
	    $vf  = `optionVar -q frameRateVisibility`;
	    $vc  = `optionVar -q cameraNamesVisibility`;
	    $va  = `optionVar -q viewAxisVisibility`;
	    setObjectDetailsVisibility(0);
	    setPolyCountVisibility(0);
	    setAnimationDetailsVisibility(0);
	    setFrameRateVisibility(0);
	    setCameraNamesVisibility(0);
	    setViewAxisVisibility(0);

	    if ($panel=="modelPanel1"||$panel=="modelPanel2"||$panel=="modelPanel3"||$panel=="modelPanel4") {
		$dispflag = `modelEditor -q -grid  $panel`;

		if ($wGrid == 1)  
		    modelEditor -e -grid 1 $panel;
		else 
		    modelEditor -e -grid 0 $panel;
	    }

	    
	    $ext =`getAttr defaultRenderGlobals.imageFormat `;
	    setAttr defaultRenderGlobals.imageFormat 7;


	    // to generate a small image using playblast
	    int $Ctime = `currentTime -q`;
	    string $imfile = `playblast -st $Ctime -et $Ctime -f $fileQuery -forceOverwrite -fmt "image" -percent 100 -wh $size $size -v $disp -showOrnaments $Ornam`;
	 
            // to rename file name 
		string $curtime;
		if ($Ctime>=0 && $Ctime<=9999){
		    $curtime = "0000"+$Ctime;
		    $num = size($curtime);
		    $curtime = `substring $curtime ($num-3) $num`;
		}else if ($Ctime>-999 && $Ctime<0){
		    $curtime = "0000"+(-$Ctime);
		    $num = size($curtime);
		    $curtime = `substring $curtime ($num-2) $num`;
		    $curtime = "-"+$curtime;
		}else $curtime = $Ctime;

		string $oldname; 
		if (`gmatch $imfile "*.iff"`)
			$oldname = $fileQuery+"."+$curtime+".iff";
		else
			$oldname = $fileQuery+"."+$curtime;

                if (`filetest -f $oldname`) {
			string $newname = `substitute ("."+$curtime) $oldname ""`;
			if (`gmatch $newname "*.iff"` == 0)
				$newname += ".iff";

			if (`file -q -exists $newname`)
			    sysFile -delete $newname;
			sysFile  -rename $newname $oldname; 
		if ($disp)
                	system("fcheck "+"\""+$newname+"\"");
                }
	    
	    setAttr defaultRenderGlobals.imageFormat $ext;

	    if ($dispflag==1)
	       modelEditor -e -grid 1 $panel;
	    else if ($dispflag==0)
	       modelEditor -e -grid 0 $panel;
	  
	    setObjectDetailsVisibility($vo);
	    setPolyCountVisibility($vp);
	    setAnimationDetailsVisibility($van);
	    setFrameRateVisibility($vf);
	    setCameraNamesVisibility($vc);
	    setViewAxisVisibility($va);

            print ($fileQuery+".iff");
	    return ($fileQuery+".iff");

       }
       else  warning "file has not been saved!\n";

       return "";
}
