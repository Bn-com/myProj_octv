/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc kcProjectNurbsCB(string $radioCollection)
{
	string $selected[] = `ls -selection`;

	if ((size($selected) != 2) || (!kcIsType($selected[0], "nurbsSurface")) || (!kcIsType($selected[1], "mesh")))
		error("Please select a NURBS surface and a polygon surface (in that order).");

	string $radios[] = `radioCollection -q -collectionItemArray $radioCollection`;

	for ($i = 0; $i < size($radios); $i++)
		if (`radioButton -q -select $radios[$i]`)
		{
			evalDeferred("projectNurbsCmd " + $i);
			break;
		}
}

global proc kcProjectNurbsGUI()
{
	if (!`pluginInfo -q -loaded projectNurbsCmd`)
		loadPlugin projectNurbsCmd;
		
	if (`window -exists kcProjectNurbsGUIWnd`)
		deleteUI -window kcProjectNurbsGUIWnd;


	window -title "Project Nurbs Surf to Polygon Surf Command" -resizeToFitChildren true kcProjectNurbsGUIWnd;
		string $formLayout = `formLayout`;
			string $columnLayout = `columnLayout`;
				text -label "THIS COMMAND IS UNDOABLE";
				text -label "Select Nurbs surface then polygon surface (in that order) before clicking Apply";
				text -label "Projection Direction:";
				string $radioCollection = `radioCollection`;
					string $radio1 = `radioButton -label "yPos then yNeg" -select`;
					string $radio2 = `radioButton -label "yNeg then yPos"`;
					string $radio3 = `radioButton -label "xPos then xNeg"`;
					string $radio4 = `radioButton -label "xNeg then xPos"`;
					string $radio5 = `radioButton -label "zPos then zNeg"`;
					string $radio6 = `radioButton -label "zNeg then zPos"`;
				setParent ..;
			setParent ..;

			$button1 = `button -label "Apply" -command ("kcProjectNurbsCB " + $radioCollection)`;
			$button2 = `button -label "Cancel" -command ("deleteUI -window kcProjectNurbsGUIWnd")`;
		setParent ..;

		formLayout -edit
			-attachForm		$columnLayout		"top"		10
			-attachForm		$columnLayout		"left"		10

			-attachControl	$button1		"top"		10	$columnLayout
			-attachForm		$button1		"left"		10
			-attachPosition	$button1		"right"		0	50

			-attachControl	$button2		"top"		10	$columnLayout
			-attachControl	$button2		"left"		0	$button1
			-attachForm		$button2		"right"		10
		$formLayout;

	showWindow kcProjectNurbsGUIWnd;
}