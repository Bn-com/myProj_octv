/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Tran Nguyen Phuong
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


// COPYRIGHT (c) 2003 IDMT CHINA
//
// Author : Tran Nguyen Phuong <tran@idmt.org.cn>
//
// Date: 12.07.2003
// 
// Description:
//		Create a grid of locators with the corner at the origin.
//		The locators are jittered with a random jitter amount in 
//		xz plane.
//
// Usage: tnpCrowdGridLocator <rows> <cols> <distRow> <distCol> <jitterAmount>
//
global proc tnpCrowdGridLocator(int $noOfRows,
						 int $noOfColumns,
						 float $distRow,
						 float $distColumn,
						 float $jitter){
	string	$locators[];
	float	$jitterCol;
	float	$jitterRow;
	for($i=0; $i<$noOfRows; $i++)
		for($j=0; $j<$noOfColumns; $j++){
			
			string $loc[] = `spaceLocator -n crowdLocator0`;
			
			$locators[size($locators)] = $loc[0];
			
			float $jitterU = rand(-$jitter, $jitter);
			float $jitterV = rand(-$jitter, $jitter);
			float $distU = $j*$distColumn + $jitterU;
			float $distV = $i*$distRow + $jitterV;

			//print ($jitterU + "   " + $jitterV + "\n");
			eval("xform -ws -translation " + $distU + " 0 " + $distV + " " + $loc[0]);
		}

	// GROUP
	string $group = eval("group -em -n GroupCrowdLocators");
	for($item in $locators) eval("parent " + $item + " " + $group);
}

global proc tnpCrowdGridLocatorUI(){
	string $win = `window 
					-title "Create a grid of locators" 
					-widthHeight 200 200
					-minimizeButton false
					-maximizeButton false
					-resizeToFitChildren true`;

	columnLayout -adjustableColumn true;
	
		string $rowsSlider = `intSliderGrp
				-label "No Of Rows" 
				-field true
				-min 1
				-value 10`;

		string $colsSlider = `intSliderGrp
				-label "No Of Columns" 
				-field true
				-min 1
				-value 20`;
		
		string $distRowSlider = `floatSliderGrp
				-label "RowSpacing" 
				-field true
				-min 0.1
				-value 3.5`;
		
		string $distColSlider = `floatSliderGrp
				-label "ColumnSpacing" 
				-field true
				-min 0.1
				-value 4.5`;
		string $jitterSlider = `floatSliderGrp
				-label "JitterAmount"
				-field true
				-min 0
				-value 0.5`;
		string $createBut = `button
				-label "CREATE"`;

	showWindow $win;

	button -e -command ("createButtonCmd " 
						+ $rowsSlider + " "
						+ $colsSlider + " "
						+ $distRowSlider + " "
						+ $distColSlider + " "
						+ $jitterSlider
						+ "; deleteUI -window " + $win) $createBut;
}
global proc createButtonCmd (string $rowsSlider,
				   string $colsSlider,
				   string $distRowSlider,
				   string $distColSlider,
				   string $jitterSlider){
	
	int		$noOfRows = `intSliderGrp -q -value $rowsSlider`;	
	int		$noOfCols = `intSliderGrp -q -value $colsSlider`;	
	float	$distRow = `floatSliderGrp -q -value $distRowSlider`;	
	float	$distCol = `floatSliderGrp -q -value $distColSlider`;	
	float	$jitter = `floatSliderGrp -q -value $jitterSlider`;	

	tnpCrowdGridLocator $noOfRows $noOfCols $distRow $distCol $jitter;
}