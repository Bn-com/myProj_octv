/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: 黄仲维
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


// 选择一个组，为组添加属性，并把组的属性连接到组底下灯的属性
global proc zwUpLightControl()
{
	string $light[];

//	string $attrGrp[] = {"up_Color", "up_int", "up_ShadowColor", "up_shadow_Res", "up_Filter_Size", "up_shadow", "up_reuse_shadow"};
	string $attrGrp[] = {"up_Color", "up_int", "up_ShadowColor", "up_shadow_Res", "up_Filter_Size", "up_shadow"};
	string $attrLight[] = {"color", "intensity", "shadowColor", "dmapResolution", "dmapFilterSize", "useDepthMapShadows", "reuseDmap"};

	string $buf[] = `ls -long -selection`;
	for ($grp in $buf)
	{
		clear $light;
		string $buf1[] = `listRelatives -fullPath -noIntermediate -children`;
		for ($node in $buf1)
		{
			string $buf2[] = `listRelatives -fullPath -noIntermediate -shapes -type "light" $node`;
			if (size($buf2))
				$light[size($light)] = $buf2[0];
		}
		if (size($light))
		{
// 添加属性
			if (!`objExists ($grp + ".up_Color")`)
			{
				addAttr -longName "up_Color" -usedAsColor -attributeType "float3" $grp;
				addAttr -longName "up_ColorR" -attributeType "float" -keyable true -parent "up_Color" $grp;
				addAttr -longName "up_ColorG" -attributeType "float" -keyable true -parent "up_Color" $grp;
				addAttr -longName "up_ColorB" -attributeType "float" -keyable true -parent "up_Color" $grp;
			}
			if (!`objExists ($grp + ".up_int")`)
				addAttr -longName "up_int" -attributeType "double" -keyable true -minValue 0 -maxValue 6 $grp;
			if (!`objExists ($grp + ".up_ShadowColor")`)
			{
				addAttr -longName "up_ShadowColor" -attributeType "float3" -usedAsColor $grp;
				addAttr -longName "up_ShadowColorR" -attributeType "float" -keyable true -parent "up_ShadowColor" $grp;
				addAttr -longName "up_ShadowColorG" -attributeType "float" -keyable true -parent "up_ShadowColor" $grp;
				addAttr -longName "up_ShadowColorB" -attributeType "float" -keyable true -parent "up_ShadowColor" $grp;
			}
			if (!`objExists ($grp + ".up_shadow_Res")`)
				addAttr -longName "up_shadow_Res" -attributeType "long" -keyable true -minValue 64 -maxValue 2048 $grp;
			if (!`objExists ($grp + ".up_Filter_Size")`)
				addAttr -longName "up_Filter_Size" -attributeType "long" -keyable true -minValue 0 -maxValue 12 $grp;
			if (!`objExists ($grp + ".up_shadow")`)
				addAttr -longName "up_shadow" -attributeType "bool" -keyable true $grp;
		//	if (!`objExists ($grp + ".up_reuse_shadow")`)
		//		addAttr -longName "up_reuse_shadow" -attributeType "bool" -keyable true $grp;
// 属性初值			
			for ($i=0; $i<size($attrGrp); $i++)
			{
				if (!`isConnected ($grp + "." + $attrGrp[$i]) ($light[0] + "." + $attrLight[$i])`)
				{
					if (`connectionInfo -isDestination ($light[0] + "." + $attrLight[$i])`)
					{
						if (`isConnected ($light[0] + "." + $attrLight[$i]) ($grp + "." + $attrGrp[$i])`)
							disconnectAttr ($light[0] + "." + $attrLight[$i]) ($grp + "." + $attrGrp[$i]);
						else
						{
							$buf1 = `listConnections -destination false -plugs true ($light[0] + "." + $attrLight[$i])`;
							connectAttr -force $buf1[0] ($grp + "." + $attrGrp[$i]);
						}
					}
					else
					{
						connectAttr -force ($light[0] + "." + $attrLight[$i]) ($grp + "." + $attrGrp[$i]);
						disconnectAttr ($light[0] + "." + $attrLight[$i]) ($grp + "." + $attrGrp[$i]);
					}
				}
			}
// 连接属性
			for ($i=0; $i<size($light); $i++)
			{
				for ($j=0; $j<size($attrGrp); $j++)
				{
					if (!`isConnected ($grp + "." + $attrGrp[$j]) ($light[$i] + "." + $attrLight[$j])`)
						connectAttr -force ($grp + "." + $attrGrp[$j]) ($light[$i] + "." + $attrLight[$j]);
				}
			}
		}
	}
}