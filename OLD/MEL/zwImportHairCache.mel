global proc zwImportHairCache()
{
	string $sceneName = `file -query -sceneName -shortName`;
	if ($sceneName == "")
	{
		error "请先存盘";
	}
	string $character = `match "[^_]+hair" (tolower($sceneName))`;
	$character = `substitute "hair$" $character ""`;
	if ($character == "")
	{
		error "文件名应该类似 wt_666_007_icywitchhair_dy_001.mb";
	}
	string $folder = `zwGetPath "hair" ""`;
	if (!`filetest -d $folder`)
	{
		error "Hair Cache未上传";
	}

	string $tempDir = `diskCache -tempDir`;
	string $hairSystems[] = `ls -type "hairSystem"`;
	if (size($hairSystems) == 0)
	{
		return;
	}
	select $hairSystems;
	DeleteHairCache;
	float $currentTime = `currentTime -query`;
	doHairDiskCache 1 { "3", 1, $currentTime, $currentTime, 1 } ;
	for ($hairSystem in $hairSystems)
	{
		string $diskCache[] = `listConnections -type "diskCache" $hairSystem`;
		string $hiddenCacheName = `getAttr ($diskCache[0] + ".hiddenCacheName")`;
		string $filespec = $folder + "/" + `substitute "^[^_]+" $hiddenCacheName "*"`;
		string $files[] = `getFileList -filespec $filespec`;
		for ($file in $files)
		{
			if (`match (tolower($character)) (tolower($file))` != "" && `match "^[^_]+_[^_]+_[^_]+_dy_" $file` != "")
			{
				sysFile -copy ($tempDir + "/" + $hiddenCacheName) ($folder + "/" + $file);
				break;
			}
		}
	}
}