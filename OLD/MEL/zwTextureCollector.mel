/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: »ÆÖÙÎ¬
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwTextureCollector()
{
	if(`window -exists zwTextureCollectorProc`)
		deleteUI zwTextureCollectorProc;

	window -menuBar true -title "Texture Collector" -width 500 -height 200 zwTextureCollectorProc;
		menu -label "Help";
			menuItem -label "Help on Texture Collector..." -command "zjHelpCallback zwTextureCollector";
		string $formLayout = `formLayout`;
			string $tabLayout = `tabLayout -tabsVisible false -scrollable true -childResizable true`;
				columnLayout -adjustableColumn true;
					text -label "WARNING: This tool can not collect textures to the DIRECTOR_FINAL or WORKING folder!";
					string $checkBoxGrp1 = `checkBoxGrp -adjustableColumn 2 -label "" -label1 "Selected objects only"`;
					string $textFieldButtonGrp = `textFieldButtonGrp -label "Collect textures to:" -buttonLabel "Browse" -adjustableColumn 2`;
					textFieldButtonGrp -edit -buttonCommand ("zwTextureCollectorBrowse \"" + $textFieldButtonGrp + "\"") $textFieldButtonGrp;
					string $checkBoxGrp2 = `checkBoxGrp -adjustableColumn 2 -label "" -label1 "Modify texture path" -value1 true`;
					string $checkBoxGrp3 = `checkBoxGrp -adjustableColumn 2 -label "" -label1 "Convert to iff"`;
					setParent..;
				setParent..;
			string $button1 = `button -label "Collect" -command ("zwTextureCollectorOK \"" + $textFieldButtonGrp + "\" \"" + $checkBoxGrp1 + "\" \"" + $checkBoxGrp2 + "\" \"" + $checkBoxGrp3 + "\"; deleteUI zwTextureCollectorProc")`;
			string $button2 = `button -label "Apply" -command ("zwTextureCollectorOK \"" + $textFieldButtonGrp + "\" \"" + $checkBoxGrp1 + "\" \"" + $checkBoxGrp2 + "\" \"" + $checkBoxGrp3 + "\"")`;
			string $button3 = `button -label "Close" -command "deleteUI zwTextureCollectorProc"`;

	formLayout -edit
		-attachForm	$tabLayout	"top"		0
		-attachForm	$tabLayout	"left"		0
		-attachForm	$tabLayout	"right"		0
		-attachControl	$tabLayout	"bottom"	5	$button1

		-attachForm	$button1	"left"		5
		-attachForm	$button1	"bottom"	5
		-attachPosition	$button1	"right"		5	33

		-attachControl	$button2	"left"		5	$button1
		-attachForm	$button2	"bottom"	5
		-attachPosition	$button2	"right"		5	66

		-attachControl	$button3	"left"		5	$button2
		-attachForm	$button3	"bottom"	5
		-attachForm	$button3	"right"		5
	$formLayout;

	showWindow zwTextureCollectorProc;
}

global proc zwTextureCollectorBrowse(string $textFieldButtonGrp)
{
	string $folder = `textFieldGrp -query -fileName $textFieldButtonGrp`;
	$folder = `idmtFolderDialog -initialDir $folder`;

	//modified by TaZz, 17/12/2004
	//if(`filetest -d $folder`)
	//	textFieldGrp -edit -fileName $folder $textFieldButtonGrp;

	string $disfolder = `zwFullPath $folder`;
	if(`filetest -d $disfolder`)
		textFieldGrp -edit -fileName $disfolder $textFieldButtonGrp;

}

global proc zwTextureCollectorOK(string $textFieldButtonGrp, string $checkBoxGrp1, string $checkBoxGrp2, string $checkBoxGrp3)
{
	eval "source \"zwCopyTextureImage.mel\"";

	string $destFolder = `textFieldGrp -query -fileName $textFieldButtonGrp`;
	$destFolder = `substitute "/$" $destFolder ""`;

	int $select = `checkBoxGrp -query -value1 $checkBoxGrp1`;
	int $modifyPath = `checkBoxGrp -query -value1 $checkBoxGrp2`;
	int $convertToIff = `checkBoxGrp -query -value1 $checkBoxGrp3`;

	if(!($convertToIff && $destFolder == "") && !`filetest -d $destFolder`)
		error ("Ä¿Â¼²»´æÔÚ£º" + $destFolder);

	string $project = zwGetProject("");
	zwCopyTextureImage 3 {"file", "fileTextureName", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	if (`pluginInfo -query -loaded "Mayatomr"`)
	{
		zwCopyTextureImage 3 {"mentalrayTexture", "fileTextureName", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
		zwCopyTextureImage 3 {"mentalrayIblShape", "texture", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	}
	zwCopyTextureImage 3 {"imagePlane", "imageName", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	zwCopyTextureImage 3 {"brush", "imageName", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	zwCopyTextureImage 3 {"brush", "leafImage", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	zwCopyTextureImage 3 {"brush", "flowerImage", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	if (`pluginInfo -query -loaded "mtoa"`)
	{
		zwCopyTextureImage 3 {"aiImage", "filename", $destFolder, $modifyPath, $convertToIff, "0", "100", $project, "0", $select};
	}
}