global proc zwSetRenderGlobals()
{
	string $buf[];

	string $sceneName = `file -query -shortName -sceneName`;

	if (`match "bu_[^_]+_fl_" $sceneName` != "")	// bu_102_[^_]+_fl_an
	{
		return;
	}

	string $str = `idmtService "GetProjectByFile" $sceneName`;
	if (`tokenize $str "|" $buf` != 11)
	{
		return;
	}
	string          $project = $buf[1];
	int                 $fps = $buf[4];
	int               $width = $buf[5];
	int              $height = $buf[6];
	float $deviceAspectRatio = $buf[10];

	string $time;
	switch ($fps)
	{
	case 15:
		$time = "game";
		break;
	case 24:
		$time = "film";
		break;
	case 25:
		$time = "pal";
		break;
	case 30:
		$time = "ntsc";
		break;
	case 48:
		$time = "show";
		break;
	case 50:
		$time = "palf";
		break;
	case 60:
		$time = "ntscf";
		break;
	}

	currentUnit -linear "centimeter" -angle "degree" -time $time;

	if (`match "^lighting_" $sceneName` == "")
	{
		if ($project == "MayaTheBee" && `match "_FinalLayout_" $sceneName` != "")
		{
			$width = 960;
			$height = 540;
		}
		zwSetAttrInt "defaultResolution.width" $width;
		zwSetAttrInt "defaultResolution.height" $height;
		zwSetAttrFloat "defaultResolution.deviceAspectRatio" $deviceAspectRatio;
		zwSetAttrInt "defaultResolution.lockDeviceAspectRatio" true;
	}

	if ($project == "Enyo" && zwGetMode("") != "effect")
	{
		if (`getAttr "defaultRenderGlobals.currentRenderer"` == "mentalRay")
		{
			if (!`pluginInfo -query -loaded "Mayatomr"`)
			{
				loadPlugin "Mayatomr";
			}
			mentalrayUI "";
			//setAttr -type "string" "defaultRenderGlobals.currentRenderer" "mentalRay";
			catch (`nodePreset -load "miDefaultOptions" "Enyo_mi"`);
		}
		//catch (`nodePreset -load "miDefaultOptions" "Enyo_mi"`);
	}

	zjUnlockdefaultRenderGlobals();

	if (`objExists defaultRenderGlobals`)
	{
		setAttr defaultRenderGlobals.numCpusToUse 0 ;
	}
}