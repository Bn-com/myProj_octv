// 将材质球加回到Hypershade
global proc zwDefaultShaderList()
{
	string $shadingEngines[] = `ls -type "shadingEngine"`;
	for ($shadingEngine in $shadingEngines)
	{
		string $materials[] = `listConnections -destination false $shadingEngine`;
		for ($material in $materials)
		{
			string $buf[] = `listConnections -type "defaultShaderList" ($material + ".message")`;
			if (!size($buf))
			{
				connectAttr ($material + ".message") ":defaultShaderList1.shaders" -nextAvailable;
			}
		}
	}

	string $files[] = `ls -type "file"`;
	for ($file in $files)
	{
		string $buf[] = `listConnections -type "defaultTextureList" ($file + ".message")`;
		if (!size($buf))
		{
			connectAttr ($file + ".message") ":defaultTextureList1.textures" -nextAvailable;
		}
	}
}