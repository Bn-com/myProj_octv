//////////////////////////////////////////////////////////
//
//	Script Name: ddJointOnCurve
//	Verision: 0.31 beta
//	Author: Julii FreeMAN, SZ-CN
//	Date Created: January 25 2005
//	Changed Projcet Date:
//	Last Updated: June 11 2006
//	Update/Change this file at: Julii FreeMAN
//	Note: durdur's Joint On Curve
//
//////////////////////////////////////////////////////////


global proc ddJointOnCurve(int $num,int $att,int $par)
{
 int $i ;
 string $selected[],$baseCurve,$tempName ;

 $selected = `ls -sl` ;
 $baseCurve = $selected[0] ;
 if($baseCurve == "") error "no curve!" ;
 select -cl ;
 for($i=0;$i<$num;$i++)
 {
  
  $tempName = `joint -name ($baseCurve + "_joint_" + $i)` ;
  $tempName = `pathAnimation -fractionMode true -follow true -followAxis x -upAxis y -worldUpType "vector" -worldUpVector 0 1 0 $tempName $baseCurve` ;
  delete($tempName + "_uValue") ;
  setAttr ($tempName + ".uValue") (1.0/($num-1)*$i) ;
//  refresh ;
//  delete $tempName ;
  select -cl ;
 }

 refresh ;

 if($att == false)
 {
  for($i=0;$i<$num;$i++)
  {
   delete `listConnections ($baseCurve + "_joint_" + $i + ".rotateOrder")` ;
  }
 }

 if($par == true)
 {
  for($i=($num-1);$i>0;$i--)
  {
   parent ($baseCurve + "_joint_" + $i) ($baseCurve + "_joint_" + ($i-1)) ;
  }
 }

}

global proc ddAttachJointToCurve(int $option)
{
 int $numTokens ;
 float $uValue,$temp ;
 string $selected[],$buffer[],$tempName,$curveShapes[],$tmp ;

 $selected = `ls -sl` ;

 for($tmp in $selected)
 {
  $numTokens = `tokenize $tmp "[" $buffer` ;
 
  $uValue = `substitute "]" $buffer[1] ""` ;

  tokenize $buffer[0] "." $buffer ;
 
  $curveShapes = `listRelatives -shapes $buffer[0]` ;
 
  select -cl ;

  $tempName = `joint` ;
  $tempName = `pathAnimation -fractionMode false -follow true -followAxis x -upAxis y -worldUpType "vector" -worldUpVector 0 1 0 $tempName $buffer[0]` ;
  print ($tempName + "_uValue") ;
  print (`objExists ($tempName + "_uValue")`) ;
  delete($tempName + "_uValue") ;


  setAttr ($tempName + ".uValue") $uValue ;
 }
 if($option == 0)
 {
  delete $tempName ;
 }

}
global proc ddJointOnCurveCMD()
{
 int $temp ;
 $temp = `textField -query -text numberJonit` ;
 if($temp<2) error "The number of joint be no less than 2." ;
 ddJointOnCurve($temp,`checkBox -query -value attachToCurve`,`checkBox -query -value ParentJoint`) ;
 select -cl ;
}

/////////////////////////////////////////////
// Procedure Name: ddJointOnCurveUI
// interface
//
/////////////////////////////////////////////
global proc ddJointOnCurveUI()
{
  //³õÊ¼»¯Ãæ°å
 if(`window -ex ddJointOnCurveUI`)
 {
  deleteUI ddJointOnCurveUI ;
  if(`windowPref -exists ddJointOnCurveUI`)
  {
   windowPref -remove ddJointOnCurveUI ;
  }
 }
 
 window -width 460
	-height 200
	-title "durdur's Toyboxes Joint On Curve v0.10 beta"
	ddJointOnCurveUI ;
 
 menuBarLayout ;
 menu -label "Edit" editmenu ;
      menuItem -label "Reset Settings" -command "cleartext()";
 menu -label "Help" helpmenu ;
      menuItem -label "Help on Rename Tools..." ;
 setParent .. ;


 columnLayout -columnAlign "center" ;
 //rowLayout -numberOfColumns 2 -columnWidth 1 80 ;
  rowLayout -numberOfColumns 3
	    -columnWidth 1 110
	    -columnWidth 2 80 
	    -columnWidth 3 100 ;
   separator -style "none" ;
   text -label "Number of Joint" -width 80 ;
   textField -width 100 -text 6 numberJonit ;
  setParent .. ;
  
  rowLayout -numberOfColumns 2
	    -columnWidth 1 110 ;
   separator -style "none" ;
   checkBox -label "Attach to Curve" -value false attachToCurve ;
  setParent .. ;

  rowLayout -numberOfColumns 2
	    -columnWidth 1 110 ;
   separator -style "none" ;
   checkBox -label "Parent Joint" -value true ParentJoint ;
  setParent .. ;

  separator -style "none" ;
  rowLayout -numberOfColumns 3 
	   -columnWidth 1 151
	   -columnWidth 2 151
	   -columnWidth 3 151
	   -columnAlign3 "center" "center" "center"
	    ;
   button -label "Create Joint" 
	  -c "ddJointOnCurveCMD;deleteUI ddJointOnCurveUI;"
	  -width 150
	  -height 30 ;
   button -label "Apply" 
	  -c "ddJointOnCurveCMD;"
	  -width 150
	  -height 30 ;
   button -label "Close"
	  -c "deleteUI ddJointOnCurveUI;"
	  -width 150
	  -height 30 ;
  setParent .. ;
  
  separator -width 452 ;
  button -label "Attach a Joint to Curve"
	 -command "ddAttachJointToCurve 1"
	 -width 452
	 -height 30 ;

  button -label "Attach a Joint to Curve Only Position"
	 -command "ddAttachJointToCurve 0"
	 -width 452
	 -height 30 ;

 showWindow ddJointOnCurveUI ;
}

//End of file.