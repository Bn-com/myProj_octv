/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: »ÆÖÙÎ¬
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwHistoryUI()
{
	int $i;
	string $history, $buf[], $buf1[];

	if (`objExists time1.history`)
		$history = `getAttr time1.history`;
	else
		confirmDialog -title "Import/Reference Information" -message "There is neither import nor reference node in the scene." -button "OK";

	if (`window -exists zwHistoryUI`)
		deleteUI zwHistoryUI;

	window -menuBar true -title "Import/Reference Information" -width 600 -height 400 zwHistoryUI;
		menu -label "Help";
			menuItem -label "Help on Import/Reference Information..." -command "zjHelpCallback zwHistoryUI";
		tokenize $history "\n" $buf;
		string $formLayout = `formLayout`;
			string $checkBox = `checkBox -label "Display c, p files only"`;
			string $rowLayout = `rowLayout -numberOfColumns 5 -adjustableColumn 5 -columnAttach 1 "both" 0 -columnAttach 2 "both" 0 -columnAttach 3 "both" 0 -columnAttach 4 "both" 0 -columnWidth 1 25 -columnWidth 2 70 -columnWidth 3 120 -columnWidth 4 100`;
				text -label "";
				string $button1 = `button -docTag 1 -label "Type"`;
				string $button2 = `button -docTag 1 -label "¨‹ Date"`;
				string $button3 = `button -docTag 1 -label "Doby"`;
				string $button4 = `button -docTag 1 -label "Filename"`;
				setParent..;
			string $tabLayout = `tabLayout -tabsVisible false -scrollable true -childResizable true`;
				string $columnLayout = `columnLayout -docTag $history -adjustableColumn true`;
				for ($i=size($buf)-1; $i>=0; $i--)
				{
					if ($buf[$i] == "")
						break;
					tokenize $buf[$i] "\t" $buf1;
					string $time = $buf1[0];
					string $doby = $buf1[1];
					if ($doby == "unknown")
						$doby = "";
					string $type = $buf1[2];
					string $path = $buf1[3];
					tokenize $path "\\" $buf1;
					string $file = $buf1[size($buf1)-1];
					rowLayout -numberOfColumns 5 -adjustableColumn 5 -columnAttach 1 "both" 0 -columnAttach 2 "both" 0 -columnAttach 3 "both" 0 -columnAttach 4 "both" 0 -columnWidth 1 25 -columnWidth 2 70 -columnWidth 3 120 -columnWidth 4 100;
						string $symbolButton = `symbolButton -docTag $buf[$i] -enable false -image "info.xpm"`;
						symbolButton -edit -command ("zwHistoryUIdetail " + $symbolButton) $symbolButton;

						textField -editable false -text $type;
						textField -editable false -text $time;
						textField -editable false -text $doby;
						textField -editable false -text $file -annotation $path;
						setParent..;
				}
	formLayout -edit
		-attachForm	$checkBox	"top"		5
		-attachForm	$checkBox	"left"		25
		-attachForm	$checkBox	"right"		0

		-attachControl	$rowLayout	"top"		5	$checkBox
		-attachForm	$rowLayout	"left"		0
		-attachForm	$rowLayout	"right"		0

		-attachControl	$tabLayout	"top"		0	$rowLayout
		-attachForm	$tabLayout	"left"		0
		-attachForm	$tabLayout	"bottom"	0
		-attachForm	$tabLayout	"right"		0
	$formLayout;

	checkBox -edit -changeCommand ("zwHistoryUIFilter \"" + $columnLayout + "\" \"" + $checkBox + "\"") $checkBox;

	button -edit -command ("zwHistoryUISortByType \"" + $columnLayout + "\" \"" + $checkBox + "\" \"" + $rowLayout + "\" \"" + $button1 + "\" 2") $button1;
	button -edit -command ("zwHistoryUISortByType \"" + $columnLayout + "\" \"" + $checkBox + "\" \"" + $rowLayout + "\" \"" + $button2 + "\" 0") $button2;
	button -edit -command ("zwHistoryUISortByType \"" + $columnLayout + "\" \"" + $checkBox + "\" \"" + $rowLayout + "\" \"" + $button3 + "\" 1") $button3;
	button -edit -command ("zwHistoryUISortByType \"" + $columnLayout + "\" \"" + $checkBox + "\" \"" + $rowLayout + "\" \"" + $button4 + "\" 3") $button4;

	showWindow zwHistoryUI;
}

// $sortBy: 0 time, 1 doby, 2 type, 3 filename
global proc zwHistoryUISortByType(string $columnLayout, string $checkBox, string $rowLayout, string $button, int $sortBy)
{
	string $historyArray[], $sortByArray[], $buf[];

	int $value = `checkBox -query -value $checkBox`;

	int $desc = `button -query -docTag $button`;
	string $label = `button -query -label $button`;
	if (`tokenize $label " " $buf` != 2)
	{
		if ($sortBy == 0)
			$desc = -1;
		else
			$desc = 1;
	}
	button -edit -docTag (0-$desc) $button;

	string $childArray[] = `rowLayout -query -childArray $rowLayout`;
	for ($i=1; $i<size($childArray); $i++)
	{
		string $label = `button -query -label $childArray[$i]`;
		tokenize $label " " $buf;
		button -edit -label $buf[size($buf)-1] $childArray[$i];
	}

	$label = `button -query -label $button`;
	if ($desc == 1)
		button -edit -label ("¡ø " + $label) $button;
	else
		button -edit -label ("¨‹ " + $label) $button;

	string $history = `columnLayout -query -docTag $columnLayout`;
	int $size = `tokenize $history "\n" $historyArray`;
	for ($i=0; $i<$size; $i++)
	{
		tokenize $historyArray[$i] "\t" $buf;
		$sortByArray[$i] = $buf[$sortBy];
		if ($sortBy == 1)
		{
			if ($sortByArray[$i] == "unknown")
				$sortByArray[$i] = "";
		}
		else if ($sortBy == 3)
		{
			tokenize $buf[$sortBy] "/\\" $buf;
			$sortByArray[$i] = $buf[size($buf)-1];
		}
	}

	for ($i=$size-1; $i>=0; $i--)
	{
		for ($j=$i-1; $j>=0; $j--)
		{
			if (`strcmp $sortByArray[$i] $sortByArray[$j]` == $desc)
			{
				string $temp = $sortByArray[$i];
				$sortByArray[$i] = $sortByArray[$j];
				$sortByArray[$j] = $temp;

				$temp = $historyArray[$i];
				$historyArray[$i] = $historyArray[$j];
				$historyArray[$j] = $temp;
			}
		}
	}

	$childArray = `columnLayout -query -childArray $columnLayout`;
	for ($i=0; $i<$size; $i++)
	{
		tokenize $historyArray[$size-$i-1] "\t" $buf;
		string $time = $buf[0];
		string $doby = $buf[1];
		if ($doby == "unknown")
			$doby = "";
		string $type = $buf[2];
		string $path = $buf[3];
		tokenize $path "/\\" $buf;
		string $file = $buf[size($buf)-1];

		$buf = `rowLayout -query -childArray $childArray[$i]`;

		symbolButton -edit -docTag $historyArray[$size-$i-1] $buf[0];
		textField -edit -text $type $buf[1];
		textField -edit -text $time $buf[2];
		textField -edit -text $doby $buf[3];
		textField -edit -text $file -annotation $path $buf[4];

		string $str = `match "_[cp][0-9]{3}[.]" $file`;
		rowLayout -edit -visible (!$value || $str != "") $childArray[$i];
	}
}

global proc zwHistoryUIFilter(string $columnLayout, string $checkBox)
{
	int $value = `checkBox -query -value $checkBox`;
	string $rowLayout[] = `columnLayout -query -childArray $columnLayout`;
	for ($i=0; $i<size($rowLayout); $i++)
	{
		string $buf[] = `rowLayout -query -childArray $rowLayout[$i]`;
		string $filename = `textField -query -text $buf[4]`;
	
		string $str = `match "_[cp][0-9]{3}[.]" $filename`;
		rowLayout -edit -visible (!$value || $str != "") $rowLayout[$i];
	}
}

global proc zwHistoryUIdetail(string $symbolButton)
{
	
	string $id, $creator, $comments, $buf[];

	string $str = `symbolButton -query -docTag $symbolButton`;
	tokenize $str "\t" $buf;
	string $time = $buf[0];
	string $doby = $buf[1];
	if ($doby == "unknown")
		$doby = "";
	string $type = $buf[2];
	string $path = $buf[3];
	tokenize $path "/\\" $buf;
	string $file = $buf[size($buf)-1];

	string $iff = `substring $path 1 (size($path) - 2)` + "iff";
	string $txt = $path + ".txt";

	if (`filetest -f $txt`)
	{
		int $fileId = `fopen $txt "r"`;
		$str = `fgetline $fileId`;
		if (`gmatch $str "ID:*"`)
			$id = `substring $str 5 (size($str))`;
		else
			$creator = $str;
		while (!`feof $fileId`)
		{
			$str = `fgetline $fileId`;
			if(`gmatch $str "Creator:*"`)
				$creator = `substring $str 10 (size($str))`;
			else if(`gmatch $str "Comments:*"`)
			{
				if(size($str)>10)
					$comments += `substring $str 11 (size($str))`;
			}
			else
				$comments += $str;
		}
		fclose $fileId;
	}

	string $modified = `idmtFile -modified $path`;	// by huangzhongwei 20060425

	if (`window -exists zwHistoryUIdetail`)
		deleteUI zwHistoryUIdetail;	
	window -title $file -widthHeight 465 190 zwHistoryUIdetail;
		columnLayout -adjustableColumn true;
			textFieldGrp -editable false -columnWidth 1 80 -adjustableColumn 2 -label "Path:" -text $path;
			formLayout formLayout1;
				columnLayout -adjustableColumn true columnLayout1;
					textFieldGrp -editable false -adjustableColumn 2 -columnWidth 1 80 -label ($type + ":") -text $time;
					textFieldGrp -editable false -adjustableColumn 2 -columnWidth 1 80 -label "Modified:" -text $modified;
					textFieldGrp -editable false -adjustableColumn 2 -columnWidth 1 80 -label "ID:" -text $id;
					textFieldGrp -editable false -adjustableColumn 2 -columnWidth 1 80 -label "Creator:" -text $creator;
					scrollField -editable false -wordWrap true -text $comments;
					setParent..;
				if (`filetest -f $iff`)
				{
					chdir "C:";
					string $iffInfo = `system ("fcheck -cv " + $iff)`;
					tokenize $iffInfo "\n" $buf;
					tokenize $buf[3] " " $buf;
					if($buf[2] == "64x64")
					{
						image -width 64 -height 64 -image $iff image1;
						formLayout -edit
							-attachForm	image1		"top"	37
							-attachForm	image1		"right"	37

							-attachForm	columnLayout1	"left"	0
							-attachControl	columnLayout1	"right"	37	image1
						formLayout1;
					}
					else
					{
						image -width 128 -height 128 -image $iff image1;
						formLayout -edit
							-attachForm	image1		"top"	5
							-attachForm	image1		"right"	5

							-attachForm	columnLayout1	"left"	0
							-attachControl	columnLayout1	"right"	5	image1
						formLayout1;
					}
				}

	showWindow zwHistoryUIdetail;
}