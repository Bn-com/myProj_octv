global proc LayoutCameraTool()
{
	global string $layoutCameraPath;
	$layoutCameraPath = `getenv "IDMT_PROJECTS"` + "/Thru_The_Moebius_Strip/Production/MASTER/animation/";
//	$layoutCameraPath = "//file-cluster/GDC/Scratch/publish/MASTER/animation/";
	
	if (`window -exists LayoutCameraTool`)
		deleteUI LayoutCameraTool;
	window -title "Layout Camera" -width 150 LayoutCameraTool;
		columnLayout -adjustableColumn true;
			button -label "Publish Camera" -command "publishCamera";
			button -label "Reference Camera" -command "referenceCamera";
			button -label "Update Camera" -command "updateCamera";
			button -label "Edit Camera" -command "editCamera";
			button -label "Upload Camera" -command "uploadCamera";
	showWindow LayoutCameraTool;
//	eval ($type + "Camera");
}

//Create Reference
//
global proc referenceCamera()
{
	global string $layoutCameraPath;
	string $str, $buffer[];

	if(`window -exists layoutCamera`)
		deleteUI layoutCamera;
	window -title "Reference Camera" -width 300 -height 400 layoutCamera;
		formLayout formLayout1;
			text -label "Scene:" text1;
			text -label "Camera:" text2;
			textScrollList -selectCommand "referenceCameraListCamera" textScrollListFile;
			textScrollList -allowMultiSelection true -doubleClickCommand "referenceCameraProc" textScrollListCamera;
			button -label "Reference Camera" -command "referenceCameraProc" button1;
		formLayout -edit
			-attachForm		text1			"top"		5
			-attachForm		text1			"left"		10

			-attachForm		text2			"top"		5
			-attachPosition		text2			"left"		5	50

			-attachForm		button1			"left"		5
			-attachForm		button1			"bottom"	5
			-attachForm		button1			"right"		5

			-attachControl		textScrollListFile	"top"		5	text1
			-attachForm		textScrollListFile	"left"		5
			-attachControl		textScrollListFile	"bottom"	5	button1
			-attachPosition		textScrollListFile	"right"		5	50
			
			-attachControl		textScrollListCamera	"top"		5	text2
			-attachPosition		textScrollListCamera	"left"		0	50
			-attachControl		textScrollListCamera	"bottom"	5	button1
			-attachForm		textScrollListCamera	"right"		5
		formLayout1;

		$buffer = `getFileList -folder $layoutCameraPath -filespec "scene_???"`;
		for ($str in $buffer)
			textScrollList -edit -append $str textScrollListFile;

	showWindow layoutCamera;
}

global proc referenceCameraListCamera()
{
	global string $layoutCameraPath;
	string $str, $buffer[];
	$buffer = `textScrollList -query -selectItem textScrollListFile`;
	$str = $layoutCameraPath + $buffer[0] + "/scene_camera/approved/cam_*_master.mb";
	textScrollList -edit -removeAll textScrollListCamera;
	$buffer = `getFileList -filespec $str`;
	for ($str in $buffer)
	{
		$str = `substring $str 1 (size($str) - 10)`;
		textScrollList -edit -append $str textScrollListCamera;
	}
}

global proc referenceCameraProc()
{
	global string $layoutCameraPath;
	string $str, $camera, $file, $buffer[], $buffer1[];
	$buffer = `textScrollList -query -selectItem textScrollListFile`;
	$file =  $layoutCameraPath + $buffer[0] + "/scene_camera/approved/";
	$buffer = `textScrollList -query -selectItem textScrollListCamera`;
	for ($camera in $buffer)
	{
		$str = $file + $camera + "_master.mb";
		$buffer1 = `file -query -reference`;
		if(`stringArrayCount $str $buffer1`)
			file -loadReference `file -query -referenceNode $str` $str;
		else
		{
			$buffer1 = `namespaceInfo -listNamespace`;
			if(`stringArrayCount ($camera + "_master") $buffer1`)
				catch(`namespace -removeNamespace ($camera + "_master")`);
			file -reference -namespace ($camera + "_master") $str;
		}
	}
}

//Load Reference
//
global proc updateCamera()
{
	global string $layoutCameraPath;
	string $str, $buffer[];

	if(`window -exists layoutCamera`)
		deleteUI layoutCamera;
	window -title "Update Camera" -width 300 -height 400 layoutCamera;
		formLayout formLayout1;
			textScrollList -allowMultiSelection true -doubleClickCommand "updateCameraProc" textScrollListCamera;
			button -label "Update Camera" -command "updateCameraProc" button1;
		formLayout -edit
			-attachForm		button1			"left"		5
			-attachForm		button1			"bottom"	5
			-attachForm		button1			"right"		5

			-attachForm		textScrollListCamera	"top"		5
			-attachForm		textScrollListCamera	"left"		5
			-attachControl		textScrollListCamera	"bottom"	5	button1
			-attachForm		textScrollListCamera	"right"		5
		formLayout1;

		$buffer = `file -query -reference`;
		for ($str in $buffer)
		{
			if(`gmatch $str ($layoutCameraPath + "scene_???/scene_camera/approved/cam_???_???_master.mb")`)
			{
				$str = `substring $str (size($str)-20) (size($str)-10)`;
				textScrollList -edit -append $str textScrollListCamera;
			}
		}

	showWindow layoutCamera;
}

global proc updateCameraProc()
{
	global string $layoutCameraPath;
	string $str, $buffer[];

	$buffer = `textScrollList -query -selectItem textScrollListCamera`;
	for ($str in $buffer)
	{
		$str =  $layoutCameraPath + "scene_" + `substring $str 5 7 ` + "/scene_camera/approved/" + $str + "_master.mb";
		file -loadReference `file -query -referenceNode $str` $str;
	}
}

//Import Objects from Reference
//
global proc editCamera()
{
	global string $layoutCameraPath;
	string $str, $buffer[];

	if(`window -exists layoutCamera`)
		deleteUI layoutCamera;
	window -title "Edit Camera" -width 300 -height 400 layoutCamera;
		formLayout formLayout1;
			textScrollList -allowMultiSelection true -doubleClickCommand "editCameraProc" textScrollListCamera;
			button -label "Edit Camera" -command "editCameraProc" button1;
		formLayout -edit
			-attachForm		button1			"left"		5
			-attachForm		button1			"bottom"	5
			-attachForm		button1			"right"		5

			-attachForm		textScrollListCamera	"top"		5
			-attachForm		textScrollListCamera	"left"		5
			-attachControl		textScrollListCamera	"bottom"	5	button1
			-attachForm		textScrollListCamera	"right"		5
		formLayout1;

		$buffer = `file -query -reference`;
		for ($str in $buffer)
		{
			if(`gmatch $str ($layoutCameraPath + "scene_???/scene_camera/approved/cam_???_???_master.mb")`)
			{
				$str = `substring $str (size($str)-20) (size($str)-10)`;
				textScrollList -edit -append $str textScrollListCamera;
			}
		}

	showWindow layoutCamera;
}

global proc editCameraProc()
{
	global string $layoutCameraPath;
	string $str, $file, $buffer[];

	$buffer = `textScrollList -query -selectItem textScrollListCamera`;
	for ($str in $buffer)
	{
		$file =  $layoutCameraPath + "scene_" + `substring $str 5 7` + "/scene_camera/approved/" + $str + "_master.mb";
		file -removeReference $file;
		file -import -type "mayaBinary" -namespace ($str + "_master") $file;
		editCameraNamespace $str;
		editCameraNamespace $str;
		catch (`namespace -removeNamespace ($str + "_master")`);
	//	file -importReference $file;
		textScrollList -edit -removeItem $str textScrollListCamera;
	}
}

global proc editCameraNamespace(string $camera)
{
	string $obj, $new, $objs[], $buf[];

	$objs = `ls ($camera + "_master*:*")`;
	for($obj in $objs)
	{
		if (`objExists $obj`)
		{
			tokenize $obj ":" $buf;
			$new = $buf[size($buf)-1];
			while(`objExists $new`)
				$new += "_new";
			catch (`rename $obj $new`);
		}
	}
}

//Upload Reference
//
global proc uploadCamera()
{
	global string $layoutCameraPath;
	string $str, $buffer[];

	if(`window -exists layoutCamera`)
		deleteUI layoutCamera;
	window -title "Upload Camera" -width 300 -height 400 layoutCamera;
		formLayout formLayout1;
			textScrollList -allowMultiSelection true -doubleClickCommand "uploadCameraProc" textScrollListCamera;
			button -label "Upload Camera" -command "uploadCameraProc" button1;
		formLayout -edit
			-attachForm		button1			"left"		5
			-attachForm		button1			"bottom"	5
			-attachForm		button1			"right"		5

			-attachForm		textScrollListCamera	"top"		5
			-attachForm		textScrollListCamera	"left"		5
			-attachControl		textScrollListCamera	"bottom"	5	button1
			-attachForm		textScrollListCamera	"right"		5
		formLayout1;

		$buffer = `ls "cam_???_???"`;
		for ($str in $buffer)
		{
			if(!`reference -query -isNodeReferenced $str`)
				textScrollList -edit -append $str textScrollListCamera;
		}
	
	showWindow layoutCamera;
}

global proc uploadCameraProc()
{
	global string $layoutCameraPath;
	string $str, $file, $dest, $camera, $buffer[], $buffer1[], $staff[];
	$staff = {
		"jerry",
		"denzhenyu",
		"luomaofeng",
		"yuyahui",
		"zhonghai",
		"kongdeyi",
		"zhuwenchao",
		"xiongyurong",
		"lifengxuan",
		"lizhe",
		
		"shine",
		"yanjunbin",
		"mengzhihui",
		"yangten",
		"zhangwei",
		"zhouchen",
		"huangyan",
		"zhangrunhua",
		"pengfei",
		"guojianhua",
		"wanghong",
		"wangqiming",
		"zhouxun",
		"chenjinsong",
		"sundixin",
		"wangxiao",
		"tangwei",
		"xiaochunling",
		"qiujunfeng",
		
		"linting",
		"michell",
		"xiaoyong",
		"zhangchunyue",
		"qiuyongmei",
		"lupeng",
		"zhangyi",
		"yutao",
		
		"manny",
		"wayne",
		"kelven",
		"bob",
		
		"sarah",
		"duanlongge",
		"xuyifan",
		"liaoshangheng",
		"cz",
		
		"harry",
		"tanmeide",
		"guolei",
		"guoqichen",
		"wufang",
		"chenming",
		"qiaowenbin",
		"liqingning",
		"jiyuanwen",
		"chenzhiguo",
		"wupeng",
		"shike",
		"chenmingdong",
		"yuanjianhua",
		"wangcongxin",
		"gaoqiang"
		};
	
	$buffer = `textScrollList -query -selectItem textScrollListCamera`;
	for ($camera in $buffer)
	{
		$buffer1 = `listAttr -keyable $camera`;
		for ($str in $buffer1)
		{
			if(!`keyframe -time "31:31" -query -keyframeCount ($camera + "." + $str)`)
			{
				select $camera;
				error ($camera + "." + $str + " has not keyframe in 31f.");
			}
			if(`keyframe -time "31:" -query -keyframeCount ($camera + "." + $str)` < 2)
			{
				select $camera;
				error ($camera + "." + $str + " has less than 2 keyframes after 31f.");
			}
		}
	}
	for ($camera in $buffer)
	{
		$buffer1 = `listRelatives -children $camera`;
		$shape = $buffer1[0];
		$buffer1 = `listAttr -keyable $camera`;
		for ($str in $buffer1)
			setAttr -lock true ($camera + "." + $str);
		$buffer1 = `listAttr -keyable $shape`;
		for ($str in $buffer1)
			setAttr -lock true ($shape + "." + $str);
		select $camera;
		
		$file = `internalVar -userTmpDir` + $camera + "_master.mb";
		if(`filetest -f $file`)
			sysFile -delete $file;
		file -exportAsReference -type "mayaBinary" -namespace ($camera + "_master") $file;
		
		$dest =  $layoutCameraPath + "scene_" + `substring $camera 5 7` + "/scene_camera/approved";
		zwPublishCopyFile $file $dest "maya" "MASTER" false;
		$str = `internalVar -userTmpDir` + `publishCameraFilename $dest $camera`;
		if(`filetest -f $str`)
			sysFile -delete $str;
		sysFile -rename $str $file;
		zwPublishCopyFile $str $dest "maya" "MASTER" false;
		$dest = $dest + "/" + $camera + "_master.mb";
		file -loadReference `file -query -referenceNode $file` $dest;
		sysFile -delete $str;
		textScrollList -edit -removeItem $camera textScrollListCamera;
		chdir "c:";
	//	for ($str in $staff)
	//		system ("shell net send " + $str + " " + $camera + " has been changed.");
	}
}

//Publish Reference
//
global proc publishCamera()
{
	global string $layoutCameraPath;
	string $str, $buffer[];

	if(`window -exists layoutCamera`)
		deleteUI layoutCamera;
	window -title "Publish Camera" -width 300 -height 400 layoutCamera;
		formLayout formLayout1;
			textScrollList -doubleClickCommand "publishCameraProc" textScrollListFile;
			button -label "Publish Camera" -command "publishCameraProc" button1;
		formLayout -edit
			-attachForm		button1			"left"		5
			-attachForm		button1			"bottom"	5
			-attachForm		button1			"right"		5

			-attachForm		textScrollListFile	"top"		5
			-attachForm		textScrollListFile	"left"		5
			-attachControl		textScrollListFile	"bottom"	5	button1
			-attachForm		textScrollListFile	"right"		5
		formLayout1;

		$buffer = `getFileList -folder $layoutCameraPath -filespec "scene_???"`;
		for ($str in $buffer)
			textScrollList -edit -append $str textScrollListFile;

	showWindow layoutCamera;
}

global proc publishCameraProc()
{
	global string $layoutCameraPath;
	string $str, $camera, $shape, $dest, $file, $cameras[], $buffer[];

	$buffer = `textScrollList -query -selectItem textScrollListFile`;
	if(!size($buffer))
		error "Please a scene.";
	$dest =  $layoutCameraPath + $buffer[0] + "/scene_camera/approved";

	$cameras = `ls -transforms "cam_*"`;
	for ($camera in $cameras)
	{
		$buffer = `listRelatives -children $camera`;
		$shape = $buffer[0];
		if(`nodeType $shape` == "camera")
		{
			$buffer = `listAttr -keyable $camera`;
			for ($str in $buffer)
			{
				if(!`keyframe -time "31:31" -query -keyframeCount ($camera + "." + $str)`)
				{
					select $camera;
					error ($camera + "." + $str + " has not keyframe in 31f.");
				}
				if(`keyframe -time "31:" -query -keyframeCount ($camera + "." + $str)` < 2)
				{
					select $camera;
					error ($camera + "." + $str + " has less than 2 keyframes after 31f.");
				}
			}
		}
	}

	$cameras = `ls -transforms "cam_*"`;
	for ($camera in $cameras)
	{
		$buffer = `listRelatives -children $camera`;
		$shape = $buffer[0];
		if(`nodeType $shape` == "camera")
		{
			$buffer = `listAttr -keyable $camera`;
			for ($str in $buffer)
				setAttr -lock true ($camera + "." + $str);
			$buffer = `listAttr -keyable $shape`;
			for ($str in $buffer)
				setAttr -lock true ($shape + "." + $str);
			select $camera;
			$file = `internalVar -userTmpDir` + `publishCameraFilename $dest $camera`;
			if(`filetest -f $file`)
				sysFile -delete $file;
			file -exportSelected -type "mayaBinary" $file;
			zwPublishCopyFile $file $dest "maya" "MASTER" false;
			sysFile -rename (`internalVar -userTmpDir` + $camera + "_master.mb") $file;
			$file = `internalVar -userTmpDir` + $camera + "_master.mb";
			zwPublishCopyFile $file $dest "maya" "MASTER" false;
			sysFile -delete $file;
		}
	}
	confirmDialog -title "Publish Cameras" -message "Publishing cameras is successful!" -button "OK";
	deleteUI layoutCamera;
}

global proc string publishCameraFilename(string $dir, string $camera)
{
	int $i, $j;
	string $str, $buffer[];
	$str = $dir + "/" + $camera + "_???.mb";
	$buffer = `getFileList -filespec $str`;
	for ($str in $buffer)
	{
		$str = `substring $str (size($str)-5) (size($str)-3)`;
		$j = (int)$str;
		if($j>$i)
			$i = $j;
	}
	$i += 1;
	if($i<10)
		$str = "00" + $i;
	else if($i<100)
		$str = "0" + $i;
	else
		$str = "" + $i;
	$str = $camera + "_" + $str + ".mb";
	return $str;
}