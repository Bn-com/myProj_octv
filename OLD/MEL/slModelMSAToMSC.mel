//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 2010-6-17
//
//	Update/Change: WansHoulOng
//	Last Updated: 2010-6-18
//	Description:
//				通过ms_anim和tx文件制作ms_cache文件
//				注意：
//					1.ms_anim文件中必须有CACHE_OBJS|MESHES的set组，且set组的内容为制作Geocache的所有物体
//					2.ms_anim文件和tx文件必须是checkin到数据库上的
//					3.打开网上的ms_anim文件使用工具即可，不能将文件复制到本地使用
//				
//////////////////////////////////////////////////////////


global proc slModelMSAToMSC()
{
	string $sel[] = `sets -q MESHES`;
	if (!size($sel))
		error "请先建立正确的名为CACHE_OBJS|MESHES的SET";

	string $filename = `file -q -sn`;
	$filename = `substitute "/master/" $filename "/texture/"`;
	string $tx = `substitute "ms_anim" $filename "tx"`;
	$tx = `substitute "ms_dyn" $tx "tx"`;	// 黄仲维20100806加，赵志杰说ms_dyn文件也要用这个工具
	if (!`filetest -r $tx`)
		error "tx文件不存在";

	string $mayaType[] = `file -q -type`;//增加判断类型的mb、ma都可以使用  wanshoulong@idmt.com.cn  11/8/2011
	string $source = `file -q -sn -shn`;
	string $fd = `workspace -q  -rootDirectory` + "scenes/";
//	string $fn = $fd + `substitute "ms_anim" $source "ms_cache"`;	// 黄仲维20100806加，赵志杰说ms_dyn文件也要用这个工具
	string $cache = `substitute "ms_anim" $source "ms_cache"`;
	$cache = `substitute "ms_dyn" $cache "ms_cache"`;
	string $fn = $fd + $cache;
	select -r $sel;
	
	string $expsets[] = `ls -type objectSet "Smooth" "No_smooth"`;
	if (size($expsets))
	{
		select -add -ne  $expsets;
	}

	file -f -constructionHistory 0 -channels 0 -constraints 0 -expressions 0 -shader 0 -type $mayaType[0] -exportSelected $fn;
	//delete $res;
	 
	
	string $mayalocation =`getenv "MAYA_LOCATION"`;
	string $cmd = ("\"\"" + `substituteAllString $mayalocation "/" "\\"` + "\\bin\\mayabatch.exe\" -command \"zjApplyMaterialcache " + "\\\"" + $tx + "\\\"\" -file \"" + `substituteAllString $fn "/" "\\"` + "\"\"");
	print `system $cmd`;
	print ("\nfile:" + $fn);

	file -f -options "v=0;p=17"  -ignoreVersion  -typ $mayaType[0] -o $fn;
}