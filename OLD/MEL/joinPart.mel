//Partools is a aid tool for particle system in Maya,you can combine multi particle object to one,
//or split a particle object into two pieces;
//Written by Jerry Mitchell.7.2002.


//Combine multi-particle Object procedure;
global proc joinPart()
{
	string $selParts[] = `ls -sl`;
	int $count = 0;
	string $comStr = "particle ";
	for ($part in $selParts)
	{
		$count = `particle -q -count $part`;
		for ($i=0;$i<$count;$i++)
		{
			float $pos[3] = `pointPosition -w ($part+".pt["+$i+"]")`;
			$comStr += "-p " + $pos[0] + " " + $pos[1] + " " + $pos[2] + " ";
		}
		delete $part;
	}
	$comStr += ";\n";
	eval $comStr;	
}



//Separate a particle object into two pieces;
global proc splitPart()
{
	string $partPoints[] = `filterExpand -sm 47`;
	string $buffer[];
	tokenize $partPoints[0] "." $buffer;
	int $count = `particle -q -count $buffer[0]`;
	string $comStr1 = "particle ";
	string $comStr2 = "particle ";
	int $factor = 0;
	for ($i = 0;$i < $count;$i ++)
	{
		float $pos[3] = `pointPosition -w ($buffer[0]+".pt["+$i+"]")`;		
		for ($points in $partPoints)
		{
			if ($points == $buffer[0]+".pt["+$i+"]")
			{
			$comStr1 += "-p " + $pos[0] + " " + $pos[1] + " " + $pos[2] + " ";
			$factor = 1;
			break;
			}
			else
			$factor = 0;
		}
		if ($factor == 1)
		{
			continue;
		}
		else
		$comStr2 += "-p " + $pos[0] + " " + $pos[1] + " " + $pos[2] + " ";
	}
	$comStr1 += ";\n";
	$comStr1 += $comStr2;
	delete $buffer[0];
	eval $comStr1;
	
}



//Place tools,for locate the crowd characters.
//Select the particle transform node at first,and then select the object which would be duplicated 
//and placed to the points position,and run the procdure,they will be there.
//
global proc point2Obj()
{
	string $selParts[] = `ls -sl`;
	int $count = 0;
	string $grp = `group -empty -n locGrp`; 
	$count = `particle -q -count $selParts[0]`;
	for ($i=0;$i<$count;$i++)
	{
		float $pos[3] = `pointPosition -w ($selParts[0]+".pt["+$i+"]")`;
		string $obj[] = `duplicate $selParts[1]`;
		setAttr ($obj[0]+".tx") $pos[0];
		setAttr ($obj[0]+".ty") $pos[1];
		setAttr ($obj[0]+".tz") $pos[2];
		parent -relative $obj[0] $grp;
	}
}