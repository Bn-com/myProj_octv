/// Author: liang
///
/// Creation Date: 2007/02/01
///
/// Description: 层次控制添加器(在原物体上级添加一个与其相同的物体用于控制它)

/// Modify: liang

///////////////////////////////////////////////////////////////////

global proc lidControlAdd(string $lidSuffix)
{
	//$lidSuffix为新lid的后缀名
	string $tempList[];			//选择物体
	string $tempCurrentList;	//循环处理中当前处理的选择物体
	
	
	string $tempList[] = `ls -sl`;
	for ($tempCurrentList in $tempList)
	{
		//判断此物体新生成的lid名是否与其父物体同名，如果是，则提示改后缀或换选其父物体进行加lid操作
		string $parentName[] = `listRelatives -p $tempCurrentList`;
		if(`gmatch $parentName[0] ($tempCurrentList + $lidSuffix)` ==0)
		{
			//复制当前处理的物体，并将新复制的物体改名为原物体名加上后缀
			select $tempCurrentList;
			string $newDupName[] = `duplicate`;
			rename $newDupName[0] ($tempCurrentList + $lidSuffix);
			
			//删除新复制物体的子物体
			select -hi ($tempCurrentList + $lidSuffix);
			select -d ($tempCurrentList + $lidSuffix);
			string $tempSel[] = `ls -sl`;
			if(`size $tempSel` != 0)
			{
				delete;
			}
			//将原物体作为新复制物体的子物体
			parent $tempCurrentList ($tempCurrentList + $lidSuffix);
			if (`attributeExists "inverseScale" $tempCurrentList`==1)
			{
				connectAttr ($tempCurrentList + $lidSuffix + ".scale") ($tempCurrentList + ".inverseScale");
			}
			select ($tempCurrentList + $lidSuffix);
		}
		else
		{
		print ("你选择的物体"+ $tempCurrentList +"新生成的lid名与其父物体同名，请改变后缀或换选其父物体进行加lid操作" + "\n");
		}
	}
}
