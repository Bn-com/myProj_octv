global proc maNus_bmp2xpm_sa(){
	string $file;
	string $all[] = `textScrollList -q -ai bmp2xpm_list`;
	for($file in $all)
		textScrollList -e -si $file bmp2xpm_list;}

//	===================================================================================================================================
// get all images from selected directory
global proc maNus_bmp2xpm_loadFiles(string $filename, string $fileType ){
	string $file;
	string $bmp_files[] = `getFileList -folder ($filename + "/") -filespec "*.bmp"`;
	
//	create directory text
	text -l $filename -vis 0 -p bmp2xpm_main bmp2xpm_dir;
	
	for ($file in $bmp_files)
		textScrollList -e -a $file bmp2xpm_list;}

//	===================================================================================================================================
// display directory picker
global proc maNus_bmp2xpm_getDirectory(){
	fileBrowser "maNus_bmp2xpm_loadFiles" "Set Directory" "" 4;}	

//	===================================================================================================================================
// convert images
global proc maNus_bmp2xpm_convert(){
string $dir = (`text -q -l bmp2xpm_dir` + "/");

string $file;
string $files[] = `textScrollList -q -si bmp2xpm_list`;
for($file in $files){
//	get the name of the file
	string $buffer[];
	tokenize $file "." $buffer;

	string $command = ($dir + $file + " xpm:" + $dir + $buffer[0] + ".xpm");
	print ("// converting " + $file + " to " + $buffer[0] + ".xpm.\n");
	system ("D:/Alias/MAYA8.5/bin/imconvert " + $command);}
	
//	print done
	print ("// done converting image(s).\n");}

//	===================================================================================================================================
// main proc and main UI
global proc maNus_bmp2xpm(){
	if(`window -ex bmp2xpm`)
		deleteUI bmp2xpm;
//	create UI
	string $win = `window -t "maNus_bmp2xpm" bmp2xpm`;
	string $for = `formLayout bmp2xpm_main`;
	string $f_l = `textScrollList -h 200 -w 200 -ams 1		         -p $for bmp2xpm_list`;
	string $g_b = `button         -h  20 -w 100 -l "set directory"		 -p $for -c "maNus_bmp2xpm_getDirectory"`;
	string $s_a = `button	      -h  20 -w 100 -l "select all"		 -p $for -c "maNus_bmp2xpm_sa"`;
	string $c_b = `button	      -h  20 -w 200 -l "convert selected images" -p $for -c "maNus_bmp2xpm_convert"`;

//	edit UI
	formLayout -e

		-attachForm	 $f_l	"left"	 4
		-attachPosition	 $f_l	"top"	 0	2

		-attachForm	 $g_b	"left"	 4
		-attachPosition	 $g_b	"top"	 0	83

		-attachForm	 $s_a	"left"	 104
		-attachPosition	 $s_a	"top"	 0	83

		-attachForm	 $c_b	"left"	 4
		-attachPosition	 $c_b	"top"	 0	91

	$for;

	window -e 
		-w 214
		-h 280
	$win;
	showWindow;}

//	===================================================================================================================================