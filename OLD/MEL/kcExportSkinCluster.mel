/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc kcExportSkinCluster(string $filename, string $mesh, string $skinCluster)
{
	int $fileId = `fopen $filename "w"`;

	if ($fileId == 0)
		error("Cannot open " + $filename);

	fprint $fileId ($mesh + "\n");

	int $numVtx = `getAttr -size ($mesh + ".vrts")`;
	
	fprint $fileId ($numVtx + "\n");

	for ($i = 0; $i < $numVtx; $i++)
	{
		string $vtx = $mesh + ".vtx[" + $i + "]";
		string $joints[] = `skinPercent -ib 0.00001 -q -transform $skinCluster $vtx`;

		fprint $fileId $i;

		for ($joint in $joints)
		{
			float $weight = `skinPercent -transform $joint -q $skinCluster $vtx`;

			fprint $fileId (" " + $joint + " " + $weight);
		}

		fprint $fileId "\n";
	}

	fclose $fileId;
}