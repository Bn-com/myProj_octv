/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: 尹晟
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc  CycleSplitCloth()
{
		int $textField=`textField -q -enable FS_PlayblastIn`;			
		if ($textField==0)
		{
			print "Please press button \"Get Time Range\"";
			return;
		}
		
		int $minTime = int(`textField -q -text FS_PlayblastIn`);		//如果138	
		int $maxTime = int(`textField -q -text FS_PlayblastOut`);		//如果162
		int $CutValue=$minTime-`intSliderGrp -q -value ObligateKeyframe`;	//133
		playbackOptions -min $CutValue -max $maxTime ;				//范围设为138-162
		currentTime $CutValue;							//把动画起始帧调为133
		string $clothObj[] = `ls -sl`;
		string $BlendClothGroup[];
		if (size($clothObj)==1&&`nodeType $clothObj[0]`=="transform")
		{
			for ($i=0;$i<$maxTime-$CutValue;$i++)
			{
				string $buf[] = `duplicate $clothObj[0]`;
				$BlendClothGroup[$i]=$buf[0];
				currentTime ($CutValue+$i+1);
			}
		}									//复制cloth为29个
		int $ObligateKeyframeCount=`intSliderGrp -q -value ObligateKeyframe`;	//如果5
		float $Count=1.000/($ObligateKeyframeCount+1);
		string $NewClothObject[];
		for ($i=0;$i<$ObligateKeyframeCount;$i++)
		{
			if (`objExists ("ycClothBlendShape"+$i)`)
			{
				delete ("ycClothBlendShape"+$i);
			}
			blendShape -name ("ycClothBlendShape"+$i) $BlendClothGroup[$i];
			blendShape -edit -target $BlendClothGroup[$i] 0 $BlendClothGroup[$maxTime-$CutValue-$ObligateKeyframeCount+$i] 1.0 ("ycClothBlendShape"+$i);		
			setAttr ("ycClothBlendShape"+$i+"."+$BlendClothGroup[$maxTime-$CutValue-$ObligateKeyframeCount+$i]) (($ObligateKeyframeCount-$i)*$Count);
			string $DuplicateClothName[]=`duplicate $BlendClothGroup[$i]`;
			delete $BlendClothGroup[$i];
			delete $BlendClothGroup[$maxTime-$CutValue-$ObligateKeyframeCount+$i];
			$BlendClothGroup[$i]="";
			$BlendClothGroup[$maxTime-$CutValue-$ObligateKeyframeCount+$i]=$DuplicateClothName[0];
		}									//生成新的序列
		playbackOptions -min $minTime -max $maxTime ;				//把动画播放范围设为循环

		currentTime $minTime;
		string $ClothBlendShape[] = `duplicate $BlendClothGroup[$ObligateKeyframeCount]`;	
		string $blendShapeName[]=`blendShape $ClothBlendShape[0]`;
		string $BlendAttr[];
		for ($i=$ObligateKeyframeCount;$i<size($BlendClothGroup);$i++)
		{
			blendShape -edit -target $ClothBlendShape[0] ($i-$ObligateKeyframeCount) $BlendClothGroup[$i] 1.0 $blendShapeName[0];
			delete $BlendClothGroup[$i];
			$BlendAttr[$i-$ObligateKeyframeCount]=$blendShapeName[0]+"."+$BlendClothGroup[$i];
		}
		int $BlendAttrLength=size($BlendAttr);
		for ($j=0;$j<$BlendAttrLength;$j++)
		{
			for ($i=$minTime;$i<=$maxTime;$i++)
			{
				if ($j==($i-$minTime))
				{
					setKeyframe -time $i -value 1 $BlendAttr[$j];
				}
				else
				{
					setKeyframe -time $i -value 0 $BlendAttr[$j];
				}
			}
			keyTangent -itt flat -ott flat $BlendAttr[$j];
		}
		//cutKey $BlendAttr[$BlendAttrLength-1];
}

global proc CycleSplit()
{
	int $textField=`textField -q -enable FS_PlayblastIn`;
	if ($textField==0)
	{
		print "Please press button \"Get Time Range\"";
		return;
	}

	string $ListObj[]=`ls -type transform -sl`;

	if ($ListObj[0] == ""||`match "MASTER" $ListObj[0]`!="MASTER")
	{
		string $whatShouldiDo = `confirmDialog 
					-title "Nothing is selected!" 
					-message "You must first select your MASTER group before running this script.\nIf you would like the script to select the object named MASTER group, then click \"AutoSelect\""
					-button "AutoSelect" -button "Cancel" -defaultButton "AutoSelect"
					-cancelButton "Cancel" -dismissString "Cancel"`;

		if ($whatShouldiDo == "Cancel")
		{
			print ("AutoSelect canceled. Select the MASTER group first, then execute the script.");
			return;
		}		
		else if ($whatShouldiDo == "AutoSelect")
		{
			select "\*MASTER\*";
		}
	}

	select -hi;
	string $AllListObj[]=`ls -type transform -sl`;
	int $minTime = int(`textField -q -text FS_PlayblastIn`);
	int $maxTime = int(`textField -q -text FS_PlayblastOut`);
	int $ObligatKeyframeValue=`intSliderGrp -q -value ObligateKeyframe`;
	int $CutValue=$minTime-`intSliderGrp -q -value ObligateKeyframe`;

	for ($i=0;$i<size($AllListObj);$i++)
	{
		string $ListAnimatableAttr[]=`listAttr -k -v $AllListObj[$i]`;
		for ($j=0;$j<size($ListAnimatableAttr);$j++)
		{
			if (`nodeType $AllListObj[$i]`=="transform"||`nodeType $AllListObj[$i]`=="joint")
			{
				int $JudgeKeyFrameCount=`keyframe -q -keyframeCount ($AllListObj[$i]+"."+$ListAnimatableAttr[$j])`;
				if ($JudgeKeyFrameCount<$ObligatKeyframeValue)
				{
					continue;
				}
				else
				{
					float $firstCurveValue =`findKeyframe -which first ($AllListObj[$i]+"."+$ListAnimatableAttr[$j])`;				//发现第一个关键帧
					float $lastCurveValue = `findKeyframe -which last  ($AllListObj[$i]+"."+$ListAnimatableAttr[$j])`;
					setKeyframe -insert -time $minTime ($AllListObj[$i]+"."+$ListAnimatableAttr[$j]);						//添加关键帧
					setKeyframe -insert -time $maxTime ($AllListObj[$i]+"."+$ListAnimatableAttr[$j]);						//添加关键帧
					if ($firstCurveValue<=$CutValue)
					{
						setKeyframe -insert -time $CutValue ($AllListObj[$i]+"."+$ListAnimatableAttr[$j]);						//添加关键帧
						float $previousCurveValue = `findKeyframe -which previous -time $CutValue ($AllListObj[$i]+"."+$ListAnimatableAttr[$j])`;	//发现前一个关键帧	
						cutKey -t ($firstCurveValue+":"+$previousCurveValue) -clear -attribute $ListAnimatableAttr[$j] -option keys $AllListObj[$i];	//删除关键帧
						float $NextCurveValue = `findKeyframe -which next -time $maxTime ($AllListObj[$i]+"."+$ListAnimatableAttr[$j])`;	//发现前一个关键帧	
						cutKey -t ($NextCurveValue+":"+$lastCurveValue) -clear -attribute $ListAnimatableAttr[$j] -option keys $AllListObj[$i];	//删除关键帧
					}
					else
					{
						continue;
					}
				}
			}
		}
	}	
}
global proc GetTimeRange()
{
	int $minTime = int(`playbackOptions -query -minTime`);
	int $maxTime = int(`playbackOptions -query -maxTime`);

	textField -e -enable 1 -text $minTime FS_PlayblastIn;
	textField -e -enable 1 -text $maxTime FS_PlayblastOut;
}
global proc ycCycleSplit()
{
	int $uiLabelWidth = 150;
	int $uiInputWidth = 80;
	int $uiWidth = $uiLabelWidth + $uiInputWidth + 110;

	if ( `window -exists ycCycleSplit` )
		deleteUI ycCycleSplit;

	window
		-maximizeButton false
		-resizeToFitChildren false
		-menuBar true
		-menuBarVisible true
		-title "yc Cycle Split V1.01"
		-iconName "yc Cycle Split" ycCycleSplit;
	menu
		-label "Help"
		-tearOff false;
	menuItem
    		-label "help Crowd Locator Aim"
    		-c "help";
 
	string $sp_form = `formLayout -numberOfDivisions 100`;
		string $sp_scroll = `scrollLayout -hst 16 -vst 16 -childResizable true -minChildWidth $uiWidth`;
			columnLayout -adjustableColumn true;
				frameLayout -label "Set Attribute"  -cll true  -mh 3 -bs "etchedIn" -bv true sp_ssFrame;
					columnLayout -adjustableColumn true;
						rowLayout -numberOfColumns 1 -cat 1 "left" 5 -columnWidth 1 400;
							intSliderGrp -field true -min 1 -max 10 -fmn 1 -fmx 100 -label "Obligate keyframe" -value 5 ObligateKeyframe;
							setParent ..;
						rowLayout -numberOfColumns 5 -cat 1 "left" 40 -columnWidth 1 145 -columnWidth 2 30 -columnWidth 3 80 -columnWidth 4 30 -columnWidth 5 80;
							button -w 100 -l "  Get Time Range" -al "center" -c "GetTimeRange";
							text -label "Min";
							textField -text "0" -enable false FS_PlayblastIn;
							text -label "Max";
							textField -text "10" -enable false FS_PlayblastOut;
							setParent ..;

						setParent ..;
					setParent ..;
				setParent ..;
			setParent ..;
		string $PolyConToParticle = `button -label "Split Animation Keyframe" -command "CycleSplit"`;
		string $OnlyRandomParticle = `button -label "Split Cloth Animation" -command "CycleSplitCloth"`;
		string $sp_close = `button -label "Close" -command "deleteUI ycCycleSplit"`;
		setParent ..;

	// set form layouts
	formLayout -edit
		-attachForm $sp_scroll "top" 2
		-attachForm $sp_scroll "left" 2
		-attachControl $sp_scroll "bottom" 2 $PolyConToParticle
		-attachForm $sp_scroll "right" 2

		-attachNone $PolyConToParticle "top"
		-attachForm $PolyConToParticle "left" 2
		-attachForm $PolyConToParticle "bottom" 2
		-attachPosition $PolyConToParticle "right" 2 33

		-attachNone $OnlyRandomParticle "top"
		-attachPosition $OnlyRandomParticle "left" 2 33
		-attachForm $OnlyRandomParticle "bottom" 2
		-attachPosition $OnlyRandomParticle "right" 2 67

		-attachNone $sp_close "top"
		-attachPosition $sp_close "left" 2 67
		-attachForm $sp_close "bottom" 2
		-attachForm $sp_close "right" 2
		$sp_form;

	showWindow ycCycleSplit;	
}
ycCycleSplit;