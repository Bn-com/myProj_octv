global proc zwRemoveTextureEdit()
{
//	global string $gReferenceEditorPanel;
//	string $selFileReal[] = `sceneEditor -query -selectItem $gReferenceEditorPanel`;
	string $selFileReal[] = `file -query -reference`;
	for ($file in $selFileReal)
	{
		string $node = `file -query -referenceNode $file`;

		string $cmds[];
		clear $cmds;
		string $edits[] = {"setAttr", "connectAttr", "disconnectAttr", "addAttr", "deleteAttr", "parent"};
		for ($edit in $edits)
		{
			string $buf[] = `referenceQuery -fld 0 -scs 1 -ec $edit -es $node`;
			$cmds = stringArrayCatenate($cmds, $buf);
		}

		int $find = false;
		for ($cmd in $cmds)
		{
			if (`match "[.]fileTextureName" $cmd` != "")
			{
				$find = true;
				break;
			}
		}
		if (!$find)
		{
			continue;
		}

		file -unloadReference $node;
		file -cleanReference $node;
		file -loadReference $node;

		for ($cmd in $cmds)
		{
			if (`match "[.]fileTextureName" $cmd` == "")
			{
				catch(`evalEcho $cmd`);
			}
		}
	}
}