global proc string[] zwGetSequence(string $path)
{
	string $filenames[];
	clear $filenames;

	string $pattern;
	string $filename = `match "[^/\\]+$" $path`;
	if (`match "#" $filename` != "")	// name#.ext
	{
		$pattern = `substituteAllString $filename "#" "[0-9]"`;
		$pattern = `substituteAllString $pattern "." "[.]"`;
		$pattern = "^" + $pattern + "$";
	}
	else if (`match "<UDIM>" $filename` != "")	// <UDIM>
	{
		$pattern = `substitute "<UDIM>" $filename "[0-9]+"`;
		$pattern = `substituteAllString $pattern "." "[.]"`;
		$pattern = "^" + $pattern + "$";
	}
	else if (`match "^[^.]+[.][0-9]+[.][^.]+$" $filename` != "")		// name.#.ext
	{
		$pattern = "^" + `substitute "[.][0-9]+[.]" $filename "[.][0-9]+[.]"` + "$";
	}
	else if (`match "^[^.]+[0-9]+[.][^.]+$" $filename` != "")	// name#.ext
	{
		$pattern = "^" + `substitute "[0-9]+[.]" $filename "[0-9]+[.]"` + "$";
	}

	string $folder = `substring $path 1 (size($path) - size($filename))`;
	string $buf[] = `getFileList -filespec ($folder + "*")`;
	for ($filename in $buf)
	{
		if (`match $pattern $filename` != "")
		{
			$filenames[size($filenames)] = $filename;
		}
	}

	return $filenames;
}