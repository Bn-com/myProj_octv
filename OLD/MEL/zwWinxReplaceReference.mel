global proc int zwWinxReplaceReference(string $type)
{
	global string $gReferenceEditorPanel;
	string $selFileReal[] = `sceneEditor -query -selectItem $gReferenceEditorPanel`;
	int $rs = (size($selFileReal) > 0);

	int $i;
	for ($i=0; $i<size($selFileReal); $i++)
	{
		string $buf1[];
		clear $buf1;
		tokenize $selFileReal[$i] "." $buf1;
		string $finalstr = $buf1[0] + ".mb";

		if (size($selFileReal[$i]) > 0 && `file -q -ex $finalstr`)
		{
			string $file;
			$file = `substitute "_anim.*\.mb$" $finalstr ""`;
			$file = `substitute "_cache.*\.mb$" $file ""`;
			$file = `substitute "_render.*\.mb$" $file ""`;
			$file = $file + "_" + `match "[^+]+" $type`;
			string $buf[] = `getFileList -filespec ($file + "*.mb")`;

			string $title;
			if (`gmatch $type "*+*"`)
			{
				$title = "添加哪一个文件？";
			}
			else
			{
				$title = "替换成哪一个文件？";
			}

			string $destFilename = "";
			string $currentFileName = `match "[^/\\]+$" $finalstr`;
			int $count = 0;
			string $confirmDialog = "confirmDialog -message \"" + $title + "\" -dismissString \"\"";
			for ($button in $buf)
			{
				if (tolower($button) != tolower($currentFileName))
				{
					$confirmDialog += " -button \"" + $button + "\"";
					$destFilename = $button;
					$count ++;
				}
			}
			if ($count > 1)
			{
				$destFilename = `eval $confirmDialog`;
			}
			if ($destFilename != "")
			{
				$destFilename = `substitute "[^/\\]+$" $file $destFilename`;

				if (`gmatch $type "*+*"`)
				{
					string $renamePrefix = `match "[^/\\]+$" $destFilename`;
				//	$renamePrefix = `match "[^.]+" $renamePrefix`;
					$renamePrefix = `match "[^_]+_[^_]+" $renamePrefix` + "_0";
					file -reference -namespace $renamePrefix $destFilename; 
				}
				else
				{
					string $node = `file -query -referenceNode $selFileReal[$i]`;
					file -loadReference $node $destFilename;
				}

				$rs = $rs && true;
			}
			else
			{
				$rs = false;
			}
		}
	}

	return $rs;
}