global proc zwCheckNormal()
{
	print "\n\nCheck 'Match face normal'...\n";

	select -clear;

	string $meshes[] = `ls -type "mesh"`;
	for ($mesh in $meshes)
	{
		int $faceCount[] = `polyEvaluate -face $mesh`;
		for ($i=0; $i<$faceCount[0]; $i++)
		{
			int $find = false;
			string $str[] = `polyInfo -faceNormals ($mesh + ".f[" + $i + "]")`;
			tokenize $str[0] " " $str;
			vector $normalFace = <<float($str[size($str)-3]), float($str[size($str)-2]), float($str[size($str)-1])>>;
			string $vertexFaces[] = `polyListComponentConversion -toVertexFace ($mesh + ".f[" + $i + "]")`;
			$vertexFaces = `ls -flatten $vertexFaces`;
			for ($vertexFace in $vertexFaces)
			{
				vector $normalVertex = `polyNormalPerVertex -q -xyz $vertexFace`;
				float $angle = `angle $normalFace $normalVertex`;
				if (abs($angle) > 0.001)
				{
				//	print ($vertexFace + "\n");
					$find = true;
					break;
				}
			}
			if ($find)
			{
				select -add $mesh;
				print ($mesh + "\n");
				break;
			}
		}
	}

	if (size(`ls -sl`))
	{
		confirmDialog -button "OK" -message "不是“Match face normal”的Mesh已经被选上，详情请查看Script Editor";
	}
	else
	{
		confirmDialog -button "OK" -message "没有找到不是“Match face normal”的Mesh";
	}
}