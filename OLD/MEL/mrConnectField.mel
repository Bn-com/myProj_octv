global proc mrConnectField()
{
	string $selected[] = `ls -selection -shortNames`;

	string $fields[];
	clear $fields;

	string $MASTER[];
	clear $MASTER;

	for ($item in $selected)
	{
		if ((`nodeType $item` == "airField") ||
			(`nodeType $item` == "dragField") ||
			(`nodeType $item` == "gravityField") ||
			(`nodeType $item` == "newtonField") ||
			(`nodeType $item` == "radialField") ||
			(`nodeType $item` == "turbulenceField") ||
			(`nodeType $item` == "uniformField") ||
			(`nodeType $item` == "vortexField") ||
			(`nodeType $item` == "volumeAxisField"))
			$fields[size($fields)] = $item;
		else
		{
			string $temp[] = `ls ($item + "_MASTER")`;

			if (size($temp) == 1)
				// MASTER node exists
				$MASTER[size($MASTER)] = $temp[0];
			else
				if (size($temp) > 1)
					warning("More than 1 object has name \"" + $item + "_MASTER\". Not linking fields.");
		}
	}

	// select all the MASTER nodes

	select $MASTER;

	string $particles[] = `listRelatives -allDescendents -type particle`;

	for ($j = 0; $j < size($particles); $j++)
		for ($i = 0; $i < size($fields); $i++)
			connectDynamic -f $fields[$i] $particles[$j];


	string $rigidBodies[];

	for ($item in $MASTER)
		if (`gmatch $item "*mushvine*a01*"` == true)
			$rigidBodies[size($rigidBodies)] = `substitute "_MASTER" $item "_rigidSphere"`;
		else
			if (`gmatch $item "*mushvine*b01*"` == true)
			{
				$rigidBodies[size($rigidBodies)] = `substitute "_MASTER" $item "_rigidSphere1"`;
				$rigidBodies[size($rigidBodies)] = `substitute "_MASTER" $item "_rigidSphere2"`;
			}

	for ($j = 0; $j < size($rigidBodies); $j++)
		for ($i = 0; $i < size($fields); $i++)
			connectDynamic -f $fields[$i] $rigidBodies[$j];

	// restore selection

	select $selected;
}