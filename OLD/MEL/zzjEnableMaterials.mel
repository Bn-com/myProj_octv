global proc zzjEnableMaterials( int $enable )
// Usage:
//   enableMaterials( 0 );  -- turn off all Materials.  Everything renders
//                             gray.
//   enableMaterials( 1 );  -- turn on all Materials (normal render)
{
	string $attributes[] = {
		"color",
		"transparency", "transparencyR", "transparencyG", "transparencyB",
		"ambientColor", "ambientColorR", "ambientColorG", "ambientColorB",
		"incandescence", "incandescenceR", "incandescenceG", "incandescenceB",
		"normalCamera", "diffuse", "translucence"
		};

	string $materials[] = `ls -materials`;

	for ( $mat in $materials )
	{
		for ( $attr in $attributes )
		{
			if ( `attributeQuery -node $mat -exists $attr` )
			{
				shadingConnection -e -cs $enable ( $mat + "." + $attr );
			}
		}
	}
}
