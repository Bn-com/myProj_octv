global proc string zwGetOverrideAttr(string $layer, string $attr)
{
	if ($layer == `editRenderLayerGlobals -query -currentRenderLayer`)
	{
		return $attr;
	}

	string $buf[] = `editRenderLayerAdjustment -layer $layer -query`;
	int $find = false;
	for ($i=0; $i<size($buf); $i++)
	{
		if ($buf[$i] == $attr)
		{
			$find = true;
			break;
		}
	}
	if (!$find)
	{
		$buf = `listConnections "renderLayerManager.renderLayerId[0]"`;
		$layer = $buf[0];
	}

	$buf = `listConnections -source false -plugs true -connections true $attr`;
	for ($i=1; $i<size($buf); $i+=2)
	{
		if ($buf[$i] == `match ($layer + "[.]adjustments\\[[0-9]+\\][.]plug") $buf[$i]`)
		{
			return (`match ($layer + "[.]adjustments\\[[0-9]+\\]") $buf[$i]` + ".value");
		}
	}

	return $attr;
}