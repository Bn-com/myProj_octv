//Creates a camera constrained to a selected object
//Useful for viewing facial animation on a moving character
//(constrain to the head joint)
//original script by Erik Malvarez

global proc makeObjectCam () {
	string $sel_list[]=`ls -sl`;
	if(size($sel_list)<1){
		error "Please select an object";
	}
	string $selected = $sel_list[0];
        string $rig = `createNode -n ($selected + "_objectcam_rig") transform`;
	pointConstraint $selected $rig;
	orientConstraint $selected $rig;
	string $rig_offset = `createNode -n ($selected + "_objectcam_rigoffset") transform`;
	parent $rig_offset $rig;
	setAttr ($rig_offset + ".translate") 0 0 0;
	string $center = `createNode -p $rig_offset -n ($selected + "_objectcam_center") transform`;
	string $attach = `createNode -p $center -n ($selected + "_objectcam_att") transform`;
	string $offset = `createNode -p $attach -n ($selected + "_object_offset") transform`;
	setAttr ($attach + ".translate") 0 0 10;

	// make camera
	string $object_cam = `createNode -n ($selected + "_objectcam") transform`;
	string $object_cam_shape = `createNode -p $object_cam -n ($selected + "_objectcamShape") camera`;
	string $attr_ls[] = `listAttr -k $object_cam`;
	for ($attr in $attr_ls) {
		setAttr -k 0 ($object_cam + "." + $attr);
	}
	addAttr -ln "orbit" -at "float" -k 1 $object_cam;
	addAttr -ln "tilt" -at "float" -k 1 $object_cam;
	addAttr -ln "roll" -at "float" -k 1 $object_cam;
	addAttr -ln "dolly" -at "float" -k 1 -dv 10 $object_cam;

	// attach camera to rig and make connections
	pointConstraint $offset $object_cam;
	orientConstraint $offset $object_cam;
	setAttr ($center + ".rotateOrder") 2;
	connectAttr -f ($object_cam + ".orbit") ($center + ".ry");
	connectAttr -f ($object_cam + ".tilt") ($center + ".rx");
	connectAttr -f ($object_cam + ".roll") ($center + ".rz");
	connectAttr -f ($object_cam + ".dolly") ($attach + ".tz");

}
