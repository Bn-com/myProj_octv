/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: mw
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc mwMyoBuilder(string $character, string $myo, int $dir)
{
	select -cl;
	string $jt = `joint -n ($character+"_"+$myo+"_origin") -p 0 0 0` ;
	int $x, $y, $z;
	switch($dir)
	{
		case 1: $x = 1; $y = 0; $z = 0; break;
		case 2: $x = -1; $y = 0; $z = 0; break;
		case 3: $x = 0; $y = 1; $z = 0; break;
		case 4: $x = 0; $y = -1; $z = 0; break;
		case 5: $x = 0; $y = 0; $z = 1; break;
		case 6: $x = 0; $y = 0; $z = -1; break;
	}
	string $jt1 = `joint -n ($character+"_"+$myo+"_insertion") -p $x $y $z` ;
	joint -e -zso -oj xyz $jt;
	string $loc[] = `spaceLocator`;
	string $loc1 = `rename $loc[0] ($character+"_"+$myo+"_origin_loc")`;
	$loc = `spaceLocator`;
	string $loc2 = `rename $loc[0] ($character+"_"+$myo+"_insertion_loc")`;
	move -a $x $y $z $loc2;
	string $handle1[] = `ikHandle -n ($character+"_"+$myo+"_handle") -sj $jt -ee $jt1`;
	pointConstraint -weight 1 $loc2 $handle1[0];
	orientConstraint -weight 1 $loc2 $jt1;
	string $dist = `distanceDimension -sp 0 0 0 -ep $x $y $z`;
	string $dim[] = `pickWalk -d up $dist`;
	$dist = `rename $dim ($character+"_"+$myo+"_"+$dim[0])`;
	hide $dist;
	connectAttr -f ($dist+".distance") ($jt+".sx");
	parent $jt $loc1;
	if(`objExists ($character+"_MUSCLE_STUFF")`==0)
	{
		group -n ($character+"_MUSCLE_STUFF") -empty;
	}
	parent $handle1[0] ($character+"_MUSCLE_STUFF");
	parent $dist ($character+"_MUSCLE_STUFF");
	select $loc1;
}