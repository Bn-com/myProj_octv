//-----------------------------------------------------------------------
//  Alias Script File
//  MODIFY THIS AT YOUR OWN RISK
//
//  Creation Date:  January 16, 2003
//
//  Procedure Name:
//      check_AddUsercheck
//
//  Description:
//
//		Adds a new check operation to the Optimize Scene Size dialog
//		box.  If enabled, this operation will run when the user clicks
//		Check Scene.
//
//  Input Arguments: 
//
//		$name - a string that will be used to uniquely identify this
//				operation (should have no spaced in it)
//		$label - user-friendly label that will appear in the Optimize
//				 Scene Size dialog
//		$default - 1 if the optimization should be enabled by default
//		$cmd - MEL command string to execute when the optimization is
//			   to be run.
//
//	Return Value: None.
//
//	Related Functions:
//
//		For progress reporting and interruptability, user-defined check
//		steps might want to make use of the following utility functions:
//
//		check_StartProgress() - called before an operation begins
//		check_SetProgress() - called as the operation is running, to 
//								update the progress bar.
//		check_EndProgress() - called to signal that the operation is 
//								finished.
//		check_CheckInterrupt() - called between check operations to
//								   detect when a previous operation has
//								   been interrupted.
//		check_Summary() - called to report the results of check 
//							operations.
//
//-----------------------------------------------------------------------

global proc check_AddUsercheck( string $name, 
									string $label, 
									int $default, 
									string $cmd )
{
	//	global variable that stores user-defined check operation 
	//	information
	//
	global string $gUserScenechecks[];

	//	see if this optimization is already registered
	//
	int $len = size($gUserScenechecks)/4;
	int $i;
	int $foundIndex = $len;
	for( $i = 0; $i < $len; $i++ )
	{
		if( $gUserScenechecks[4*$i] == $name )
		{
			$foundIndex = $i;
			break;
		}
	}

	//	store the relevant information for this optimization in
	//	the array
	//
	$gUserScenechecks[4*$foundIndex] = $name;
	$gUserScenechecks[4*$foundIndex+1] = $label;
	$gUserScenechecks[4*$foundIndex+2] = $default;
	$gUserScenechecks[4*$foundIndex+3] = $cmd;
	
}

