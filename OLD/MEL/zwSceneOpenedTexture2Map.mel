global proc int zwSceneOpenedTexture2Map(int $force)
{
	int $dirty = false;

	string $IDMT_PROJECTS = `getenv "IDMT_PROJECTS"`;

	string $files[] = `ls -type "file"`;

	int $max = size($files);
	if ($max == 0)
	{
		return $dirty;
	}
	int $progress = 0;
	if ($force && !`about -batch`)
	{
		progressWindow -progress $progress -max $max -title "Tga2Map" -status ($progress + "/" + $max) -isInterruptable true;
	}

	for ($file in $files)
	{
		if ($force && !`about -batch`)
		{
			if (`progressWindow -query -isCancelled`)
			{
				break;
			}
			$progress ++;
			progressWindow -edit -progress $progress -status ($progress + "/" + $max + "    " + $file);
		}

		string $str = `getAttr ($file + ".fileTextureName")`;
		string $fileTextureName = `substitute ".*/Enyo/" $str ($IDMT_PROJECTS + "/Enyo/")`;

		if (`match "[.]map$" $fileTextureName` == "")
		{
			string $map = $fileTextureName + ".map";
			if (`filetest -f $fileTextureName` && !`filetest -f $map` && $force)
			{
				string $temp = `internalVar -userTmpDir` + `match "[^/\\]+$" $map`;
				zwImgcvt $fileTextureName $temp;
				zwSysFile "move" $temp $map true;
			}
			$fileTextureName = $map;
		}

		if ($fileTextureName != $str && `filetest -f $fileTextureName`)
		{
			evalEcho ("setAttr \"" + $file + ".fileTextureName\" -type \"string\" \"" + $fileTextureName + "\"");
			$dirty = true;
		}
	}

	if ($force && !`about -batch`)
	{
		progressWindow -endProgress;
	}

	return $dirty;
}