/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: 黄仲维
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwOutliner()
{
	if (`window -exists zwOutliner`)
		deleteUI zwOutliner;

	string $selectionConnection1 = `selectionConnection`;
	string $selectionConnection2 = `selectionConnection`;

	window -title "Change Group" -width 600 -height 600 -resizeToFitChildren true zwOutliner;
		string $formLayout = `formLayout`;
			string $paneLayout = `paneLayout -configuration "vertical2"`;
				string $formLayout1 = `formLayout`;
					string $text = `text -align "center" -label "Children"`;
					string $paneLayout1 = `paneLayout -configuration "horizontal2" -paneSize 1 100 100`;
						string $outlinerEditor = `outlinerEditor -unParent -mainListConnection "worldList" -selectionConnection $selectionConnection1 -highlightConnection "worldList"`;
						outlinerEditor -edit -parent $paneLayout1 $outlinerEditor;
						$outlinerEditor = `outlinerEditor -unParent -masterOutliner $outlinerEditor`;
						outlinerEditor -edit -parent $paneLayout1 $outlinerEditor;
						setParent..;
					setParent..;
				formLayout -edit
					-attachForm	$text		"top"		0
					-attachForm	$text		"left"		0
					-attachForm	$text		"right"		0

					-attachControl	$paneLayout1	"top"		0	$text
					-attachForm	$paneLayout1	"left"		0
					-attachForm	$paneLayout1	"bottom"	0
					-attachForm	$paneLayout1	"right"		0
				$formLayout1;

				$formLayout1 = `formLayout`;
					$text = `text -align "center" -label "Parent"`;
					$paneLayout1 = `paneLayout -configuration "horizontal2" -paneSize 1 100 100`;
						$outlinerEditor = `outlinerEditor -unParent -mainListConnection "worldList" -selectionConnection $selectionConnection2`;
						outlinerEditor -edit -parent $paneLayout1 $outlinerEditor;
						$outlinerEditor = `outlinerEditor -unParent -masterOutliner $outlinerEditor`;
						outlinerEditor -edit -parent $paneLayout1 $outlinerEditor;
						setParent..;
					setParent..;
				formLayout -edit
					-attachForm	$text		"top"		0
					-attachForm	$text		"left"		0
					-attachForm	$text		"right"		0

					-attachControl	$paneLayout1	"top"		0	$text
					-attachForm	$paneLayout1	"left"		0
					-attachForm	$paneLayout1	"bottom"	0
					-attachForm	$paneLayout1	"right"		0
				$formLayout1;

				setParent..;

			string $button = `button -label "Change Group" -command ("zwOutlinerProc \"" + $selectionConnection1 + "\" \"" + $selectionConnection2 + "\"")`;

	formLayout -edit
		-attachForm	$paneLayout	"top"		0
		-attachForm	$paneLayout	"left"		0
		-attachControl	$paneLayout	"bottom"	4	$button
		-attachForm	$paneLayout	"right"		0

		-attachForm	$button		"left"		0
		-attachForm	$button		"bottom"	0
		-attachForm	$button		"right"		0
	$formLayout;

	showWindow zwOutliner;
}

global proc zwOutlinerProc(string $selectionConnection1, string $selectionConnection2)
{
	int $lock[];
	string $attr[] = {"translateX", "translateY", "translateZ", "rotateX", "rotateY", "rotateZ", "scaleX", "scaleY", "scaleZ", "rotatePivotX", "rotatePivotY", "rotatePivotZ", "scalePivotX", "scalePivotY", "scalePivotZ", "rotatePivotTranslateX", "rotatePivotTranslateY", "rotatePivotTranslateZ", "scalePivotTranslateX", "scalePivotTranslateY", "scalePivotTranslateZ"};

	string $children[] = `selectionConnection -query -object $selectionConnection1`;

	string $buf[] = `selectionConnection -query -object $selectionConnection2`;
	if (size($buf) != 1)
		error "请选择一个父物体";
	string $parent = $buf[0];

	for ($obj in $children)
	{
		for ($i=0; $i<size($attr); $i++)
		{
			$lock[$i] = `getAttr -lock ($obj + "." + $attr[$i])`;
			setAttr -lock false ($obj + "." + $attr[$i]);
		}
		$buf = `parent $obj $parent`;
		$obj = $buf[0];
		for ($i=0; $i<size($attr); $i++)
			setAttr -lock $lock[$i] ($obj + "." + $attr[$i]);
	}
}