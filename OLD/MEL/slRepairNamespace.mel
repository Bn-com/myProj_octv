//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 
//
//	Update/Change: WansHoulOng
//	Last Updated: 2008-10-24
//	Description:
//				解决由于替换参考导致的前缀名不同的问题
//////////////////////////////////////////////////////////


global proc slRepairNamespace(int $type)
{
	string $referencefiles[] = `file -query -reference`;

	string $rfnode,$rffile;
	
	string $namespace[]={};
	for ($ref in $referencefiles)
	{
		if (`match "ddAP" $ref` == "")
		{
			string $nodes[] = `referenceQuery -nodes $ref`;
			if (size($nodes))
			{
				string $oldns = `file -q -rpr $ref`;

				string $buff[];	
				tokenize $ref "\/" $buff;
				tokenize $buff[size($buff)-1] "_" $buff;
				string $ns = $buff[0]+"_"+$buff[1]+"_";
				
				if (!startsWith($oldns,$ns))
				{
					for($i=0;;$i++)
					{
						if(!`namespace -ex ($ns+$i)`) 
						{
							$ns = $ns+$i;break;
						}
					}
					file -e -ns $ns $ref;

					
					string $buff1[];
					tokenize $ns "_" $buff1;
					if (startsWith($buff1[1],"sc"))
					{
						$rfnode = `file -q -rfn $ref`;
						$rffile = $ref;
					}
				}
			}
		}
	}

	print ($rfnode+"------"+$rffile+"\n");

	//刷新一个参考，保证控制器能正常选择
	if (size($rfnode) && $type)
	{
		file -unloadReference $rfnode $rffile;
		file -loadReference $rfnode $rffile;
	}

	//修正group组
	string $chr[] = `ls  "*:CHR" "*:*:CHR"`;
	for ($c in $chr )
	{
		string $pre = `substring $c 1 (size($c)-4)`;
		
		string  $parent[] = `listRelatives -f -p $c`;
		if (size($parent) && match("RNg",$parent[0]) == "RNg")
		{
			if (match($pre,$parent[0]) != $pre)
			{
				rename $parent[0] ($pre+"RNgroup");
			}
		}
	}
}