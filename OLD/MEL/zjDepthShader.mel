
global proc zjDepthShader()
{
	//首先确定将要渲染的摄像机视窗
	string $whichPanel = `getPanel -withFocus`;
	if(`gmatch $whichPanel "modelPanel*"`)
	{
		//根据视窗得到相应的摄像机
		string $whichCamera = `modelPanel -q -camera $whichPanel`;
		
		//利用viewClipPlane命令自动设置near/far clip
		viewClipPlane -acp $whichCamera;
		
		//用变量存储near/far clip值
		float $nearCP = `camera -q -nearClipPlane $whichCamera`;
		float $farCP = `camera -q -farClipPlane $whichCamera`;
		
		//创建相应的材质网络
	
		string $shader = `shadingNode -name "depthShader" -asShader surfaceShader`;
		addAttr -longName "near" -keyable 1 -attributeType double -defaultValue $nearCP $shader;
		addAttr -longName "far" -keyable 1 -attributeType double -defaultValue $farCP $shader;
		addAttr -longName "flatmax" -keyable 1 -attributeType double -defaultValue 1 $shader;
		addAttr -longName "flatmin" -keyable 1 -attributeType double -defaultValue 0 $shader;
		addAttr -longName "maxvalue" -keyable 1 -attributeType double -defaultValue 1 $shader;
		addAttr -longName "minvalue" -keyable 1 -attributeType double -defaultValue 0 $shader;
		addAttr -longName "ReverseColor" -keyable 1 -attributeType bool -defaultValue 0 $shader;
		string $sam = `shadingNode -asUtility samplerInfo`;
		string $exp = "float $depth= " + $shader + ".far;\n";
		      $exp += "float $near = " + $shader + ".near;\n";
		      $exp += "float $flatmin = " + $shader + ".flatmin;\n";
		      $exp += "float $flatmax = " + $shader + ".flatmax;\n";
		      $exp += "float $minv = " + $shader + ".minvalue;\n";
		      $exp += "float $maxv = " + $shader + ".maxvalue;\n";
		      $exp += "float $distance = sqrt(pow(" + $sam + ".pointCameraX,2) + pow(" + $sam + ".pointCameraY,2) + pow(" + $sam + ".pointCameraZ,2)) - $near;\n\n";
		      $exp += "if( " + $shader + ".ReverseColor == 0)\n";
		      $exp += "{\n	$distance = 1 -  $distance/($depth - $near);\n";
		      $exp += "}else{\n";
		      $exp += "	$distance =  $distance/($depth - $near);\n}";
		      $exp += "\n\nif($distance < $flatmin)\n";
		      $exp += "	$distance = $minv;\n";
		      $exp += "if($distance > $flatmax)\n";
		      $exp += "	$distance = $maxv;\n\n";
		      $exp += $shader + ".outColorR=$distance;\n";
		      $exp += $shader + ".outColorG=$distance;\n";
		      $exp += $shader + ".outColorB=$distance;";
		      
		expression -string $exp -object "" -alwaysEvaluate 1 -unitConversion all ;
	}
	else
		error "请先选择摄像机视图";

}




