//////////////////////////////////////////////////////////
//
//	Script Name: ddBlendShapeTools
//	Verision: 0.01 beta
//	Author: Julii FreeMAN, SZ-CN
//	Date Created: October 22 2006
//	Changed Projcet Date: December 07 2006
//	Last Updated: December 08 2006
//	Update/Change this file at: Julii FreeMAN
//	Note: 
//		
//		
//		导出blendShape 
//////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////
// Procdures Name: ddBlendShapeExport
//
//
// Arguments :
//	string $object 操作物体
//	string $blendShape 相关的blendShape
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddBlendShapeExport (string $object,string $blendShape)

{
 int $size,$i ;
 string $temp,$alias ;

 $size = `getAttr -size ($blendShape + ".weight")` ; //找出有多少个target

 //关闭所有的target
 for ($i=0;$i<$size;$i++)
 {
  if(`getAttr -lock ($blendShape + ".weight[" + $i + "]")` == 0)
  {
   setAttr ($blendShape + ".weight[" + $i + "]") 0 ;
  }
 }

 //duplicate target
 for ($i=0;$i<$size;$i++)
 {
  if(`getAttr -lock ($blendShape + ".weight[" + $i + "]")` == 0)
  {
   setAttr ($blendShape + ".weight[" + $i + "]") 1 ;
   $alias = `aliasAttr -query ($blendShape + ".weight[" + $i + "]")` ;
   if($alias == "")
   {
	$alias = ("weight" + $i) ;
   }
   select -r $object ;
   duplicate -name ($alias + "_" + $object);
   select -clear ;
   setAttr ($blendShape + ".weight[" + $i + "]") 0 ;
  }
 }
}

//////////////////////////////////////////////////////////
// Procdures Name: ddBlendShapeExportCMD
//
//
// Arguments : NULL
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddBlendShapeExprotCMD()
{
 string $object,$blendShapes[],$temp ;
 $object = `textField -query -text ddBSTObjTextField` ;
 $blendShapes = `textScrollList -query -selectItem ddBlendShapeList` ;

 for($temp in $blendShapes)
 {
  ddBlendShapeExport($object,$temp) ;
 }
}

//////////////////////////////////////////////////////////
// Procdures Name: ddRefreshBlendShapeList
//
//
// Arguments : NULL
//
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddRefreshBlendShapeList(int $parameter)
{
 int $sign = 1 ;
 string $bs,$object,$blendShapeList[],$tempList[],$appStr[] ;
 if($parameter == 0)
 {
  $blendShapeList = `ls -type blendShape` ;
 }
 else if($parameter == 1)
 {
  $object = `textField -query -text ddBSTObjTextField` ;
  if($object != "")
  {
   if(`objExists $object`)
   {
    $tempList = `listHistory $object` ;
   }
   else
   {
    error "object 物体名不存在" ;
   }
  }
  else
  {
   error "object 不能为空." ;
  }
  
  clear $blendShapeList ;
  for ($object in $tempList)
  {
   if(`nodeType $object` == "blendShape")
   {
    $appStr[0] = $object ;
    appendStringArray($blendShapeList, $appStr, 1);
   }
  }
 }

 textScrollList -edit -removeAll ddBlendShapeList ;
 for ($bs in $blendShapeList)
 {
   textScrollList -edit -append $bs ddBlendShapeList ;
 }
}
//////////////////////////////////////////////////////////
// Procdures Name: ddBlendShapeToolsUI
//
//
// Arguments : NULL
//
// Return : NULL
//////////////////////////////////////////////////////////
global proc ddBlendShapeToolsUI()
{
 if(`window -exists ddBlendShapeToolsWin` == true)
  {
   deleteUI ddBlendShapeToolsWin ;
   if(`windowPref -exists ddBlendShapeToolsWin`)
   {
    windowPref -remove ddBlendShapeToolsWin ;
   }
  }
  window 
    -title "durdur's blendShape Tools v0.21 beta" 
    -widthHeight 408 450
    -sizeable true
    ddBlendShapeToolsWin ;

 menuBarLayout;
 menu -label "Edit" editmenu ;
      menuItem -label "Save Settings" ;
      menuItem -label "Reset Settings" ;
 menu -label "Help" helpmenu ;
      menuItem -label "Help on durdur 's durdur's blendShape Tools ..." ;
	  menuItem -label "blendShape Export AVI..."
			   -command "system (\"load Z://Resource//Groups//Production//setup//durdur's Toyboxes//HELP//video//ddBlendShapeExport.avi\")" ;

 setParent .. ;

 columnLayout ;
	rowLayout -numberOfColumns 3
			  -columnWidth3 36 230 100 ;
		text "object" ;
		textField -editable true
				  -width 230
				  ddBSTObjTextField ;
		button -label "<<<"
			   -command "textField -edit -text `ls -sl` ddBSTObjTextField"
			   ddBSTObjGet ;
	setParent .. ;

	textScrollList -width 300
				   -numberOfRows 6
				   -allowMultiSelection true
				   -showIndexedItem 4
				   ddBlendShapeList ;
	rowLayout -numberOfColumns 3
			  -columnWidth3 120 140 100 ;
		button -label "List alL blendShape" 
				-command  "ddRefreshBlendShapeList 0 ;";
		button -label "List object's blendShape"
				-command  "ddRefreshBlendShapeList 1 ;";
		text "T.T" ;
	setParent .. ;

	rowLayout -numberOfColumns 3
			  -columnWidth3 100 100 100 ;
		button -label "expOrt"
				-width 120
				-height 28
				-align "center"
				-command "ddBlendShapeExprotCMD;deleteUI ddBlendShapeToolsWin;" ;
		button -label "aPPly"
				-width 120
				-height 28
				-align "center"
				-command "ddBlendShapeExprotCMD;" ;
		button -label "Close"
				-width 120
				-height 28
				-align "center"
				-command "deleteUI ddBlendShapeToolsWin;" ;
	setParent .. ;

//	text "有史第一难看的界面诞生了！！！T.T..." ;

 showWindow ddBlendShapeToolsWin ;
}

//End of file.