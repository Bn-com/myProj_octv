/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: mw
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc mwGetSkinWeights()
{
	$tmp = `ls -sl`;
	string $cv = $tmp[0];
	print ("CV: "+$cv+"\n");
	string $name_parts[];
	int $tok = tokenize($cv, ".", $name_parts);
	string $shape[] = `listRelatives -c $name_parts[0]`;
	string $pt = ($shape[0]+"."+$name_parts[1]);
	string $cons[] = `listConnections -s 1 -d 0 $shape[0]`;
	string $cluster;
	for($con in $cons)
	{
		string $type[] = `ls -st $con`;
		int $comp = `strcmp "skinCluster" $type[1]`;
		if($comp==0)
		{
			$cluster = $con;
			break;
		}
	}
	if(`size $cluster`==0)
	{
		for($con in $cons)
		{
			if(`gmatch $con "skinCluster*Set"`)
			{
				$cluster = `substitute "Set" $con ""`;
				break;
			}
		}
	}
	print ("SKIN CLUSTER: "+$cluster+"\n");
	string $trans[] = `skinPercent -ib 0.00001 -q -t $cluster $pt`;
	string $tran;
	int $spacing = 0;
	for($tran in $trans)
	{
		if(`size $tran`>$spacing)
		{
			$spacing = `size $tran`;
		}
	}
	$spacing = $spacing + 5;
	for($tran in $trans)
	{
		if(`size $tran`<$spacing)
		{
			float $val = `skinPercent -t $tran -q $cluster $pt`;
			print ("JOINT: "+$tran);
			for($i=`size $tran`;$i<$spacing;$i++)
			{
				print " ";
			}
			print ("WEIGHT: "+$val+"\n");
		}
	}
}