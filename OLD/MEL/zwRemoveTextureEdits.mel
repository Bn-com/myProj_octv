global proc zwRemoveTextureEdits()
{
	string $references[] = `file -query -reference`;
	for ($reference in $references)
	{
		string $referenceNode = `file -query -referenceNode $reference`;
		int $loaded = (size(`referenceQuery -nodes $reference`) > 0);

		int $find = false;
		string $edits[] = `referenceQuery -failedEdits true -successfulEdits true -editCommand "setAttr" -editStrings $reference`;
		for ($edit in $edits)
		{
			string $buf[];
			tokenize $edit " " $buf;
			if (`match "[.]fileTextureName$" $buf[1]` != "")
			{
				if ($loaded && !$find)
				{
					file -unloadReference $referenceNode;
					$find = true;
				}
				evalEcho ("referenceEdit -failedEdits true -successfulEdits true -editCommand \"setAttr\" -removeEdits \"" + $buf[1] + "\"");
			}
		}

		if ($loaded && $find)
		{
			string $unresolvedName = `referenceQuery -unresolvedName -filename -withoutCopyNumber $reference`;
			catch(`file -loadReference $referenceNode $unresolvedName`);
		}
	}
}