/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: 黄仲维
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwPanshelPupil()
{
	string $buf1[];

	string $buf[] = `ls "*_pupil_mlocator"`;
	if (!size($buf))
	{
	//	warning "找不到locator： *_pupil_mlocator";
		return;
	}
	
	for ($locator in $buf)
	{
		string $attr = $locator + "." + `substitute "_mlocator" $locator "_small"`;
		if (!`objExists $attr`)
			$attr = $locator + ".pupil_small";
		if (!`objExists $attr`)
		{
			warning ("找不到属性：" + $attr);
			continue;
		}
		
		string $place3dTexture = `substitute "_pupil_mlocator" $locator "_eye_place3dTexture"`;
		if (!`objExists $place3dTexture`)
		{
			warning ("找不到place3dTexture：" + $place3dTexture);
			continue;
		}

		string $small[] = {"babypanda01", "babypanda02", "babypanda03", "babypanda04", "babypanda05", "babypanda06", "babypanda07", "babypanda08", "babypanda09", "babypanda10", "babypanreddy", "babypanshel", "panshelan", "panshelun", "panshelon", "pandevin"};
		string $middle[] = {"pand01", "pand02", "pand03", "pand04", "pand05", "pand06", "pand07", "pand08", "pand09", "pand10", "pand11", "pand12", "pand13", "pand14", "pand15", "pand16", "pand17", "pand18", "pand19", "pand20", "pand21", "pand22", "pand23", "panbee", "pandee", "panpinon", "panpiyon", "panreddy", "panshel", "panshela", "panshenee"};
		string $big[] = {"bigpanda01", "bigpanda02", "bigpanda03", "bigpanda04", "bigpanda05", "bigpanda06", "bigpanda07", "bigpanda08", "panshely", "pandeo"};

		float $sy1 = 0.5;
		float $sy2 = 0.08;

		tokenize $locator "_" $buf1;
		$buf1[0] = `tolower $buf1[0]`;
		if (`stringArrayCount $buf1[0] $small`)
		{
			$sy1 = 0.4;
			$sy2 = 0.07;
		}
		else if (`stringArrayCount $buf1[0] $big`)
		{
			$sy1 = 0.57;
			$sy2 = 0.1;
		}
		
		delete `listConnections -source false -destination true -type "expression" $attr`;

		setAttr -lock false ($place3dTexture + ".scaleX");
		setAttr -lock false ($place3dTexture + ".scaleY");
		setAttr -lock false ($place3dTexture + ".scaleZ");

		string $str = "// Created by Panshel Scale Pupil place3dTexture Tool, HuangZhongwei R&D IDMT\n\n";
		$str += "// This expression use " + $attr + " to control " + $place3dTexture + ".scale\n\n";

		$str += "// when " + $attr + " = 0.2\n";
		$str += "float $sx1 = 0.454038;\n";
		$str += "float $sy1 = " + $sy1 + ";\n";
		$str += "float $sz1 = 0.172169;\n\n";

		$str += "// when " + $attr + " = 1.0\n";
		$str += "float $sx2 = 0.045;\n";
		$str += "float $sy2 = " + $sy2 + ";\n";
		$str += "float $sz2 = 0.017;\n\n";

		$str += $place3dTexture + ".scaleX = $sx1 + ($sx2 - $sx1) / 0.8 * (" + $attr + " - 0.2);\n";
		$str += $place3dTexture + ".scaleY = $sy1 + ($sy2 - $sy1) / 0.8 * (" + $attr + " - 0.2);\n";
		$str += $place3dTexture + ".scaleZ = $sz1 + ($sz2 - $sz1) / 0.8 * (" + $attr + " - 0.2);\n";

		string $exp = $place3dTexture + "Exp";
		$exp = `expression -string $str -name $exp`;
		print ($exp + "\n");
	}
}