global proc zwFluidTextureScale()
{
	if (`window -exists zwFluidTextureScale`)
	{
		deleteUI zwFluidTextureScale;
	}
	window -title "Fluid - Texture Scale" -width 360 -height 80 -sizeable false zwFluidTextureScale;
		formLayout formLayout1;
			checkBoxGrp -numberOfCheckBoxes 3 -label "Texture Scale" -labelArray3 "X" "Y" "Z" -valueArray3 true true true -columnWidth 2 50 -columnWidth 3 50 -columnWidth 4 50 checkBoxGrpXYZ;
			button -width 50 -label "Half" -command "zwFluidTextureScaleProc 0.5" buttonHalf;
			button -width 50 -label "Double" -command "zwFluidTextureScaleProc 2.0" buttonDouble;
		formLayout -edit
			-attachForm	checkBoxGrpXYZ	"top"	5
			-attachForm	checkBoxGrpXYZ	"left"	5

			-attachControl	buttonHalf	"top"	5	checkBoxGrpXYZ
			-attachForm	buttonHalf	"left"	125

			-attachControl	buttonDouble	"top"	5	checkBoxGrpXYZ
			-attachControl	buttonDouble	"left"	10	buttonHalf
		formLayout1;
	showWindow zwFluidTextureScale;
}

global proc zwFluidTextureScaleProc(float $factor)
{
	string $attrs[] = {"textureScaleX", "textureScaleY", "textureScaleZ"};
	int $valueArray[3];
	$valueArray[0] = `checkBoxGrp -query -value1 checkBoxGrpXYZ`;
	$valueArray[1] = `checkBoxGrp -query -value2 checkBoxGrpXYZ`;
	$valueArray[2] = `checkBoxGrp -query -value3 checkBoxGrpXYZ`;

	string $fluidShapes[] = `ls -sl -noIntermediate -dagObjects -type "fluidShape"`;
	for ($fluidShape in $fluidShapes)
	{
		for ($i=0; $i<size($attrs); $i++)
		{
			if ($valueArray[$i])
			{
				float $value = `getAttr ($fluidShape + "." + $attrs[$i])`;
				$value = $value * $factor;
				evalEcho ("setAttr \"" + $fluidShape + "." + $attrs[$i] + "\" " + $value);
			}
		}
	}
}