global proc zwRenameDuplicateShapeName()
{
	string $shapes[] = `ls -shapes`;
	for ($shape in $shapes)
	{
		string $name = `match "[^|]+$" $shape`;
		if ($name != $shape)
		{
			if (`objExists $shape`)
			{
				string $buf[];
				if (catch($buf = `ls $name`))
				{
					continue;
				}
				if (size($buf) == 1)
				{
					continue;
				}
				string $allParents[] = `listRelatives -allParents $shape`;
				if (size($allParents) == size($buf))
				{
					continue;
				}
				string $oldName[];
				for ($i=0; $i<size($buf); $i++)
				{
					if (!`objExists $buf[$i]`)
					{
						continue;
					}
					if (!`reference -isNodeReferenced $buf[$i]`)
					{
						$oldName[$i] = $buf[$i];
						$buf[$i] = `rename $buf[$i] "Noname#"`;
					}
				}
				$name = `substitute "[0-9]+$" $name ""` + "#";
				for ($i=0; $i<size($buf); $i++)
				{
					if (!`objExists $buf[$i]`)
					{
						continue;
					}
					if (!`reference -isNodeReferenced $buf[$i]`)
					{
						string $newName = `rename $buf[$i] $name`;
						print ("rename \"" + $oldName[$i] + "\" \"" + $newName + "\";\n");
					}
				}
			}
		}
	}
}