global proc zjMotionCurve()
{
	//首先记录场景的时间滑条的起始桢
	float $start=`playbackOptions -q -min`;
	float $end= `playbackOptions -q -max`;
	//记住当前桢，程序运行完后还原用
	float $oldTime=`currentTime -q`;

	//可选多个物体，循环执行
	string $sel[]=`ls -sl`;

	if (size($sel)==0)
		error "Select at least one object";

	//对所选物体循环运行生成曲线功能
	for ($item in $sel)
	{
		currentTime $start;
		//我们首先在初始位置生成一条只含一个点的曲线，然后逐步扩充它
		float $pos[]=`xform -q -worldSpace -t $item`;		//得到物体的世界坐标位置信息
		//根据点的信息来建立曲线
		string $mcurve = `curve -degree 3 -worldSpace -point $pos[0] $pos[1] $pos[2] -knot 0 -knot 0 -knot 0`;
		for ($i=$start+1;$i<$end;$i++)
		{
			currentTime $i;
			$pos=`xform -q -ws -t $item`;
			curve -worldSpace -append -point $pos[0] $pos[1] $pos[2] $mcurve;
		}
	}
	currentTime $oldTime;
}