/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc kcCrowdInitGoalUV(string $particle, string $floor)
{
	if (!`pluginInfo -q -loaded crowdUtility`)
		loadPlugin crowdUtility;
		
	if (($particle == "") || ($floor == ""))
	{
		// get from selection

		string $selected[] = `ls -selection`;

		if ((size($selected) != 2) || !kcIsType($selected[0], "particle") || !kcIsType($selected[1], "nurbsSurface"))
			error("Select particle and NURBS surface in that order.");

		$particle = $selected[0];
		$floor = $selected[1];
	}

	if (!kcIsType($particle, "particle"))
		error($particle + " is not a particle.");

	string $shapes[] = `listRelatives -shapes $particle`;

	kcParticleAddAttr($shapes[0], "parentU", "doubleArray");
	kcParticleAddAttr($shapes[0], "parentV", "doubleArray");

	int $size = `particle -q -count $particle`;

	for ($i = 0; $i < $size; $i++)
	{
		float $pos[] = `particle -attribute "worldPosition" -order $i -q $particle`;
		
		string $cmd = "projectYCmd " + $floor + " " + $pos[0] + " " + $pos[1] + " " + $pos[2];
		float $result[] = eval($cmd);
	
		float $u = $result[4];
		float $v = $result[5];

		particle -e -attribute parentU -order $i -floatValue $u $particle;		
		particle -e -attribute parentV -order $i -floatValue $v $particle;		
	}
}