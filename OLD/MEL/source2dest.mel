/////////////////////////////////////////////////////////////////////
//
// To copy the source object vertex position to the destination 
// object in object coordinates. Only for polygonal mesh.
//
// Usage: select the source object, then the destination object.
//
/////////////////////////////////////////////////////////////////////

global proc source2dest()
{
	string $selected[] = `ls -selection`;

	if (size($selected) != 2)
		error("Please select source object, then target object before using this tool.\n");

	string $source = $selected[0];
	string $destination = $selected[1];

	string $temp[];
	string $sourcePts[], $destPts[];

	$temp = `ls ($source + ".vtx[*]")`;
	$sourcePts = `filterExpand -sm 31 $temp[0]`;

	$temp = `ls ($destination + ".vtx[*]")`;
	$destPts = `filterExpand -sm 31 $temp[0]`;

	if (size($sourcePts) != size($destPts))
		error("Source and destination objects do not have same number of points.\n");

	// copy all the points from source to destination

	for ($i = 0; $i < size($sourcePts); $i++)
	{
		float $pos[] = `xform -q -os -t $sourcePts[$i]`;

		xform -os -t $pos[0] $pos[1] $pos[2] $destPts[$i];
	}
}