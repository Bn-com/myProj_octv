/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: »ÆÖÙÎ¬
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwWorkingTextureToLowest()
{
	string $buf[];

	string $shot[] = {"", "_cs", "_ms", "_fs", "_vs"};
	string $image[] = {"", "_h", "_l", "_m", "_s", "_v"};

	string $file[] = `ls -type "file"`;
	for ($node in $file)
	{
		string $fileTextureName = `getAttr ($node + ".fileTextureName")`;

		$fileTextureName = `substitute "[.]bot$" $fileTextureName ".iff"`;

		tokenize $fileTextureName "/" $buf;
		string $filename = $buf[size($buf) - 1];
		string $folder = `substring $fileTextureName 1 (size($fileTextureName) - size($filename))`;

		$folder = `substitute "/WORKING/" $folder "/DIRECTOR_FINAL/"` + "approved/";
	
		$filename = `substitute "_[cmfv]s[.]" $filename "."`;
		$filename = `substitute "_[hmlsv][.]" $filename "."`;

		for ($i=size($shot)-1; $i>=0; $i--)
		{
			for ($j=size($image)-1; $j>=0; $j--)
			{
				string $newTexture = $folder + `substitute "[.]" $filename ($image[$j] + $shot[$i] + ".")`;
				if (`filetest -f $newTexture`)
				{
					if ($newTexture != $fileTextureName)
						setAttr ($node + ".fileTextureName") -type "string" $newTexture;
					$i = -1;
					$j = -1;
				}
			}
		}
	}
}