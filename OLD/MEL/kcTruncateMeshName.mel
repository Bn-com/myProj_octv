/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


//
// Truncate mesh node names which are too long because Inkworks will crash.
// Replace substring with _ins to shape. For example, 
//
// flyingship02_c_hi_board_63_ins62_ins61_ins60_...   to
// flyingship02_c_hi_board_63_shape
//

proc string leafNode(string $name)
{
	string $buf[];
	tokenize $name "|" $buf;
	return $buf[size($buf) - 1];
}

global proc kcTruncateMeshName()
{
	string $meshes[] = `ls -type mesh`;

	for ($item in $meshes)
	{
		// get only the shape name

		string $full = $item;
		$item = leafNode($item);

		if (`size $item` > 50)
		{
			string $buf[];
			int $numTokens = `tokenize $item "_" $buf`;

			int $flag = false;
			string $newName = "";

			for ($i = 0; $i < $numTokens; $i++)
				if (`gmatch $buf[$i] "ins*"` == 1)
				{
					$flag = true;
					break;
				}

			if ($flag)
			{
				for ($j = 0; $j < $i; $j++)
					$newName += $buf[$j] + "_";

				$newName += "shape";
			}
			else
				$newName = $item;

			if (`size $newName` > 50)
			{
				// take the first 50 alphabets

				$newName = `substring $newName 1 50`;
				$newName += "_shape";
			}

			string $result = `rename $full $newName`;
			$result = leafNode($result);

			print("\n");
			print("old: " + $item + "\n");
			print("new: " + $result + "\n");
		}
	}
}
