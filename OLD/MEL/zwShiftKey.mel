/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ª∆÷ŸŒ¨
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwShiftKey()
{
	if (!`optionVar -exists "zwShiftKeyHierarchy"`)
		optionVar -intValue "zwShiftKeyHierarchy" 1;
	if (!`optionVar -exists "zwShiftKeyChannels"`)
		optionVar -intValue "zwShiftKeyChannels" 1;
	if (!`optionVar -exists "zwShiftKeyConrolPoints"`)
		optionVar -intValue "zwShiftKeyConrolPoints" 0;	
	if (!`optionVar -exists zwShiftKeyShapes`)
		optionVar -intValue "zwShiftKeyShapes" 1;

	if(`window -exists shiftKey`)
		deleteUI shiftKey;

	window -menuBar true -title "Shift Key" -width 520 -height 360 shiftKey;
		menu -label "Help";
			menuItem -label "Help on Shift Key..." -command "zjHelpCallback zwShiftKey";
		string $formLayout = `formLayout`;
			string $tabLayout = `tabLayout -childResizable true -scrollable true -tabsVisible false`;
				columnLayout -rowSpacing 5 -adjustableColumn true columnLayout1;
					radioButtonGrp -numberOfRadioButtons 2 -select `optionVar -query "zwShiftKeyHierarchy"` -onCommand1 "optionVar -intValue \"zwShiftKeyHierarchy\" 1" -onCommand2 "optionVar -intValue \"zwShiftKeyHierarchy\" 2" -label "Hierarchy" -labelArray2 "Selected" "Below" -columnAlign 1 "right" radioButtonGrp1;
					radioButtonGrp -numberOfRadioButtons 2 -select `optionVar -query "zwShiftKeyChannels"` -onCommand1 "optionVar -intValue \"zwShiftKeyChannels\" 1" -onCommand2 "optionVar -intValue \"zwShiftKeyChannels\" 2" -label "Channels" -labelArray2 "All Keyable" "From Channel Box" -columnAlign 1 "right" radioButtonGrp2;
//					checkBoxGrp -numberOfCheckBoxes 1 -label "Driven Channels";
					checkBoxGrp -numberOfCheckBoxes 1 -label "Conrol Points" -value1 `optionVar -query "zwShiftKeyConrolPoints"` -changeCommand1 "optionVar -intValue \"zwShiftKeyConrolPoints\" #1" checkBoxGrp1;
					checkBoxGrp -numberOfCheckBoxes 1 -label "Shapes" -value1 `optionVar -query "zwShiftKeyShapes"` -changeCommand1 "optionVar -intValue \"zwShiftKeyShapes\" #1" checkBoxGrp2;
					separator;
					radioButtonGrp -numberOfRadioButtons 3 -select 3 -label "Time Range" -labelArray3 "All" "Start/End" "Time Slider" -columnAlign 1 "right" -onCommand2 "floatFieldGrp -edit -enable true floatFieldGrp1;floatFieldGrp -edit -enable true floatFieldGrp2;" -offCommand2 "floatFieldGrp -edit -enable false floatFieldGrp1;floatFieldGrp -edit -enable false floatFieldGrp2;" radioButtonGrp3;
					floatFieldGrp -enable false -label "Start Time" -value1 0.0 -columnWidth 2 80 -columnAlign 1 "right" floatFieldGrp1;
					floatFieldGrp -enable false -label "End Time" -value1 10.0 -columnWidth 2 80 -columnAlign 1 "right" floatFieldGrp2;
					floatSliderGrp -precision 0 -label "Time Offset" -field true -minValue -50.0 -maxValue 50.0 -fieldMinValue -10000.0 -fieldMaxValue 10000.0 floatSliderGrp1;
					floatSliderGrp -precision 3 -label "Value Offset" -field true -minValue -10.0 -maxValue 10.0 -fieldMinValue -1000000.0 -fieldMaxValue 1000000.0 floatSliderGrp2;
					setParent ..;
				setParent ..;
			string $button1 = `button -label "Shift Key" -command "shiftKeyOk;\ndeleteUI shiftKey;"`;
			string $button2 = `button -label "Apply" -command "shiftKeyOk"`;
			string $button3 = `button -label "Close" -command "deleteUI shiftKey"`;

		formLayout -edit
			-attachForm	$tabLayout	"top"		0
			-attachForm	$tabLayout	"left"		0
			-attachControl	$tabLayout	"bottom"	5	$button1
			-attachForm	$tabLayout	"right"		0

			-attachForm	$button1	"left"		5
			-attachForm	$button1	"bottom"	5
			-attachPosition	$button1	"right"		0	33

			-attachControl	$button2	"left"		5	$button1
			-attachForm	$button2	"bottom"	5
			-attachPosition	$button2	"right"		0	66

			-attachControl	$button3	"left"		5	$button2
			-attachForm	$button3	"bottom"	5
			-attachForm	$button3	"right"		5
		$formLayout;

	showWindow shiftKey;
}

global proc shiftKeyOk()
{
	string $buf[] = `ls -sl`;
	string $attr, $obj;
	int $i;
	for ($i=0; $i<size($buf); $i++)
	{
		$obj += "\"" + $buf[$i] + "\"";
		if ($i != size($buf) - 1)
			$obj += ", ";
	}
	if($obj == "")
	{
		warning "No objects selected to shift keys";
		return;
	}

	setParent shiftKey;

	string $hierarchy = "none";
	if(`radioButtonGrp -query -select radioButtonGrp1` == 2)
		$hierarchy = "below";

	if (`radioButtonGrp -query -select radioButtonGrp2` == 2)
	{
		global string $gChannelBoxName;
		$buf = `channelBox -query -selectedMainAttributes $gChannelBoxName`;
		for ($i=0; $i<size($buf); $i++)
			$attr = $attr + " -at \"" + $buf[$i] + "\"";
		$buf = `channelBox -query -selectedShapeAttributes $gChannelBoxName`;
		for ($i=0; $i<size($buf); $i++)
			$attr = $attr + " -at \"" + $buf[$i] + "\"";
		$buf = `channelBox -query -selectedHistoryAttributes $gChannelBoxName`;
		for ($i=0; $i<size($buf); $i++)
			$attr = $attr + " -at \"" + $buf[$i] + "\"";
		$buf = `channelBox -query -selectedOutputAttributes $gChannelBoxName`;
		for ($i=0; $i<size($buf); $i++)
			$attr = $attr + " -at \"" + $buf[$i] + "\"";
		if ($attr == "")
		{
			warning "No channels selected in channel box";
			return;
		}
	}

	int $controlPoints = `checkBoxGrp -query -value1 checkBoxGrp1`;
	int $shape = `checkBoxGrp -query -value1 checkBoxGrp2`;

	string $start = "";
	string $end = "";
	if(`radioButtonGrp -query -select radioButtonGrp3` == 2)
	{
		$start = `floatFieldGrp -query -value1 floatFieldGrp1`;
		$end = `floatFieldGrp -query -value1 floatFieldGrp2`;
	}
	else if(`radioButtonGrp -query -select radioButtonGrp3` == 3)
	{
		$start = `playbackOptions -query -minTime`;
		$end = `playbackOptions -query -maxTime`;
	}

	int $num = evalEcho("cutKey -time \"" + $start + ":" + $end + "\" -hierarchy " + $hierarchy + " -controlPoints " + $controlPoints + " -shape " + $shape + $attr + " {" + $obj + "}");
	if ($num != 0)
	{
		float $timeOffset = `floatSliderGrp -query -value floatSliderGrp1`;
		float $valueOffset = `floatSliderGrp -query -value floatSliderGrp2`;
		evalEcho("pasteKey -option \"merge\" -timeOffset " + $timeOffset + " -valueOffset " + $valueOffset + " {" + $obj + "}");
	}
}