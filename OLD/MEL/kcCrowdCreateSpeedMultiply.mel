/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc kcCrowdCreateSpeedMultiply()
{
	string $selected[] = `ls -selection`;

	if (size($selected) == 0)
		error("Please select the MASTER node.");

	// create multiply divide node

	string $multiply = `createNode -name crowdSpeedMultiply multiplyDivide`;

	string $unitToTimeConversion = `createNode unitToTimeConversion`;
	setAttr ($unitToTimeConversion + ".conversionFactor") 250;

	connectAttr -force "time1.outTime" ($multiply + ".input1X");
	connectAttr -force ($multiply + ".outputX") ($unitToTimeConversion + ".input"); 

	for ($item in $selected)
	{
		select -hierarchy $item;
		string $nodes[] = `ls -selection`;

		for ($node in $nodes)
		{
			string $history[] = `listHistory $node`;

			for ($historyNode in $history)
				if ((`nodeType $historyNode` == "animCurveTA") ||
					(`nodeType $historyNode` == "animCurveTL") ||
					(`nodeType $historyNode` == "animCurveTT") ||
					(`nodeType $historyNode` == "animCurveTU"))
				{
					connectAttr -force ($unitToTimeConversion + ".output") ($historyNode + ".input");
				}
		}
	}
}