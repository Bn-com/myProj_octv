global proc mrSelectField()
{
	string $selected[] = `ls -selection -shortNames`;

	string $MASTER[];
	clear $MASTER;

	for ($item in $selected)
	{
		string $temp[] = `ls ($item + "_MASTER")`;

		if (size($temp) == 1)
			// MASTER node exists
			$MASTER[size($MASTER)] = $temp[0];
		else
			if (size($temp) > 1)
				warning("More than 1 object has name \"" + $item + "_MASTER\". Not linking fields.");
	}

	select $MASTER;
	string $particles[] = `listRelatives -allDescendents -type particle`;

	select -clear;

	for ($j = 0; $j < size($particles); $j++)
	{
		string $fields[] = `listConnections ($particles[$j] + ".fieldData")`;
		select -add $fields;
	}


	string $rigidBodies[];

	for ($item in $MASTER)
		if (`gmatch $item "*mushvine*"` == true)
			$rigidBodies[size($rigidBodies)] = `substitute "_MASTER" $item "_rigidBody"`;

	for ($j = 0; $j < size($rigidBodies); $j++)
	{
		string $fields[] = `listConnections ($rigidBodies[$j] + ".fieldData")`;
		select -add $fields;
	}
}