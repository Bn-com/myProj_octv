/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: Kiem Ching
///
/// Creation Date: 2006/11/15
///
/// Description:
///


global proc gdcModifyMapPath(string $sourcePath, string $destPath, int $objectApplied)
{
	$sourcePath = `substitute "[{]" $sourcePath "[{]"`;

	string $nodes[];
	if ($objectApplied == 1)
	{
		$nodes = `ls -type "aiStandIn" -l`;
	}
	else
	{
		$nodes = `ls -sl -type "aiStandIn" -l`;
	}

	for ($node in $nodes)
	{
		string $source = `getAttr ($node + ".dso")`;
		$source = `substituteAllString $source "\\" "/"`;
		string $dest = `substitute $sourcePath $source $destPath`;
		if ($source != $dest)
		{
			evalEcho ("setAttr -type \"string\" \"" + $node + ".dso\" \"" + $dest + "\"");
		}
	}

	if ($objectApplied == 1 && `pluginInfo -query -loaded "Fur.mll"`)
	{
		string $furs[] = `ls -type "FurDescription"`;
		for ($fur in $furs)
		{
			string $attrs[] = `listAttr -string "*Map" $fur`;
			for ($attr in $attrs)
			{
				string $maps[] = `listConnections -plugs true -connections true ($fur + "." + $attr)`;
				for ($i=0; $i<size($maps); $i+=2)
				{
					string $source = `getAttr $maps[$i]`;
					$source = `substituteAllString $source "\\" "/"`;
					string $dest = `substitute $sourcePath $source $destPath`;
					if ($source != $dest)
					{
						setMapFile($maps[$i], $dest);
					}
				}
			}
		}
	}
}

global proc gdcModifyMapPathCB(string $sourcePathTextField, string $destPathTextField,string $radio)
{
	$sourcePath = `textFieldGrp -q -text $sourcePathTextField`;
	$destPath = `textFieldGrp -q -text $destPathTextField`;

	$sourcePath = `substituteAllString $sourcePath "\\" "/"`;
	$destPath = `substituteAllString $destPath "\\" "/"`;

	$objectApplied = `radioButtonGrp -q -select $radio`;

	gdcModifyMapPath($sourcePath, $destPath,$objectApplied);
}

global proc ModifyStandInPathGUI()
{
	if(!`window -exists gdcModifyMapPathWnd`)
	{
		window -menuBar true -title "Modify AiStandIn Path" -width 500 -height 200 gdcModifyMapPathWnd;
//			menu -label "Help";
//				menuItem -label "Help on Scale Texture ..." -command "zjHelpCallback zwTextureScale";
			formLayout formLayout1;
				text -label "WARNING: Path is case-sensitive." text1;

				string $radio = `radioButtonGrp -numberOfRadioButtons 2 -label "apply to" -labelArray2 "all aiStandIn" "selected aiStandIn" -select 1`;

				string $sourcePathTextField = `textFieldGrp -label "Source Path:" -text ""`;
				string $destPathTextField = `textFieldGrp -label "Destination Path:" -text ""`;

//				textFieldButtonGrp -label "Textures Folder:" -buttonLabel "Browse" -buttonCommand "textureCollectorBrowse" -adjustableColumn 2 textureCollectorBrowse;
//				checkBoxGrp -label "Modify Texture Path" -value1 true checkBoxGrp1;
//				checkBoxGrp -label "Convert to iff" checkBoxGrp2;
				string $button1 = `button -label "Modify" -command ("gdcModifyMapPathCB " + $sourcePathTextField + " " + $destPathTextField + " " + $radio+ ";\ndeleteUI gdcModifyMapPathWnd;")`;
				string $button2 = `button -label "Apply" -command ("gdcModifyMapPathCB " + $sourcePathTextField + " " + $destPathTextField + " " + $radio)`;
				string $button3 = `button -label "Close" -command "deleteUI gdcModifyMapPathWnd"`;

			formLayout -edit
				-attachForm	text1			"top"		10
				-attachForm	text1			"left"		10

				-attachControl	$radio	"top"		5	text1
				-attachForm		$radio	"left"		5
				-attachForm		$radio	"right"		5

				-attachControl	$sourcePathTextField	"top"		5	$radio
				-attachForm		$sourcePathTextField	"left"		5
				-attachForm		$sourcePathTextField	"right"		5

				-attachControl	$destPathTextField	"top"		10	$sourcePathTextField
				-attachForm		$destPathTextField	"left"		5
				-attachForm		$destPathTextField	"right"		5

//				-attachControl	checkBoxGrp1		"top"		5	textureCollectorBrowse
//				-attachForm	checkBoxGrp1		"left"		5

//				-attachControl	checkBoxGrp2		"top"		5	checkBoxGrp1
//				-attachForm	checkBoxGrp2		"left"		5

				-attachControl	$button1	"top"		5	$destPathTextField
				-attachForm		$button1	"left"		5
				-attachPosition	$button1	"right"		0	33

				-attachControl	$button2	"top"		5	$destPathTextField
				-attachControl	$button2	"left"		0	$button1
				-attachPosition	$button2	"right"		0	66

				-attachControl	$button3	"top"		5	$destPathTextField
				-attachControl	$button3	"left"		0	$button2
				-attachForm		$button3	"right"		5
			formLayout1;
	}
	showWindow gdcModifyMapPathWnd;
}