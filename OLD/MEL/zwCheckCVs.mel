global proc int zwCheckCVs()
{
	string $err[];
	clear $err;

	string $mesh[] = `ls -noIntermediate -type "mesh"`;

	int $max = `size $mesh`;
	if ($max && !`about -batch`)
	{
		progressWindow -progress 0 -max $max -title "Check CVs" -status ("0/" + $max) -isInterruptable true;
	}

	for ($i=0; $i<$max; $i++)
	{
		if (!`about -batch`)
		{
			if (`progressWindow -query -isCancelled`)
			{
				break;
			}
			progressWindow -edit -progress ($i + 1) -status (($i + 1) + "/" + $max + "    " + $mesh[$i]);
		}

		int $vertex[] = `polyEvaluate -vertex $mesh[$i]`;
		for ($j=0; $j<$vertex[0]; $j++)
		{
			float $pnt[] = `getAttr ($mesh[$i] + ".pnts[" + $j + "]")`;
			if (!($pnt[0] == 0 && $pnt[1] == 0 && $pnt[2] == 0))
			{
				string $parent[] = `listRelatives -fullPath -parent $mesh[$i]`;
				$err[size($err)] = $parent[0];
				break;
			}
		}
	}

	if ($max && !`about -batch`)
	{
		progressWindow -endProgress;
	}

	int $rs = (size($err) == 0);
	if ($rs)
	{
		print "没有发现可能会飞点的物体";
	}
	else
	{
		print "以下物体可能会飞点：\n";
		print $err;
		select $err;
		if (!`about -batch`)
		{
			confirmDialog -message "可能会飞点的物体已经被选上，并列在Script Editor" -button "OK";
		}
	}

	return $rs;
}