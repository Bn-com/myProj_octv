/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ³ÂÖÙ
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


//-----------------------------------------------------------------------
//
//  Creation Date:  August 16, 2006
//
//  Procedure Name:
//		czDisplayReferenceEdits
//
//  Description:
//
//		Display all the edits of specified referenced-file
//		in the script editor.
//
//  Input Arguments: none
//
//  Return Value: None.
//
//-----------------------------------------------------------------------

global proc czDisplayReferenceEdits()
{
	global string $gReferenceEditorPanel;
	
	string $buffer[];

	// get the reference file name
	string $referenceFile[] = `sceneEditor -q -si $gReferenceEditorPanel`;

	// Headup display
	print ("// Edits Within "+$referenceFile[0]+":\n");

	// get the reference node
	string $referenceNode = `referenceQuery -referenceNode $referenceFile[0]`;

	// print edits list
	$buffer = referenceEditsList( $referenceNode, "setAttr");
	print "\n";
	print $buffer;

	$buffer = referenceEditsList( $referenceNode, "connectAttr");
	print "\n";
	print $buffer;

	$buffer = referenceEditsList( $referenceNode, "disconnectAttr");
	print "\n";
	print $buffer;

	$buffer = referenceEditsList( $referenceNode, "addAttr");
	print "\n";
	print $buffer;

	$buffer = referenceEditsList( $referenceNode, "deleteAttr");
	print "\n";
	print $buffer;

	$buffer = referenceEditsList( $referenceNode, "parent");
	print "\n";
	print $buffer;
}

global proc string[] referenceEditsList(string $reference, string $edit)
{
	//	query successful and failed edits separately. Failed edits can always
	//	be removed, successful edits can only be removed when the ref is unloaded
	string $refEditsSuccessful[] = `referenceQuery -fld 0 -scs 1 -ec $edit -es $reference`;
	string $refEditsFailed[] = `referenceQuery -fld 1 -scs 0 -ec $edit -es $reference`;

	// editList
	string $editList[];

	if (`size ($refEditsSuccessful)`){

		//	add separator and title
			$editList[size($editList)] = "// ----------------------------------------";

			$editList[size($editList)] = ("// -------- " + $edit + " --------");

		//	add edits
		for ($refEdit in $refEditsSuccessful){

			$editList[size($editList)] =  $refEdit + ";";

		}
	}

	if (`size ($refEditsFailed)`){

		//	add separator and title
		$editList[size($editList)] = "// ----------------------------------------";

		$editList[size($editList)] = ("// -------- failed_" + $edit + " --------");

		//	add edits
		for ($refEdit in $refEditsFailed){

			$editList[size($editList)] = $refEdit + ";";

		}
	}

	// return the editList
	return $editList;

}