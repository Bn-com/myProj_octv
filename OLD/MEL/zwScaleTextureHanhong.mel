/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ª∆÷ŸŒ¨
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zwScaleTextureHanhongCB()
{
	string $folder = `textFieldButtonGrp -query -fileName textFieldButtonGrpDest`;	// `optionVar -query "zwScaleTextureHanhongDestination"`;
	string $files[] = `getFileList -filespec ($folder + "/*.*")`;
	
	int $max = `size $files`;
	if ($max == 0)
		return;

	progressWindow -max $max -title "Scale Texture" -isInterruptable true;
	for ($i=0; $i<size($files); $i++)
	{
		if (`progressWindow -query -isCancelled`)
		{
			break;
		}
		progressWindow -edit -progress ($i + 1) -status ($i + 1 + "/" + $max + "    " + $files[$i]);

		if (`match "_half." $files[$i]` != "" || `match "_quarter." $files[$i]` != "")
		{
			continue;
		}
		string $source = $folder + "/" + $files[$i];
		string $format = zwGetFormatByExt($source);
		string $half = `substitute "[.]" $files[$i] "_half."`;
		string $dest = $folder + "/" + $half;
		if (!`zwToggleMapsExists $source $dest`)
		{
			float $scale = 0.5;
			zwToggleMapsIdmtImageScale($scale, $source, $dest, $format);
		}
		string $quarter = `substitute "[.]" $files[$i] "_quarter."`;
		string $dest = $folder + "/" + $quarter;
		if (!`zwToggleMapsExists $source $dest`)
		{
			float $scale = 0.25;
			zwToggleMapsIdmtImageScale($scale, $source, $dest, $format);
		}
	}
	progressWindow -endProgress;
}

global proc zwScaleTextureHanhongBrowse(string $textFieldButtonGrp)
{
	string $zwScaleTextureHanhongDestination = `optionVar -query "zwScaleTextureHanhongDestination"`;
	$zwScaleTextureHanhongDestination = `idmtFolderDialog -initialDir $zwScaleTextureHanhongDestination`;
	$zwScaleTextureHanhongDestination = `substituteAllString $zwScaleTextureHanhongDestination "\\" "/"`;
	if(`filetest -d $zwScaleTextureHanhongDestination`)
	{
		optionVar -stringValue "zwScaleTextureHanhongDestination" $zwScaleTextureHanhongDestination;
		textFieldButtonGrp -edit -fileName $zwScaleTextureHanhongDestination $textFieldButtonGrp;
	}
}

global proc zwScaleTextureHanhongChange(string $textFieldButtonGrp)
{
	string $zwScaleTextureHanhongDestination = `textFieldButtonGrp -query -fileName $textFieldButtonGrp`;
	optionVar -stringValue "zwScaleTextureHanhongDestination" $zwScaleTextureHanhongDestination;
}

global proc zwScaleTextureHanhongReset()
{
	optionVar -intValue "zwScaleTextureHanhongApplyTo" 1;
	optionVar -intValue "zwScaleTextureHanhongModifyPath" 1;
	optionVar -stringValue "zwScaleTextureHanhongDestination" "";

	zwScaleTextureHanhong;
}

global proc zwScaleTextureHanhong()
{
	eval "source \"zwToggleMaps.mel\"";

	if (!`optionVar -exists "zwScaleTextureHanhongApplyTo"`)
		optionVar -intValue "zwScaleTextureHanhongApplyTo" 1;
	if (!`optionVar -exists "zwScaleTextureHanhongModifyPath"`)
		optionVar -intValue "zwScaleTextureHanhongModifyPath" 1;
	if (!`optionVar -exists "zwScaleTextureHanhongDestination"`)
		optionVar -stringValue "zwScaleTextureHanhongDestination" "";

	int $zwScaleTextureHanhongApplyTo = `optionVar -query "zwScaleTextureHanhongApplyTo"`;
	int $zwScaleTextureHanhongModifyPath = `optionVar -query "zwScaleTextureHanhongModifyPath"`;
	string $zwScaleTextureHanhongDestination = `optionVar -query "zwScaleTextureHanhongDestination"`;

	if (`window -exists zwScaleTextureHanhong`)
		deleteUI zwScaleTextureHanhong;

	window -title "Scale Texture" -width 500 -height 200 zwScaleTextureHanhong;
		string $formLayout = `formLayout`;
			string $tabLayout = `tabLayout -tabsVisible false -scrollable true -childResizable true`;
				columnLayout -adjustableColumn true -rowSpacing 5;
					string $textFieldButtonGrp = `textFieldButtonGrp -label "Folder:" -fileName $zwScaleTextureHanhongDestination -buttonLabel "Browse" -adjustableColumn 2 textFieldButtonGrpDest`;
					setParent..;
				setParent..;
			string $button1 = `button -label "Scale" -command "zwScaleTextureHanhongCB;\ndeleteUI zwScaleTextureHanhong;"`;
			string $button2 = `button -label "Apply" -command "zwScaleTextureHanhongCB"`;
			string $button3 = `button -label "Close" -command "deleteUI zwScaleTextureHanhong"`;

	formLayout -edit
		-attachForm	$tabLayout	"top"		0
		-attachForm	$tabLayout	"left"		0
		-attachForm	$tabLayout	"right"		0
		-attachControl	$tabLayout	"bottom"	5	$button1

		-attachForm	$button1	"left"		5
		-attachForm	$button1	"bottom"	5
		-attachPosition	$button1	"right"		5	33

		-attachControl	$button2	"left"		5	$button1
		-attachForm	$button2	"bottom"	5
		-attachPosition	$button2	"right"		5	66

		-attachControl	$button3	"left"		5	$button2
		-attachForm	$button3	"bottom"	5
		-attachForm	$button3	"right"		5
	$formLayout;

	textFieldButtonGrp -edit -changeCommand ("zwScaleTextureHanhongChange " + $textFieldButtonGrp) -buttonCommand ("zwScaleTextureHanhongBrowse " + $textFieldButtonGrp) $textFieldButtonGrp;

	showWindow zwScaleTextureHanhong;
}