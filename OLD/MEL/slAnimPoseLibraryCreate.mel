//////////////////////////////////////////////////////////
//
//	Script Name: 
//	Author: WansHoulOng
//	Created: 
//
//	Update/Change: WansHoulOng
//	Last Updated: 
//	Description:
//				
//////////////////////////////////////////////////////////


global proc slAnimPoseLibraryCreateUI()
{
	source slAnimExportImportForSetMainLayout.mel;
	
	if(`window -exists slAnimPoseLibraryCreateWin`)
		deleteUI slAnimPoseLibraryCreateWin;
	
	window -t "Create Pose&Animation Library" -mb 1 slAnimPoseLibraryCreateWin;
		menu -label "Edit";
			menuItem -label "清除缓存" -command "slAnimPoseLibraryCreateClearOptionVar";
		menu -label "Help";
			menuItem -label "Help on AnimPoseLibraryCreate..." -command "zjHelpCallback slAnimPoseLibraryCreateUI";
		
		string $form = `formLayout slAnimPoseLibraryCreateWin_formLayout_all`;
			string $rc = `rowColumnLayout -nc 2 -cw 1 360 `;
				optionMenuGrp -label "保存路径: " -columnWidth2 60 300 -cc "slAnimPoseLibraryCreateLocationChange" slAnimPoseLibraryCreateWin_Location;
					menuItem -l "......" -c "";
					if (`optionVar -ex optionVar_APLC_Location` && size(`optionVar -q optionVar_APLC_Location`))
					{
						string $children[] = `optionMenuGrp -q -ill  slAnimPoseLibraryCreateWin_Location`;
							deleteUI $children;
						
						string $vars[] = `optionVar -q optionVar_APLC_Location`;
						for ($item in $vars)
						{
							menuItem -label $item ;
						}
						optionMenuGrp -e -v $vars[0]  slAnimPoseLibraryCreateWin_Location;
					}
				button -l "浏览" -w 115 -c "slAnimPoseLibraryCreateLocation";
			setParent ..;
			
			
			string $rbg = `radioButtonGrp -numberOfRadioButtons 2 -label "库类型:" -labelArray2 "Pose" "Anim" -cat 1 "left" 22 -cw 1 60 -sl 1 -bgc 0 1 0 slAnimPoseLibraryCreateWin_PoseOrAnim`;
			radioButtonGrp -e -on1 "optionVar -iv optionVar_APLC_PoseOrAnim 1;radioButtonGrp -e -bgc 0 1 0 slAnimPoseLibraryCreateWin_PoseOrAnim;"  
			                             -on2 "optionVar -iv optionVar_APLC_PoseOrAnim 2;radioButtonGrp -e -bgc 1 0 0 slAnimPoseLibraryCreateWin_PoseOrAnim;" 
						     -cc "slAnimPoseLibraryCreatePoseOrAnimChange"
						     slAnimPoseLibraryCreateWin_PoseOrAnim;
			if (`optionVar -ex optionVar_APLC_PoseOrAnim`)
			{
				int $var = `optionVar -q optionVar_APLC_PoseOrAnim`;
				radioButtonGrp -e -sl $var slAnimPoseLibraryCreateWin_PoseOrAnim;
				if ($var == 1)
					radioButtonGrp -e -bgc 0 1 0 slAnimPoseLibraryCreateWin_PoseOrAnim;
				else 
					radioButtonGrp -e -bgc 1 0 0 slAnimPoseLibraryCreateWin_PoseOrAnim;
			}


			string $tfg1 = `textFieldGrp -l "类别1:" -columnWidth2 56 419 -cc "slAnimPoseLibraryCreateType1Change(1)" slAnimPoseLibraryCreateWin_Type1`;
				popupMenu ;
					menuItem -l "..." -c "";

			string $tfg2 = `textFieldGrp -l "类别2:" -columnWidth2 56 419 -cc "" slAnimPoseLibraryCreateWin_Type2`;
				popupMenu ;
					menuItem -l "..." -c ""; 

			string $tfg3 = `textFieldGrp -l "库  名:" -columnWidth2 56 425 slAnimPoseLibraryCreateWin_Name`;

			string $b = `button -l "创建" -h 40 -c "slAnimPoseLibraryCreate();"`;//deleteUI slAnimPoseLibraryCreateWin;";

			
			string $shelf = `shelfLayout  -st "iconAndTextVertical" -cwh 60 90 slAnimPoseLibraryCreateWin_Debug`;
				shelfButton -annotation "None" -style "iconAndTextVertical" -image1 "commandButton.png" -l "None"	-fn "boldLabelFont" -p $shelf;
		formLayout -edit 
			-af		$rc		"top"		1
			-af		$rc		"left"		13
			-af		$rc		"right"		1
			
			-ac		$rbg		"top"		1	$rc
			-af		$rbg		"left"		1

			-ac		$tfg1		"top"		1	$rbg
			-af		$tfg1		"left"		1

			-ac		$tfg2		"top"		1	$tfg1
			-af		$tfg2		"left"		1

			-ac		$tfg3		"top"		1	$tfg2
			-af		$tfg3		"left"		1

			-ac		$b		"top"		1	$tfg3
			-af		$b		"left"		1
			-af		$b		"right"		1

			-ac		$shelf		"top"		1	$b
			-af		$shelf		"left"		1
			-af		$shelf		"right"		1
			-af		$shelf		"bottom"	1	
		$form;
	showWindow slAnimPoseLibraryCreateWin;
	window -e -w 493 slAnimPoseLibraryCreateWin;

	slAnimPoseLibraryCreateCameraDisplay();
	slAnimPoseLibraryCreateLocationChange();
	slAnimPoseLibraryCreateType1Change(1);
	
	if (`optionVar -ex optionVar_APLC_Type1`)
	{
		
		string $var = `optionVar -q optionVar_APLC_Type1`;
		textFieldGrp -e -tx $var slAnimPoseLibraryCreateWin_Type1;
		slAnimPoseLibraryCreateType1Change(0);
	}

	if (`optionVar -ex optionVar_APLC_Type2`)
	{
		string $var = `optionVar -q optionVar_APLC_Type2`;
		textFieldGrp -e -tx $var slAnimPoseLibraryCreateWin_Type2;
	}
	
	string $filename = `file -q -sn -shn`;
	string $buff[] = stringToStringArray($filename,".");
	string $apl = $buff[0];
	textFieldGrp -e -tx $apl slAnimPoseLibraryCreateWin_Name;


	slAnimPoseLibraryCreateUpdateDebug();
}

global proc slAnimPoseLibraryCreateClearOptionVar()
{
	optionVar -rm optionVar_APLC_Location;
	optionVar -rm optionVar_APLC_PoseOrAnim;
	optionVar -rm optionVar_APLC_Type1;
	optionVar -rm optionVar_APLC_Type2;
	
	deleteUI	slAnimPoseLibraryCreateWin;
	slAnimPoseLibraryCreateUI();
	textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type1;
	textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type2; 
	
}

///保存路径改变的影响
global proc slAnimPoseLibraryCreateLocation()
{
	string $result[] = `fileDialog2  -fm 3`;
	if (size($result))
	{
		string $locations[] = `optionMenuGrp -q -ill slAnimPoseLibraryCreateWin_Location`;
		string $buff[]  =stringToStringArray($locations[0],"|");
		string $optionMenu = substituteAllString($locations[0],("|"+$buff[size($buff)-1]),"");
		
		menuItem -label $result[0] -p $optionMenu;
		optionMenuGrp -e -v $result[0] slAnimPoseLibraryCreateWin_Location;
		
		slAnimPoseLibraryCreatePoseOrAnimChange();

	}
}

global proc slAnimPoseLibraryCreatePoseOrAnimChange()
{
	slAnimPoseLibraryCreateCameraDisplay();
	optionVar -rm optionVar_APLC_Type1;
	optionVar -rm optionVar_APLC_Type2;
	slAnimPoseLibraryCreateLocationChange();
}


global proc slAnimPoseLibraryCreateLocationChange()
{
	textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type1;
	
	string $location = `optionMenuGrp -q -v slAnimPoseLibraryCreateWin_Location`;
	int $POA = `radioButtonGrp -q -sl slAnimPoseLibraryCreateWin_PoseOrAnim`;
	string $POAP="";
	if ($POA==1)
		$POAP="Pose";
	else
		$POAP="动作库";
	string $type1[]={};
	string $buff[] = `getFileList -fld ($location+"/"+$POAP+"/")`;
	for ($it in $buff)
	{
		if (`filetest -d ($location+"/"+$POAP+"/"+$it)`)
		{
			$type1[size($type1)] = $it;
		}
	}
	if (size($type1))
	{
		string $pp[] = `textFieldGrp -q -pma slAnimPoseLibraryCreateWin_Type1`;
		popupMenu -e -dai $pp[0];
		for ($mi in $type1)
		{
			menuItem -l $mi -p $pp[0] -c ("textFieldGrp -e -tx \""+$mi+"\" slAnimPoseLibraryCreateWin_Type1;slAnimPoseLibraryCreateType1Change(1);optionVar -sv optionVar_APLC_Type1 \""+$mi+"\";");
		}
	}
	else
	{
		string $pp[] = `textFieldGrp -q -pma slAnimPoseLibraryCreateWin_Type1`;
		popupMenu -e -dai $pp[0];
			menuItem -l "..." -p $pp[0];
		textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type1;
	}
	
	optionVar -sva optionVar_APLC_Location $location;
	slOptionMenuGrpArray("optionVar_APLC_Location","slAnimPoseLibraryCreateWin_Location",$location);

	slAnimPoseLibraryCreateType1Change(1);
}

global proc slAnimPoseLibraryCreateType1Change(int $slReset)
{
	if($slReset)
		textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type2;

	string $location = `optionMenuGrp -q -v slAnimPoseLibraryCreateWin_Location`;
	int $POA = `radioButtonGrp -q -sl slAnimPoseLibraryCreateWin_PoseOrAnim`;
	string $POAP="";
	if ($POA==1)
		$POAP="Pose";
	else
		$POAP="动作库";
	string $type1 = `textFieldGrp -q -tx slAnimPoseLibraryCreateWin_Type1`;
	string $type2[]={};
	if (size($type1))
	{
		print 1;
		string $buff[] = `getFileList -fld ($location+"/"+$POAP+"/"+$type1+"/")`;
		for ($it in $buff)
		{
			if (`filetest -d ($location+"/"+$POAP+"/"+$type1+"/"+$it)`)
			{
				$type2[size($type2)] = $it;
			}
		}

		if (size($type2))
		{
			print 2;
			string $pp[] = `textFieldGrp -q -pma slAnimPoseLibraryCreateWin_Type2`;
			popupMenu -e -dai $pp[0];
			for ($mi in $type2)
			{
				menuItem -l $mi -p $pp[0] -c ("textFieldGrp -e -tx \""+$mi+"\" slAnimPoseLibraryCreateWin_Type2;slAnimPoseLibraryCreateUpdateDebug();optionVar -sv optionVar_APLC_Type2 \""+$mi+"\";");
			}
		}
		else
		{
			print 3;
			string $pp[] = `textFieldGrp -q -pma slAnimPoseLibraryCreateWin_Type2`;
			popupMenu -e -dai $pp[0];
				menuItem -l "..." -p $pp[0];
			textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type2;
		}
	}
	else
	{
		print 4;
		string $pp[] = `textFieldGrp -q -pma slAnimPoseLibraryCreateWin_Type2`;
		popupMenu -e -dai $pp[0];
			menuItem -l "..." -p $pp[0];
		textFieldGrp -e -tx "" slAnimPoseLibraryCreateWin_Type2;
	}

	slAnimPoseLibraryCreateUpdateDebug();

}


//requeue optionVar
global proc slOptionMenuGrpArray(string $optionVar,string $UIName,string $UPVar)
{
	string $vars[] = `optionVar -q $optionVar`;
	optionVar -ca $optionVar;
	optionVar -sva $optionVar $UPVar;
	for ($i=0;$i<size($vars);$i++)
	{
		if ($UPVar != $vars[$i])
		{
			optionVar -sva $optionVar $vars[$i];
		}
	}

	string $items[] = `optionMenuGrp -q -ill $UIName`; 
	string $buff[]  =stringToStringArray($items[0],"|");
	string $optionMenu = substituteAllString($items[0],("|"+$buff[size($buff)-1]),"");
	deleteUI $items;
	$vars = `optionVar -q $optionVar`;
	for ($i=0;$i<size($vars);$i++)
	{
		menuItem -l $vars[$i] -p $optionMenu;
	}
	
}


//$type:
//1-Pose
//2-Animation
global proc slAnimPoseLibraryCreate()
{
	string $selected[] = `ls -sl`;
	if (size($selected)==0)
		error "没有选择任何控制器，请选择需要记录信息的控制器!";

	
	//确认路径
	string $location = `optionMenuGrp -q -v slAnimPoseLibraryCreateWin_Location`;
	int $POA = `radioButtonGrp -q -sl slAnimPoseLibraryCreateWin_PoseOrAnim`;
	string $POAP="";
	if ($POA==1)
		$POAP="Pose";
	else
		$POAP="动作库";
	string $type1 = `textFieldGrp -q -tx slAnimPoseLibraryCreateWin_Type1`;
	string $type2 = `textFieldGrp -q -tx slAnimPoseLibraryCreateWin_Type2`;
	string $name = `textFieldGrp -q -tx slAnimPoseLibraryCreateWin_Name`;
	string $path = $location+"/"+$POAP+"/"+$type1+"/"+$type2;
	if($location=="" || $type1=="" || $type2 == "" || $name == "")
		error "请填写路径及库名";

	if (!`filetest -d $path`)
		sysFile -md $path;
	string $filename = $path+"/"+$name;

	string $exists[]  = `getFileList -fs ($filename+".*")`;
	if (size($exists))
	{
		string $confirm = `confirmDialog -title "Confirm" -message "库文件已经存在，是否覆盖?" -button "Yes" -button "No" -defaultButton "No"   -cancelButton "No" -dismissString "No"`;

		if ($confirm=="No")
			return;
	}

	string $modelPanel = `getPanel -withFocus`;
	modelEditor -e -grid 0 -da smoothShaded $modelPanel;
	modelEditor -e -allObjects 0 $modelPanel;
	modelEditor -e -nurbsSurfaces  1 $modelPanel;
	modelEditor -e -polymeshes  1 $modelPanel;
	modelEditor -e -subdivSurfaces  1 $modelPanel;
	
	//创建图标
	select -cl;
	slAnimPoseLibraryCreateIcon($filename,0);
	select -r $selected;


	int $start = `playbackOptions -q -min`;
	int $end = `playbackOptions -q -max`;
	global string $gPlayBackSlider;
	float $timeRange[]=`timeControl -q -ra $gPlayBackSlider`;
	if (($timeRange[0]+1) != $timeRange[1])
	{
		$start = $timeRange[0];
		$end = $timeRange[1];
	}
	if ($POA == 1)
	{
		slAnimPoseLibraryCreateDAT($filename);
		slAnimPoseLibraryCreateIcon($filename,1);
	}
	else if ($POA == 2)
	{
		slAnimPoseLibraryCreateSLA($filename,$start,$end);
		slAnimPoseLibraryCreateAVI($filename,$start,$end);
	}
	
	modelEditor -e -nurbsCurves 1 $modelPanel;


	slAnimPoseLibraryCreateUpdateDebug();
}

global proc slAnimPoseLibraryCreateIcon(string $filename,int $images)
{
	int $facialLibIconsSize[] = {60,60};
	if($images)
		$facialLibIconsSize = {600,600};
	float $currentTime = `currentTime -q`; 

	int $currentIF = `getAttr "defaultRenderGlobals.imageFormat"`;
	setAttr "defaultRenderGlobals.imageFormat" 20;
	if($images)
		setAttr "defaultRenderGlobals.imageFormat" 8;
	
	string $post = "bmp";
	if($images)
		$post="jpg";


	string $camera = `lookThru -q`;
	if(nodeType($camera)!="camera")
	{
		string $rel[] = `listRelatives $camera`;
		$camera = $rel[0];
	}
	float $currentOS = `getAttr ($camera+".overscan")`;
	setAttr ($camera+".overscan") 1;

	if(`about -apiVersion` >= 201200)
	{
		string $temp = `internalVar -userTmpDir`;
		string $tempOld[] = `getFileList -filespec ($temp + "*."+$post)`;
		string $dest = `playblast -v false -frame $currentTime -w $facialLibIconsSize[0] -h $facialLibIconsSize[1] -orn 0 -cf ($filename+"."+$post) -fmt "image" -p 100 -compression $post`;
		string $tempNew[] = `getFileList -filespec ($temp + "*."+$post)`;
		$tempNew = stringArrayRemove($tempOld, $tempNew);
		if (size($tempNew) == 1)
		{
			sysFile -delete $dest;
			sysFile -move $dest ($temp + $tempNew[0]);
		}
		//print $dest;
	}
	else
	{
		playblast -v false -frame $currentTime -w $facialLibIconsSize[0] -h $facialLibIconsSize[1] -orn 0 -cf ($filename+"."+$post) -fmt "image" -p 100;
	}

	setAttr "defaultRenderGlobals.imageFormat" $currentIF;
	setAttr ($camera+".overscan") $currentOS;
}

global proc slAnimPoseLibraryCreateAVI(string $fileName,float $start,float $end)
{
	int $AviSize[] = {960,540};
	float $currentTime = `currentTime -q`; 

	playblast -v false  -fmt avi -f $fileName -fo -sqt 0 -cc 1 -orn 0 -p 100 -c "none" -qlt 100 -wh $AviSize[0] $AviSize[1] -st $start -et $end;

	currentTime $currentTime;
}

global proc slAnimPoseLibraryCreateDAT(string $fileName)
{
	string $file;
	if (endsWith($fileName,".dat"))
		$file = $fileName;
	else 
		$file = $fileName+".dat";
	
	string $wrongObj[]={};
	string $user = `getenv username`;
	string $date = `date`;
	string $facialData = "Pose Data (Author : "+$user+" Date:"+$date+" )...\n";
	string $objs[] = `ls -sl`;
	string $udShapes[]={};
	if (size($objs))
	{
		//get some shapes
		for ($obj in $objs)
		{
			string $expShapes[] = `listRelatives -pa -ni -shapes $obj` ;
			for ($es in $expShapes)
			{
				if (match("|",$es) == "")
				{
					string $esKeys[] = `listAttr -k -ud $es`;
					string $esNonKeys[] = `listAttr -cb -ud $es`;
					if (size($esKeys) || size($esNonKeys))
					{
						$udShapes[size($udShapes)]=$es;
					}
				}
				else 
				{
					string $buff[] = stringToStringArray($es,"|");
					if (!catchQuiet(`ls $buff[size($buff)-1]`) && size(`ls $buff[size($buff)-1]`)==1)
						$udShapes[size($udShapes)]=$buff[size($buff)-1];
				}
			}
		}
		$udShapes = stringArrayRemoveDuplicates($udShapes);
	}

	$objs = stringArrayCatenate($objs,$udShapes);

	for ($obj in $objs)
	{
		if (match("|",$obj) != "")
		{
			$wrongObj[size($wrongObj)]=$obj;
		}
		else 
		{
			string $keys[] =`listAttr -k $obj`;
			string $nonKeys[] = `listAttr -cb $obj`;
			if (stringArrayCount($obj,$udShapes))
			{
				$keys =`listAttr -k -ud $obj`;
				$nonKeys = `listAttr -cb -ud $obj`;
			}
			string $allAttr[] = stringArrayCatenate($keys,$nonKeys);
			
			//string $keyableAttrs[] = `listAnimatable $con`;
			for ($attr in $allAttr)
			{
				string $buff[]  =stringToStringArray($obj,":");
				string $name = $buff[size($buff)-1];
				string $value = `getAttr ($obj+"."+$attr)`;
				
				$facialData += $name+"."+$attr + "   " + $value + "\n";
			}
		}
	}
	$facialData += "End...";

	$fileId = `fopen $file w`;	
	fprint $fileId $facialData;
	fclose $fileId;
}

global proc slAnimPoseLibraryCreateSLA(string $fileName,float $start,float $end)
{
	string $file;
	if (endsWith($fileName,".sla"))
		$file = $fileName;
	else 
		$file = $fileName+".sla";
	
	string $objs[] = `ls -sl`;
	string $udShapes[]={};
	string $wrongObj[]={};
	string $user = `getenv username`;
	string $date = `date`;
	string $title = "Anim Data (Author : "+$user+" Date:"+$date+" )...\n";
	if (size($objs))
	{
		//get some shapes
		for ($obj in $objs)
		{
			string $expShapes[] = `listRelatives -pa -ni -shapes $obj` ;
			for ($es in $expShapes)
			{
				if (match("|",$es) == "")
				{
					string $esKeys[] = `listAttr -k -ud $es`;
					string $esNonKeys[] = `listAttr -cb -ud $es`;
					if (size($esKeys) || size($esNonKeys))
					{
						$udShapes[size($udShapes)]=$es;
					}
				}
				else 
				{
					string $buff[] = stringToStringArray($es,"|");
					if (!catch(`ls $buff[size($buff)-1]`) && size(`ls $buff[size($buff)-1]`)==1)
						$udShapes[size($udShapes)]=$buff[size($buff)-1];
				}
			}
		}
		$udShapes = stringArrayRemoveDuplicates($udShapes);

		$objs = stringArrayCatenate($objs,$udShapes);
		
		
		$fileId = `fopen $file "w"`;
		//-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
		string $mayaVersion = `about -v`;
		string $linearUnit = `currentUnit -q -f -l`;
		string $angularUnit = `currentUnit -q -f -a`;
		string $timeUnit = `currentUnit -q -f -t`;
		fprint $fileId $title;
		fprint $fileId ("mayaVersion  "+$mayaVersion+";\n");
		fprint $fileId ("linearUnit  "+$linearUnit+";\n");
		fprint $fileId ("angularUnit  "+$angularUnit+";\n");
		fprint $fileId ("timeUnit  "+$timeUnit+";\n");
		//-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	
		for ($obj in $objs)
		{
			//if (`objectType -isType "transform" $obj` || `objectType -isType "joint" $obj`)
			//{
			if (match("|",$obj) != "")
			{
				$wrongObj[size($wrongObj)]=$obj;
			}
			else 
			{
				string $buff[] =  stringToStringArray($obj,":");
				string $name=$buff[size($buff)-1];
				
				
				string $keys[] =`listAttr -k $obj`;
				string $nonKeys[] = `listAttr -cb $obj`;
				if (stringArrayCount($obj,$udShapes))
				{
					$keys =`listAttr -k -ud $obj`;
					$nonKeys = `listAttr -cb -ud $obj`;
				}
				string $allAttr[] = stringArrayCatenate($keys,$nonKeys);

				for ($key in $allAttr)
				{
					string $animCurv[] ;
					if (`objExists ($obj+"."+$key)`)
						$animCurv = `listConnections -s true -d false ($obj+"."+$key)`;
					if (`objExists $animCurv[0]` && isAnimCurve($animCurv[0]))
					{
						//--------------------------------------------------------------------------------------------
						setKeyframe -t $start -t $end -i ($obj+"."+$key);
						//--------------------------------------------------------------------------------------------
						
						fprint $fileId ("anim "+$name+"."+$key+"\n{\n");

						float $time[] = `keyframe -q -tc $animCurv[0]`;
						float $value[] = `keyframe -q -vc $animCurv[0]`;
						string $inputType[] = `keyTangent -q -itt $animCurv[0]`;
						string $outputType[] = `keyTangent -q -ott $animCurv[0]`;
						int $lock[] = `keyTangent -q -l $animCurv[0]`;
						int $weightLock[] = `keyTangent -q -wl $animCurv[0]`;
						float $inputAngle[] = `keyTangent -q -ia $animCurv[0]`;
						float $inputWeight[] = `keyTangent -q -iw $animCurv[0]`;
						float $outputAngle[] = `keyTangent -q -oa $animCurv[0]`;
						float $outputWeight[] = `keyTangent -q -ow $animCurv[0]`;


						for ($i=0;$i<size($time) ;$i++ )
						{
							if ($time[$i]>=$start && $time[$i]<=$end)
							{
								float $fixTime = $time[$i]-$start+1;
								fprint $fileId ("	"+$fixTime+" "+$value[$i]+" "+$inputType[$i]+" "+$outputType[$i]+" "+$weightLock[$i]);
								if (($inputType[$i] == "fixed" || $outputType[$i] == "fixed") && $weightLock[$i])
									fprint $fileId (" "+$inputAngle[$i]+" "+$outputAngle[$i]);
								else if (($inputType[$i] == "fixed" || $outputType[$i] == "fixed") && !$weightLock[$i])
									fprint $fileId (" "+$inputAngle[$i]+" "+$inputWeight[$i]+" "+$outputAngle[$i]+" "+$outputWeight[$i]);
								fprint $fileId ";\n";
							}
						}

						fprint $fileId "}\n";
					}
					else if (`objExists ($obj+"."+$key)`)
					{
						//print ($obj+"."+$key+"\n");
						if (`getAttr -type ($obj+"."+$key)` != "double3")
						{
							string $value = getAttr ($obj+"."+$key);
							fprint $fileId ("non-anim "+$name+"."+$key+"	"+$value+";\n");
						}
					}
				}
			}
		}
		fprint $fileId "End...";
		fclose $fileId;
	}
}


global proc slAnimPoseLibraryCreateUpdateDebug()
{
	string $path = `optionMenuGrp -q -v slAnimPoseLibraryCreateWin_Location`;
	string $libSel = `radioButtonGrp -q -sl slAnimPoseLibraryCreateWin_PoseOrAnim`;
	string $type1= `textFieldGrp -q -tx slAnimPoseLibraryCreateWin_Type1`;
	string $type2= `textFieldGrp -q -tx slAnimPoseLibraryCreateWin_Type2`;

	string $lib="";
	if($libSel==1)
		$lib = "Pose";
	else if($libSel==2)
		$lib="动作库";

	if(size($path) && size($type1) && size($type2))
	{
		string $folder = $path+"/"+$lib+"/"+$type1+"/"+$type2+"/";

		string $libfilepost = "";
		string $command="";
		if($lib=="Pose")
		{	
			$libfilepost = "dat";
			$command = "slAnimPoseLibraryCreatePoseKey ";
		}
		else if($lib=="动作库")
		{
			$libfilepost = "sla";
			$command = "slAnimImportForSet ";
		}
		//else if($lib=="动作库")
			string $sla[] = `getFileList -fs ("*."+$libfilepost) -fld $folder`;
			if(size($sla))
			{
				string $shelf="slAnimPoseLibraryCreateWin_Debug";
				string $children[] = `shelfLayout -q -ca $shelf`;
				deleteUI $children;
				for($item in $sla)
				{
					string $base = substring($item,1,(size($item)-4));
					string $image = $folder+$base+".bmp";
					string $filename = $folder+$base+"."+$libfilepost;
					$filename = substituteAllString($filename,"\\","/");
					string $filetype = $libfilepost;
					string $preview="";
					if($libSel==1)//pose
						$preview = $folder+$base+".jpg";
					else if($libSel==2)//动作库
						$preview=$folder+$base+".avi";
					if(!`filetest -r $image`)
						$image = "commandButton.png";
					string $sb = `shelfButton -style "iconAndTextVertical" -image1 $image -l $base -fn "boldLabelFont" -p $shelf
						                  -c ($command+"\""+$filename+"\" \""+$filetype+"\"")`;
						popupMenu -button 3  ;
						string $am = `menuItem -en true -label "预览" -c ("system (\"load "+$preview+"\")")`;
						//string $am1 = `menuItem -en true -label "删除" -c ("slAnimPoseLibraryCreateRemoveKey(\""+$filename+"\",\""+$filetype+"\");slAnimPoseLibraryCreateUpdateDebug;")`;
					string $pma[] = `shelfButton -q -pma $sb`;
					//print $pma;
					if(size($pma)==2)
						deleteUI $pma[0];
				}
			}
			else
			{
				string $shelf="slAnimPoseLibraryCreateWin_Debug";
				string $children[] = `shelfLayout -q -ca $shelf`;
				deleteUI $children;
				shelfButton -annotation "None" -style "iconAndTextVertical" -image1 "commandButton.png" -l "None"	-fn "boldLabelFont" -p $shelf;
			}
	}
	else
	{
		string $shelf="slAnimPoseLibraryCreateWin_Debug";
		string $children[] = `shelfLayout -q -ca $shelf`;
		deleteUI $children;
		shelfButton -annotation "None" -style "iconAndTextVertical" -image1 "commandButton.png" -l "None"	-fn "boldLabelFont" -p $shelf;
	}

}

global proc slAnimPoseLibraryCreateRemoveKey(string $filename,string $filetype)
{
	string $buff[] = stringToStringArray($filename,"\\/");
	string $path = substituteAllString($filename,$buff[size($buff)-1],"");

	string $base[] =  stringToStringArray($buff[size($buff)-1],"");
	string $bmp = $path + $base[0] + ".bmp";
	string $prv = $path + $base[0];
	if($filetype=="sla")
		$prv = $prv + ".avi" ;
	else 
		$prv = $prv + ".jpg" ;
	
	sysFile -delete $filename;
	sysFile -delete $bmp;
	sysFile -delete $prv;

	//slAnimPoseLibraryCreateUpdateDebug();
}


global proc slAnimPoseLibraryCreateCameraDisplay()
{
	source createMayaSoftwareCommonGlobalsTab.mel;
	if(!`floatFieldGrp -ex resRatio`)
	{
		unifiedRenderGlobalsWindow;
		window -e -vis 0 unifiedRenderGlobalsWindow;
	}
	
	string $libSel = `radioButtonGrp -q -sl slAnimPoseLibraryCreateWin_PoseOrAnim`;
	int $wh[]={600,600};//pose
	float $os=3.0;
	if($libSel==2)//动作库
	{
		$wh={960,540};
		$os = 1.5;
	}

	setAttr "defaultResolution.aspectLock" 0;
	setAttr defaultResolution.lockDeviceAspectRatio 0;
	setAttr "defaultResolution.pixelAspect" 1;
	setAttr "defaultResolution.w" $wh[0];
	setAttr "defaultResolution.h" $wh[1];
	updateMayaSoftwarePixelAspectRatio;

	string $camera = `lookThru -q`;
	if(nodeType($camera)!="camera")
	{
		string $rel[] = `listRelatives $camera`;
		$camera = $rel[0];
	}
	setAttr ($camera+".displayResolution") 1;
	setAttr ($camera+".displayGateMask") 1;
	setAttr ($camera+".overscan") $os;
}

global proc slAnimPoseLibraryCreatePoseKey(string $filename,string $filetype)
{
	string $buff[]={};
	string $master[] = `ls -sl`;
	if (!`objExists $master[0]`)
		error "请选择需要导入Pose的角色！";
	int $num = `tokenize $master[0] ":" $buff`;
	string $char = `substring $master[0] 1 (size($master[0])-size($buff[$num-1]))`;

	
	if ($char != "NONE")
	{
		float $extraValue = 1;
		
		string $dataName = $filename;
		
		$fileId = `fopen $dataName r`;
			string $nextLine = `fgetline $fileId`;
			$nextLine = `fgetline $fileId`;
			while (size($nextLine) > 0 )
			{
				string $buff[]={};
				tokenize $nextLine " " $buff;
				string $name = $buff[0];
				string $value = $buff[1];
				float $mirror = 1;

				string $extrabuff[] = {};
				tokenize $name "." $extrabuff;

				if (size($master)==1 || (size($master)>1 && stringArrayCount(($char+$extrabuff[0]),$master)))
				{
					if (`objExists ($char+$name)`&& size(`ls ($char+$name)`)==1 && `getAttr -se ($char+$name)` )
					{
						//print ($char+$name+"\n");
						setAttr ($char+$name) (((float)$value)*$extraValue*$mirror);
						setKeyframe ($char+$name);
					}
				}
				$nextLine = `fgetline $fileId`;
			}
		fclose $fileId;
	}
}
