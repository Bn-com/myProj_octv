//
// Save camera matrix to file
//
proc MXSaveCameraTrack(string $CameraName, 
							  int    $FromFrame, 
							  int    $ToFrame,
							  string $Filename)
{
 if (!(`objExists ($CameraName)`))
    return;
 select $CameraName;
 
 $fileId = `fopen $Filename "w"`;

 fprint ($fileId, "PEDIT_CAMERA_FILE\n");
 fprint ($fileId, $FromFrame  + "\n");
 fprint ($fileId, $ToFrame    + "\n");
 
 //
 // Get shape name
 //
 string $dagNames[] = (`ls -dag -ap -sl`);
 string $fullName = $dagNames[1];
 
 int $cnt = 0; 
 for ($i = $FromFrame; $i < $ToFrame+2; $i++)
 {
	  fprint ($fileId, $cnt + "\n");
	  currentTime $i;
	  
	  float $CamFOV    =   45.0;
	  float $CamNEAR   =    1.0;
	  float $CamFAR    = 1000.0;
	  float $CamASPECT =    1.5;
	  
	  if (`objExists ($fullName + ".focalLength")`)
		  $CamFOV = `getAttr ($fullName + ".focalLength")`;
		  
	  if (`objExists ($fullName + ".nearClipPlane")`)
		  $CamNEAR = `getAttr ($fullName + ".nearClipPlane")`;
		  
	  if (`objExists ($fullName + ".farClipPlane")`)
		  $CamFAR = `getAttr ($fullName + ".farClipPlane")`;
		  
	  //
	  // Get camera node total matrix
	  //
	  float $worldMat[];
	        $worldMat[ 0] = 1.0; $worldMat[ 1] = 0.0; $worldMat[ 2] = 0.0; $worldMat[ 3] = 0.0;
	        $worldMat[ 4] = 0.0; $worldMat[ 5] = 1.0; $worldMat[ 6] = 0.0; $worldMat[ 7] = 0.0;
	        $worldMat[ 8] = 0.0; $worldMat[ 9] = 0.0; $worldMat[10] = 1.0; $worldMat[11] = 0.0;
	        $worldMat[12] = 0.0; $worldMat[13] = 0.0; $worldMat[14] = 0.0; $worldMat[15] = 1.0;
	        
	  if (`objExists ($fullName + ".worldMatrix")`)
		  $worldMat = `getAttr ($fullName + ".worldMatrix")`;
	  
      fprint ($fileId, $CamFOV    + "\n");
      fprint ($fileId, $CamNEAR   + "\n");
      fprint ($fileId, $CamFAR    + "\n");
      fprint ($fileId, $CamASPECT + "\n");
      
      //
      // Clamp small translations
      //
      if (abs($worldMat[12]) < 0.0001) $worldMat[12] = 0.0;
      if (abs($worldMat[13]) < 0.0001) $worldMat[13] = 0.0;
      if (abs($worldMat[14]) < 0.0001) $worldMat[14] = 0.0;

      //
      // Eye position
      //
      fprint ($fileId, $worldMat[12] + " " + $worldMat[13] + " " + $worldMat[14] + " " + $worldMat[15] + "\n"); // Camera position
      
      //
      // LookAt
      //
      float $lookAt[4]; 
	        $lookAt[0] = 0.0; $lookAt[1] = 0.0; $lookAt[2] =-1.0; $lookAt[3] = 0.0;
	        
	  float $lookAtFinal[4]; 
	  
	  $lookAtFinal[0] = ($worldMat[ 0] * $lookAt[0]) + ($worldMat[ 4] * $lookAt[1]) + ($worldMat[ 8] * $lookAt[2]);
	  $lookAtFinal[1] = ($worldMat[ 1] * $lookAt[0]) + ($worldMat[ 5] * $lookAt[1]) + ($worldMat[ 9] * $lookAt[2]);
	  $lookAtFinal[2] = ($worldMat[ 2] * $lookAt[0]) + ($worldMat[ 6] * $lookAt[1]) + ($worldMat[10] * $lookAt[2]);
	  $lookAtFinal[3] = 0.0;
	  
	  //
	  // Clamp small numbers
	  //
	  if (abs($lookAtFinal[0]) < 0.0001) $lookAtFinal[0] = 0.0;
	  if (abs($lookAtFinal[1]) < 0.0001) $lookAtFinal[1] = 0.0;
	  if (abs($lookAtFinal[2]) < 0.0001) $lookAtFinal[2] = 0.0;
	  
      fprint ($fileId, $lookAtFinal[ 0] + " " + $lookAtFinal[ 1] + " " + $lookAtFinal[ 2] + " " + $lookAtFinal[ 3] + "\n");	        
      
      //
      // UpVector
      //
      float $UpVec[4]; 
	        $UpVec[0] = 0.0; $UpVec[1] = 1.0; $UpVec[2] = 0.0; $UpVec[3] = 0.0;
	        
	  float $UpVecFinal[4]; 
	  
	  $UpVecFinal[0] = ($worldMat[ 0] * $UpVec[0]) + ($worldMat[ 4] * $UpVec[1]) + ($worldMat[ 8] * $UpVec[2]);
	  $UpVecFinal[1] = ($worldMat[ 1] * $UpVec[0]) + ($worldMat[ 5] * $UpVec[1]) + ($worldMat[ 9] * $UpVec[2]);
	  $UpVecFinal[2] = ($worldMat[ 2] * $UpVec[0]) + ($worldMat[ 6] * $UpVec[1]) + ($worldMat[10] * $UpVec[2]);
	  $UpVecFinal[3] = 0.0;
	  
	  //
	  // Clamp small numbers
	  //
	  if (abs($UpVecFinal[0]) < 0.0001) $UpVecFinal[0] = 0.0;
	  if (abs($UpVecFinal[1]) < 0.0001) $UpVecFinal[1] = 0.0;
	  if (abs($UpVecFinal[2]) < 0.0001) $UpVecFinal[2] = 0.0;
	  
      fprint ($fileId, $UpVecFinal[ 0] + " " + $UpVecFinal[ 1] + " " + $UpVecFinal[ 2] + " " + $UpVecFinal[ 3] + "\n");	        
      
      //
      // Unused
      //
      fprint ($fileId, "0 0 0 0\n");
	  
	  $cnt++;
 }

 fclose $fileId; 
 return;
}


global proc zjMXSaveCameraTrack()
{
	if (`window -q -exists zjMXSaveCameraTrackWin`)
		deleteUI zjMXSaveCameraTrackWin;

	window -menuBar true -title "ParticleGen Camera" -resizeToFitChildren true zjMXSaveCameraTrackWin;

				menu -label "Help";
					menuItem -label "Help on ParticleGen Camera..." -command "zjHelpCallback zjMXSaveCameraTrack";

				$i = 0;

				string $formLayout = `formLayout`;
 					$child[$i++] = `textFieldGrp -columnWidth 1 100 -adjustableColumn 2 -label "Output Folder:" -editable false -text ""`;
					$child[$i++] = `button -label "Browse" -w 80 -command ("zjMXSaveCamerabrow " + $child[0])`;
					$child[$i++] = `button -label "Output" -command ("zjMXSaveCameraTrackfunc " + $child[0])`;
				setParent ..;

				formLayout -edit
					-attachForm		$child[0]	"top"		10
					-attachForm		$child[0]	"left"		0
					-attachControl		$child[0]	"right"		5	$child[1]

					-attachForm		$child[1]	"top"		10
					-attachForm		$child[1]	"right"		5

					-attachControl		$child[2]	"top"		10	$child[0]
					-attachForm		$child[2]	"left"		5
					-attachForm		$child[2]	"right"		5
				$formLayout;

				setParent ..;

	showWindow zjMXSaveCameraTrackWin;
}


global proc zjMXSaveCameraTrackfunc(string $path)
{

	string $selected[] = `ls -sl`;
	if (size($selected)==1)
	{
		string $buf[];
		clear $buf;
		tokenize $selected[0] "|" $buf;
		string $folder = `textFieldGrp -q -fileName $path`;
		float $sf = `playbackOptions -q -min`;
		float $ef = `playbackOptions -q -max`;
		MXSaveCameraTrack($selected[0], $sf, $ef, ($folder + "/" + $buf[size($buf)-1] + ".ctr"));
	}
	else warning "please select a camera";
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//for scene use
global proc zjMXSaveCamerabrow(string $control)
{
	fileBrowserDialog -mode 4  -fileCommand ("zjMXSaveCamerabrowProc " + $control) -fileType "mayaBinary" -actionName "Browse" -operationMode "Reference";
}

global proc int zjMXSaveCamerabrowProc(string $control, string $filename, string $fileType)
{
	textFieldGrp -e -text $filename $control;
	return true;
}
