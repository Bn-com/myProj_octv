/// Copyright (C) 2000-2005 IDMT. All rights reserved.
///
/// Author: ’‘÷æΩ‹
///
/// Creation Date: 2006/11/15
///
/// Description: 
///


global proc zjBuildLine()
{
	if (`window -q -exists zjBuildLineWnd`)
		return;

	window -title "Build Curve from NurbsSurface" -resizeToFitChildren true zjBuildLineWnd;
		menuBarLayout;
			menu -label "Help";
				menuItem -label "Help on Build Curve from NurbsSurface..." -command "zjHelpCallback zjBuildLine";
			setParent ..;

		
    		columnLayout -adjustableColumn true;

			string $direction = ` radioButtonGrp  -numberOfRadioButtons 2 -select 2  -labelArray2 "U" "V" 
		                -columnWidth 1 100 -columnWidth 2 100 `;
			string $button = `button -label "Build" -command ("zjBuildLinefun " + $direction)`;

	showWindow zjBuildLineWnd;
}

global proc zjBuildLinefun(string $ctl)
{
	string $sel[] = `ls -sl`;

	// check at least a NURBS surface is selected

	int $valid = false;

	for ($item in $sel)
		if ( kcIsType($item, "nurbsSurface") )
		{
			$valid = true;
			break;
		}

	if (!$valid)
		error("Select nurbsSurface to build curves.");

	int $option = `radioButtonGrp -q -select $ctl`;
	string $direct,$spandirect;
	if ($option == 1)
	{
		$spandirect = "U";
		$direct = "u";
	}
	else
	{
		$spandirect = "V";
		$direct = "v";
	}

	for ( $i=0; $i<size($sel); $i++ )
	{
		int $spans = `getAttr ($sel[$i] + ".maxValue" + $spandirect)` - `getAttr ($sel[$i] + ".minValue" + $spandirect)`;
		float $midspan = $spans/2.0;
		string $mid = $sel[$i] + "." + $direct + "[" + $midspan + "]";
		string $oldname = $sel[$i];
		string $dupvs[] = `duplicateCurve -ch 1 -rn 0 -local 0 $mid`;
		string $newname = $oldname + "_mid_curve";
		rename $dupvs[0] $newname;
	}
}
